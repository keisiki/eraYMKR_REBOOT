;=============================================================================
;봉사정신
;=============================================================================
@ABLUP13
DRAWLINE
SIF ABL:봉사정신 >= 5
    JUMP ABLUP_MAX

;従順が奉仕精神＋１レベルでないといけない
PRINTFORML %ABLNAME:10% {ABL:봉사정신 + 1}LV 이상, 그리고 

;ＬＶ３から４、４から５に上げるときは異常経験必要（素質：[妄信]なら無視できる）
SIF COST13(5)
    PRINTFORML %EXPNAME:50% {COST13(5)} 이상, 그리고 

PRINTFORM [0] - %PALAMNAME:14%의 구슬×{COST13(0)}
SIF COST13(4) > 0
    PRINTFORM , %EXPNAME:2% {COST13(4)} 이상, %EXPNAME:4% {COST13(4)} 이상
PRINT ……
CALL SHOW_ISABLUP_COMMON, CAN13(0)

IF COST13(1) > 0
    PRINTFORM [1] - %PALAMNAME:12%의 구슬×{COST13(1)}
    SIF COST13(3) > 0
        PRINTFORM , %EXPNAME:20% {COST13(3)} 이상
    PRINT ……
    CALL SHOW_ISABLUP_COMMON, CAN13(1)
ENDIF

IF COST13(2) > 0
    PRINTFORM [2] - %PALAMNAME:11%의 구슬×{COST13(2)}, %EXPNAME:2% 1 이상……
    CALL SHOW_ISABLUP_COMMON, CAN13(2)
ENDIF

PRINTL [100] - 돌아간다
$INPUT_LOOP
INPUT
IF RESULT == 100
    RETURN 0
ELSEIF (RESULT >= 0 && RESULT <= 2) && CAN13(RESULT) != 0
    PRINTL 조건을 만족하고 있지 않습니다. 다시 입력해 주십시오
    RESTART
ELSEIF RESULT == 0
    JUEL:14 -= COST13(0)
ELSEIF RESULT == 1
    JUEL:12 -= COST13(1)
ELSEIF RESULT == 2
    JUEL:11 -= COST13(2)
ELSE
    RETURN 0
ENDIF

ABL:봉사정신 += 1
PRINTFORMW %ABLNAME:13%의 레벨이 %조사처리(TOSTR(ABL:봉사정신),"가")% 되었습니다.

@AUTO_ABLUP13
#LOCALSIZE 1
#LOCALSSIZE 1
IF CAN13(2) == 0
    JUEL:11 -= COST13(2)
ELSEIF CAN13(1) == 0
    JUEL:12 -= COST13(1)
ELSEIF CAN13(0) == 0
    JUEL:14 -= COST13(0)
ELSE
    RETURN 0
ENDIF

ABL:봉사정신 += 1
RETURN 1

;--------------------------------------------------
;レベルアップに必要な珠計算、レベルアップ予告処理
;--------------------------------------------------
@ISABLUP13
SIF ABL:봉사정신 >= 5
    RETURN 0

CALLF CALC_COST13, "CALC"

;条件別にＯＫかダメかを記録する
RETURN CAN_ABLUP13("CHECK")

@COST13(ARG)
#FUNCTION
RETURNF CALC_COST13("GET", ARG)

@CALC_COST13(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
    RETURNF LOCAL:ARG
;必要な条件の算出
VARSET LOCAL, 0

IF ABL:봉사정신 == 0
    LOCAL:0 = 100
    LOCAL:1 = 20
    LOCAL:2 = 100
    LOCAL:3 = 1
    LOCAL:4 = 1
ELSEIF ABL:봉사정신 == 1
    LOCAL:0 = 1200
    LOCAL:1 = 100
    LOCAL:2 = 0
    LOCAL:3 = 1
    LOCAL:4 = 3
ELSEIF ABL:봉사정신 == 2
    LOCAL:0 = 5000
    LOCAL:1 = 600
    LOCAL:2 = 0
    LOCAL:3 = 5
    LOCAL:4 = 6
ELSEIF ABL:봉사정신 == 3
    LOCAL:0 = 10000
    LOCAL:1 = 2000
    LOCAL:2 = 0
    LOCAL:3 = 20
    LOCAL:4 = 10
    ;一線越えない
    IF TALENT:일선을넘지않음
        TIMES LOCAL:0 , 2.00
        TIMES LOCAL:1 , 2.00
        TIMES LOCAL:3 , 1.50
        TIMES LOCAL:4 , 1.50
    ENDIF
ELSEIF ABL:봉사정신 == 4
    LOCAL:0 = 30000
    LOCAL:1 = 8000
    LOCAL:2 = 0
    LOCAL:3 = 100
    LOCAL:4 = 20
    ;一線越えない
    IF TALENT:일선을넘지않음
        TIMES LOCAL:0 , 3.00
        TIMES LOCAL:1 , 3.00
        TIMES LOCAL:3 , 2.50
        TIMES LOCAL:4 , 2.50
    ENDIF
ENDIF

;反抗的
IF TALENT:반항적
    TIMES LOCAL:0 , 1.20
    TIMES LOCAL:1 , 1.40
    TIMES LOCAL:3 , 1.20
;素直
ELSEIF TALENT:솔직함
    TIMES LOCAL:0 , 0.80
    TIMES LOCAL:1 , 0.80
    TIMES LOCAL:3 , 0.70
ENDIF
;気丈
IF TALENT:꿋꿋함
    TIMES LOCAL:0 , 1.20
    TIMES LOCAL:3 , 1.20
ENDIF
;生意気
IF TALENT:건방짐
    TIMES LOCAL:0 , 1.10
    TIMES LOCAL:1 , 1.10
ENDIF
;プライド高い
IF TALENT:프라이드가높음
    TIMES LOCAL:0 , 1.40
    TIMES LOCAL:1 , 1.30
    TIMES LOCAL:3 , 1.20
;プライド低い
ELSEIF TALENT:프라이드가낮음
    TIMES LOCAL:0 , 0.90
    TIMES LOCAL:1 , 0.80
    TIMES LOCAL:3 , 0.80
ENDIF
;自制心
IF TALENT:자제심
    TIMES LOCAL:0 , 1.20
    TIMES LOCAL:1 , 1.20
    TIMES LOCAL:3 , 1.20
    TIMES LOCAL:4 , 1.20
ENDIF
;無関心
IF TALENT:무관심
    TIMES LOCAL:0 , 1.10
    TIMES LOCAL:1 , 1.10
    TIMES LOCAL:3 , 1.10
ENDIF
;感情乏しい
IF TALENT:감정결여
    TIMES LOCAL:0 , 1.20
    TIMES LOCAL:1 , 1.20
    TIMES LOCAL:3 , 1.40
ENDIF
;保守的
IF TALENT:보수적
    TIMES LOCAL:0 , 1.30
    TIMES LOCAL:1 , 1.30
    TIMES LOCAL:2 , 1.30
    TIMES LOCAL:3 , 1.20
    TIMES LOCAL:4 , 1.20
ENDIF
;楽観的
IF TALENT:낙관적
    TIMES LOCAL:0 , 0.90
    TIMES LOCAL:1 , 0.80
    TIMES LOCAL:3 , 0.70
    TIMES LOCAL:4 , 0.75
;悲観的
ELSEIF TALENT:비관적
    TIMES LOCAL:0 , 0.80
    TIMES LOCAL:1 , 0.80
    TIMES LOCAL:3 , 0.80
    TIMES LOCAL:4 , 0.80
ENDIF
;目立ちたがり
IF TALENT:튀고싶어함
    TIMES LOCAL:0 , 0.80
    TIMES LOCAL:1 , 0.80
    TIMES LOCAL:2 , 0.80
    TIMES LOCAL:3 , 0.80
ENDIF
;習得早い
IF TALENT:습득빠름
    TIMES LOCAL:2 , 0.80
;習得遅い
ELSEIF TALENT:습득느림
    TIMES LOCAL:2 , 1.60
ENDIF
;舌使い
SIF TALENT:혀놀림
    TIMES LOCAL:2 , 0.80

;献身的
IF TALENT:헌신적
    TIMES LOCAL:0 , 0.75
    TIMES LOCAL:1 , 0.75
    TIMES LOCAL:3 , 0.60
ENDIF

;快感に素直
IF TALENT:쾌감에솔직
    TIMES LOCAL:4 , 0.70
;快感の否定
ELSEIF TALENT:쾌감을부정
    TIMES LOCAL:4 , 1.40
ENDIF
;倒錯的
IF TALENT:도착적
    TIMES LOCAL:0 , 0.75
    TIMES LOCAL:1 , 0.75
    TIMES LOCAL:2 , 0.75
    TIMES LOCAL:3 , 0.75
ENDIF
;サド
IF TALENT:새드
    TIMES LOCAL:0 , 1.30
    TIMES LOCAL:1 , 1.30
    TIMES LOCAL:3 , 1.30
ENDIF
;小悪魔
IF TALENT:소악마
    TIMES LOCAL:0 , 1.20
    TIMES LOCAL:1 , 1.20
    TIMES LOCAL:2 , 1.20
    TIMES LOCAL:3 , 1.20
ENDIF

;ＬＶ３から４、４から５に上げるときは異常経験必要（素質：[妄信]、[服従]なら無視できる）
SIF ABL:봉사정신 >= 3 && TALENT:망신 == 0 && TALENT:복종 == 0
    LOCAL:5 = ABL:봉사정신 - 2

RETURNF 0

@CAN13(ARG)
#FUNCTION
RETURNF CAN_ABLUP13("GET", ARG)

@CAN_ABLUP13(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
    RETURNF LOCAL:ARG
VARSET LOCAL, 0
;従順が奉仕精神＋１レベルでないといけない
IF ABL:순종 < ABL:봉사정신 + 1
    ;従順が不足
    LOCAL:0 |= 4
    LOCAL:1 |= 4
    LOCAL:2 |= 4
ENDIF

;異常経験が必要な場合
IF EXP:이상경험 < COST13(5)
    LOCAL:0 |= 2
    LOCAL:1 |= 2
    LOCAL:2 |= 2
ENDIF

;屈服의 구슬で上げる
SIF JUEL:14 < COST13(0)
    LOCAL:0 |= 1
;절정経験が必要な場合
SIF EXP:절정경험 < COST13(4)
    LOCAL:0 |= 2
;精液経験が必要な場合
SIF EXP:정액경험 < COST13(4)
    LOCAL:0 |= 2

;恭順의 구슬で上げる
IF COST13(1) > 0
    SIF JUEL:12 < COST13(1)
        LOCAL:1 |= 1
    ;この場合奉仕快楽経験が必要なことあり
    SIF EXP:봉사쾌락경험 < COST13(3)
        LOCAL:1 |= 2
ELSE
    LOCAL:1 = 256
ENDIF

;習得의 구슬で上げる
IF COST13(2) > 0
    SIF JUEL:11 < COST13(2)
        LOCAL:2 |= 1
    ;この場合절정経験が1이상必要
    SIF EXP:절정경험 < 1
        LOCAL:2 |= 2
ELSE
    LOCAL:2 = 256
ENDIF

RETURNF (LOCAL:0 == 0 || LOCAL:1 == 0 || LOCAL:2 == 0)

;=============================================================================
;촬영 기능
;=============================================================================
@ABLUP71
SIF (FLAG:15 & 128) == 0 || (ITEM:50 == 0 && ITEM:51 == 0 && NOITEM == 0)
    RETURN 0
DRAWLINE
SIF ABL:촬영기능 >= 5
    JUMP ABLUP_MAX

PRINTFORM [0] - %PALAMNAME:11%의 구슬×{COST71(0)}, %EXPNAME:71% {COST71(1)} 이상……
CALL SHOW_ISABLUP_COMMON, CAN71(0)

PRINTL [100] - 돌아간다
$INPUT_LOOP
INPUT
IF RESULT == 100
    RETURN 0
ELSEIF RESULT == 0 && CAN71(0) != 0
    PRINTL 조건을 만족하고 있지 않습니다.
    RESTART
ELSEIF RESULT == 0
    JUEL:11 -= COST71(0)
ELSE
    GOTO INPUT_LOOP
ENDIF

ABL:촬영기능 += 1
PRINTFORMW %ABLNAME:71%의 레벨이 %조사처리(TOSTR(ABL:촬영기능),"가")% 되었습니다.

@AUTO_ABLUP71
#LOCALSIZE 1
#LOCALSSIZE 1
IF CAN71(0) == 0
    JUEL:11 -= COST71(0)
ELSE
    RETURN 0
ENDIF

ABL:촬영기능 += 1
RETURN 1

;--------------------------------------------------
;レベルアップに必要な珠計算、レベルアップ予告処理
;--------------------------------------------------
@ISABLUP71
SIF ABL:촬영기능 >= 5
    RETURN 0

CALLF CALC_COST71, "CALC"

;条件別にＯＫかダメかを記録する
RETURN CAN_ABLUP71("CHECK")

@COST71(ARG)
#FUNCTION
RETURNF CALC_COST71("GET", ARG)

@CALC_COST71(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
    RETURNF LOCAL:ARG
;必要な条件の算出
VARSET LOCAL, 0

IF ABL:촬영기능 == 0
    LOCAL:0 = 1
    LOCAL:1 = 10
ELSEIF ABL:촬영기능 == 1
    LOCAL:0 = (EXP:71 >= EXPLV:3) ? 15 # 25
    LOCAL:1 = 30
ELSEIF ABL:촬영기능 == 2
    LOCAL:0 = (EXP:71 >= EXPLV:4) ? 150 # 200
    LOCAL:1 = 60
ELSEIF ABL:촬영기능 == 3
    LOCAL:0 = (EXP:71 >= EXPLV:5) ? 1500 # 3000
    LOCAL:1 = 100
ELSEIF ABL:촬영기능 == 4
    LOCAL:0 = (EXP:71 >= EXPLV:5) ? 10000 # 20000
    LOCAL:1 = 150
ENDIF

;習得早い
IF TALENT:50
    TIMES LOCAL:0 , 0.80
    TIMES LOCAL:1 , 0.75
;習得遅い
ELSEIF TALENT:51
    TIMES LOCAL:0 , 1.20
    TIMES LOCAL:1 , 1.10
ENDIF
;報道者 (例：あやや）
IF TALENT:196
    TIMES LOCAL:0 , 0.90
    TIMES LOCAL:1 , 0.90
ENDIF
;工作名人（例：にとり）
IF TALENT:195
    TIMES LOCAL:0 , 0.80
    TIMES LOCAL:1 , 0.80
ENDIF

;ゆかりん
IF NO:TARGET == 22
    TIMES LOCAL:0 , 0.90
    TIMES LOCAL:1 , 0.90
;早苗さん
ELSEIF NO:TARGET == 42
    TIMES LOCAL:0 , 0.90
    TIMES LOCAL:1 , 0.90
ENDIF

RETURNF 0

@CAN71(ARG)
#FUNCTION
RETURNF CAN_ABLUP71("GET", ARG)

@CAN_ABLUP71(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
    RETURNF LOCAL:ARG
VARSET LOCAL, 0
;習得의 구슬で上げる
SIF JUEL:11 < COST71(0)
    LOCAL:0 |= 1
;撮影経験が必要
SIF EXP:71 < COST71(1)
    LOCAL:0 |= 2

RETURNF (LOCAL:0 == 0)

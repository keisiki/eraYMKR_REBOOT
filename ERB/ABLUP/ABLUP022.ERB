;=============================================================================
;레즈끼
;=============================================================================
@ABLUP22
DRAWLINE
IF TALENT:남자
    RETURN 0
ELSEIF ABL:레즈끼 >= 5
    JUMP ABLUP_MAX
ENDIF

;ＬＶ３から４、４から５に上げるときは異常経験必要（素質：[両刀]なら無視できる）
SIF COST22(4)
    PRINTFORML %EXPNAME:50% {COST22(4)} 이상, 그리고 

PRINTFORM [0] - %PALAMNAME:13%의 구슬×{COST22(0)}
SIF COST22(1) > 0
    PRINTFORM , %PALAMNAME:14%의 구슬×{COST22(1)}
PRINTFORM , %EXPNAME:12% {COST22(3)} 이상……
CALL SHOW_ISABLUP_COMMON, CAN22(0)

IF COST22(2) > 0
    PRINTFORM [1] - %PALAMNAME:0%의 구슬×{COST22(2)}, %EXPNAME:12% {COST22(3)} 이상……
    CALL SHOW_ISABLUP_COMMON, CAN22(1)
ENDIF

PRINTL [100] - 돌아간다
$INPUT_LOOP
INPUT
IF RESULT == 100
    RETURN 0
ELSEIF (RESULT == 0 || RESULT == 1) && CAN22(RESULT) != 0
    PRINTL 조건을 만족하고 있지 않습니다.
    RESTART
ELSEIF RESULT == 0
    JUEL:13 -= COST22(0)
    JUEL:14 -= COST22(1)
ELSEIF RESULT == 1
    JUEL:0 -= COST22(2)
ELSE
    GOTO INPUT_LOOP
ENDIF

ABL:레즈끼 += 1
PRINTFORMW %ABLNAME:22%의 레벨이 %조사처리(TOSTR(ABL:레즈끼),"가")% 되었습니다.

@AUTO_ABLUP22
#LOCALSIZE 1
#LOCALSSIZE 1
IF CAN22(1) == 0
    JUEL:0 -= COST22(2)
ELSEIF CAN22(0) == 0
    JUEL:13 -= COST22(0)
    JUEL:14 -= COST22(1)
ELSE
    RETURN 0
ENDIF

ABL:레즈끼 += 1
RETURN 1

;--------------------------------------------------
;レベルアップに必要な珠計算、レベルアップ予告処理
;--------------------------------------------------
@ISABLUP22
SIF ABL:레즈끼 >= 5 || TALENT:남자
    RETURN 0

CALLF CALC_COST22, "CALC"

;条件別にＯＫかダメかを記録する
RETURN CAN_ABLUP22("CHECK")

@COST22(ARG)
#FUNCTION
RETURNF CALC_COST22("GET", ARG)

@CALC_COST22(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
    RETURNF LOCAL:ARG
;必要な条件の算出
VARSET LOCAL, 0

IF ABL:레즈끼 == 0
    LOCAL:0 = 200
    LOCAL:1 = 0
    LOCAL:2 = 1000
    LOCAL:3 = 50
ELSEIF ABL:레즈끼 == 1
    LOCAL:0 = 1000
    LOCAL:1 = 0
    LOCAL:2 = 5000
    LOCAL:3 = 200
ELSEIF ABL:레즈끼 == 2
    LOCAL:0 = 3000
    LOCAL:1 = 1000
    LOCAL:2 = 0
    LOCAL:3 = 500
ELSEIF ABL:레즈끼 == 3
    LOCAL:0 = 8000
    LOCAL:1 = 2000
    LOCAL:2 = 0
    LOCAL:3 = 1000
    ;一線越えない
    IF TALENT:일선을넘지않음
        LOCAL:0 *= 2
        LOCAL:1 *= 2
        LOCAL:3 *= 2
    ENDIF
ELSEIF ABL:레즈끼 == 4
    LOCAL:0 = 20000
    LOCAL:1 = 5000
    LOCAL:2 = 0
    LOCAL:3 = 2000
    ;一線越えない
    IF TALENT:일선을넘지않음
        LOCAL:0 *= 3
        LOCAL:1 *= 3
        LOCAL:3 *= 3
    ENDIF
ENDIF

;倒錯的
IF TALENT:80
    TIMES LOCAL:0 , 0.75
    TIMES LOCAL:1 , 0.75
    TIMES LOCAL:2 , 0.75
    TIMES LOCAL:3 , 0.75
ENDIF
;両刀
IF TALENT:83
    TIMES LOCAL:0 , 0.25
    TIMES LOCAL:1 , 0.25
    TIMES LOCAL:2 , 0.25
    TIMES LOCAL:3 , 0.25
ENDIF

;ＬＶ３から４、４から５に上げるときは異常経験必要（素質：[両刀]なら無視できる）
SIF ABL:레즈끼 >= 3 && TALENT:83 == 0
    LOCAL:4 = ABL:레즈끼 - 2

RETURNF 0

@CAN22(ARG)
#FUNCTION
RETURNF CAN_ABLUP22("GET", ARG)

@CAN_ABLUP22(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
    RETURNF LOCAL:ARG
VARSET LOCAL, 0
;異常経験が必要な場合
IF EXP:이상경험 < COST22(4)
    LOCAL:0 |= 2
    LOCAL:1 |= 2
ENDIF

;レズ経験が必要
IF EXP:레즈경험 < COST22(3)
    LOCAL:0 |= 2
    LOCAL:1 |= 2
ENDIF

;欲情의 구슬で上げる
SIF JUEL:13 < COST22(0)
    LOCAL:0 |= 1
;屈服의 구슬で上げる
SIF JUEL:14 < COST22(1)
    LOCAL:0 |= 1

IF COST22(2) > 0
    ;快Ｃ의 구슬で上げる
    SIF JUEL:0 < COST22(2)
        LOCAL:1 |= 1
ELSE
    LOCAL:1 = 256
ENDIF

RETURNF (LOCAL:0 == 0 || LOCAL:1 == 0)

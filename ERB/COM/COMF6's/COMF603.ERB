;=============================================================================
;69 파이즈리
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
;더미 함수(커맨드를 직접 선택할 수는 없음)
@COM_ABLE603
RETURN 0

@EXCOM_ABLE603
;性器が汚れているとMASTERの場合６９파이즈리はできない(조교자가 汚れ無視か愛液, 母乳のよごれならＯＫ)
SIF STAIN:보지 >= 2 && (STAIN:보지 != 16 && STAIN:보지 != 17) && ASSIPLAY == 0 && TALENT:MASTER:64 == 0
    RETURN 0
;性器が汚れており, 汚臭敏感で순종4미만의 ASSI일 경우６９파이즈리はできない(조교자가 汚れ無視か愛液, 母乳のよごれならＯＫ)
IF STAIN:보지 >= 2 && (STAIN:보지 != 16 && STAIN:보지 != 17) && ASSIPLAY
    SIF ABL:ASSI:순종 <= 3 && TALENT:ASSI:62 && TALENT:ASSI:64 == 0
        RETURN 0
ENDIF
;조교자에게 페니스가 없으면 불가
SIF EXIST_BAR(PLAYER) == 0
    RETURN 0
;남자라면 안 됨
SIF TALENT:120
    RETURN 0
;[절벽]이면 불가
IF TALENT:110
    RETURN 0
;[貧乳]だと기교5이상 필요
ELSEIF TALENT:111 && ABL:기교 < 5
    RETURN 0
;[巨乳]なら기교3이상 필요
ELSEIF TALENT:112 && ABL:기교 < 3
    RETURN 0
;[爆乳]なら기교LV2이상 필요
ELSEIF TALENT:113 && ABL:기교 < 2
    RETURN 0
;그 외의 경우에는 기교4이상 필요
ELSEIF TALENT:111 == 0 && TALENT:112 == 0 && TALENT:113 == 0 && ABL:기교 < 4
    RETURN 0
ENDIF
;조교자가 고양이혀일 경우 고양이 펠라 패치 유효가 아니면 불가
SIF TALENT:PLAYER:54 && (FLAG:14 & 16) == 0
    RETURN 0
;조교자가 고양이혀일 경우 조교자에게 기교4 필요
SIF TALENT:PLAYER:54 && ABL:PLAYER:기교 < 4
    RETURN 0
;수면 중에는 불가
SIF EQUIP:14
    RETURN 0
;실신 중에는 불가
SIF TFLAG:899 > 0
    RETURN 0
;패닉 중에는 불가
SIF TFLAG:108 > 0
    RETURN 0
;밧줄 사용 중에는 불가
SIF TEQUIP:40
    RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
    RETURN 0
;욕실 플레이 중에는 플레이 매트가 없으면 불가
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
    RETURN 0
;샤워 중에는 불가
SIF TEQUIP:54
    RETURN 0
;○○목욕탕 입욕 중에는 불가
SIF TEQUIP:55
    RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
    RETURN 0
;조수가 코스프레 중에는 불가
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 > 2
    RETURN 0
;코스프레 중에는 불가
SIF TEQUIP:59 > 2
    RETURN 0
;나체정식 중에는 불가
SIF TEQUIP:60
    RETURN 0
;시간정지 중에는 무리
SIF TEQUIP:71
    RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM603
PRINTL 69 파이즈리

;-------------------------------------------------
;실행할 수 있을까의 판정
;-------------------------------------------------
IF TFLAG:699 == 0
    CALL COM_ORDER, 603
    ;실행할 수 없다
    SIF RESULT == 0
        RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------
COM_NAME = 69 파이즈리
SELECTCOM = 603
CALL TRAIN_MESSAGE_COM603

;-------------------------------------------------
;사정 게이지 체크
;-------------------------------------------------
LOCAL = 0

;ABL:기교를 본다
IF ABL:기교 == 0
    LOCAL = 1500
ELSEIF ABL:기교 == 1
    LOCAL = 2100
ELSEIF ABL:기교 == 2
    LOCAL = 2900
ELSEIF ABL:기교 == 3
    LOCAL = 4000
ELSEIF ABL:기교 == 4
    LOCAL = 5000
ELSE
    LOCAL = 6000
ENDIF

;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;봉사 정신을 본다
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)
;정액 중독을 본다
LOCAL = EJACULATIONCHECK_ABL_31_2(TARGET, LOCAL)

;조교자의 C감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
    BASE:PLAYER:사정 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 10
DOWNBASE:기력 += 150

SOURCE:22 = 1800
SOURCE:31 = 900

;ABL:봉사 정신을 본다
IF ABL:봉사정신 == 0
    SOURCE:15 = 420
    SOURCE:16 = 150
    SOURCE:30 = 400
ELSEIF ABL:봉사정신 == 1
    SOURCE:15 = 500
    SOURCE:16 = 300
    SOURCE:30 = 300
ELSEIF ABL:봉사정신 == 2
    SOURCE:15 = 580
    SOURCE:16 = 600
    SOURCE:30 = 150
ELSEIF ABL:봉사정신 == 3
    SOURCE:15 = 660
    SOURCE:16 = 900
    SOURCE:30 = 50
ELSEIF ABL:봉사정신 == 4
    SOURCE:15 = 740
    SOURCE:16 = 1500
    SOURCE:30 = 20
ELSE
    SOURCE:15 = 820
    SOURCE:16 = 2200
    SOURCE:30 = 0
ENDIF

;ABL:B감각을 본다
IF ABL:B감각 == 0
    LOCAL:1 = 100
ELSEIF ABL:B감각 == 1
    LOCAL:1 = 200
ELSEIF ABL:B감각== 2
    LOCAL:1 = 400
ELSEIF ABL:B감각 == 3
    LOCAL:1 = 800
ELSEIF ABL:B감각 == 4
    LOCAL:1 = 1200
ELSE
    LOCAL:1 = 1500
ENDIF

;B민감, B둔감
IF TALENT:106
    TIMES LOCAL:1 , 1.20
ELSEIF TALENT:107
    TIMES LOCAL:1 , 0.70
ENDIF
;큰 가슴, 폭유
SIF TALENT:112 || TALENT:113
    TIMES LOCAL:1 , 1.20
;음유
SIF TALENT:174
    TIMES LOCAL:1 , 1.40

SOURCE:3 += LOCAL:1

;ABL:C감각을 본다
IF ABL:C감각 == 0
    LOCAL:2 = 100
ELSEIF ABL:C감각 == 1
    LOCAL:2 = 200
ELSEIF ABL:C감각== 2
    LOCAL:2 = 400
ELSEIF ABL:C감각 == 3
    LOCAL:2 = 800
ELSEIF ABL:C감각 == 4
    LOCAL:2 = 1200
ELSE
    LOCAL:2 = 1500
ENDIF

;조교자가 혀사용
SIF TALENT:PLAYER:52
    TIMES LOCAL:2 , 2.00
;조교자의 고양이혀을 본다
SIF TALENT:PLAYER:54
    TIMES LOCAL:2 , 0.70
;C민감, C둔감
IF TALENT:100
    TIMES LOCAL:2 , 1.20
ELSEIF TALENT:101
    TIMES LOCAL:2 , 0.70
ENDIF

SOURCE:0 += LOCAL:2

;ABL:기교를 본다
IF ABL:기교 == 0
    TIMES SOURCE:15 , 0.50
    TIMES SOURCE:16 , 0.50
ELSEIF ABL:기교 == 1
    TIMES SOURCE:15 , 0.80
    TIMES SOURCE:16 , 0.80
ELSEIF ABL:기교 == 2
    TIMES SOURCE:15 , 1.00
    TIMES SOURCE:16 , 1.00
ELSEIF ABL:기교 == 3
    TIMES SOURCE:15 , 1.20
    TIMES SOURCE:16 , 1.20
ELSEIF ABL:기교 == 4
    TIMES SOURCE:15 , 1.50
    TIMES SOURCE:16 , 1.50
ELSE
    TIMES SOURCE:15 , 2.00
    TIMES SOURCE:16 , 2.00
ENDIF

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

IF TFLAG:91
;사정하고 있다
    TIMES SOURCE:15 , 3.00

    ;ABL:정액 중독을 본다
    IF ABL:정액중독 == 0
        SOURCE:24 = 0
        TIMES SOURCE:16 , 2.00
        TIMES SOURCE:22, 6.00
    ELSEIF ABL:정액중독 == 1
        SOURCE:24 = 500
        TIMES SOURCE:16 , 3.00
        TIMES SOURCE:22, 4.50
    ELSEIF ABL:정액중독 == 2
        SOURCE:24 = 1200
        TIMES SOURCE:16 , 4.00
        TIMES SOURCE:22, 3.50
    ELSEIF ABL:정액중독 == 3
        SOURCE:24 = 3000
        TIMES SOURCE:16 , 6.00
        TIMES SOURCE:22, 3.00
    ELSEIF ABL:정액중독 == 4
        SOURCE:24 = 6000
        TIMES SOURCE:16 , 9.00
        TIMES SOURCE:22, 2.00
    ELSE
        SOURCE:24 = 12000
        TIMES SOURCE:16 , 15.00
        TIMES SOURCE:22, 1.50
    ENDIF

    ;사정처 타입의 설정
    TFLAG:703 = 21
    ;사정처 선택의 가능·불가
    TFLAG:704 = 0
    ;범용적인 처리는 이 함수로 실시한다
    ;(사정 게이지의 재계산이나 사정 경험의 상승과 공통 부분의 표시)
    CALL SAMEN_SHOOT
ENDIF

;노예의 B⇔조교자의 P의 불결이 이동
STAIN:가슴 |= STAIN:PLAYER:자지
STAIN:PLAYER:자지 |= STAIN:가슴
;노예의 V⇔조교자의 입의 불결이 이동
STAIN:보지 |= STAIN:PLAYER:입
STAIN:PLAYER:입 |= STAIN:보지

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
    TCVAR:12 += 7
ELSEIF TALENT:120 && TALENT:PLAYER:120
    TCVAR:13 += 7
ENDIF
;주인 경험 플래그
SIF ASSIPLAY == 0 && EXP:펠라경험 >= EXPLV:3
    TFLAG:50 += 1

;봉사 쾌락 경험 입수 판정
TFLAG:100 |= 1

SIF PLAYER == MASTER
    TCVAR:41 += 3

;조교자가 후타나리
SIF TALENT:PLAYER:121
    SOURCE:22 /= 2

;조교 대상의 파이즈리 실행 회수
CFLAG:56 += 1
;조교자의 파이즈리 능숙 회수
CFLAG:PLAYER:57 += 1

RETURN 1

;--------------------------------------------------
;명령 거부의 메시지
;--------------------------------------------------
@DENIAL_MESSAGE_COM_603
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM603
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
IF TALENT:111
    PRINTL 납작한 가슴으로 페니스를 
ELSEIF TALENT:112
    PRINTL 풍만한 가슴으로 페니스를 감싸 
ELSEIF TALENT:113
    PRINTL 지나치게 풍만한 가슴으로 페니스를 파묻듯이 해 
ELSE
    PRINTL 가슴으로 페니스를 끼워 
ENDIF
PRINTFORMW %CALLNAME:PLAYER%의 혀가 가져오는 쾌감에 농락 당하면서도 열심히 비비고 있다….

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_603, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:욕망
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:욕망
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99})
ENDIF
;ABL:봉사 정신
IF ABL:봉사정신
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:봉사정신
    TFLAG:240 += LOCAL:99 * 4
    RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:정액 중독
IF ABL:정액중독
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:정액중독
    TFLAG:240 += LOCAL:99 * 3
    RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;쾌락 각인
IF MARK:쾌락각인
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = MARK:쾌락각인
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99})
ENDIF

;PALAM:욕정
GETPALAMLV PALAM:13, 5
LOCAL:2 = RESULT
IF LOCAL:2
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = LOCAL:2
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;부끄럼쟁이
IF TALENT:35
    RESULTS = %RESULTS% - 
    LOCAL:99 = 3
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;남자 싫다
IF TALENT:남성혐오 && TALENT:PLAYER:120
    RESULTS = %RESULTS% - 
    LOCAL:99 = 12
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;여자 증오
ELSEIF TALENT:여성혐오 && TALENT:PLAYER:120 == 0
    RESULTS = %RESULTS% - 
    LOCAL:99 = 12
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;악취 둔감
IF TALENT:61
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 1
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:61%({LOCAL:99})
;악취 민감
ELSEIF TALENT:62
    RESULTS = %RESULTS% - 
    LOCAL:99 = 3
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:62%({LOCAL:99})
ENDIF
;헌신적
IF TALENT:63
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 6
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:63%({LOCAL:99})
ENDIF
;쾌감의 부정
IF TALENT:71
    RESULTS = %RESULTS% - 
    LOCAL:99 = 3
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;연모
IF TALENT:150 && ASSIPLAY == 0
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 5
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF
;음유
IF TALENT:174
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 8
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:174%({LOCAL:99})
ENDIF

;조교자가 후타나리
IF TALENT:PLAYER:121
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 8
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %조사처리(CALLNAME:PLAYER,"가")% %TALENTNAME:121%({LOCAL:99})
ENDIF

;조교자의 P의 더러움
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)

;서로 불결
IF TFLAG:242
    RESULTS = %RESULTS% - 
    TFLAG:240 -= TFLAG:242
    RESULTS = %RESULTS% 서로 불결
    ;악취 둔감
    IF TALENT:61
        RESULTS = %RESULTS%, %TALENTNAME:61%
    ELSEIF TALENT:62
        RESULTS = %RESULTS%, %TALENTNAME:62%
    ENDIF
    RESULTS = %RESULTS%({TFLAG:242})
    LOCAL:99 = 1
ENDIF

TFLAG:241 = 34

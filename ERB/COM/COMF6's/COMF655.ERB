;=============================================================================
;거미집 섹스
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
;더미 함수(커맨드를 직접 선택할 수는 없음)
@COM_ABLE655
RETURN 0

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM655
PRINTL 거미집 섹스
COM_NAME = 거미집간
SELECTCOM = 655
CALL TRAIN_MESSAGE_COM655

;-------------------------------------------------
;사정 게이지 체크
;-------------------------------------------------
LOCAL = 0

;ABL:기교를 본다
IF ABL:기교 == 0
    LOCAL = 75
ELSEIF ABL:기교 == 1
    LOCAL = 300
ELSEIF ABL:기교 == 2
    LOCAL = 900
ELSEIF ABL:기교 == 3
    LOCAL = 1800
ELSEIF ABL:기교 == 4
    LOCAL = 2400
ELSE
    LOCAL = 3600
ENDIF

;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;봉사 정신을 본다
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)

;조교자의 V감각을 본다
LOCAL = EJACULATIONCHECK_ABL_1_1(PLAYER, LOCAL)
;조교자의 V경험을 본다
LOCAL = EJACULATIONCHECK_EXP_0_1(PLAYER, LOCAL)

;조교 대상의 C감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(TARGET, LOCAL)

SIF TALENT:120 || TALENT:121
    BASE:사정 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 40
DOWNBASE:기력 += 240

SOURCE:30 = 1500
SOURCE:22 = 1800
SOURCE:31 = 3000

;ABL:순종을 본다
IF ABL:순종 == 0
    TIMES SOURCE:22 , 0.80
    SOURCE:23 = 100
ELSEIF ABL:순종 == 1
    TIMES SOURCE:22 , 0.90
    SOURCE:23 = 200
ELSEIF ABL:순종 == 2
    TIMES SOURCE:22 , 1.00
    SOURCE:23 = 350
ELSEIF ABL:순종 == 3
    TIMES SOURCE:22 , 1.10
    SOURCE:23 = 500
ELSEIF ABL:순종 == 4
    TIMES SOURCE:22 , 1.20
    SOURCE:23 = 800
ELSE
    TIMES SOURCE:22 , 1.30
    SOURCE:23 = 1400
ENDIF

;ABL:C감각을 본다
IF ABL:C감각 == 0
    SOURCE:0 = 800
    SOURCE:15 = 1600
ELSEIF ABL:C감각 == 1
    SOURCE:0 = 1400
    SOURCE:15 = 1900
ELSEIF ABL:C감각 == 2
    SOURCE:0 = 2000
    SOURCE:15 = 2300
ELSEIF ABL:C감각 == 3
    SOURCE:0 = 2500
    SOURCE:15 = 2700
ELSEIF ABL:C감각 == 4
    SOURCE:0 = 2900
    SOURCE:15 = 3100
ELSE
    SOURCE:0 = 3200
    SOURCE:15 = 3500
ENDIF


;ABL:기교를 본다
IF ABL:PLAYER:기교 == 0
    TIMES SOURCE:0 , 0.50
    TIMES SOURCE:15 , 0.50
ELSEIF ABL:PLAYER:기교 == 1
    TIMES SOURCE:0 , 0.80
    TIMES SOURCE:15 , 0.80
ELSEIF ABL:PLAYER:기교 == 2
    TIMES SOURCE:0 , 1.00
    TIMES SOURCE:15 , 1.00
ELSEIF ABL:PLAYER:기교 == 3
    TIMES SOURCE:0 , 1.50
    TIMES SOURCE:15 , 1.50
ELSEIF ABL:PLAYER:기교 == 4
    TIMES SOURCE:0 , 2.50
    TIMES SOURCE:15 , 2.50
ELSE
    TIMES SOURCE:0 , 4.00
    TIMES SOURCE:15 , 4.00
ENDIF

;조교자가 처녀
IF TALENT:PLAYER:0
    PRINTFORML <%조사선택(CALLNAME:PLAYER, "가")% 처녀>
    CALL LOST_VIRGIN, TARGET, PLAYER
    IF TALENT:PLAYER:0 == 1
        TIMES SOURCE:22 , 20.00
        TIMES SOURCE:31 , 3.00
        IF (CFLAG:21 & 2) == 0
            TCVAR:50 += 1
            CFLAG:21 |= 2
        ENDIF
        IF ABL:순종 < 1
            PRINTFORML 그리고 %ABLNAME:10%이 1이 되었다.
            ABL:순종 = 1
        ENDIF
    ELSE
        TIMES SOURCE:22 , 15.00
        TIMES SOURCE:31 , 2.50
    ENDIF
    TFLAG:(4+ASSIPLAY) += TALENT:PLAYER:0
    TALENT:PLAYER:0 = 0
    STAIN:PLAYER:3 |= 32
ENDIF
TCVAR:PLAYER:0 += 1
SIF ASSIPLAY == 0 && EXIST_BAR(TARGET)
    CFLAG:175 += 1

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
;노예의 사정은 TRAIN_CHECK.ERB에서 관리
    ;사정처 타입의 설정
    TFLAG:703 = 9
    ;사정처 선택의 가능·불가
    TFLAG:704 = 0
    ;노예 사정처 선정이 끝난 플래그
    TFLAG:705 = 1


IF TALENT:120 || TALENT:121
    ;노예의 P⇔조교자의 V의 불결이 이동
    STAIN:2 |= STAIN:PLAYER:3
    STAIN:PLAYER:3 |= STAIN:2
ENDIF

SIF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
    TCVAR:12 += 4

;봉사 쾌락 경험 입수 판정
TFLAG:100 |= 1

;처녀 상실의 후에 페니스를 뽑고 있을까
TFLAG:101 = (TFLAG:101 == 2) ?  1 # 0
RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM655
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
;전회와 이번 조교자가 같아, 전회의 조교가 역강간이나 거미집간이며, 뽑지 않는 플래그가 있는 경우
IF TFLAG:102 && (PREVCOM == 25 || PREVCOM == 655) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% 구속되어 몸의 자유가 없는 걸 기회로,
    PRINTFORMW %CALLNAME:TARGET%의 페니스에 올라탄 채로 다시 허리를 흔들기 시작했다….
ELSE
    PRINTFORM %조사선택(CALLNAME:PLAYER, "는")% 
    SIF TALENT:PLAYER:81
        PRINT 가학적인 미소를 띄워, 
    PRINTFORM 끈적한 거미줄로 %CALLNAME:TARGET%의 페니스를 잡고, 
    SIF TALENT:PLAYER:0 == 1
        PRINT 남자를 받아들인 적 없는 
    PRINTFORMW 자신의 보지 깊이 찔러 넣었다….
ENDIF
IF PALAM:17 >= PALAMLV:3
    PRINTL
    PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 어떻게든 거미집에서 탈출하려고 날뛰고 있다….
ENDIF

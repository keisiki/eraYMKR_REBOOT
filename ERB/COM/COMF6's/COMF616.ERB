;=============================================================================
;후배위 가슴애무
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
;더미 함수(커맨드를 직접 선택할 수는 없음)
@COM_ABLE616
RETURN 0

@EXCOM_ABLE616
;Ｖ조교 필터가 오프되어 있지 않으면 불가
SIF FLAG:VA이상조교필터 & 1
    RETURN 0
;V경험レベルが0かつ조교자가 助手でオトコではない場合, 이상계 조교 필터가 오프되어 있지 않으면 불가
SIF FLAG:VA이상조교필터 & 4 && EXP:V경험 < EXPLV:1 && ASSIPLAY && TALENT:PLAYER:남자 == 0
    RETURN 0
;처녀はダメ
SIF TALENT:처녀 == 1
    RETURN 0
;조교자에게 ペニスがない, かつペニスバンドもないとダメ
SIF EXIST_BAR(PLAYER) == 0 && ITEM:2 == 0 && NOITEM == 0
    RETURN 0
;남자라면 안 됨
SIF TALENT:120
    RETURN 0
;처녀で, 순종4미만 · 레즈끼4미만의 ASSI일 경우, 조교 대상이 동성이라면 실행할 수 없다(새드거나 새드끼Lv3이상, 또는 망신이나 복종이 있다면 ＯＫ)
IF TALENT:처녀 == 1 && ASSIPLAY
    SIF (ABL:ASSI:순종 < 4 || (TALENT:120 == 0 && TALENT:ASSI:남자 == 0 && ABL:ASSI:레즈끼 < 4)) && TALENT:ASSI:새드 == 0 && ABL:ASSI:새드끼 < 3 && TALENT:ASSI:망신 == 0 && TALENT:ASSI:복종 == 0
        RETURN 0
ENDIF
;윤활 부족이고, 순종4미만 · 레즈끼3미만의 ASSI일 경우, 조교 대상이 동성이라면 실행할 수 없다(새드거나 새드끼Lv3이상, 또는 망신이나 복종이 있다면 ＯＫ)
IF PALAM:10 < PALAMLV:2 && ASSIPLAY
    SIF (ABL:ASSI:순종 < 4 || (TALENT:120 == 0 && TALENT:ASSI:남자 == 0 && ABL:ASSI:레즈끼 < 3)) && TALENT:ASSI:새드 == 0 && ABL:ASSI:새드끼 < 3 && TALENT:ASSI:망신 == 0 && TALENT:ASSI:복종 == 0
        RETURN 0
ENDIF
;Ｖ계 장착구 사용 중에는 불가
SIF TEQUIP:20 || TEQUIP:21
    RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
    RETURN 0
;욕실 플레이 중에는 플레이 매트가 없으면 불가
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
    RETURN 0
;샤워 중에는 불가
SIF TEQUIP:54
    RETURN 0
;○○목욕탕 입욕 중에는 불가
SIF TEQUIP:55
    RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
    RETURN 0
;조수가 훈도시 착용 중일 경우 기교4이상 필수
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17 && ABL:기교 < 4
    RETURN 0
;훈도시 착용 중일 경우 조교자의 기교4이상 필수
SIF TEQUIP:59 == 17 && ABL:PLAYER:기교 < 4
    RETURN 0
;ウェディングドレス · ゴスロリ着用中の場合기교4이상 필수
SIF (TEQUIP:59 == 19 || TEQUIP:59 == 20) && ABL:PLAYER:기교 < 4
    RETURN 0
;나체정식 중에는 불가
SIF TEQUIP:60
    RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
    RETURN 0
;인어는 안됨
SIF TALENT:츠쿠모가미 == 1
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM616
PRINTL 후배위 가슴 애무
COM_NAME = 후배위 가슴 애무
SELECTCOM = 616
CALL TRAIN_MESSAGE_COM616

;V경험을 수반하는 커맨드의 플래그(처녀막 재생 대응)
TFLAG:1 = 1
SIF TALENT:처녀 && TALENT:연모 && ASSIPLAY == 0
    TFLAG:2 = TALENT:처녀

;-------------------------------------------------
;사정 게이지 체크
;-------------------------------------------------
LOCAL = 0

;ABL:기교를 본다
IF ABL:기교 == 0
    LOCAL = 1500
ELSEIF ABL:기교 == 1
    LOCAL = 1600
ELSEIF ABL:기교 == 2
    LOCAL = 1800
ELSEIF ABL:기교 == 3
    LOCAL = 2000
ELSEIF ABL:기교 == 4
    LOCAL = 2400
ELSE
    LOCAL = 3000
ENDIF

;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;욕망을 본다
LOCAL = EJACULATIONCHECK_ABL_11_1(TARGET, LOCAL)
;윤활을 본다
LOCAL = EJACULATIONCHECK_PALAM_10_1(TARGET, LOCAL)

;조교자의 C감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
    BASE:PLAYER:사정 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 80
DOWNBASE:기력 += 200

SOURCE:15 = 60
SOURCE:30 = 20
SOURCE:12 = 900

;ABL:V감각을 본다
IF ABL:V감각 == 0
    SOURCE:1 = 40
    SOURCE:11 = 50
ELSEIF ABL:V감각 == 1
    SOURCE:1 = 150
    SOURCE:11 = 150
ELSEIF ABL:V감각 == 2
    SOURCE:1 = 400
    SOURCE:11 = 250
ELSEIF ABL:V감각 == 3
    SOURCE:1 = 1000
    SOURCE:11 = 350
ELSEIF ABL:V감각 == 4
    SOURCE:1 = 1700
    SOURCE:11 = 600
ELSE
    SOURCE:1 = 2200
    SOURCE:11 = 850
ENDIF

;ABL:B감각을 본다
IF ABL:B감각 == 0
    SOURCE:3 = 20
    SOURCE:11 += 50
ELSEIF ABL:B감각 == 1
    SOURCE:3 = 100
    SOURCE:11 += 100
ELSEIF ABL:B감각 == 2
    SOURCE:3 = 500
    SOURCE:11 += 160
ELSEIF ABL:B감각 == 3
    SOURCE:3 = 1200
    SOURCE:11 = 200
ELSEIF ABL:B감각 == 4
    SOURCE:3 = 2000
    SOURCE:11 += 230
ELSE
    SOURCE:3 = 2800
    SOURCE:11 += 250
ENDIF

;EXP:V경험을 본다
IF EXP:V경험 < EXPLV:1
    TIMES SOURCE:1 , 0.20
    SOURCE:20 = 5000
    SIF ASSIPLAY && TALENT:PLAYER:남자 == 0
        TCVAR:50 += 1
ELSEIF EXP:V경험 < EXPLV:2
    TIMES SOURCE:1 , 0.60
    SOURCE:20 = 220
ELSEIF EXP:V경험 < EXPLV:3
    TIMES SOURCE:1 , 1.00
    SOURCE:20 = 30
ELSEIF EXP:V경험 < EXPLV:4
    TIMES SOURCE:1 , 1.20
    SOURCE:20 = 5
ELSEIF EXP:V경험 < EXPLV:5
    TIMES SOURCE:1 , 1.30
    SOURCE:20 = 0
ELSE
    TIMES SOURCE:1 , 1.80
    SOURCE:20 = 0
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
    TIMES SOURCE:1 , 0.10
    SOURCE:20 += 900
    TIMES SOURCE:20 , 3.00
ELSEIF PALAM:10 < PALAMLV:2
    TIMES SOURCE:1 , 0.40
    SOURCE:20 += 250
    TIMES SOURCE:20 , 1.00
ELSEIF PALAM:10 < PALAMLV:3
    TIMES SOURCE:1 , 1.00
    TIMES SOURCE:20 , 0.50
ELSEIF PALAM:10 < PALAMLV:4
    TIMES SOURCE:1 , 1.40
    TIMES SOURCE:20 , 0.20
ELSE
    TIMES SOURCE:1 , 1.80
    TIMES SOURCE:20 , 0.10
ENDIF

;[작은체형]
SIF TALENT:128
    TIMES SOURCE:20 , 2.00
;음유
IF TALENT:음유
    TIMES SOURCE:3 , 1.20
    TIMES SOURCE:11 , 1.20
    TIMES SOURCE:30 , 0.80
ENDIF

;조교자가[남자]
SIF TALENT:PLAYER:남자
    TIMES SOURCE:1 , 2.50
;조교자가［유아］,［유치］
IF TALENT:PLAYER:유아 || TALENT:PLAYER:유치
    TIMES SOURCE:3 , 1.20
    TIMES SOURCE:11 , 1.20
ENDIF
;조교자가［유아 퇴행］
SIF TALENT:PLAYER:유아퇴행
    TIMES SOURCE:11 , 1.20

;정조관념
IF TALENT:30
    IF TALENT:처녀 == 1
        TIMES SOURCE:11 , 0.60
        SOURCE:32 = 10000
    ELSE
        TIMES SOURCE:11 , 0.60
        SOURCE:32 = 1000
    ENDIF
;정조무구애
ELSEIF TALENT:31
    IF TALENT:처녀 == 1
        TIMES SOURCE:11 , 0.60
        SOURCE:32 = 300
    ENDIF
ELSE
    SIF TALENT:처녀 == 1
        SOURCE:32 = 3000
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    TIMES SOURCE:1 , 0.60
    TIMES SOURCE:11 , 0.30
ELSEIF PALAM:13 < PALAMLV:2
    TIMES SOURCE:1 , 0.80
    TIMES SOURCE:11 , 0.60
ELSEIF PALAM:13 < PALAMLV:3
    TIMES SOURCE:1 , 1.00
    TIMES SOURCE:11 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
    TIMES SOURCE:1 , 1.20
    TIMES SOURCE:11 , 1.50
ELSE
    TIMES SOURCE:1 , 1.50
    TIMES SOURCE:11 , 1.80
ENDIF

;ABL:순종을 본다
IF ABL:순종 == 0
    TIMES SOURCE:1 , 0.50
    TIMES SOURCE:11 , 0.60
    TIMES SOURCE:32 , 2.00
ELSEIF ABL:순종 == 1
    TIMES SOURCE:1 , 0.80
    TIMES SOURCE:11 , 0.80
    TIMES SOURCE:32 , 1.50
ELSEIF ABL:순종 == 2
    TIMES SOURCE:1 , 1.00
    TIMES SOURCE:11 , 1.00
    TIMES SOURCE:32 , 1.00
ELSEIF ABL:순종 == 3
    TIMES SOURCE:1 , 1.30
    TIMES SOURCE:11 , 1.20
    TIMES SOURCE:32 , 0.80
ELSEIF ABL:순종 == 4
    TIMES SOURCE:1 , 1.60
    TIMES SOURCE:11 , 1.40
    TIMES SOURCE:32 , 0.60
ELSE
    TIMES SOURCE:1 , 2.00
    TIMES SOURCE:11 , 1.60
    TIMES SOURCE:32 , 0.30
ENDIF

TCVAR:0 += 1
IF PLAYER == MASTER
    TCVAR:42 += 1
    SIF EXIST_BAR(MASTER)
        CFLAG:173 += 1
ENDIF

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;사정시의 처리
IF TFLAG:91 >= 1
    ;커맨드 마다 고유의 처리는 이 아래에 쓴다

    ;사정처 타입의 설정
    TFLAG:703 = 1
    ;사정처 선택의 가능·불가
    TFLAG:704 = 1
    ;범용적인 처리는 이 함수로 실시한다
    ;(사정 게이지의 재계산이나 사정 경험의 상승과 공통 부분의 표시)
    CALL SAMEN_SHOOT
ENDIF

;사정에 관계없이 행해지는 처리는 여기로부터
;노예의 V⇔조교자의 P의 불결이 이동
STAIN:보지 |= STAIN:PLAYER:자지
STAIN:PLAYER:자지 |= STAIN:보지
;노예의 B⇔조교자의 손가락의 불결이 이동
STAIN:가슴 |= STAIN:PLAYER:손
STAIN:PLAYER:손 |= STAIN:가슴

SIF TALENT:120 == 0 && TALENT:PLAYER:남자 == 0
    TCVAR:12 += 4

;주인 경험 플래그
SIF ASSIPLAY == 0
    TFLAG:50 += (ABL:V감각 >= 3) ?  2 # 1

SIF TFLAG:101
    TFLAG:101 = 2
;처녀 상실의 후에 페니스를 뽑고 있을까
TFLAG:101 = (TFLAG:101 == 2) ?  1 # 0
RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM616
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
;전회와 이번 조교자가 같아, 전회의 조교가 후배위이며, 뽑지 않는 플래그가 있는 경우
IF TFLAG:102 && (PREVCOM == 21 || PREVCOM == 616) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    ;애태우기플레이
    IF TEQUIP:68
        PRINTFORM %CALLNAME:TARGET%의 
        SIF EX:21
            PRINT 펄떡펄떡 경련하는 
        PRINTW 허리를 누르고 가슴을 만지작 거리며, 삽입한 채로 작게 비볐다….
    ;조교자가 남자나 후타나리이다
    ELSEIF EXIST_PENIS(PLAYER)
        PRINTFORML 뒤로부터 삽입한 채로, %CALLNAME:TARGET%의 가슴에 손을 뻗어,
        PRINTFORMW %CALLNAME:TARGET%의 가슴과 질 내의 감촉을 맛보면서, 다시 허리를 움직이기 시작했다….
    ;페니스 밴드를 사용하고 있는 경우
    ELSE
        PRINTFORMW %CALLNAME:TARGET%의 질에서 뽑지 않고 가슴을 만지작 거리며, 다시 허리를 움직이기 시작했다….
    ENDIF
ELSE
    PRINTFORM 뒤로부터 %CALLNAME:TARGET%의 가슴을 만지작 거리며, 
    IF TALENT:처녀 == 1
        PRINT 아직 남자를 모르는 
    ELSEIF PALAM:10 >= PALAMLV:4
        PRINT 애액으로 미끌거리는 
    ENDIF
    SIF EX:21
        PRINT 안타까운 듯이 열렸다 닫혔다를 반복하는 
    IF TALENT:128
        PRINT 어린 균열
    ELSE
        PRINT 보지
    ENDIF
    IF TEQUIP:68
        PRINTW 에 끄트머리만 살짝 삽입했다….
    ELSE
        PRINTW 의 안쪽 깊이 찔러 넣었다….
    ENDIF
ENDIF

;=============================================================================
;사지 긴박
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
;더미 함수(커맨드를 직접 선택할 수는 없음)
@COM_ABLE622
RETURN 0

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM622
COM_TOOL_NAME = 밧줄
IF FLAG:1100 & 1
    ;技師パッチ有効なら特殊な名前チェック
    FLAG:1110 = 29
    CALL NAMECHECK_CUSTOMTOOL
ELSE
    COM_TOOL_CALLNAME = %COM_TOOL_NAME%
ENDIF
PRINTL 사지 결박
COM_NAME = 사지 결박
SELECTCOM = 622
CALL TRAIN_MESSAGE_COM622

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
;긴박 경험이 높으면 소비 감소
IF EXP:긴박경험 < EXPLV:3
    DOWNBASE:0 += 100
    DOWNBASE:1 += 100
ELSEIF EXP:긴박경험 < EXPLV:4
    DOWNBASE:0 += 80
    DOWNBASE:1 += 80
ELSE
    DOWNBASE:0 += 50
    DOWNBASE:1 += 50
ENDIF

LOCAL:1 = 250
;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    TIMES LOCAL:1 , 0.80
ELSEIF PALAM:13 < PALAMLV:2
    TIMES LOCAL:1 , 0.90
ELSEIF PALAM:13 < PALAMLV:3
    TIMES LOCAL:1 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
    TIMES LOCAL:1 , 1.10
ELSE
    TIMES LOCAL:1 , 1.20
ENDIF

;ABL:순종을 본다
IF ABL:순종 == 0
    TIMES LOCAL:1 , 0.40
ELSEIF ABL:순종 == 1
    TIMES LOCAL:1 , 0.60
ELSEIF ABL:순종 == 2
    TIMES LOCAL:1 , 0.80
ELSEIF ABL:순종 == 3
    TIMES LOCAL:1 , 1.00
ELSEIF ABL:순종 == 4
    TIMES LOCAL:1 , 1.10
ELSE
    TIMES LOCAL:1 , 1.20
ENDIF
;ABL:마조끼를 본다
IF ABL:마조끼 == 0
    TIMES LOCAL:1 , 0.80
ELSEIF ABL:마조끼 == 1
    TIMES LOCAL:1 , 1.00
ELSEIF ABL:마조끼 == 2
    TIMES LOCAL:1 , 1.30
ELSEIF ABL:마조끼 == 3
    TIMES LOCAL:1 , 1.60
ELSEIF ABL:마조끼 == 4
    TIMES LOCAL:1 , 2.00
ELSE
    TIMES LOCAL:1 , 3.00
ENDIF

;도착적
SIF TALENT:80
    TIMES LOCAL:1 , 2.00

SOURCE:20 = 400
SOURCE:12 = 1300
SOURCE:22 = 300
SOURCE:31 = 550 + LOCAL:1
SOURCE:23 = 450 + LOCAL:1

IF FLAG:1100 & 1
    ;技師パッチ有効なら道具使用時ソースチェックを呼ぶ
    ;FLAG 1110:道具番号 1111:改善タイプ 1112:SOURCEの実値 
    FLAG:1110 = 29
    FLAG:1111 = 1
    FLAG:1112 = SOURCE:12
    CALL SOURCECHECK_CUSTOMTOOL
    SOURCE:12 = FLAG:1120
    FLAG:1112 = SOURCE:22
    CALL SOURCECHECK_CUSTOMTOOL
    SOURCE:22 = FLAG:1120
    FLAG:1112 = SOURCE:23
    CALL SOURCECHECK_CUSTOMTOOL
    SOURCE:23 = FLAG:1120
    FLAG:1111 = 2
    FLAG:1112 = SOURCE:20
    CALL SOURCECHECK_CUSTOMTOOL
    SOURCE:20 = FLAG:1120
ENDIF

TCVAR:31 += 3

;줄의 착탈
TEQUIP:40 = 2
;SM교육 경험 플래그
SIF ASSIPLAY
    TFLAG:55 = 2

RETURN 1

;-------------------------------------------------
;사지 긴박중
;-------------------------------------------------
@EQUIP_COM622
PRINTL <사지 결박 중>
;긴박 경험이 높으면 소비 감소
IF EXP:긴박경험 < EXPLV:3
    DOWNBASE:0 += 50
    DOWNBASE:1 += 50
ELSEIF EXP:긴박경험 < EXPLV:4
    DOWNBASE:0 += 40
    DOWNBASE:1 += 40
ELSE
    DOWNBASE:0 += 20
    DOWNBASE:1 += 20
ENDIF

;ABL:마조끼를 본다
IF ABL:마조끼 == 0
    LOCAL = 60
ELSEIF ABL:마조끼 == 1
    LOCAL = 180
ELSEIF ABL:마조끼 == 2
    LOCAL = 300
ELSEIF ABL:마조끼 == 3
    LOCAL = 480
ELSEIF ABL:마조끼 == 4
    LOCAL = 700
ELSE
    LOCAL = 850
ENDIF

;도착적
SIF TALENT:80
    TIMES LOCAL , 2.00

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    TIMES LOCAL , 0.80
ELSEIF PALAM:13 < PALAMLV:2
    TIMES LOCAL , 0.90
ELSEIF PALAM:13 < PALAMLV:3
    TIMES LOCAL , 1.00
ELSEIF PALAM:13 < PALAMLV:4
    TIMES LOCAL , 1.10
ELSE
    TIMES LOCAL , 1.20
ENDIF

LOCAL:1 = LOCAL + 100
LOCAL:2 = LOCAL + 100
LOCAL:3 = 300
LOCAL += 500
IF FLAG:1100 & 1
    ;技師パッチ有効なら道具使用時ソースチェックを呼ぶ
    ;FLAG 1110:道具番号 1111:改善タイプ 1112:SOURCEの実値 
    FLAG:1110 = 29
    FLAG:1111 = 1
    FLAG:1112 = LOCAL
    CALL SOURCECHECK_CUSTOMTOOL
    LOCAL = FLAG:1120
    FLAG:1112 = LOCAL:1
    CALL SOURCECHECK_CUSTOMTOOL
    LOCAL:1 = FLAG:1120
    FLAG:1111 = 2
    FLAG:1112 = LOCAL:3
    CALL SOURCECHECK_CUSTOMTOOL
    LOCAL:3 = FLAG:1120
ENDIF

SOURCE:20 += LOCAL:3
SOURCE:12 += LOCAL
SOURCE:22 += LOCAL:1
SOURCE:31 += LOCAL:2

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
    TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:120
    TCVAR:13 += 1
ENDIF

TCVAR:31 += 2
;촉수 경험 플래그
SIF TEQUIP:90
    TFLAG:90 += 1

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM622
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
PRINTFORM %조사선택(CALLNAME:PLAYER, "는")% 
;도착적이어 공포 각인이 1 이하로 반발 각인이 없을 때
IF TALENT:80 && MARK:공포각인 < 2 && MARK:반발각인 < 1
    PRINTFORML %CALLNAME:TARGET%의 사지를 %조사선택(COM_TOOL_CALLNAME, "로")% 구속했다.
    PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 밧줄의 감촉을 전신으로 느끼며, 쾌감을 마음껏 맛보고 있는 것 같다….
;반항적인가 저항을 가져 함락 하지 않는 경우
ELSEIF (TALENT:반항적 || TALENT:34) && TALENT:150 == 0 && TALENT:160 == 0 && TALENT:170 == 0
    PRINTFORMW 격렬하게 거부하는 %CALLNAME:TARGET%의 사지를 억지로 %조사선택(COM_TOOL_CALLNAME, "로")% 구속했다….
;겁쟁이 소유나 공포 각인 2이상으로 기장과 감정결여를 가지지 않고, 함락 하고 있지 않는 경우
ELSEIF (TALENT:겁쟁이 || MARK:공포각인 >= 2) && TALENT:꿋꿋함 == 0 && TALENT:감정결여 == 0 && TALENT:150 == 0 && TALENT:160 == 0 && TALENT:170 == 0
    PRINTFORMW 공포에 질린 %CALLNAME:TARGET%의 사지를 %조사선택(COM_TOOL_CALLNAME, "로")% 정중하게 구속했다….
;프라이드 높은가 신령해 게다가, 굴복 각인과 치욕 각인이 1 이하로, 함락 하고 있지 않는 경우
ELSEIF (TALENT:프라이드가높음 || TALENT:222) && MARK:굴복각인 < 2 && MARK:치욕각인 < 2 && TALENT:150 == 0 && TALENT:160 == 0 && TALENT:170 == 0
    PRINTFORML %CALLNAME:TARGET%의 사지를 %조사선택(COM_TOOL_CALLNAME, "로")% 구속했다.
    PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 구속되면서도, 그 프라이드만은 꺾이지 않는 것 처럼 보였다….
;해방이나 쾌감에 솔직한가 봄의 사람으로 아픔에 약하지만 않고, 반발 각인이 없는 경우
ELSEIF (TALENT:해방 || TALENT:70 || TALENT:263) && TALENT:아픔에약함 == 0 && MARK:반발각인 <= 0
    PRINTFORMW 어쩐지 즐기는 듯한 %CALLNAME:TARGET%의 사지를 %조사선택(COM_TOOL_CALLNAME, "로")% 정중하게 구속했다….
ELSE
    PRINTFORMW %CALLNAME:TARGET%의 사지를 %조사선택(COM_TOOL_CALLNAME, "로")% 구속했다….
ENDIF

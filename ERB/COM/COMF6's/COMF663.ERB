;=============================================================================
;꼬리 자위
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE663
;개별 설정 커멘드 필터
SIF DITEMTYPE:999:663 > 0
    RETURN 0
;동물귀 이외 불가
SIF TALENT:동물귀 == 0
    RETURN 0
;순종 4 미만·레즈비언기분(BL기분) 4 미만의 ASSI의 경우, 조교 대상이 동성이라면 실행할 수 없다(소악마인가 망신이나 복종이라면 OK)
IF ASSIPLAY
    SIF (ABL:ASSI:순종 < 4 || ((TALENT:남자 == 0 && TALENT:ASSI:남자 == 0 && ABL:ASSI:레즈끼 < 4) || (TALENT:남자 && TALENT:ASSI:남자 && ABL:ASSI:BL끼 < 4))) && TALENT:ASSI:소악마 == 0 && TALENT:ASSI:망신 == 0 && TALENT:ASSI:복종 == 0
        RETURN 0
ENDIF
;공개 자위를 실시했던 적이 없는 경우의 비디오 촬영중은, 이상계 조교 필터가 오프가 되지 않으면 안 됨
SIF FLAG:VA이상조교필터 & 4 && (CFLAG:첫특수커맨드 & 1) == 0 && TEQUIP:50
    RETURN 0
;수면중은 불가
SIF EQUIP:14
    RETURN 0
;실신중은 불가
SIF TFLAG:899 > 0
    RETURN 0
;쿠스코, 이외의 V계 장착도구, 사용중은 불가
SIF TEQUIP:20 >= 1 || TEQUIP:26 == 1
    RETURN 0
;줄사용중은 불가
SIF TEQUIP:40
    RETURN 0
;승마중은 안 됨
SIF TEQUIP:43
    RETURN 0
;옴트프레이중은 안 됨
SIF TEQUIP:58
    RETURN 0
;곤착용중의 경우 기교 4이상 필수
SIF TEQUIP:59 == 17 && ABL:기교 < 4
    RETURN 0
;목욕탕 플레이중은 플레이 매트가 없으면 안 됨
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
    RETURN 0
;샤워중은 안 됨
SIF TEQUIP:54
    RETURN 0
;00목욕탕 입욕중은 안 됨
SIF TEQUIP:55
    RETURN 0
;여체 번화가안은 안 됨
SIF TEQUIP:60
    RETURN 0
;때 멈춤중은 무리
SIF TEQUIP:71
    RETURN 0
;촉수 조교중은 안 됨
SIF TEQUIP:90
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM663
PRINTL 꼬리 자위
COM_NAME = 꼬리 자위
;-------------------------------------------------
;실행할 수 있을까의 판정
;-------------------------------------------------
IF TFLAG:699 == 0
    CALL COM_ORDER
    ;실행할 수 없다
    SIF RESULT == 0
        RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------
;COM_NAME = %LOCALS%
CALL TRAIN_MESSAGE_COM663

DOWNBASE:0 += 50
DOWNBASE:기력 += 150

SOURCE:31 = 400
LOCAL:1 = 0
LOCAL:2 = 0
LOCAL:3 = 0

IF TEQUIP:50
    SOURCE:18 = 110
    SOURCE:19 = 130
ENDIF

;ABL:V감각을 본다
IF ABL:V감각 == 0
    SOURCE:1 = 7
    SOURCE:12 = 2000
    SOURCE:22 = 600
ELSEIF ABL:V감각 == 1
    SOURCE:1 = 25
    SOURCE:12 = 2300
    SOURCE:22 = 1000
ELSEIF ABL:V감각 == 2
    SOURCE:1 = 150
    SOURCE:12 = 2600
    SOURCE:22 = 1500
ELSEIF ABL:V감각 == 3
    SOURCE:1 = 350
    SOURCE:12 = 2900
    SOURCE:22 = 2000
ELSEIF ABL:V감각 == 4
    SOURCE:1 = 550
    SOURCE:12 = 3200
    SOURCE:22 = 2800
ELSE
    SOURCE:1 = 500
    SOURCE:12 = 3500
    SOURCE:22 = 3300
ENDIF

;EXP:V경험을 본다
IF EXP:V경험 < EXPLV:1
    TIMES SOURCE:1 , 0.50
    LOCAL:3 += 500
ELSEIF EXP:V경험 < EXPLV:2
    TIMES SOURCE:1, 1.00
    LOCAL:3 += 75
ELSEIF EXP:V경험 < EXPLV:3
    TIMES SOURCE:1 , 1.10
    LOCAL:3 += 10
ELSEIF EXP:V경험 < EXPLV:4
    TIMES SOURCE:1 , 1.20
    LOCAL:3 += 0
ELSEIF EXP:V경험 < EXPLV:5
    TIMES SOURCE:1 , 1.40
    LOCAL:3 += 0
ELSE
    TIMES SOURCE:1 , 1.60
    LOCAL:3 += 0
ENDIF

;애널 바이브래이터 삽입중을 확인·계산
IF TEQUIP:25
    DOWNBASE:0 += 30
    DOWNBASE:기력 += 80

    ;ABL:A감각을 본다
    IF ABL:A감각 == 0
        LOCAL:2 += 40
        LOCAL:1 += 150
    ELSEIF ABL:A감각 == 1
        LOCAL:2 += 120
        LOCAL:1 += 400
    ELSEIF ABL:A감각 == 2
        LOCAL:2 += 300
        LOCAL:1 += 700
    ELSEIF ABL:A감각 == 3
        LOCAL:2 += 500
        LOCAL:1 += 900
    ELSEIF ABL:A감각 == 4
        LOCAL:2 += 650
        LOCAL:1 += 1000
    ELSE
        LOCAL:2 += 850
        LOCAL:1 += 1200
    ENDIF

    ;EXP:A경험을 본다
    IF EXP:A경험 < EXPLV:1
        TIMES LOCAL:2 , 0.50
        LOCAL:3 += 500
    ELSEIF EXP:A경험 < EXPLV:2
        TIMES LOCAL:2 , 1.00
        LOCAL:3 += 75
    ELSEIF EXP:A경험 < EXPLV:3
        TIMES LOCAL:2 , 1.10
        LOCAL:3 += 10
    ELSEIF EXP:A경험 < EXPLV:4
        TIMES LOCAL:2 , 1.20
        LOCAL:3 += 0
    ELSEIF EXP:A경험 < EXPLV:5
        TIMES LOCAL:2 , 1.40
        LOCAL:3 += 0
    ELSE
        TIMES LOCAL:2 , 1.60
        LOCAL:3 += 0
    ENDIF
    ;PALAM:윤활을 본다
    IF PALAM:10 < PALAMLV:1
        TIMES LOCAL:2 , 0.40
        TIMES SOURCE:1 , 0.40
        LOCAL:3 += 800
    ELSEIF PALAM:10 < PALAMLV:2
        TIMES LOCAL:2 , 0.80
        TIMES SOURCE:1 , 0.80
        LOCAL:3 += 500
    ELSEIF PALAM:10 < PALAMLV:3
        TIMES LOCAL:2 , 1.00
        TIMES SOURCE:1 , 1.00
        LOCAL:3 += 300
    ELSEIF PALAM:10 < PALAMLV:4
        TIMES LOCAL:2 , 1.40
        TIMES SOURCE:1 , 1.40
        LOCAL:3 += 120
    ELSE
        TIMES LOCAL:2 , 1.80
        TIMES SOURCE:1 , 1.80
        LOCAL:3 += 100
    ENDIF

    ;PALAM:욕정을 본다
    IF PALAM:13 < PALAMLV:1
        TIMES LOCAL:2 , 0.80
        TIMES SOURCE:1 , 0.80
    ELSEIF PALAM:13 < PALAMLV:2
        TIMES LOCAL:2 , 0.90
        TIMES SOURCE:1 , 0.90
    ELSEIF PALAM:13 < PALAMLV:3
        TIMES LOCAL:2 , 1.00
        TIMES SOURCE:1 , 1.00
    ELSEIF PALAM:13 < PALAMLV:4
        TIMES LOCAL:2 , 1.10
        TIMES SOURCE:1 , 1.10
    ELSE
        TIMES LOCAL:2 , 1.20
        TIMES SOURCE:1 , 1.20
    ENDIF

    ;ABL:순종을 본다
    IF ABL:순종 == 0
        TIMES LOCAL:1 , 0.80
        TIMES LOCAL:2 , 0.80
        TIMES SOURCE:1 , 0.80
    ELSEIF ABL:순종 == 1
        TIMES LOCAL:1 , 0.90
        TIMES LOCAL:2 , 0.90
        TIMES SOURCE:1 , 0.90
    ELSEIF ABL:순종 == 2
        TIMES LOCAL:1 , 1.00
        TIMES LOCAL:2 , 1.00
        TIMES SOURCE:1 , 1.00
    ELSEIF ABL:순종 == 3
        TIMES LOCAL:1 , 1.10
        TIMES LOCAL:2 , 1.10
        TIMES SOURCE:1 , 1.10
    ELSEIF ABL:순종 == 4
        TIMES LOCAL:1 , 1.20
        TIMES LOCAL:2 , 1.20
        TIMES SOURCE:1 , 1.20
    ELSE
        TIMES LOCAL:1 , 1.30
        TIMES LOCAL:2 , 1.30
        TIMES SOURCE:1 , 1.30
    ENDIF

    ;정조관념
    SIF TALENT:정조관념
        TIMES LOCAL:3, 3.00
    ;[작은체형]
    SIF TALENT:작은체형
        TIMES LOCAL:3 , 2.00
ENDIF

;A민감, 둔감을 본다
IF TALENT:A둔감
    TIMES LOCAL:3 , 1.50
    TIMES LOCAL:1 , 1.50
ELSEIF TALENT:A민감
    TIMES LOCAL:3 , 0.60
    TIMES LOCAL:1 , 0.60
ENDIF

;V민감, 둔감을 본다
IF TALENT:V둔감
    TIMES LOCAL:3 , 1.50
    TIMES LOCAL:1 , 1.50
ELSEIF TALENT:V민감
    TIMES LOCAL:3 , 0.60
    TIMES LOCAL:1 , 0.60
ENDIF

;한 번 단독으로 계산
SOURCE:22 += LOCAL:1

SOURCE:2 += LOCAL:2
SOURCE:20 += LOCAL:3

;ABL:기교를 본다
IF ABL:기교 == 0
    SOURCE:15 = 100
    TIMES SOURCE:2 , 0.30
ELSEIF ABL:기교 == 1
    SOURCE:15 = 160
    TIMES SOURCE:2 , 0.70
ELSEIF ABL:기교 == 2
    SOURCE:15 = 220
    TIMES SOURCE:2 , 1.00
ELSEIF ABL:기교 == 3
    SOURCE:15 = 280
    TIMES SOURCE:2 , 1.20
ELSEIF ABL:기교 == 4
    SOURCE:15 = 340
    TIMES SOURCE:2 , 1.40
ELSE
    SOURCE:15 = 400
    TIMES SOURCE:2 , 1.60
ENDIF

;ABL:자위 중독을 본다
IF ABL:자위중독 == 0
    SOURCE:24 = 0
    TIMES SOURCE:2 , 1.00
ELSEIF ABL:자위중독 == 1
    SOURCE:24 = 150
    TIMES SOURCE:2 , 1.10
ELSEIF ABL:자위중독 == 2
    SOURCE:24 = 330
    TIMES SOURCE:2 , 1.20
ELSEIF ABL:자위중독 == 3
    SOURCE:24 = 850
    TIMES SOURCE:2 , 1.30
ELSEIF ABL:자위중독 == 4
    SOURCE:24 = 1600
    TIMES SOURCE:2 , 1.50
ELSE
    SOURCE:24 = 2700
    TIMES SOURCE:2 , 1.70
ENDIF

;공개 때는, ABL:노출벽을 본다
IF TEQUIP:50
    IF ABL:노출증 == 0
        SOURCE:24 += 0
        TIMES SOURCE:1 , 1.00
        TIMES SOURCE:12 , 1.00
    ELSEIF ABL:노출증 == 1
        SOURCE:24 += 100
        TIMES SOURCE:1 , 1.10
        TIMES SOURCE:12 , 1.20
    ELSEIF ABL:노출증 == 2
        SOURCE:24 += 300
        TIMES SOURCE:1 , 1.20
        TIMES SOURCE:12 , 1.40
    ELSEIF ABL:노출증 == 3
        SOURCE:24 += 800
        TIMES SOURCE:1 , 1.30
        TIMES SOURCE:12 , 1.60
    ELSEIF ABL:노출증 == 4
        SOURCE:24 += 1500
        TIMES SOURCE:1 , 1.50
        TIMES SOURCE:12 , 2.00
    ELSE
        SOURCE:24 += 2500
        TIMES SOURCE:1 , 1.70
        TIMES SOURCE:12 , 3.00
    ENDIF
ENDIF

;비디오 촬영시는 경험에 플러스
IF TEQUIP:50
    TCVAR:1 += 1
    TCVAR:10 += 2
    TCVAR:11 += 2
ELSE
    TCVAR:1 += 1
    TCVAR:10 += 1
    TCVAR:11 += 1
ENDIF
IF TEQUIP:50 && (CFLAG:첫특수커맨드 & 1) == 0
    TCVAR:50 += 1
    CFLAG:첫특수커맨드 |= 1
ENDIF


;촬영시의 조교 내용 체크
SIF TEQUIP:25
    TFLAG:122 = 992

IF TALENT:남자 == 0 && TALENT:PLAYER:남자 == 0
    TCVAR:12 += 3
ELSEIF TALENT:남자 && TALENT:PLAYER:남자
    TCVAR:13 += 3
ENDIF

;노출 쾌락 경험 입수 판정
TFLAG:100 |= 2

RETURN 1

;--------------------------------------------------
;명령 거부의 메시지
;--------------------------------------------------
@DENIAL_MESSAGE_COM_663
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
IF TEQUIP:68
    SIF PALAM:0 >= (PALAMLV:4 - 1) ||  PALAM:1 >= (PALAMLV:4 - 1) ||  PALAM:2 >= (PALAMLV:4 - 1) ||  PALAM:3 >= (PALAMLV:4 - 1)
        PRINT 당장이라도 갈 것 같지만 
    PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 명령을 거부했다….
ELSE
    PRINTFORMW %CALLNAME:TARGET%에게 자위를 명령했지만 거부했다. 좀 더 조교가 필요한 것 같다.
ENDIF

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM663
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
;애태우기플레이중에 자위계·기승위계·조수를 범하게 한다
IF TEQUIP:68
    SIF PALAM:0 >= (PALAMLV:4 - 1) ||  PALAM:1 >= (PALAMLV:4 - 1) ||  PALAM:2 >= (PALAMLV:4 - 1) ||  PALAM:3 >= (PALAMLV:4 - 1)
        PRINT 간절히 원하는 것 같아 보여, 
    PRINTFORMW %조사선택(CALLNAME:TARGET, "가")% 좋아하게 하도록 했다.
ENDIF
IF TFLAG:699 > 0
    PRINTFORML 꼬리로 자위하라고 명령하자 %조사선택(CALLNAME:TARGET, "는")% 
    SIF TALENT:음란 || TEQUIP:11 == 1
        PRINT 더 이상 참을 수 없다는 표정으로 
    SIF TALENT:새드
        PRINTFORM 애원하는 시선을 %CALLNAME:MASTER%에게 보내면서 
    SIF TALENT:질투
        PRINT 질투에 미쳐 
    SIF TALENT:광기 && TEQUIP:42 == 0
        PRINT 망가진 듯이 웃어대며, 
    PRINTW 꼬리를 사용한 쾌감에 잠겨 있다….
ELSE
    IF EX:22
        PRINTFORM 내버려뒀던 %CALLNAME:TARGET%에게 자위를 허락하자 
    ELSEIF TFLAG:240 < 50
        PRINTFORM 자위를 명령하자 %조사선택(CALLNAME:TARGET, "는")% 
        SIF TALENT:프라이드가높음
            PRINT 자신의 높은 프라이드를 보이며 
        SIF TALENT:반항적
            PRINT 반항적인 태도로 
        PRINT 노려봤지만 다시 한 번 명령하자 주저하며 
    ELSEIF TFLAG:240 < 80
        PRINTFORML 조교의 성과가 나왔는지, %조사선택(CALLNAME:TARGET, "는")% 명령에 따라,
    ELSE
        PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 자위 명령에도 전혀 싫어하는 기색을 보이지 않고, 완전히 굴복한 표정으로
    ENDIF
    SIF TEQUIP:50
        PRINT 비디오 카메라 앞으로 가서 
    SIF ABL:노출증 > 3
        PRINT 남들에게 보여주듯이 
    IF TEQUIP:21
        PRINT 보지를 쿠스코로 열어둔 채 꼬리를 보지에 찔러 넣으며 
    ELSEIF TEQUIP:25 == 1
        PRINT 애널 딜도와 꼬리로 장내를 압박하며 
    ELSEIF TEQUIP:25 == 5
        PRINT 애널 비즈와 꼬리로 장내를 압박하며 
    ELSE
        PRINT 보지를 스스로의 꼬리로 만지작거리며 
    ENDIF
    PRINTL 쾌감을 즐기고 있다….
    IF TEQUIP:20 == 1 && TEQUIP:25 == 1 && ABL:C감각 >= 5 && ABL:V감각 >= 5 && ABL:A감각 >= 5 && ABL:B감각 >= 5 && PALAM:13 >= PALAMLV:4 && PREVCOM == 182
        PRINTL
        PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 침을 흘리며, 덜덜 몸을 떨\@(TEQUIP:42) ? 면서 울부짖 # \@고 있다.
        PRINTW 아무래도 절정이 멈추지 않는 듯, 계속해서 가고 있는 것 같다….
    ELSEIF TEQUIP:25 == 1 && ABL:C감각 >= 5 && ABL:A감각 >= 5 && ABL:B감각 >= 5 && PALAM:13 >= PALAMLV:4 && PREVCOM == 182
        PRINTL
        PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 덜덜 몸을 떨면서, 항문에 바이브를 찌르고 있다.
        PRINTW 아무래도 벽을 비벼댈 때 마다 몇 번이나 가버리고 있는 것 같다….
    ELSEIF TEQUIP:25 == 5 && ABL:C감각 >= 5 && ABL:A감각 >= 5 && ABL:B감각 >= 5 && PALAM:13 >= PALAMLV:4 && PREVCOM == 182
        PRINTL
        PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 부들부들 몸을 떨며 애널 비즈를 찔러 넣고 있다.
        PRINTW 아무래도 구슬이 빠져 나갈 때마다 몇 번이나 절정 해 버리고 있는 것 같다….
    ELSEIF ABL:C감각 >= 5 && ABL:B감각 >= 5 && ABL:자위중독 >= 3 && PALAM:13 >= PALAMLV:4 && PREVCOM == 182
        PRINTL
        PRINTFORML %조사선택(CALLNAME:TARGET, "는")% \@(TEQUIP:42) ? 스스로의 # 교성을 지르면서 스스로의 \@ 보지를 꼬리로 자극하고 있다.
        PRINTW 아무래도 멈추고 싶어도 꼬리가 제 멋대로 움직이는 것 같다….
    ENDIF
ENDIF

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_663, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:욕망
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:욕망
    TFLAG:240 += LOCAL:99 * 3
    RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF
;ABL:노출벽
IF ABL:노출증
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:노출증
    TFLAG:240 += LOCAL:99 * 4
    RESULTS = %RESULTS% %ABLNAME:14%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:자위 중독
IF ABL:자위중독
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:자위중독
    TFLAG:240 += LOCAL:99 * 3
    RESULTS = %RESULTS% %ABLNAME:30%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;쾌락 각인
IF MARK:쾌락각인
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = MARK:쾌락각인
    TFLAG:240 += LOCAL:99 * 3
    RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;PALAM:욕정
GETPALAMLV PALAM:13, 5
LOCAL:2 = RESULT
IF LOCAL:2
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = LOCAL:2 * 3
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;자제심
IF TALENT:자제심
    RESULTS = %RESULTS% - 
    LOCAL:99 = 5
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:20%({LOCAL:99})
ENDIF
;부끄럼쟁이
IF TALENT:부끄럼쟁이
    RESULTS = %RESULTS% - 
    LOCAL:99 = 5
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
;부끄럼없음
ELSEIF TALENT:부끄럼없음
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 2
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:36%({LOCAL:99})
ENDIF
;자위하기 쉽다
IF TALENT:자위하기쉬움
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 5
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:60%({LOCAL:99})
ENDIF
;쾌감에 솔직
IF TALENT:쾌감에솔직
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 5
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:70%({LOCAL:99})
;쾌감의 부정
ELSEIF TALENT:쾌감을부정
    RESULTS = %RESULTS% - 
    LOCAL:99 = 5
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF

;미약
IF TEQUIP:11
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 8
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %ITEMNAME:61%({LOCAL:99})
ENDIF

;난이도 상승
;공개로+10, 애널 바이브래이터로―5, 비즈로―5
TFLAG:241 = 48
SIF TEQUIP:50
    TFLAG:241 += 10
SIF TEQUIP:25 == 1
    TFLAG:241 -= 5
SIF TEQUIP:25 == 5
    TFLAG:241 -= 5

;=============================================================================
;촉수 강제 회복
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE560
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:560 > 0
    RETURN 0
;신사적인 촉수계 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 4096) == 0
    RETURN 0
;아이템進化の秘法を持っているか, 아이템なし設定でないとダメ
SIF ITEM:59 == 0 && NOITEM == 0
    RETURN 0
;촉수調教中じゃないとはダメ
SIF TEQUIP:90 == 0
    RETURN 0
;조교자는 촉수사역LV5이상 필요
SIF ABL:PLAYER:촉수사역 < 5
    RETURN 0
;촉수가 [養分注入촉수]を得ていないとダメ
SIF (FLAG:75 & 32) == 0
    RETURN 0
;해제는 언제든 가능
SIF TEQUIP:91
    RETURN 1
;조교자의 기력または체력이 1000이하だとダメ
SIF BASE:PLAYER:체력 < 1000 || BASE:PLAYER:기력 < 1000
    RETURN 0
;奴隷の체력이 最大だとダメ
SIF BASE:0 >= MAXBASE:0
    RETURN 0
;기교가 4이상 없으면 안 됨
SIF ABL:PLAYER:기교 < 4
    RETURN 0
;ボールギャグ使用中及び촉수貫通中は촉수가 [針状촉수]을 갖고 있지 않으면 불가
IF TEQUIP:25 == 3 || TEQUIP:42
    SIF (FLAG:75 & 2) == 0
        RETURN 0
ENDIF
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM560
;말용으로 TFLAG:201을 사용. (0=사용한 시점에서 촉수 관통, 구욕은 아닌, 1=사용한 시점에서 촉수 관통중, 2=사용한 시점에서 촉수구욕중, 3=사용한 시점에서 촉수 관통중 또한 촉수구욕중)
PRINTL 촉수 강제 회복
COM_NAME = 촉수 강제 회복

;말용
TFLAG:201 = 0
IF TEQUIP:25 == 3 && TEQUIP:42 == 2
    ;촉수 관통＋촉수구욕
    TFLAG:201 = 3
ELSEIF TEQUIP:25 == 3
    ;촉수 관통만
    TFLAG:201 = 1
ELSEIF TEQUIP:42 == 2
    ;촉수구욕 마셔
    TFLAG:201 = 2
ENDIF
CALL TRAIN_MESSAGE_COM560

;촉수 강제 회복 해제
IF TEQUIP:91
    TEQUIP:91 = 0
    ;촉수 경험 플래그
    TFLAG:90 = 0
    RETURN 1
ENDIF

;촉수 강제 회복
IF TEQUIP:25 == 3 || TEQUIP:42 == 1
    TEQUIP:91 = 2
ELSEIF TEQUIP:42 == 2
    TEQUIP:42 = 0
    TEQUIP:91 = 1
ELSE
    TEQUIP:91 = 1
ENDIF

LOCAL:1 = 100
LOCAL:2 = 500

;EXP:촉수 경험을 본다
IF EXP:촉수경험 < EXPLV:1
    TIMES LOCAL:1 , 0.80
    TIMES LOCAL:2 , 3.00
    SOURCE:25 += 2000
ELSEIF EXP:촉수경험 < EXPLV:2
    TIMES LOCAL:2 , 2.50
    SOURCE:25 += 1000
ELSEIF EXP:촉수경험 < EXPLV:3
    TIMES LOCAL:1 , 1.10
    TIMES LOCAL:2 , 2.00
    SOURCE:25 += 100
ELSEIF EXP:촉수경험 < EXPLV:4
    TIMES LOCAL:1 , 1.30
    TIMES LOCAL:2 , 1.50
ELSEIF EXP:촉수경험 < EXPLV:5
    TIMES LOCAL:1 , 1.50
    TIMES LOCAL:2 , 1.00
ELSE
    TIMES LOCAL:1 , 1.80
    TIMES LOCAL:2 , 0.80
ENDIF

;ABL:촉수 중독을 본다
IF ABL:촉수중독 == 0
    LOCAL:3 = 0
    TIMES LOCAL:2 , 1.50
ELSEIF ABL:촉수중독 == 1
    LOCAL:3 = 5
    TIMES LOCAL:2 , 1.20
ELSEIF ABL:촉수중독 == 2
    LOCAL:3 = 20
    TIMES LOCAL:2 , 0.90
ELSEIF ABL:촉수중독 == 3
    LOCAL:3 = 80
    TIMES LOCAL:2 , 0.60
ELSEIF ABL:촉수중독 == 4
    LOCAL:3 = 300
    TIMES LOCAL:2 , 0.30
ELSE
    LOCAL:3 = 1000
    TIMES LOCAL:2 , 0.10
ENDIF

;약에 들어 있는 독 내성
SIF TALENT:44
    TIMES LOCAL:1 , 0.40
;중독 하기 쉽다
SIF TALENT:84
    TIMES LOCAL:1 , 1.10
;회복 빠르다
IF TALENT:137
    TIMES LOCAL:1 , 1.30
;회복 늦다
ELSEIF TALENT:138
    TIMES LOCAL:1 , 0.75
ENDIF
;기계
SIF TALENT:206
    TIMES LOCAL:1 , 0.20

LOCAL:4 = LOCAL:1 /4
;조교자가 [작은체형]·[소인]이라면 소모가 크다
SIF TALENT:127 || TALENT:128
    TIMES LOCAL:4 , 1.20
SIF LOCAL:4 >= BASE:PLAYER:체력
    LOCAL:4 = BASE:PLAYER:체력 - 1
;조교자의 체력을 양식으로 하고 있다
BASE:PLAYER:체력 -= LOCAL:4
PRINTFORML %조사선택(CALLNAME:TARGET, "를")% 회복하기 위해 %조사선택(CALLNAME:PLAYER, "는")% 자신의 체력을 희생했다.
SIF BASE:PLAYER:체력 < 500
    CALL TRAIN_EVENT_COM560

;기생(영양이 일부 놓치고 있으므로)
SIF TALENT:146
    TIMES LOCAL:1 , 0.70

LOCAL:4 = LOCAL:1
SIF LOCAL:4 + BASE:0 > MAXBASE:0
    LOCAL:4 = MAXBASE:0 - BASE:0
BASE:0 = MIN(BASE:0 + LOCAL:4, MAXBASE:0)
PRINTFORML 체력+{LOCAL:4} 
LOCAL:4 = LOCAL:1 /2
SIF LOCAL:4 + BASE:기력 > MAXBASE:기력
    LOCAL:4 = MAXBASE:기력 - BASE:기력
BASE:기력 = MIN(BASE:기력 + LOCAL:4, MAXBASE:기력)
PRINTFORML 기력+{LOCAL:4} 

SOURCE:25 += LOCAL:2 * 2
SOURCE:24 += LOCAL:3
SOURCE:30 += LOCAL:1 + LOCAL:2
SOURCE:31 += LOCAL:2 * 2
SOURCE:32 += LOCAL:2
SOURCE:23 += (LOCAL:2 + LOCAL:3) / 2

IF TEQUIP:25 == 3 || TEQUIP:42 == 1
    STAIN:1 |= 2
    STAIN:1 |= 64
ELSE
    STAIN:0 |= 2
    STAIN:0 |= 64
ENDIF

;커맨드 속성：촉수
TFLAG:98 = 8
;촉수 경험 플래그
TFLAG:90 = 1
RETURN 1

;--------------------------------------------------
;촉수 강제 회복중
;--------------------------------------------------
@EQUIP_COM560
PRINTL <촉수 강제 회복 중>
SIF SELECTCOM != 560
    CALL EQUIP_MESSAGE_COM560

LOCAL = 250
LOCAL:1 = 300

;EXP:촉수 경험을 본다
IF EXP:촉수경험 < EXPLV:1
    TIMES LOCAL , 0.90
    TIMES LOCAL:1 , 3.00
ELSEIF EXP:촉수경험 < EXPLV:2
    TIMES LOCAL , 1.00
    TIMES LOCAL:1 , 2.50
ELSEIF EXP:촉수경험 < EXPLV:3
    TIMES LOCAL , 1.10
    TIMES LOCAL:1 , 2.00
ELSEIF EXP:촉수경험 < EXPLV:4
    TIMES LOCAL , 1.30
    TIMES LOCAL:1 , 1.50
ELSEIF EXP:촉수경험 < EXPLV:5
    TIMES LOCAL , 1.50
    TIMES LOCAL:1 , 1.00
ELSE
    TIMES LOCAL:1 , 1.80
    TIMES LOCAL:1 , 0.80
ENDIF

;ABL:촉수 중독을 본다
IF ABL:촉수중독 == 0
    LOCAL:2 = 0
    TIMES LOCAL:1 , 1.50
ELSEIF ABL:촉수중독 == 1
    LOCAL:2 = 5
    TIMES LOCAL:1 , 1.20
ELSEIF ABL:촉수중독 == 2
    LOCAL:2 = 20
    TIMES LOCAL:1 , 0.90
ELSEIF ABL:촉수중독 == 3
    LOCAL:2 = 80
    TIMES LOCAL:1 , 0.60
ELSEIF ABL:촉수중독 == 4
    LOCAL:2 = 300
    TIMES LOCAL:1 , 0.30
ELSE
    LOCAL:2 = 1000
    TIMES LOCAL:1 , 0.10
ENDIF

;약에 들어 있는 독 내성
SIF TALENT:44
    TIMES LOCAL , 0.40
;중독 하기 쉽다
SIF TALENT:84
    TIMES LOCAL , 1.10
;회복 빠르다
IF TALENT:137
    TIMES LOCAL , 1.30
;회복 늦다
ELSEIF TALENT:138
    TIMES LOCAL , 0.75
ENDIF
;기계
SIF TALENT:206
    TIMES LOCAL , 0.20

;기생(영양이 일부 놓치고 있으므로)
SIF TALENT:146
    TIMES LOCAL , 0.70

LOCAL:3 = LOCAL
SIF LOCAL:3 + BASE:0 > MAXBASE:0
    LOCAL:3 = MAXBASE:0 - BASE:0
BASE:0 = MIN(BASE:0 + LOCAL:3, MAXBASE:0)
PRINTFORML 체력+{LOCAL:3} 
LOCAL:3 = LOCAL/2
SIF LOCAL:3 + BASE:기력 > MAXBASE:기력
    LOCAL:3 = MAXBASE:기력 - BASE:기력
BASE:기력 = MIN(BASE:기력 + LOCAL:3, MAXBASE:기력)
PRINTFORML 기력+{LOCAL:3} 

SOURCE:25 += LOCAL:1 * 2
SOURCE:23 += (LOCAL:1 + LOCAL:2) / 4
SOURCE:24 += LOCAL:2
SOURCE:26 += LOCAL:2 / 5
SOURCE:30 += LOCAL + LOCAL:1
SOURCE:31 += LOCAL:1 * 2
SOURCE:32 += LOCAL:1 / 5

;약물 경험
TCVAR:40 += 1

LOCAL:4 = LOCAL/5
;조교자가 [작은체형]·[소인]이라면 소모가 크다
SIF TALENT:PLAYER:127 || TALENT:PLAYER:128
    TIMES LOCAL:4 , 1.20
SIF LOCAL:4 >= BASE:PLAYER:체력
    LOCAL:4 = BASE:PLAYER:체력 - 1
;조교자의 체력을 양식으로 하고 있다
BASE:PLAYER:체력 -= LOCAL:4
SIF BASE:PLAYER:체력 < 500
    CALL TRAIN_EVENT_COM560
;촉수 경험 플래그
TFLAG:90 /= 2
RETURN 1

;--------------------------------------------------
;체력 조각의 처리
;--------------------------------------------------
@TRAIN_EVENT_COM560
IF ASSIPLAY
    PRINTFORML %CALLNAME:ASSI%의 다리가 휘청휘청하고 있다.
ELSE
    PRINTL 조금 다리가 휘청거린다.
ENDIF
PRINTL 더 이상 계속하지 않는 게 좋을 것 같다….
TEQUIP:91 = 0

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM560
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
IF TEQUIP:91
    IF TEQUIP:91 == 2
        PRINTFORMW %CALLNAME:TARGET%의 몸에 영양액을 주입하고 있던 촉수가 뽑혀졌다.
    ELSE
        PRINTFORMW 영양액을 흘려 넣고 있던 촉수가 %CALLNAME:TARGET%의 입에서 뽑혀졌다.
    ENDIF
ELSE
    IF TEQUIP:25 == 3 || TEQUIP:42 == 1
        PRINTFORMW 주사바늘과 같은 촉수가 %CALLNAME:TARGET%의 팔을 찌르고, 고영양의 액체를 주입하기 시작했다….
    ELSEIF TEQUIP:42 == 2
        PRINTFORMW %CALLNAME:TARGET%의 입을 범하고 있던 촉수는 갑자기, 즉효성의 회복 효과가 있는 액체를 목의 안쪽으로 흘려 넣기 시작했다.
    ELSE
        PRINTFORMW 관 모양의 촉수가 억지로 %CALLNAME:TARGET%의 입 안에 파고들어, 고영양 즉효성의 회복약을 흘려 넣었다….
    ENDIF
ENDIF

;--------------------------------------------------
;장착시 메시지
;--------------------------------------------------
@EQUIP_MESSAGE_COM560

[IF ENABLE_KOJO_EQUIP_MESSAGE]
CALL KOJO_EQUIP_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0
[ENDIF]

PRINTL
IF TEQUIP:91 == 2
    PRINTFORML 촉수는 %CALLNAME:TARGET%의 몸에 영양액을 쏟아대고 있다….
ELSE
    PRINTFORML 촉수는 %CALLNAME:TARGET%의 입에 영양액을 흘려 넣고 있다….
ENDIF

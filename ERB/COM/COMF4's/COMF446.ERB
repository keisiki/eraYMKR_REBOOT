;=============================================================================
;설교
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE446
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:446 > 0
    RETURN 0
;캐릭터 전용 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:커맨드확장 & 16384) == 0
    RETURN 0
;対象, または조교자가 映姫様でないとダメ
SIF NO:TARGET != 36 && NO:PLAYER != 36
    RETURN 0
;調教開始直後じゃないとダメ
SIF PREVCOM != -1
    RETURN 0
;対象だった場合, 反発刻印があったらダメ
SIF NO:TARGET == 36 && MARK:반발각인 < 0
    RETURN 0
;수면 중에는 불가
SIF EQUIP:14
    RETURN 0
;실신 중에는 불가
SIF TFLAG:899 > 0
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;에이키 님의 설교를 받는다. 조교 대상의 경우, 호감도와 달성감이 심상하지 않게 증가한다
;한편 조교자였던 경우, 쿄우준과 굴종이 그 나름대로 증가한다
;
;다만 다른 조교 실행 후는 사용 불가, 및 사용하면(자) 조교 종료
@COM446
;조수의 에이키 님이 할 때는 당신도 말려 들어가 「받는다」 것으로
IF NO:MASTER == 36
    PRINTL 설교를 한다
    COM_NAME = 설교를 한다
ELSE
    PRINTL 설교를 받는다
    COM_NAME = 설교를 받는다
ENDIF
CALL TRAIN_MESSAGE_COM446

;에이키 님을 조교
IF NO:TARGET == 36
    DOWNBASE:0 += 10
    DOWNBASE:기력 += 10
    DOWNBASE:MASTER:기력 += 333
    SIF ASSI >= 0
        DOWNBASE:ASSI:기력 += 333
    SOURCE:11 = 100
    SOURCE:16 = 60000
    SIF FLAG:게임모드 <= 1
        CFLAG:호감도 += 99

    ;주인 및 존재하면 조수의 수강 회수(여기는 조수는 도망가지 않는다)
    CFLAG:MASTER:조교설교잡학수강수 += 1
    SIF ASSI >= 0 && EQUIP:ASSI:14 == 0
        CFLAG:ASSI:조교설교잡학수강수 += 1
;에이키 님이 조교
ELSE
    DOWNBASE:0 += 50
    DOWNBASE:기력 += 9999
    SOURCE:17 = 6000
    SOURCE:22 = 2000

    ;ABL:순종을 본다
    IF ABL:순종 == 0
        TIMES SOURCE:17 , 1.00
    ELSEIF ABL:순종 == 1
        TIMES SOURCE:17 , 0.50
    ELSEIF ABL:순종 == 2
        TIMES SOURCE:17 , 0.40
    ELSEIF ABL:순종 == 3
        TIMES SOURCE:17 , 0.30
    ELSEIF ABL:순종 == 4
        TIMES SOURCE:17 , 0.20
    ELSE
        TIMES SOURCE:17 , 0.10
    ENDIF

    ;조교 대상의 수강 회수(조수가 있었다고 해도 도망치는 w)
    CFLAG:조교설교잡학수강수 += 1
ENDIF
TFLAG:107 = 999
RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM446
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
IF NO:TARGET == 36
    PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% %조사선택(CALLNAME:PLAYER, "를")% 향해 설교를 하기 시작했다.
ELSE
    PRINTFORMW %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%에게 설교를 시작했다.
ENDIF

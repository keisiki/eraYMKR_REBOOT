;=============================================================================
;풀장에서 수영
;=============================================================================
;プールにおいての基本コマンド　水泳経験を稼ごう
;恐怖　　　　　　→（接触　支配　トラウマ）　消費気力ガッツリ
;水に慣れてない　→（接触　支配　トラウマ）　消費体力ガッツリ
;慣れてる　　　　→達成感大 主導権小
;欲情　　　　　　→接触　露出
;従順　　　　　　→接触　達成感小
;LOCAL            →プラス効果基礎値　（達成感　主導権）
;LOCAL:1        →マイナス効果基礎値（接触　支配　トラウマ）
;--------------------------------------------------
;풀장에서 수영  실행 판정
;--------------------------------------------------
@COM_ABLE356
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:356 > 0
    RETURN 0
;풀장을 소유하고 있지 않으면 불가
SIF (FLAG:89 & 1) == 0
    RETURN 0
;吸血鬼や動物耳には従順4がないと使用不可（プレイヤーがサドならＯＫ）
IF TALENT:흡혈귀 || TALENT:116
    SIF ABL:순종 < 4 && TALENT:PLAYER:새드 == 0
        RETURN 0
ENDIF
;助手調教で[妄信][服従]もなく、調教対象と同性の場合、どちらかもしくは両方が脱いでいて《従順》3未満または《レズっ気》(《ＢＬっ気》)2未満だと実行できない
IF ASSIPLAY && !(TALENT:PLAYER:남자 ^^ TALENT:120) && TALENT:PLAYER:망신 == 0 && TALENT:PLAYER:복종 == 0
    SIF ABL:PLAYER:순종 < 3 || ABL:PLAYER:(22 + TALENT:남자) < 2 && !(TEQUIP:59 && TEQUIP:ASSI:59)
        RETURN 0
ENDIF
;睡眠中は不可
SIF EQUIP:14
    RETURN 0
;パニック中は不可
SIF TFLAG:108 > 0
    RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
    RETURN 0
;電極使用中は不可
SIF TEQUIP:20 == 4 || TEQUIP:25 == 4 || TEQUIP:30 == 4 || TEQUIP:30 == 6 || TEQUIP:32 == 4 || TEQUIP:35 == 4
    RETURN 0
;プール中じゃないとダメ
SIF TEQUIP:53 != 2
    RETURN 0
;シャワー中はダメ
SIF TEQUIP:54
    RETURN 0
;○○風呂入浴中はダメ
SIF TEQUIP:55
    RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:58
    RETURN 0
;助手がスク水とビキニと褌以外のコスプレ中はだめ
SIF ASSIPLAY && TEQUIP:PLAYER:59 > 1 && TEQUIP:PLAYER:59 != 2 && TEQUIP:PLAYER:59 != 12 && TEQUIP:PLAYER:59 != 17
    RETURN 0
;ビキニと褌以外のコスプレ中はだめ
SIF TEQUIP:59 > 2 && TEQUIP:59 != 12 && TEQUIP:59 != 17
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM356
PRINTL 풀장에서 수영

;-------------------------------------------------
;실행할 수 있을까의 판정
;-------------------------------------------------
IF TFLAG:699 == 0
    CALL COM_ORDER
    ;실행할 수 없다
    SIF RESULT == 0
        RETURN 0
ENDIF

COM_NAME = 풀장에서 수영
CALL TRAIN_MESSAGE_COM356

DOWNBASE:0 += 80
DOWNBASE:기력 += 40

LOCAL = 300
LOCAL:1 = 100

;河童
SIF TALENT:캇파
    TIMES LOCAL:1 , 0.20
    TIMES DOWNBASE:0 , 0.50
    TIMES DOWNBASE:기력 , 0.50
;気丈
SIF TALENT:12
    TIMES LOCAL:1 , 0.80
    
;PALAM:恐怖をみる
IF PALAM:17 < PALAMLV:1
    TIMES LOCAL:1 , 1.00
    TIMES DOWNBASE:기력 , 1.00
ELSEIF PALAM:17 < PALAMLV:2
    TIMES LOCAL:1 , 1.10
    TIMES DOWNBASE:기력 , 2.00
ELSEIF PALAM:17 < PALAMLV:3
    TIMES LOCAL:1 , 1.30
    TIMES DOWNBASE:기력 , 3.00
ELSEIF PALAM:17 < PALAMLV:4
    TIMES LOCAL:1 , 1.60
    TIMES DOWNBASE:기력 , 4.00
ELSE
    TIMES LOCAL:1 , 2.00
    TIMES DOWNBASE:기력 , 5.00
ENDIF
;ABL:水泳技能をみる
IF ABL:수영기능 == 0
    TIMES LOCAL , 1.00
    TIMES LOCAL:1 , 3.20
    TIMES DOWNBASE:0 , 6.00
ELSEIF ABL:수영기능 == 1
    TIMES LOCAL , 2.00
    TIMES LOCAL:1 , 1.60
    TIMES DOWNBASE:0 , 5.00
ELSEIF ABL:수영기능 == 2
    TIMES LOCAL , 4.00
    TIMES LOCAL:1 , 0.80
    TIMES DOWNBASE:0 , 4.00
ELSEIF ABL:수영기능 == 3
    TIMES LOCAL , 8.00
    TIMES LOCAL:1 , 0.40
    TIMES DOWNBASE:0 , 3.00
ELSEIF ABL:수영기능 == 4
    TIMES LOCAL , 16.00
    TIMES LOCAL:1 , 0.20
    TIMES DOWNBASE:0 , 2.00
ELSE
    TIMES LOCAL , 32.00
    TIMES LOCAL:1 , 0.10
    TIMES DOWNBASE:0 , 1.00
ENDIF

;一度代入
;達成感
SOURCE:16 += LOCAL
;主導権
SOURCE:14 += LOCAL /4
;接触
SOURCE:10 += LOCAL:1
;支配
SOURCE:23 += LOCAL:1 /2
;トラウマ
SOURCE:25 += LOCAL:1 /2

;PALAM:欲情をみる
IF PALAM:13 < PALAMLV:1
    TIMES SOURCE:10 , 1.00
    SOURCE:12 = 30
ELSEIF PALAM:13 < PALAMLV:2
    TIMES SOURCE:10 , 1.20
    SOURCE:12 = 50
ELSEIF PALAM:13 < PALAMLV:3
    TIMES SOURCE:10 , 1.30
    SOURCE:12 = 80
ELSEIF PALAM:13 < PALAMLV:4
    TIMES SOURCE:10 , 1.40
    SOURCE:12 = 120
ELSE
    TIMES SOURCE:10 , 1.50
    SOURCE:12 = 200
ENDIF
;ABL:従順をみる
IF ABL:순종 == 0
    TIMES SOURCE:10 , 0.80
    TIMES SOURCE:16 , 0.80
ELSEIF ABL:순종 == 1
    TIMES SOURCE:10 , 0.90
    TIMES SOURCE:16 , 0.90
ELSEIF ABL:순종 == 2
    TIMES SOURCE:10 , 1.00
    TIMES SOURCE:16 , 1.00
ELSEIF ABL:순종 == 3
    TIMES SOURCE:10 , 1.10
    TIMES SOURCE:16 , 1.10
ELSEIF ABL:순종 == 4
    TIMES SOURCE:10 , 1.20
    TIMES SOURCE:16 , 1.20
ELSE
    TIMES SOURCE:10 , 1.30
    TIMES SOURCE:16 , 1.30
ENDIF

;動物耳の場合
IF TALENT:116
    TIMES SOURCE:31 , 1.60
    TIMES SOURCE:22 , 1.50
    TIMES SOURCE:32 , 2.00
ENDIF
;吸血鬼の場合
IF TALENT:흡혈귀
    SOURCE:20 = 100
    TIMES SOURCE:20 , 1.60
    TIMES SOURCE:22 , 2.00
    TIMES SOURCE:32 , 1.40
ENDIF

;水泳経験　調教者が教育者なら倍
TCVAR:78 += 1 * (TALENT:PLAYER:교육자 + 1)

;レズBL経験
IF TALENT:120 == 0 && TALENT:PLAYER:남자 == 0
    TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:남자
    TCVAR:13 += 1
ENDIF

;泳いだから汚れがリセット
RESET_STAIN TARGET
RESET_STAIN PLAYER

;潤滑が半分
PALAM:10 /= 2
PALAM:21 /= 2
RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM356
CALL KOJO_TRAIN_MESSAGE_COM
PRINTFORML %조사처리(CALLNAME:PLAYER,"는")% %조사처리(CALLNAME:TARGET,"를")% 풀장에서 수영시켰다….
;ABL:水泳技能をみる
IF ABL:수영기능 == 0
    PRINTFORMW 그러나 %조사처리(CALLNAME:TARGET,"는")% 아직 물을 겁내고 있다….
ELSEIF ABL:수영기능 == 1
    PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 숨 쉬기를 잘 못하는 모양이다….
ELSEIF ABL:수영기능 == 2
    PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 어색하게나마 수영할 수 있게 된 모양이다….
ELSEIF ABL:수영기능 == 3
    PRINTFORMW %CALLNAME:TARGET%의 수영은 그럴싸해지기 시작한 모양이다….
ELSEIF ABL:수영기능 >= 4
    PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 기분 좋은 듯이 수영 중이다….
ENDIF

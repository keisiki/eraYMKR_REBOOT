;=============================================================================
;목욕하며 보낸다
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE345
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:345 > 0
    RETURN 0
;조교자奉仕 · ウフフ系커맨드를 유효로 하지 않으면 불가
SIF (FLAG:커맨드확장 & 2048) == 0
    RETURN 0
;主人でないとダメ
SIF ASSIPLAY
    RETURN 0
;吸血鬼や動物耳には순종4がないと使用不可(プレイヤーが새드ならＯＫ)
IF TALENT:흡혈귀 || TALENT:동물귀
    SIF ABL:순종 < 4 && TALENT:PLAYER:새드 == 0
        RETURN 0
ENDIF
;순종4미만 · 레즈끼(BL끼)3미만의 ASSI일 경우, 조교 대상이 동성이라면 실행할 수 없다(망신か복종ならＯＫ)
IF ASSIPLAY
    SIF (ABL:ASSI:순종 < 4 || ((TALENT:남자 == 0 && TALENT:ASSI:남자 == 0 && ABL:ASSI:레즈끼 < 3) || (TALENT:남자 && TALENT:ASSI:남자 && ABL:ASSI:BL끼 < 3))) && TALENT:ASSI:망신 == 0 && TALENT:ASSI:복종 == 0
    RETURN 0
ENDIF
;수면 중에는 불가
SIF EQUIP:14
    RETURN 0
;실신 중에는 불가
SIF TFLAG:899 > 0
    RETURN 0
;Ｃ계 장착구 사용 중에는 불가
SIF TEQUIP:30
    RETURN 0
;Ｂ계 장착구 사용 중에는 불가
SIF TEQUIP:35 || TEQUIP:36
    RETURN 0
;전극사용 중에는 불가
SIF TEQUIP:20 == 4 || TEQUIP:25 == 4 || TEQUIP:30 == 4 || TEQUIP:30 == 6 || TEQUIP:32 == 4 || TEQUIP:35 == 4
    RETURN 0
;お風呂中じゃないとダメ
SIF TEQUIP:53 != 1
    RETURN 0
;샤워 중에는 불가
SIF TEQUIP:54
    RETURN 0
;○○목욕탕 입욕 중에는 불가
SIF TEQUIP:55
    RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
    RETURN 0
;助手のコスプレ中はダメ
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 > 2
    RETURN 0
;코스프레 중에는 불가
SIF TEQUIP:59 > 2
    RETURN 0
;나체정식 중에는 불가
SIF TEQUIP:60
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM345
PRINTL 목욕하며 보낸다
COM_NAME = 목욕하며 보낸다
CALL TRAIN_MESSAGE_COM345

DOWNBASE:0 += 0
DOWNBASE:기력 += 20

SOURCE:10 = 50
SOURCE:18 = 50

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    SOURCE:12 = 30
ELSEIF PALAM:13 < PALAMLV:2
    SOURCE:12 = 50
ELSEIF PALAM:13 < PALAMLV:3
    SOURCE:12 = 80
ELSEIF PALAM:13 < PALAMLV:4
    SOURCE:12 = 120
ELSE
    SOURCE:12 = 200
ENDIF

;봉사 정신을 본다
IF ABL:봉사정신 == 0
    SOURCE:11 = 10
ELSEIF ABL:봉사정신 == 1
    SOURCE:11 = 30
ELSEIF ABL:봉사정신 == 2
    SOURCE:11 = 60
ELSEIF ABL:봉사정신 == 3
    SOURCE:11 = 100
ELSEIF ABL:봉사정신 == 4
    SOURCE:11 = 160
ELSE
    SOURCE:11 = 250
ENDIF

;ABL:순종을 본다
IF ABL:순종 == 0
    TIMES SOURCE:11 , 0.80
ELSEIF ABL:순종 == 1
    TIMES SOURCE:11 , 0.90
ELSEIF ABL:순종 == 2
    TIMES SOURCE:11 , 1.00
ELSEIF ABL:순종 == 3
    TIMES SOURCE:11 , 1.10
ELSEIF ABL:순종 == 4
    TIMES SOURCE:11 , 1.20
ELSE
    TIMES SOURCE:11 , 1.30
ENDIF

;동물귀의 경우
IF TALENT:동물귀
    TIMES SOURCE:31 , 1.60
    TIMES SOURCE:22 , 1.50
    TIMES SOURCE:32 , 2.00
ENDIF
;흡혈귀의 경우
IF TALENT:흡혈귀
    SOURCE:20 = 100
    TIMES SOURCE:20 , 1.60
    TIMES SOURCE:22 , 2.00
    TIMES SOURCE:32 , 1.40
ENDIF

IF TALENT:남자 == 0 && TALENT:PLAYER:남자 == 0
    TCVAR:12 += 1
ELSEIF TALENT:남자 && TALENT:PLAYER:남자
    TCVAR:13 += 1
ENDIF

;샤워로 더러움이 리셋
RESET_STAIN TARGET
RESET_STAIN PLAYER

;(할 수 있으면) 윤활이 반에
PALAM:10 /= 2
PALAM:21 /= 2

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM345
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
PRINTFORM %조사선택(CALLNAME:PLAYER, "는")% 
IF RAND:4 == 0
    PRINTFORML 욕탕의 물을 떠올려 %CALLNAME:TARGET%에게 부었다.
    IF TALENT:연모 && RAND:3 >= 1
        PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 즐거운 듯이 욕탕의 물을 %CALLNAME:TARGET%에게 뿌리고 있다.
    ELSEIF ABL:순종 >= 4 && EXP:봉사쾌락경험 >= 250
        PRINTFORMW %CALLNAME:TARGET%의 \@(TEQUIP:42) ? 즐거운 듯한 행동 # 입가가 즐거운 듯이 느슨해지고 있는 것 \@을 %조사선택(CALLNAME:PLAYER, "는")% 놓치지 않았다….
    ELSEIF ABL:순종 >2 && EXP:봉사쾌락경험 <= 150
        PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 기가 막힌다는 눈빛으로 자신의 머리카락을 정돈했다….
    ELSE
        PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 전혀 상대해 주지 않는다….
    ENDIF
ELSEIF RAND:3 == 0
    PRINTFORML %CALLNAME:TARGET%의 몸을 씻어 주었다….
    IF TALENT:연모 && RAND:3 >= 1
        PRINTFORMW 어느새 인가 %CALLNAME:TARGET%의 옷까지 빨아주고 있다….
    ELSEIF ABL:순종 >= 4 && EXP:봉사쾌락경험 >= 250
        PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 깨끗이 되는 것이 기쁜 것 같다….
    ELSEIF ABL:순종 >= 2 && EXP:봉사쾌락경험 <= 150
        PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 간지러워 하고 있다….
    ELSE
        PRINTFORMW 그러나, %조사선택(CALLNAME:TARGET, "는")% 곧바로 샤워를 해 버렸다….
    ENDIF
ELSEIF RAND:2 == 0
    PRINTFORML %조사선택(CALLNAME:TARGET, "와")% 함께 욕조에 들어갔다….
    IF TALENT:연모 && RAND:3 >= 1
        PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 딱 몸을 밀착시켜, %CALLNAME:PLAYER%의 얼굴을 응시하고 있다….
        PRINTW …좀 멍해졌다….
    ELSEIF ABL:순종 >= 4 && EXP:봉사쾌락경험 >= 250
        PRINTFORMW %조사선택(CALLNAME:TARGET, "와")% %CALLNAME:PLAYER%의 거리가 나날이 가까워져 오고 있는 것 같다….
    ELSEIF ABL:순종 >= 2 && EXP:봉사쾌락경험 <= 150
        PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 힐끔힐끔 이쪽의 몸을 보고 있다….
    ELSE
        PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 욕조에서 나가버렸다….
    ENDIF
ELSE
    PRINTFORML 몸을 비누 투성이로 해, %조사선택(CALLNAME:TARGET, "를")% 껴안았다….
    IF TALENT:연모 && RAND:3 >= 1
        PRINTFORMW %CALLNAME:PLAYER%의 몸의 감촉을 %조사선택(CALLNAME:TARGET, "는")% 매우 마음에 들어 하고 있다….
    ELSEIF ABL:순종 >= 4 && EXP:봉사쾌락경험 >= 250
        PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 부끄러워 하면서도, 묵묵히 %CALLNAME:PLAYER%에게 안기고 있었다….
    ELSEIF ABL:순종 >= 2 && EXP:봉사쾌락경험 <= 150
        PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 복잡한 표정을 짓고 있다….
    ELSE
        PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% %조사선택(CALLNAME:PLAYER, "를")% 밀치려고 했지만, 비누에 미끄러져 버려 생각대로 잘 되지 않는다….
    ENDIF
ENDIF

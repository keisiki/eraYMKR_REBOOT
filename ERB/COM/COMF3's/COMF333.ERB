;=============================================================================
;애널 후배위 시킨다
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE333
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:333 > 0
    RETURN 0
;조교자奉仕 · ウフフ系커맨드를 유효로 하지 않으면 불가
SIF (FLAG:커맨드확장 & 2048) == 0
    RETURN 0
;Ａ조교 필터가 오프되어 있지 않으면 불가
SIF FLAG:VA이상조교필터 & 2
    RETURN 0
;조교 대상에게 페니스가 없으면 불가
SIF EXIST_BAR(TARGET) == 0
    RETURN 0
;순종4미만 · 레즈끼3미만의 ASSI일 경우, 조교 대상이 동성이라면 실행할 수 없다(망신か복종ならＯＫ)
IF ASSIPLAY
    SIF (ABL:ASSI:순종 < 4 || (TALENT:남자 == 0 && TALENT:ASSI:남자 == 0 && ABL:ASSI:레즈끼 < 3)) && TALENT:ASSI:망신 == 0 && TALENT:ASSI:복종 == 0
        RETURN 0
ENDIF
;주인이 [금단의 지식]持ちじゃないとき, 体格差が2段階以内か조교자가 [금단의 지식]持ちじゃないとダメ
SIF TALENT:MASTER:금단의지식 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
    RETURN 0
;조교자에게 일정의 A경험이 필요
SIF EXP:PLAYER:A경험 < EXPLV:3
    RETURN 0
;오나홀 · 전극 오나홀사용 중에는 불가
SIF TEQUIP:30 == 2 || TEQUIP:30 == 6
    RETURN 0
;수면 중에는 불가
SIF EQUIP:14
    RETURN 0
;실신 중에는 불가
SIF TFLAG:899 > 0
    RETURN 0
;縄사용 중에는 불가
SIF TEQUIP:40
    RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
    RETURN 0
;욕실 플레이 중에는 플레이 매트가 없으면 불가
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
    RETURN 0
;○○목욕탕 입욕 중에는 불가
SIF TEQUIP:55
    RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
    RETURN 0
;조수가 훈도시 착용 중일 경우 , 助手の기교4이상 필수
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17 && ABL:ASSI:기교 < 4
    RETURN 0
;훈도시 착용 중일 경우 기교4이상 필수
SIF TEQUIP:59 == 17 && ABL:기교 < 4
    RETURN 0
;알몸 앞치마 중에는 불가
SIF TEQUIP:59 == 1
    RETURN 0
;학교수영복 플레이 중에는 불가
SIF TEQUIP:59 == 2
    RETURN 0
;나체정식 중에는 불가
SIF TEQUIP:60
    RETURN 0
;시간정지 중에는 무리
SIF TEQUIP:71
    RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM333
PRINTL A 후배위 시킨다

;-------------------------------------------------
;실행할 수 있을까의 판정
;-------------------------------------------------
IF TFLAG:699 == 0
    CALL COM_ORDER
    ;실행할 수 없다
    SIF RESULT == 0
        RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------
COM_NAME = A 후배위 시킨다
CALL TRAIN_MESSAGE_COM333

;-------------------------------------------------
;사정 게이지 체크
;-------------------------------------------------
LOCAL = 0

;ABL:기교를 본다
IF ABL:기교 == 0
    LOCAL = 50
ELSEIF ABL:기교 == 1
    LOCAL = 300
ELSEIF ABL:기교 == 2
    LOCAL = 800
ELSEIF ABL:기교 == 3
    LOCAL = 1500
ELSEIF ABL:기교 == 4
    LOCAL = 2000
ELSE
    LOCAL = 3200
ENDIF

;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;봉사 정신을 본다
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)

;조교자의 A감각을 본다
LOCAL = EJACULATIONCHECK_ABL_2_1(PLAYER, LOCAL)
;조교자의 A경험을 본다
LOCAL = EJACULATIONCHECK_EXP_1_1(PLAYER, LOCAL)

;조교 대상의 C감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(TARGET, LOCAL)

SIF TALENT:남자 || TALENT:후타나리
    BASE:사정 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 60
DOWNBASE:기력 += 260

SOURCE:10 = 700
SOURCE:11 = 800
SOURCE:14 = 1200
SOURCE:18 = 550
SOURCE:24 = 600
SOURCE:31 = 500

;ABL:C감각을 본다
IF ABL:C감각 == 0
    SOURCE:0 = 800
    SOURCE:15 = 1200
ELSEIF ABL:C감각 == 1
    SOURCE:0 = 1000
    SOURCE:15 = 1500
ELSEIF ABL:C감각 == 2
    SOURCE:0 = 1500
    SOURCE:15 = 1800
ELSEIF ABL:C감각 == 3
    SOURCE:0 = 2000
    SOURCE:15 = 2300
ELSEIF ABL:C감각 == 4
    SOURCE:0 = 2500
    SOURCE:15 = 2700
ELSE
    SOURCE:0 = 3200
    SOURCE:15 = 3500
ENDIF

;ABL:기교를 본다
IF ABL:PLAYER:기교 == 0
    TIMES SOURCE:0 , 0.50
    TIMES SOURCE:15 , 0.50
ELSEIF ABL:PLAYER:기교 == 1
    TIMES SOURCE:0 , 0.80
    TIMES SOURCE:15 , 0.80
ELSEIF ABL:PLAYER:기교 == 2
    TIMES SOURCE:0 , 1.00
    TIMES SOURCE:15 , 1.00
ELSEIF ABL:PLAYER:기교 == 3
    TIMES SOURCE:0 , 1.50
    TIMES SOURCE:15 , 1.50
ELSEIF ABL:PLAYER:기교 == 4
    TIMES SOURCE:0 , 2.50
    TIMES SOURCE:15 , 2.50
ELSE
    TIMES SOURCE:0 , 4.00
    TIMES SOURCE:15 , 4.00
ENDIF

TCVAR:PLAYER:1 += 1
SIF ASSIPLAY == 0 && EXIST_BAR(TARGET)
    CFLAG:주인의A범한회수 += 1

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
;노예의 사정은 TRAIN_CHECK.ERB에서 관리
    ;사정처 타입의 설정
    TFLAG:703 = 16
    ;사정처 선택의 가능·불가
    TFLAG:704 = 0
    ;노예 사정처 선정이 끝난 플래그
    TFLAG:705 = 1

IF EXIST_BAR(TARGET)
    ;노예의 P⇔조교자의 A의 불결이 이동
    STAIN:자지 |= STAIN:PLAYER:애널
    STAIN:PLAYER:애널 |= STAIN:자지
ENDIF

IF TALENT:남자 == 0 && TALENT:PLAYER:남자 == 0
    TCVAR:12 += 10
ELSEIF TALENT:남자 && TALENT:PLAYER:남자
    TCVAR:13 += 10
ENDIF

RETURN 1

;--------------------------------------------------
;명령 거부의 메시지
;--------------------------------------------------
;다만, 조수 조교 때는 조수가 거절했는지 대상이 거절했는지 불명해서 표시하지 않는다.
@DENIAL_MESSAGE_COM_333
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:지문소거판정 == 1 || RESULT != 0
    RETURN 0

IF ASSIPLAY == 0
    PRINTL
    PRINTFORML %CALLNAME:TARGET%에게 페니스를 애널에 삽입하도록 명령했지만 거절당했다.
    PRINTW 모처럼, 엉덩이로 즐겁게 해 주려고 했는데….
ENDIF

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM333
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:지문소거판정 == 1 || RESULT != 0
    RETURN 0

PRINTL
;전회와 이번 조교자가 같아, 전회의 조교가 애널 후배위 시키는이어, 뽑지 않는 플래그가 있는 경우
IF TFLAG:102 && PREVCOM == 333 && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% %CALLNAME:PLAYER%의 허리를 부여잡고
    ;연모로 추가
    SIF TALENT:연모 && RAND:3 >= 1
        PRINTFORM %CALLNAME:PLAYER%의 목덜미에 혀를 가져오며 
    ;「TARGET가 진성의 새드인가」 「새드끼인가」로 분기
    IF (TALENT:새드 || TALENT:초S || ABL:새드끼 == 5) && RAND:3 == 0
        PRINTFORMW 입체적으로 허리를 그라인드 시켜, %CALLNAME:PLAYER%의 몸을 흔들어댔다….
    ELSEIF TALENT:새드 || TALENT:초S || (ABL:새드끼 >= 3 && RAND:2 == 0)
        PRINTW 짐승과 같이 덜덜 허리를 떨고 있다….
    ELSE
        PRINTW 다시 허리를 앞뒤로 움직였다….
    ENDIF
;(와)과 같이  전회의 조교가 애널 정상위 시킬까 애널 기승위하는 것 경우
ELSEIF TFLAG:102 && (PREVCOM == 332 || PREVCOM == 334) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 애널로부터 빼지 않고 그대로 %조사선택(CALLNAME:PLAYER, "를")% 엎드리게 하여, 다시금 허리를 흔들어댔다….
ELSE
    PRINTFORMW %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%의 앞에 네발로 엎드려, 애널에의 삽입을 졸랐다….
    PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
    ;연모로 추가
    SIF TALENT:연모 && RAND:3 >= 1
        PRINT 몸을 딱 밀착시켜 
    PRINTFORMW %CALLNAME:PLAYER%의 애널에 삽입했다….
ENDIF

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_333, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:욕망
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:욕망
    TFLAG:240 += LOCAL:99 * 2
    RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF

;쾌락 각인
IF MARK:쾌락각인
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = MARK:쾌락각인
    TFLAG:240 += LOCAL:99 * 2
    RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF

;PALAM:욕정
GETPALAMLV PALAM:13, 5
LOCAL:2 = RESULT
IF LOCAL:2
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = LOCAL:2 * 2
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;쾌감에 솔직
IF TALENT:쾌감에솔직
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 2
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:70%({LOCAL:99})
ENDIF
;쾌감의 부정
IF TALENT:쾌감을부정
    RESULTS = %RESULTS% - 
    LOCAL:99 = 2
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;연모
IF TALENT:연모 && ASSIPLAY == 0
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 5
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF

;조교자가 정조관념
IF TALENT:PLAYER:정조관념
    RESULTS = %RESULTS% - 
    LOCAL:99 = 10
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %조사처리(CALLNAME:PLAYER,"가")% 정조관념({LOCAL:99})
;조교자가 정조무구애
ELSEIF TALENT:PLAYER:정조관둔감
    RESULTS = %RESULTS% + 
    LOCAL:99 = 5
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %조사처리(CALLNAME:PLAYER,"가")% 정조무구애({LOCAL:99})
    LOCAL:99 = 1
ENDIF

;조교자의 ABL:기교
IF ABL:PLAYER:기교
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:PLAYER:기교
    TFLAG:240 += LOCAL:99 * 4
    RESULTS = %RESULTS% %CALLNAME:PLAYER%의 %ABLNAME:12%Lv{LOCAL:99}({LOCAL:99 * 4})
ENDIF

TFLAG:241 = 25

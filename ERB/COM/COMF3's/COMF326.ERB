;=============================================================================
;페니스밴드 삽입
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE326
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:326 > 0
    RETURN 0
;조교자奉仕 · ウフフ系커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 2048) == 0
    RETURN 0
;Ｖ조교 필터가 오프되어 있지 않으면 불가
SIF FLAG:2 & 1
    RETURN 0
;조교 대상이 オトコだとダメ
SIF TALENT:120
    RETURN 0
;윤활 부족이고, 순종4미만 · 레즈끼4미만의 ASSI일 경우, 조교 대상이 동성이라면 실행할 수 없다(새드거나 새드끼Lv3이상か狂気, 또는 망신이나 복종이 있다면 ＯＫ)
IF PALAM:10 < PALAMLV:2 && ASSIPLAY
    SIF (ABL:ASSI:순종 < 4 || (TALENT:120 == 0 && TALENT:ASSI:120 == 0 && ABL:ASSI:레즈끼 < 4)) && TALENT:ASSI:81 == 0 && ABL:ASSI:새드끼 < 3 && TALENT:ASSI:158 == 0 && TALENT:ASSI:151 == 0 && TALENT:ASSI:160 == 0
        RETURN 0
ENDIF
;ペニスバンドが無いとダメ
SIF ITEM:2 == 0 && NOITEM == 0
    RETURN 0
;Ｖ系装着도구 사용 중에는 불가
SIF TEQUIP:20 || TEQUIP:21
    RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
    RETURN 0
;○○목욕탕 입욕 중에는 불가
SIF TEQUIP:55
    RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
    RETURN 0
;조수가 훈도시 착용 중일 경우 , 奴隷と助手の기교4이상 필수
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17 && (ABL:ASSI:기교 < 4 || ABL:기교 < 4)
    RETURN 0
;훈도시 착용 중일 경우 조교자와 奴隷の쌍방이 기교4이상 필수
SIF TEQUIP:59 == 17 && (ABL:PLAYER:기교 < 4 || ABL:기교 < 4)
    RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM326
;쿠스코 삽입중 또는 전회의 조교가 G스포트 또는 손가락 삽입또한 플레이어의 기교 3이상이라면 G스포트 자극에
IF TEQUIP:21 || PREVCOM == 4 && ABL:PLAYER:기교 >= 3 || PREVCOM == 600
    CALL EXCOM_ABLE600
    SIF RESULT == 1
        JUMP COM600
ENDIF

PRINTL 페니스밴드 삽입
COM_NAME = 페니스밴드 삽입
CALL TRAIN_MESSAGE_COM326

;V경험을 수반하는 커맨드의 플래그(처녀막 재생 대응)
TFLAG:1 = 1

DOWNBASE:0 += 60
DOWNBASE:기력 += 120

SOURCE:11 = 100
SOURCE:12 = 800
SOURCE:31 = 500

;ABL:V감각을 본다
IF ABL:V감각 == 0
    SOURCE:1 = 30
    SOURCE:22 = 200
ELSEIF ABL:V감각 == 1
    SOURCE:1 = 120
    SOURCE:22 = 350
ELSEIF ABL:V감각 == 2
    SOURCE:1 = 350
    SOURCE:22 = 550
ELSEIF ABL:V감각 == 3
    SOURCE:1 = 700
    SOURCE:22 = 900
ELSEIF ABL:V감각 == 4
    SOURCE:1 = 1300
    SOURCE:22 = 1500
ELSE
    SOURCE:1 = 2000
    SOURCE:22 = 2300
ENDIF

;EXP:V경험을 본다
IF EXP:V경험 < EXPLV:1
    TIMES SOURCE:1 , 0.20
    TIMES SOURCE:22, 0.20
    SOURCE:20 = 300
ELSEIF EXP:V경험 < EXPLV:2
    TIMES SOURCE:1 , 0.50
    TIMES SOURCE:22, 0.50
    SOURCE:20 = 180
ELSEIF EXP:V경험 < EXPLV:3
    TIMES SOURCE:1 , 1.00
    TIMES SOURCE:22, 0.80
    SOURCE:20 = 80
ELSEIF EXP:V경험 < EXPLV:4
    TIMES SOURCE:1 , 1.20
    TIMES SOURCE:22, 1.00
    SOURCE:20 = 30
ELSEIF EXP:V경험 < EXPLV:5
    TIMES SOURCE:1 , 1.60
    TIMES SOURCE:22, 1.20
    SOURCE:20 = 0
ELSE
    TIMES SOURCE:1 , 1.80
    TIMES SOURCE:22, 1.50
    SOURCE:20 = 0
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
    TIMES SOURCE:1 , 0.10
    SOURCE:20 += 700
    TIMES SOURCE:20 , 3.00
ELSEIF PALAM:10 < PALAMLV:2
    TIMES SOURCE:1 , 0.20
    SOURCE:20 += 200
    TIMES SOURCE:20 , 1.00
ELSEIF PALAM:10 < PALAMLV:3
    TIMES SOURCE:1 , 0.60
    TIMES SOURCE:20 , 0.80
ELSEIF PALAM:10 < PALAMLV:4
    TIMES SOURCE:1 , 1.00
    TIMES SOURCE:20 , 0.50
ELSE
    TIMES SOURCE:1 , 2.00
    TIMES SOURCE:20 , 0.10
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    TIMES SOURCE:1 , 0.50
ELSEIF PALAM:13 < PALAMLV:2
    TIMES SOURCE:1 , 0.80
ELSEIF PALAM:13 < PALAMLV:3
    TIMES SOURCE:1 , 1.20
ELSEIF PALAM:13 < PALAMLV:4
    TIMES SOURCE:1 , 1.50
ELSE
    TIMES SOURCE:1 , 1.80
ENDIF

;V민감, 둔감을 본다
;쾌V자체의 체크는 다음에 정리해 준다
IF TALENT:103
    TIMES SOURCE:20 , 1.50
    TIMES SOURCE:22 , 1.50
    TIMES SOURCE:31 , 1.50
ELSEIF TALENT:102
    TIMES SOURCE:20 , 0.60
    TIMES SOURCE:22 , 0.60
    TIMES SOURCE:31 , 0.60
ENDIF

;처녀로 정조관념
SIF TALENT:처녀 == 1 && TALENT:30
    TIMES SOURCE:22 , 2.00

TCVAR:0 += 1

;노예의 V⇔조교자의 입의 불결이 이동
STAIN:보지 |= STAIN:PLAYER:입
STAIN:PLAYER:입 |= STAIN:보지

SIF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
    TCVAR:12 += 7

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM326
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
PRINTFORM %조사선택(CALLNAME:PLAYER, "는")% 페니스 밴드를 입에 장착해, %CALLNAME:TARGET%의 
IF TALENT:처녀 == 1
    PRINT 아직 남자를 모르는 
ELSEIF PALAM:10 >= PALAMLV:4
    PRINT 흠뻑 젖어있는 
ENDIF
SIF TEQUIP:21
    PRINT 쿠스코로 활짝 열려있는 
IF TALENT:128
    PRINT 어린 균열에 
ELSE
    PRINT 보지에 
ENDIF
PRINTL 삽입했다….
PRINTFORM %조사선택(CALLNAME:PLAYER, "는")% 
SIF PALAM:10 >= PALAMLV:4
    PRINT 얼굴을 애액 투성이로 한 채로, 
IF TALENT:150 && RAND:3 >= 1
    PRINTFORM %CALLNAME:TARGET%의 얼굴을 가만히 올려보며 
ELSEIF TALENT:PLAYER:81 || TALENT:PLAYER:177 || (ABL:PLAYER:새드끼 >= 3 && RAND:2 == 0)
    PRINT 격렬하게 
ELSE
    PRINT 천천히 
ENDIF
PRINTW 머리를 흔들고 있다….

;=============================================================================
;젖 맞대기
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE210
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:210 > 0
    RETURN 0
;Ｂ계 확장 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:커맨드확장 & 8) == 0
    RETURN 0
;조교자もしくは対象がオトコだとダメ
SIF TALENT:남자 || TALENT:PLAYER:남자
    RETURN 0
;조교자 측과 대상과의 체격 차이가 4 단계 이상이면 안 된다(금단의 지식은 무관하다)
SIF ABS(BODYSIZE(PLAYER) - BODYSIZE(TARGET)) >= 4
    RETURN 0
;Ｂ계 장착구 사용 중에는 불가
SIF TEQUIP:35 || TEQUIP:36
    RETURN 0
;수면 중에는 불가
SIF EQUIP:14
    RETURN 0
;실신 중에는 불가
SIF TFLAG:899 > 0
    RETURN 0
;패닉 중에는 불가
SIF TFLAG:108 > 0
    RETURN 0
;조수가 ウェディングドレス · ゴスロリ着用中の場合助手に기교4이상 필수
SIF ASSI >= 0 && ASSIPLAY && (TEQUIP:ASSI:59 == 19 || TEQUIP:ASSI:59 == 20) && ABL:ASSI:기교 < 4
    RETURN 0
;ウェディングドレス · ゴスロリ着用中の場合기교4이상 필수
SIF (TEQUIP:59 == 19 || TEQUIP:59 == 20) && ABL:PLAYER:기교 < 4
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;조교자와 대상이 유두를 맞추는 플레이
;외형적으로  진기한 것을 보고 즐김이지만 효과적으로는 초라하다
@COM210
PRINTL 젖 맞대기
COM_NAME = 젖 맞대기
CALL TRAIN_MESSAGE_COM210

DOWNBASE:0 += 5
DOWNBASE:기력 += 60

SOURCE:15 = 60
SOURCE:30 = 80
SOURCE:12 = 120

;ABL:B감각을 본다
IF ABL:B감각 == 0
    SOURCE:3 = 10
    SOURCE:11 = 30
ELSEIF ABL:B감각 == 1
    SOURCE:3 = 50
    SOURCE:11 = 60
ELSEIF ABL:B감각 == 2
    SOURCE:3 = 200
    SOURCE:11 = 100
ELSEIF ABL:B감각 == 3
    SOURCE:3 = 400
    SOURCE:11 = 120
ELSEIF ABL:B감각 == 4
    SOURCE:3 = 700
    SOURCE:11 = 140
ELSE
    SOURCE:3 = 1000
    SOURCE:11 = 300
ENDIF

IF TALENT:모유체질
    ;ABL:분유중독을 본다
    IF ABL:분유중독 == 0
        TIMES SOURCE:3 , 1.00
        TIMES SOURCE:11 , 1.00
    ELSEIF ABL:분유중독 == 1
        TIMES SOURCE:3 , 1.20
        TIMES SOURCE:11 , 1.00
    ELSEIF ABL:분유중독 == 2
        TIMES SOURCE:3 , 1.20
        TIMES SOURCE:11 , 1.20
    ELSEIF ABL:분유중독 == 3
        TIMES SOURCE:3 , 1.40
        TIMES SOURCE:11 , 1.20
    ELSEIF ABL:분유중독 == 4
        TIMES SOURCE:3 , 1.40
        TIMES SOURCE:11 , 1.40
    ELSE
        TIMES SOURCE:3 , 1.60
        TIMES SOURCE:11 , 1.60
    ENDIF
ENDIF

;ABL:조교자의 B감각을 본다
IF ABL:PLAYER:B감각 == 0
    SOURCE:3 += 10
ELSEIF ABL:PLAYER:B감각 == 1
    SOURCE:3 += 50
ELSEIF ABL:PLAYER:B감각 == 2
    SOURCE:3 += 200
ELSEIF ABL:PLAYER:B감각 == 3
    SOURCE:3 += 400
ELSEIF ABL:PLAYER:B감각 == 4
    SOURCE:3 += 700
ELSE
    SOURCE:3 += 1000
ENDIF

;음유
IF TALENT:음유
    TIMES SOURCE:11 , 1.20
    TIMES SOURCE:30 , 0.80
    TIMES SOURCE:3 , 1.20
ENDIF

;조교자가 음유
IF TALENT:PLAYER:음유
    TIMES SOURCE:30 , 0.80
    TIMES SOURCE:3 , 1.20
ENDIF


;노예의 B⇔조교자의 B의 불결이 이동
STAIN:가슴 |= STAIN:PLAYER:가슴
STAIN:PLAYER:가슴 |= STAIN:가슴

SIF TALENT:남자 == 0 && TALENT:PLAYER:남자 == 0
    TCVAR:12 += 5
RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM210
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
PRINTFORM %조사선택(CALLNAME:PLAYER, "는")% 
IF TALENT:PLAYER:절벽
    PRINT 거의 없는 거나 다름없는 
ELSEIF TALENT:PLAYER:작은체형 && TALENT:PLAYER:빈유
    PRINT 간신히 부풀어 오른 게 느껴지는 정도의 
ELSEIF TALENT:PLAYER:빈유
    PRINT 표준보다 약간 작은 
ELSEIF TALENT:PLAYER:거유
    PRINT 커다랗고 곡선이 아름다운 
ELSEIF TALENT:PLAYER:폭유
    PRINT 감당하기 힘들 만큼 큰 
ENDIF
PRINTL 가슴의 끄트머리의, 아직 부드러운 돌기와
PRINTFORM %CALLNAME:TARGET%의 
IF TALENT:절벽
    PRINT 거의 없는 거나 다름없는 
ELSEIF TALENT:작은체형 && TALENT:빈유
    PRINT 간신히 부풀어 오른 게 느껴지는 정도의 
ELSEIF TALENT:빈유
    PRINT 표준보다 약간 작은 
ELSEIF TALENT:거유
    PRINT 커다랗고 곡선이 아름다운 
ELSEIF TALENT:폭유
    PRINT 감당하기 힘들 만큼 큰 
ENDIF
IF TEQUIP:68
    PRINT 가슴의 
    SIF EX:23
        PRINT 탱글탱글하게 응어리진 
    PRINTW 끄트머리를 맞대었다….
ELSEIF PALAM:13 > PALAMLV:3
    PRINT 가슴의 끄트머리의, 
    PRINTW 완전히 딱딱해진 돌기를 맞대고 살짝 눌렀다….
ELSE
    PRINT 가슴의 끄트머리의, 
    PRINTW 아직 부드러운 돌기를 붙이고 살짝 눌렀다….
ENDIF

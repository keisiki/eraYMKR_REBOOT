;=============================================================================
;파이즈리펠라
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE209
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:209 > 0
    RETURN 0
;Ｂ계 확장 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:커맨드확장 & 8) == 0
    RETURN 0
;対象が猫舌だと猫フェラパッチ有効でないとダメ
SIF TALENT:고양이혀 && (FLAG:조교모드 & 16) == 0
    RETURN 0
;猫舌だと기교5 · 奉仕精神4 · 순종3が필요
SIF TALENT:고양이혀 && (ABL:기교 < 5 || ABL:봉사정신 < 4 || ABL:순종 < 3)
    RETURN 0
;조교자에게 페니스가 없으면 불가
SIF EXIST_BAR(PLAYER) == 0
    RETURN 0
;남자라면 안 됨
SIF TALENT:남자
    RETURN 0
;주인이 [금단의 지식]持ちじゃないとき, 体格差が2段階以内か조교자가 [금단의 지식]持ちじゃないとダメ
SIF TALENT:MASTER:금단의지식 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
    RETURN 0
;[절벽]이면 불가
IF TALENT:절벽
    RETURN 0
;[貧乳]だと기교5이상 필요
ELSEIF TALENT:빈유 && ABL:기교 < 5
    RETURN 0
;[巨乳]なら기교3이상 필요
ELSEIF TALENT:거유 && ABL:기교 < 3
    RETURN 0
;[爆乳]なら기교LV2이상 필요
ELSEIF TALENT:폭유 && ABL:기교 < 2
    RETURN 0
;그 외의 경우에는 기교4이상 필요
ELSEIF TALENT:빈유 == 0 && TALENT:거유 == 0 && TALENT:폭유 == 0 && ABL:기교 < 4
    RETURN 0
ENDIF
;수면 중에는 불가
SIF EQUIP:14
    RETURN 0
;실신 중에는 불가
SIF TFLAG:899 > 0
    RETURN 0
;패닉 중에는 불가
SIF TFLAG:108 > 0
    RETURN 0
;사라시 장착 중에는 불가
SIF TEQUIP:36
    RETURN 0
;조수가 훈도시 착용 중일 경우에는 불가
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17
    RETURN 0
;훈도시와 알몸 와이셔츠 외의 코스프레 중에는 안 됨
SIF TEQUIP:59 > 2 && TEQUIP:59 != 11 && TEQUIP:59 != 17
    RETURN 0
;밧줄 사용 중에는 불가
SIF TEQUIP:40
    RETURN 0
;口枷がされているとダメ
SIF TEQUIP:42
    RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
    RETURN 0
;나체정식 중에는 불가
SIF TEQUIP:60
    RETURN 0
;시간정지 중에는 무리
SIF TEQUIP:71
    RETURN 0
;촉수 조교중일 시주인의 촉수사역Lv2이하는 안 됨
SIF TEQUIP:90 && ABL:MASTER:촉수사역 <= 2
    RETURN 0
;촉수 관통중은 안됨
SIF TEQUIP:90 && TEQUIP:25 == 3
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM209
PRINTL 파이즈리펠라

;-------------------------------------------------
;실행할 수 있을까의 판정
;-------------------------------------------------
IF TFLAG:699 == 0
    CALL COM_ORDER
    ;실행할 수 없다
    SIF RESULT == 0
        RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------
COM_NAME = 파이즈리펠라
CALL TRAIN_MESSAGE_COM209

;-------------------------------------------------
;사정 게이지 체크
;-------------------------------------------------
LOCAL = 0

;ABL:기교를 본다
IF ABL:기교 == 0
    LOCAL = 1300
ELSEIF ABL:기교 == 1
    LOCAL = 2000
ELSEIF ABL:기교 == 2
    LOCAL = 2900
ELSEIF ABL:기교 == 3
    LOCAL = 4100
ELSEIF ABL:기교 == 4
    LOCAL = 5200
ELSE
    LOCAL = 6500
ENDIF

;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;봉사 정신을 본다
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)
;정액 중독을 본다
LOCAL = EJACULATIONCHECK_ABL_31_2(TARGET, LOCAL)

;TALENT:혀사용을 본다
SIF TALENT:혀놀림
    TIMES LOCAL , 2.00
;TALENT:고양이혀을 본다
SIF TALENT:고양이혀
    TIMES LOCAL , 0.70

;조교자의 C감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
    BASE:PLAYER:사정 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 15
DOWNBASE:기력 += 160

SOURCE:22 = 2000
SOURCE:31 = 1000

;ABL:봉사 정신을 본다
IF ABL:봉사정신 == 0
    SOURCE:15 = 420
    SOURCE:16 = 150
    SOURCE:30 = 400
ELSEIF ABL:봉사정신 == 1
    SOURCE:15 = 500
    SOURCE:16 = 300
    SOURCE:30 = 300
ELSEIF ABL:봉사정신 == 2
    SOURCE:15 = 580
    SOURCE:16 = 600
    SOURCE:30 = 150
ELSEIF ABL:봉사정신 == 3
    SOURCE:15 = 660
    SOURCE:16 = 900
    SOURCE:30 = 50
ELSEIF ABL:봉사정신 == 4
    SOURCE:15 = 740
    SOURCE:16 = 1500
    SOURCE:30 = 20
ELSE
    SOURCE:15 = 820
    SOURCE:16 = 2200
    SOURCE:30 = 0
ENDIF

;ABL:B감각을 본다
IF ABL:B감각 == 0
    LOCAL:1 = 100
ELSEIF ABL:B감각 == 1
    LOCAL:1 = 200
ELSEIF ABL:B감각 == 2
    LOCAL:1 = 400
ELSEIF ABL:B감각 == 3
    LOCAL:1 = 800
ELSEIF ABL:B감각 == 4
    LOCAL:1 = 1200
ELSE
    LOCAL:1 = 1500
ENDIF

;B민감, B둔감
IF TALENT:B민감
    TIMES LOCAL:1 , 1.20
ELSEIF TALENT:B둔감
    TIMES LOCAL:1 , 0.70
ENDIF
;큰 가슴
SIF TALENT:거유
    TIMES LOCAL:1 , 1.20
;음유
SIF TALENT:음유
    TIMES LOCAL:1 , 1.40

SOURCE:3 += LOCAL:1

;ABL:기교를 본다
IF ABL:기교 == 0
    TIMES SOURCE:15 , 0.50
    TIMES SOURCE:16 , 0.50
ELSEIF ABL:기교 == 1
    TIMES SOURCE:15 , 0.80
    TIMES SOURCE:16 , 0.80
ELSEIF ABL:기교 == 2
    TIMES SOURCE:15 , 1.00
    TIMES SOURCE:16 , 1.00
ELSEIF ABL:기교 == 3
    TIMES SOURCE:15 , 1.20
    TIMES SOURCE:16 , 1.20
ELSEIF ABL:기교 == 4
    TIMES SOURCE:15 , 1.50
    TIMES SOURCE:16 , 1.50
ELSE
    TIMES SOURCE:15 , 2.00
    TIMES SOURCE:16 , 2.00
ENDIF

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)
LOCAL:2 = TFLAG:91

IF TFLAG:91
;사정하고 있다
    TIMES SOURCE:15 , 3.00

    ;ABL:정액 중독을 본다
    IF ABL:정액중독 == 0
        SOURCE:24 = 0
        TIMES SOURCE:16 , 2.00
        TIMES SOURCE:22, 6.00
    ELSEIF ABL:정액중독 == 1
        SOURCE:24 = 500
        TIMES SOURCE:16 , 3.00
        TIMES SOURCE:22, 4.50
    ELSEIF ABL:정액중독 == 2
        SOURCE:24 = 1200
        TIMES SOURCE:16 , 4.00
        TIMES SOURCE:22, 3.50
    ELSEIF ABL:정액중독 == 3
        SOURCE:24 = 3000
        TIMES SOURCE:16 , 6.00
        TIMES SOURCE:22, 3.00
    ELSEIF ABL:정액중독 == 4
        SOURCE:24 = 6000
        TIMES SOURCE:16 , 9.00
        TIMES SOURCE:22, 2.00
    ELSE
        SOURCE:24 = 12000
        TIMES SOURCE:16 , 15.00
        TIMES SOURCE:22, 1.50
    ENDIF

    ;사정처 타입의 설정
    TFLAG:703 = 4
    ;사정처 선택의 가능·불가
    TFLAG:704 = 0
    ;범용적인 처리는 이 함수로 실시한다
    ;(사정 게이지의 재계산이나 사정 경험의 상승과 공통 부분의 표시)
    CALL SAMEN_SHOOT
ENDIF

TCVAR:22 += 1

;노예의 입⇔조교자의 P의 불결이 이동
STAIN:입 |= STAIN:PLAYER:자지
STAIN:PLAYER:자지 |= STAIN:입
;노예의 가슴⇔조교자의 P의 불결이 이동
STAIN:가슴 |= STAIN:PLAYER:자지
STAIN:PLAYER:자지 |= STAIN:가슴

;봉사 정신 LV2 이상, 기교 LV2 이상이라면 P의 더러움을 없는 취한다
;다만 대상이 고양이혀의 경우, 고양이 펠라 패치 유효해 게다가
;봉사 정신 LV4 이상, 기교 LV5, 순종 LV3 이상 없으면 않은 취하지 않는다
IF (TALENT:고양이혀 == 0 && ABL:봉사정신 >= 2 && ABL:기교 >= 2) || (TALENT:고양이혀 && FLAG:조교모드 & 16 && ABL:봉사정신 >= 4 && ABL:기교 >= 5 && ABL:순종 >=3)
    STAIN:PLAYER:자지 = 2
    SIF LOCAL:2 >= 1
        TFLAG:21 = 1
ENDIF

IF TALENT:남자 == 0 && TALENT:PLAYER:남자 == 0
    TCVAR:12 += 7
ELSEIF TALENT:남자 && TALENT:PLAYER:남자
    TCVAR:13 += 7
ENDIF
;주인 경험 플래그
SIF ASSIPLAY == 0 && EXP:펠라경험 >= EXPLV:3
    TFLAG:50 += 1

;노예의 B⇔조교자의 P의 불결이 이동
STAIN:가슴 |= STAIN:PLAYER:자지
STAIN:PLAYER:자지 |= STAIN:가슴
;봉사 쾌락 경험 입수 판정
TFLAG:100 |= 1
;조교 대상의 파이즈리 실행 회수
CFLAG:파이즈리한회수 += 1
;조교자의 파이즈리 능숙 회수
CFLAG:PLAYER:파이즈리받은회수 += 1

;조교자가 후타나리
SIF TALENT:PLAYER:후타나리
    SOURCE:22 /= 2

RETURN 1

;--------------------------------------------------
;명령 거부의 메시지
;--------------------------------------------------
@DENIAL_MESSAGE_COM_209
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:지문소거판정 == 1 || RESULT != 0
    RETURN 0

PRINTL
PRINTFORML %CALLNAME:TARGET%에게 그 가슴과 혀로 봉사하도록 명했지만 거절당했다.
PRINTW 아직 조금 더 조교가 필요한 것 같다.

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM209
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:지문소거판정 == 1 || RESULT != 0
    RETURN 0

PRINTL
;고양이혀 파이즈리펠라
IF TALENT:고양이혀
    PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
    SIF TALENT:연모 == 0 && TALENT:복종 == 0 && TALENT:음란 == 0 && MARK:반발각인 > 1
        PRINT 그야말로 싫다는 표정을 보이며, 
    SIF TALENT:연모
        PRINT 넋이 나간 얼굴로 
    IF TALENT:빈유
        PRINTL 납작한 가슴 뿐만이 아니라 입술도 사용해
        PRINTW 열심히 페니스 전체를 자극하려고 하지만, 어려운 것 같다….
    ELSEIF TALENT:거유
        PRINTL 페니스를 풍만한 가슴으로 감싸,
        PRINTW 남은 귀두를 혀끝에서 빨아 자극하고 있다….
    ELSEIF TALENT:폭유
        PRINTL 지나치게 풍만한 가슴으로 페니스를 숨겨 버려,
        PRINTW 자신의 가슴에 얼굴을 돌진해 페니스에 키스를 반복하고 있다….
    ELSE
        PRINTL 가슴으로 페니스를 끼워,
        PRINTW 남은 부분을 입에 넣어, 자극하고 있다….
    ENDIF
;파이즈리펠라
ELSE
    PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
    SIF TALENT:연모 == 0 && TALENT:복종 == 0 && TALENT:음란 == 0 && MARK:반발각인 > 1
        PRINT 그야말로 싫다는 표정을 보이며 
    SIF TALENT:연모
        PRINT 넋이 나간 얼굴로 
    IF TALENT:빈유
        PRINTL 납작한 가슴 뿐만이 아니라 입도 사용해
        PRINTW 열심히 페니스 전체를 자극하려고 하지만, 어려운 것 같다….
    ELSEIF TALENT:거유
        PRINTL 페니스를 풍만한 가슴으로 감싸
        PRINTW 남은 귀두를 빨아 자극하고 있다….
    ELSEIF TALENT:폭유
        PRINTL 지나치게 풍만한 가슴으로 페니스를 숨겨 버려
        PRINTW 자신의 가슴에 얼굴을 가져가 페니스를 빨고 있다….
    ELSE
        PRINTL 가슴으로 페니스를 끼우고
        PRINTW 남은 부분을 입에 넣어 자극하고 있다….
    ENDIF
ENDIF

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_209, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:욕망
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:욕망
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99})
ENDIF
;ABL:봉사 정신
IF ABL:봉사정신
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:봉사정신
    TFLAG:240 += LOCAL:99 * 4
    RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:정액 중독
IF ABL:정액중독
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:정액중독
    TFLAG:240 += LOCAL:99 * 3
    RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;쾌락 각인
IF MARK:쾌락각인
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = MARK:쾌락각인
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99})
ENDIF

;PALAM:욕정
GETPALAMLV PALAM:13, 5
LOCAL:2 = RESULT
IF LOCAL:2
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = LOCAL:2
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;부끄럼쟁이
IF TALENT:부끄럼쟁이
    RESULTS = %RESULTS% - 
    LOCAL:99 = 3
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;남자 싫다
IF TALENT:남성혐오 && TALENT:PLAYER:남자
    RESULTS = %RESULTS% - 
    LOCAL:99 = 12
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;여자 증오
ELSEIF TALENT:여성혐오 && TALENT:PLAYER:남자 == 0
    RESULTS = %RESULTS% - 
    LOCAL:99 = 12
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;악취 둔감
IF TALENT:냄새둔감
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 1
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:61%({LOCAL:99})
;악취 민감
ELSEIF TALENT:냄새민감
    RESULTS = %RESULTS% - 
    LOCAL:99 = 3
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:62%({LOCAL:99})
ENDIF
;헌신적
IF TALENT:헌신적
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 6
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:63%({LOCAL:99})
ENDIF
;쾌감의 부정
IF TALENT:쾌감을부정
    RESULTS = %RESULTS% - 
    LOCAL:99 = 3
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;연모
IF TALENT:연모 && ASSIPLAY == 0
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 5
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF
;음유
IF TALENT:음유
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 8
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:174%({LOCAL:99})
ENDIF

;조교자가 후타나리
IF TALENT:PLAYER:후타나리
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 8
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %조사처리(CALLNAME:PLAYER,"가")% %TALENTNAME:121%({LOCAL:99})
ENDIF

;조교자의 P의 더러움
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)

;서로 불결
IF TFLAG:242
    RESULTS = %RESULTS% - 
    TFLAG:240 -= TFLAG:242
    ;악취 둔감
    IF TALENT:냄새둔감
        RESULTS = %RESULTS% 서로 불결,
        PRINTS TALENTNAME:61 
    ELSEIF TALENT:냄새민감
        RESULTS = %RESULTS% 서로 불결,
        PRINTS TALENTNAME:62 
    ELSE
        RESULTS = %RESULTS% 서로 불결
    ENDIF
    RESULTS = %RESULTS%({TFLAG:242})
    LOCAL:99 = 1
ENDIF

TFLAG:241 = 35
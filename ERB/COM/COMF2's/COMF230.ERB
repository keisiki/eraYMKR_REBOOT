;=============================================================================
;이라마치오
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE230
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:230 > 0
    RETURN 0
;조교자에게 페니스가 없으면 불가
SIF EXIST_BAR(PLAYER) == 0
    RETURN 0
;[猫舌]だとダメ
SIF TALENT:54
    RETURN 0
;주인이 [금단의 지식]持ちじゃないとき, 体格差が2段階以内か조교자가 [금단의 지식]持ちじゃないとダメ
SIF TALENT:MASTER:56 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
    RETURN 0
;순종, 욕망의 합계가 6이상 없을 경우 개구기 및 밧줄을 쓰지 않으면 불가. T(170214)
SIF ABL:순종 + ABL:욕망 < 6 && (TEQUIP:42 != 3 || TEQUIP:40 == 0)
    RETURN 0
;수면 중에는 불가
SIF EQUIP:14
    RETURN 0
;실신 중에는 불가
SIF TFLAG:899 > 0
    RETURN 0
;볼 개그 사용 중에는 불가(개구기 제외). T(170214)
SIF TEQUIP:42 != 0 && TEQUIP:42 != 3
    RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
    RETURN 0
;의사 선생님 플레이 중에는 불가
SIF TEQUIP:57
    RETURN 0
;조수가 훈도시 착용 중일 경우에는 불가
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17
    RETURN 0
;알몸 앞치마 중에는 불가
SIF TEQUIP:59 == 1
    RETURN 0
;나체정식 중에는 불가
SIF TEQUIP:60
    RETURN 0
;시간정지 중에는 무리
SIF TEQUIP:71
    RETURN 0
;촉수 조교중일 시 주인의 촉수사역Lv2이하는 안 됨
SIF TEQUIP:90 && ABL:MASTER:98 <= 2
    RETURN 0
;촉수 관통중은 안됨
SIF TEQUIP:90 && TEQUIP:25 == 3
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM230
;전회의 조교가 둘이서 범하는 것이라고 하고 둘이서 범하는에
TFLAG:132 = 0
IF PREVCOM == 610
    CALL EXCOM_ABLE610
    IF RESULT == 1
        TFLAG:132 = 1
        JUMP COM610
    ENDIF
;조교자가 전회:조수로 이번:주인인가, 전회:주인으로 이번:조수 때
ELSEIF (ASSIPLAY && TFLAG:133 == 0) || (ASSIPLAY == 0 && TFLAG:133)
    ;전회의 조교가 정상위나 후배위나 후배위 애널인가 둘이서 범하는 것 때는 둘이서 범하는에
    IF PREVCOM == 20 || PREVCOM == 21 || PREVCOM == 31 || PREVCOM == 610
        CALL EXCOM_ABLE610
        SIF RESULT == 1
            JUMP COM610
    ENDIF
ENDIF

;전회와 이번 조교자가 같아 새드끼 LV3 이상이나 욕망 LV3 이상으로 줄사용중이 아니고 전회의 조교가 이라마치오 때
IF PREVCOM == 230 && TEQUIP:40 == 0 && (ABL:PLAYER:20 > 2 || ABL:PLAYER:11 > 2) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    PRINTL 딥스로트
ELSE
    PRINTL 이라마치오
ENDIF

;-------------------------------------------------
;실행할 수 있을까의 판정(개구기 및 속박이 있을 경우 무시. T(170214))
;-------------------------------------------------
IF TFLAG:699 == 0 && TEQUIP:42 == 0 && TEQUIP:40 == 0
    CALL COM_ORDER
    ;실행할 수 없다
    SIF RESULT == 0
        RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------
;전회와 이번 조교자가 같아 새드끼 LV3 이상이나 욕망 LV3 이상으로 줄사용중이 아니고 전회의 조교가 이라마치오 때
IF PREVCOM == 230 && TEQUIP:40 == 0 && (ABL:PLAYER:20 > 2 || ABL:PLAYER:11 > 2) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    COM_NAME = 딥스로트
ELSE
    COM_NAME = 이라마치오
ENDIF
CALL TRAIN_MESSAGE_COM230

;-------------------------------------------------
;사정 게이지 체크
;-------------------------------------------------
LOCAL = 0

;ABL:기교를 본다
IF ABL:기교 == 0
    LOCAL = 1200
ELSEIF ABL:기교 == 1
    LOCAL = 1700
ELSEIF ABL:기교 == 2
    LOCAL = 2300
ELSEIF ABL:기교 == 3
    LOCAL = 3000
ELSEIF ABL:기교 == 4
    LOCAL = 3600
ELSE
    LOCAL = 4200
ENDIF

;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;봉사 정신을 본다
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)
;정액 중독을 본다
LOCAL = EJACULATIONCHECK_ABL_31_1(TARGET, LOCAL)

;TALENT:혀사용을 본다
SIF TALENT:52
    TIMES LOCAL , 2.00
;TALENT:고양이혀을 본다(실행할 수 없지만 일단)
SIF TALENT:54
    TIMES LOCAL , 0.70

;조교자의 C감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
    BASE:PLAYER:2 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 200
DOWNBASE:1 += 150

SOURCE:20 = 200
SOURCE:22 = 1500
SOURCE:31 = 500
SOURCE:17 = 500

;위 쪽으로 계산한 더러운 데이터
SOURCE:30 = TFLAG:242*40 + 100

;ABL:봉사 정신을 본다
IF ABL:봉사정신 == 0
    SOURCE:15 = 420
    SOURCE:16 = 150
    TIMES SOURCE:30 , 4.00
ELSEIF ABL:봉사정신 == 1
    SOURCE:15 = 500
    SOURCE:16 = 300
    TIMES SOURCE:30 , 2.50
ELSEIF ABL:봉사정신 == 2
    SOURCE:15 = 580
    SOURCE:16 = 600
    TIMES SOURCE:30 , 1.50
ELSEIF ABL:봉사정신 == 3
    SOURCE:15 = 660
    SOURCE:16 = 900
    TIMES SOURCE:30 , 1.00
ELSEIF ABL:봉사정신 == 4
    SOURCE:15 = 740
    SOURCE:16 = 1500
    TIMES SOURCE:30 , 0.50
ELSE
    SOURCE:15 = 820
    SOURCE:16 = 2200
    TIMES SOURCE:30 , 0.10
ENDIF

;ABL:기교를 본다
IF ABL:기교 == 0
    TIMES SOURCE:15 , 0.50
    TIMES SOURCE:16 , 0.50
ELSEIF ABL:기교 == 1
    TIMES SOURCE:15 , 0.80
    TIMES SOURCE:16 , 0.80
ELSEIF ABL:기교 == 2
    TIMES SOURCE:15 , 1.00
    TIMES SOURCE:16 , 1.00
ELSEIF ABL:기교 == 3
    TIMES SOURCE:15 , 1.20
    TIMES SOURCE:16 , 1.20
ELSEIF ABL:기교 == 4
    TIMES SOURCE:15 , 1.50
    TIMES SOURCE:16 , 1.50
ELSE
    TIMES SOURCE:15 , 2.00
    TIMES SOURCE:16 , 2.00
ENDIF

;전회와 이번 조교자가 같아 새드끼 LV3 이상이나 욕망 LV3 이상으로 줄사용중이 아니고 전회의 조교가 이라마치오 때
IF PREVCOM == 230 && TEQUIP:40 == 0 && (ABL:PLAYER:20 > 2 || ABL:PLAYER:11 > 2) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    TIMES DOWNBASE:0 , 1.10
    TIMES SOURCE:17 , 1.10
    TIMES SOURCE:16 , 1.10
    TIMES SOURCE:20 , 1.10
    TIMES SOURCE:31 , 1.10
ENDIF

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)
LOCAL:2 = TFLAG:91
;사정시의 처리
IF TFLAG:91 >= 1
    ;커맨드 마다 고유의 처리는 이 아래에 쓴다
    TIMES SOURCE:15 , 3.00

    ;ABL:정액 중독을 본다
    IF ABL:정액중독 == 0
        SOURCE:24 = 0
        TIMES SOURCE:16 , 2.00
        TIMES SOURCE:22, 4.00
    ELSEIF ABL:정액중독 == 1
        SOURCE:24 = 500
        TIMES SOURCE:16 , 3.00
        TIMES SOURCE:22, 3.00
    ELSEIF ABL:정액중독 == 2
        SOURCE:24 = 1200
        TIMES SOURCE:16 , 4.00
        TIMES SOURCE:22, 2.50
    ELSEIF ABL:정액중독 == 3
        SOURCE:24 = 3000
        TIMES SOURCE:16 , 6.00
        TIMES SOURCE:22, 2.00
    ELSEIF ABL:정액중독 == 4
        SOURCE:24 = 6000
        TIMES SOURCE:16 , 9.00
        TIMES SOURCE:22, 1.50
    ELSE
        SOURCE:24 = 12000
        TIMES SOURCE:16 , 15.00
        TIMES SOURCE:22, 1.00
    ENDIF

    ;사정처 타입의 설정
    TFLAG:703 = 4
    ;사정처 선택의 가능·불가
    TFLAG:704 = 0
    ;범용적인 처리는 이 함수로 실시한다
    ;(사정 게이지의 재계산이나 사정 경험의 상승과 공통 부분의 표시)
    CALL SAMEN_SHOOT
ENDIF

;사정에 관계없이 행해지는 처리는 여기로부터
TCVAR:22 += 1

;노예의 입⇔조교자의 P의 불결이 이동
STAIN:0 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:0

;봉사 정신 LV2 이상, 기교 LV2 이상이라면 P의 더러움을 없는 취한다
;다만 대상이 고양이혀의 경우, 고양이 펠라 패치 유효해 게다가
;봉사 정신 LV4 이상, 기교 LV5, 순종 LV3 이상 없으면 않은 취하지 않는다
IF (TALENT:고양이혀 == 0 && ABL:봉사정신 >= 2 && ABL:기교 >= 2) || (TALENT:고양이혀 && FLAG:14 & 16 && ABL:봉사정신 >= 4 && ABL:기교 >= 5 && ABL:순종 >=3)
    STAIN:PLAYER:2 = 2
    SIF LOCAL:2 >= 1
        TFLAG:21 = 1
ENDIF

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
    TCVAR:12 += 7
ELSEIF TALENT:120 && TALENT:PLAYER:120
    TCVAR:13 += 7
ENDIF
;주인 경험 플래그
SIF ASSIPLAY == 0 && EXP:22 >= EXPLV:3
    TFLAG:50 += 1

;조교자가 후타나리
SIF TALENT:PLAYER:121
    SOURCE:22 /= 2
;봉사 쾌락 경험 입수 판정
TFLAG:100 |= 1

RETURN 1

;--------------------------------------------------
;명령 거부의 메시지
;--------------------------------------------------
@DENIAL_MESSAGE_COM_230
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM230
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
;전회와 이번 조교자가 같아 새드끼 LV3 이상이나 욕망 LV3 이상으로 줄사용중이 아니고 전회의 조교가 이라마치오 때
IF PREVCOM == 230 && TEQUIP:40 == 0 && (ABL:PLAYER:20 > 2 || ABL:PLAYER:11 > 2) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    PRINTFORMW %조사선택(CALLNAME:PLAYER, "는")% 다시금 %CALLNAME:TARGET%의 목의 안쪽에 쑤셔 넣었다….
ELSE
    PRINTFORMW %조사선택(CALLNAME:PLAYER, "는")% 난폭하게 %CALLNAME:TARGET%의 목까지 찔러 넣었다….
ENDIF

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_230, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:욕망
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:욕망
    TFLAG:240 += LOCAL:99 * 3
    RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF
;ABL:봉사 정신
IF ABL:봉사정신
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:봉사정신
    TFLAG:240 += LOCAL:99 * 4
    RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:정액 중독
IF ABL:정액중독
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:정액중독
    TFLAG:240 += LOCAL:99 * 3
    RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;쾌락 각인
IF MARK:1
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = MARK:1
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99})
ENDIF

;PALAM:욕정
GETPALAMLV PALAM:13, 5
LOCAL:2 = RESULT
IF LOCAL:2
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = LOCAL:2 * 3
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;부끄럼쟁이
IF TALENT:35
    RESULTS = %RESULTS% - 
    LOCAL:99 = 1
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;악취 둔감
IF TALENT:61
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 1
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:61%({LOCAL:99})
;악취 민감
ELSEIF TALENT:62
    RESULTS = %RESULTS% - 
    LOCAL:99 = 3
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:62%({LOCAL:99})
ENDIF
;헌신적
IF TALENT:63
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 6
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:63%({LOCAL:99})
ENDIF
;쾌감의 부정
IF TALENT:71
    RESULTS = %RESULTS% - 
    LOCAL:99 = 1
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;연모
IF TALENT:150 && ASSIPLAY == 0
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 5
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF

;조교자가 후타나리
IF TALENT:PLAYER:121
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 8
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %조사처리(CALLNAME:PLAYER,"가")% %TALENTNAME:121%({LOCAL:99})
ENDIF

;조교자의 P의 더러움
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)

;서로 불결
IF TFLAG:242
    RESULTS = %RESULTS% - 
    TFLAG:240 -= TFLAG:242
    RESULTS = %RESULTS% 서로 불결
    ;악취 둔감
    IF TALENT:61
        RESULTS = %RESULTS%, %TALENTNAME:61%
    ELSEIF TALENT:62
        RESULTS = %RESULTS%, %TALENTNAME:62%
    ENDIF
    RESULTS = %RESULTS%({TFLAG:242})
    LOCAL:99 = 1
ENDIF

TFLAG:241 = 36

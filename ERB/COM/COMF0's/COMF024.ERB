;=============================================================================
;기승위
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE24
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:24 > 0
    RETURN 0
;V조교 필터가 오프가 되지 않으면 안 됨
SIF FLAG:VA이상조교필터 & 1
    RETURN 0
;조교자가 조수로 그 조수가 남자가 아닌 경우, 이상계 조교 필터가 오프가 되지 않으면 안 됨
SIF FLAG:VA이상조교필터 & 4 && ASSIPLAY && TALENT:PLAYER:120 == 0
    RETURN 0
;V경험이 낮은 경우, 이상계 조교 필터가 오프가 되지 않으면 안 됨
SIF FLAG:VA이상조교필터 & 4 && EXP:V경험 < EXPLV:1
    RETURN 0
;조교자에게 페니스가 없으면 안 됨
SIF EXIST_BAR(PLAYER) == 0
    RETURN 0
;남자라면 안 됨
SIF TALENT:120
    RETURN 0
;주인이 [금단의 지식] 소유가 아닐 때, 체격차이가 2 단계 이내나 조교자가 [금단의 지식] 소유가 아니면 안 됨
SIF TALENT:MASTER:56 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
    RETURN 0
;수면중은 불가
SIF EQUIP:14
    RETURN 0
;실신중은 불가
SIF TFLAG:899 > 0
    RETURN 0
;V계 장착도구 사용중은 불가
SIF TEQUIP:20 || TEQUIP:21
    RETURN 0
;A계 장착도구 사용중의 경우 조교자의 기교 4이상 필수
SIF (TEQUIP:25 || TEQUIP:26) && ABL:PLAYER:기교 < 4
    RETURN 0
;밧줄 사용 중에는 불가
SIF TEQUIP:40
    RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
    RETURN 0
;목욕탕 플레이중은 플레이 매트가 없으면 안 됨
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
    RETURN 0
;한증과 암반 욕 이외의 00목욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
    RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
    RETURN 0
;훈도시 착용중의 경우 조교자의 기교 4이상 필수
SIF TEQUIP:59 == 17 && ABL:PLAYER:기교 < 4
    RETURN 0
;조수가 훈도시 착용중의 경우 기교 4이상 필수
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17 && ABL:기교 < 4
    RETURN 0
;나체정식 중에는 안 됨
SIF TEQUIP:60
    RETURN 0
;시간정지 중에는 무리
SIF TEQUIP:71
    RETURN 0
;촉수 조교중일 시 주인의 촉수사역Lv2이하는 안 됨
SIF TEQUIP:90 && ABL:MASTER:촉수사역 <= 2
    RETURN 0
;인어는 안됨
SIF TALENT:229 == 1
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM24

;수레 가마 파생 분기를 추가
;COM_ABLE 이후는 자궁구 공격 분기로부터 이끌어 왔습니다
;강제 실행시는 여기는 절찬 스르
IF TFLAG:699 == 0
    CALL COM_ABLE430
    IF RESULT == 1
        IF FLAG:조교모드 & 8
            PRINTL 수레가마 자세로 할까?
            PRINTL [0] - 물논!
            PRINTL [1] - 그거 뭐야 무서워
            $INPUT_LOOP
            INPUT
            IF RESULT == 0
                JUMP COM430
            ELSEIF RESULT == 1
                
            ELSE
                GOTO INPUT_LOOP
            ENDIF
        ELSE
            ;PALAM:욕정을 본다
            IF PALAM:13 < PALAMLV:1
                LOCAL = 1
            ELSEIF PALAM:13 < PALAMLV:2
                LOCAL = 2
            ELSEIF PALAM:13 < PALAMLV:3
                LOCAL = 3
            ELSEIF PALAM:13 < PALAMLV:4
                LOCAL = 4
            ELSE
                LOCAL = 5
            ENDIF
            LOCAL:1 = RAND:11
            LOCAL *= LOCAL:1
            LOCAL:1 = RAND:11
            LOCAL += (LOCAL:1) *ABL:V감각
            ;LOCAL의 값은 0~100
            ;기승위라면 50이상으로 수레 가마
            IF LOCAL >= 50
                JUMP COM430
            ENDIF
        ENDIF
    ENDIF
ENDIF

PRINTL 기승위 

;-------------------------------------------------
;실행할 수 있을까의 판정
;-------------------------------------------------
IF TFLAG:699 == 0
    CALL COM_ORDER
    ;실행할 수 없다
    SIF RESULT == 0
        RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------

;전의 플레이가 기승위 또한 조교자의 기교 3이상의 경우, 승마 G스포 꾸짖어 또는 승마 자궁구 고문에 파생
;V감각이 높게 욕정하고 있는 만큼 자궁구에 가기 쉽다
IF FLAG:커맨드확장 & 2
    IF PREVCOM == 24 && ABL:PLAYER:기교 > 2 && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
        ;랜덤 배제시
        IF FLAG:조교모드 & 8
            PRINTL 어디를 괴롭힐까?
            PRINTL [0] - G스팟
            PRINTL [1] - 자궁구
            $INPUT_LOOP_1
            INPUT
            IF RESULT == 0
                JUMP COM614
            ELSEIF RESULT == 1
                JUMP COM615
            ELSE
                GOTO INPUT_LOOP_1
            ENDIF
        ELSE
            ;PALAM:욕정을 본다
            IF PALAM:13 < PALAMLV:1
                LOCAL = 1
            ELSEIF PALAM:13 < PALAMLV:2
                LOCAL = 2
            ELSEIF PALAM:13 < PALAMLV:3
                LOCAL = 3
            ELSEIF PALAM:13 < PALAMLV:4
                LOCAL = 4
            ELSE
                LOCAL = 5
            ENDIF
            LOCAL:1 = RAND:11
            LOCAL *= LOCAL:1
            LOCAL:1 = RAND:11
            LOCAL += (LOCAL:1) *ABL:V감각
            ;LOCAL의 값은 0~100
            ;기승위라면 50이상으로 자궁구
            IF LOCAL >= 50
                JUMP COM615
            ELSE
                JUMP COM614
            ENDIF
        ENDIF
    ENDIF
ENDIF

COM_NAME = 기승위
CALL TRAIN_MESSAGE_COM024

;V경험을 수반하는 커맨드의 플래그(처녀막 재생 대응)
TFLAG:1 = 1
SIF TALENT:처녀 && TALENT:150 && ASSIPLAY == 0
    TFLAG:2 = TALENT:처녀

;-------------------------------------------------
;사정 게이지 체크
;-------------------------------------------------
LOCAL = 0

;ABL:기교를 본다
IF ABL:기교 == 0
    LOCAL = 1000
ELSEIF ABL:기교 == 1
    LOCAL = 1300
ELSEIF ABL:기교 == 2
    LOCAL = 1700
ELSEIF ABL:기교 == 3
    LOCAL = 2200
ELSEIF ABL:기교 == 4
    LOCAL = 3000
ELSE
    LOCAL = 4500
ENDIF

;윤활을 본다
LOCAL = EJACULATIONCHECK_PALAM_10_3(TARGET, LOCAL)
;욕정을 본다
LOCAL = EJACULATIONCHECK_PALAM_13_1(TARGET, LOCAL)
;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;욕망을 본다
LOCAL = EJACULATIONCHECK_ABL_11_1(TARGET, LOCAL)
;봉사 정신을 본다
LOCAL = EJACULATIONCHECK_ABL_13_2(TARGET, LOCAL)

;조교자의 C감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

;EXP:V경험을 본다
;V경험이 많아지면(자) 조금 내린다
IF EXP:V경험 < EXPLV:1
    TIMES LOCAL , 1.50
    IF ASSIPLAY && TALENT:PLAYER:120 == 0
        TCVAR:50 += 3
    ELSE
        TCVAR:50 += 2
    ENDIF
ELSEIF EXP:V경험 < EXPLV:2
    TIMES LOCAL , 1.00
ELSEIF EXP:V경험 < EXPLV:3
    TIMES LOCAL , 0.90
ELSEIF EXP:V경험 < EXPLV:4
    TIMES LOCAL , 0.80
ELSEIF EXP:V경험 < EXPLV:5
    TIMES LOCAL , 0.70
ELSE
    TIMES LOCAL , 0.60
ENDIF

SIF EXIST_PENIS(PLAYER)
    BASE:PLAYER:사정 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 60
DOWNBASE:기력 += 150

SOURCE:12 = 900
SOURCE:14 = 2500

;위 쪽으로 계산한 더러운 데이터
SOURCE:30 = TFLAG:242*10 + 60

;ABL:봉사 정신을 본다
IF ABL:봉사정신 == 0
    SOURCE:15 = 200
    SOURCE:16 = 50
    SOURCE:31 = 300
    TIMES SOURCE:30 , 4.00
ELSEIF ABL:봉사정신 == 1
    SOURCE:15 = 250
    SOURCE:16 = 200
    SOURCE:31 = 100
    TIMES SOURCE:30 , 2.50
ELSEIF ABL:봉사정신 == 2
    SOURCE:15 = 350
    SOURCE:16 = 550
    SOURCE:31 = 30
    TIMES SOURCE:30 , 1.50
ELSEIF ABL:봉사정신 == 3
    SOURCE:15 = 450
    SOURCE:16 = 900
    SOURCE:31 = 0
    TIMES SOURCE:30 , 1.00
ELSEIF ABL:봉사정신 == 4
    SOURCE:15 = 600
    SOURCE:16 = 1500
    SOURCE:31 = 0
    TIMES SOURCE:30 , 0.50
ELSE
    SOURCE:15 = 750
    SOURCE:16 = 2200
    SOURCE:31 = 0
    TIMES SOURCE:30 , 0.10
ENDIF

;ABL:V감각을 본다
IF ABL:V감각 == 0
    SOURCE:1 = 20
    SOURCE:11 = 25
    TIMES SOURCE:15 , 0.50
ELSEIF ABL:V감각 == 1
    SOURCE:1 = 75
    SOURCE:11 = 75
    TIMES SOURCE:15 , 0.80
ELSEIF ABL:V감각 == 2
    SOURCE:1 = 200
    SOURCE:11 = 125
    TIMES SOURCE:15 , 1.00
ELSEIF ABL:V감각 == 3
    SOURCE:1 = 500
    SOURCE:11 = 175
    TIMES SOURCE:15 , 1.20
ELSEIF ABL:V감각 == 4
    SOURCE:1 = 850
    SOURCE:11 = 300
    TIMES SOURCE:15 , 1.50
ELSE
    SOURCE:1 = 1100
    SOURCE:11 = 425
    TIMES SOURCE:15 , 2.00
ENDIF

;EXP:V경험을 본다
IF EXP:V경험 < EXPLV:1
    TIMES SOURCE:1 , 0.20
    SOURCE:20 = 5000
    SOURCE:12 = 12000
    SOURCE:31 = 120000
ELSEIF EXP:V경험 < EXPLV:2
    TIMES SOURCE:1 , 0.60
    SOURCE:20 = 220
ELSEIF EXP:V경험 < EXPLV:3
    TIMES SOURCE:1 , 1.00
    SOURCE:20 = 30
ELSEIF EXP:V경험 < EXPLV:4
    TIMES SOURCE:1 , 1.20
    SOURCE:20 = 5
ELSEIF EXP:V경험 < EXPLV:5
    TIMES SOURCE:1 , 1.40
ELSE
    TIMES SOURCE:1 , 1.50
ENDIF

;PALAM:윤활
IF PALAM:10 < PALAMLV:1
    TIMES SOURCE:1 , 0.20
    SOURCE:20 += 900
    TIMES SOURCE:20 , 3.00
    SOURCE:12 += 2000
ELSEIF PALAM:10 < PALAMLV:2
    TIMES SOURCE:1 , 0.60
    SOURCE:20 += 250
    TIMES SOURCE:20 , 1.00
    SOURCE:12 += 400
ELSEIF PALAM:10 < PALAMLV:3
    TIMES SOURCE:1 , 1.00
    TIMES SOURCE:20 , 0.50
ELSEIF PALAM:10 < PALAMLV:4
    TIMES SOURCE:1 , 1.30
    TIMES SOURCE:20 , 0.30
ELSEIF PALAM:10 < PALAMLV:5
    TIMES SOURCE:1 , 1.60
    TIMES SOURCE:20 , 0.20
ELSE
    TIMES SOURCE:1 , 2.00
    TIMES SOURCE:20 , 0.10
ENDIF

;조교자가[남자]
SIF TALENT:PLAYER:120
    TIMES SOURCE:1 , 2.50

;[작은체형]
SIF TALENT:128
    TIMES SOURCE:20 , 2.00

;정조관념
IF TALENT:30
    IF TALENT:처녀 == 1
        TIMES SOURCE:11 , 0.60
        TIMES SOURCE:31, 15.00
        SOURCE:32 = 10000
    ELSE
        TIMES SOURCE:11 , 0.60
        TIMES SOURCE:31, 15.00
        SOURCE:32 = 1000
    ENDIF
;정조무구애
ELSEIF TALENT:31
    IF TALENT:처녀 == 1
        TIMES SOURCE:31, 0.50
        SOURCE:32 = 300
    ELSE
        TIMES SOURCE:31, 0.50
    ENDIF
ELSE
    SIF TALENT:처녀 == 1
        SOURCE:32 = 3000
ENDIF

;PALAM:욕정
IF PALAM:13 < PALAMLV:1
    TIMES SOURCE:1 , 0.60
    TIMES SOURCE:11 , 0.30
ELSEIF PALAM:13 < PALAMLV:2
    TIMES SOURCE:1 , 0.80
    TIMES SOURCE:11 , 0.60
ELSEIF PALAM:13 < PALAMLV:3
    TIMES SOURCE:1 , 1.00
    TIMES SOURCE:11 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
    TIMES SOURCE:1 , 1.20
    TIMES SOURCE:11 , 1.50
ELSEIF PALAM:13 < PALAMLV:5
    TIMES SOURCE:1 , 1.40
    TIMES SOURCE:11 , 1.80
ELSE
    TIMES SOURCE:1 , 1.60
    TIMES SOURCE:11 , 2.20
ENDIF

;ABL:순종을 본다
IF ABL:순종 == 0
    TIMES SOURCE:1 , 0.50
    TIMES SOURCE:11 , 0.60
    TIMES SOURCE:32 , 2.00
    SOURCE:22 += 700
ELSEIF ABL:순종 == 1
    TIMES SOURCE:1 , 0.80
    TIMES SOURCE:11 , 0.80
    TIMES SOURCE:32 , 1.50
    SOURCE:22 += 500
ELSEIF ABL:순종 == 2
    TIMES SOURCE:1 , 1.00
    TIMES SOURCE:11 , 1.00
    TIMES SOURCE:32 , 1.00
    SOURCE:22 += 300
ELSEIF ABL:순종 == 3
    TIMES SOURCE:1 , 1.30
    TIMES SOURCE:11 , 1.20
    TIMES SOURCE:32 , 0.80
    SOURCE:22 += 100
ELSEIF ABL:순종 == 4
    TIMES SOURCE:1 , 1.60
    TIMES SOURCE:11 , 1.40
    TIMES SOURCE:32 , 0.60
    SOURCE:22 += 30
ELSE
    TIMES SOURCE:1 , 2.00
    TIMES SOURCE:11 , 1.60
    TIMES SOURCE:32 , 0.30
    SOURCE:22 += 0
ENDIF

;ABL:기교를 본다
IF ABL:기교 == 0
    TIMES SOURCE:1 , 0.50
    TIMES SOURCE:11 , 0.60
ELSEIF ABL:기교 == 1
    TIMES SOURCE:1 , 0.80
    TIMES SOURCE:11 , 0.80
ELSEIF ABL:기교 == 2
    TIMES SOURCE:1 , 1.00
    TIMES SOURCE:11 , 1.00
ELSEIF ABL:기교 == 3
    TIMES SOURCE:1 , 1.30
    TIMES SOURCE:11 , 1.20
ELSEIF ABL:기교 == 4
    TIMES SOURCE:1 , 1.60
    TIMES SOURCE:11 , 1.40
ELSE
    TIMES SOURCE:1 , 2.00
    TIMES SOURCE:11 , 1.60
ENDIF

TCVAR:0 += 1
IF PLAYER == MASTER
    TCVAR:42 += 2
    SIF EXIST_BAR(MASTER)
        CFLAG:173 += 1
ENDIF

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;사정시의 처리
IF TFLAG:91 >= 1
    ;커맨드 마다 고유의 처리는 이 아래에 쓴다
    TIMES SOURCE:15 , 3.00
    TIMES SOURCE:16 , 5.00
    TIMES SOURCE:22 , 1.50

    ;ABL:정액 중독을 본다
    IF ABL:정액중독 == 0
        SOURCE:24 = 0
    ELSEIF ABL:정액중독 == 1
        SOURCE:24 = 300
    ELSEIF ABL:정액중독 == 2
        SOURCE:24 = 1000
    ELSEIF ABL:정액중독 == 3
        SOURCE:24 = 1500
    ELSEIF ABL:정액중독 == 4
        SOURCE:24 = 2200
    ELSE
        SOURCE:24 = 1000 + (ABL:정액중독 * 400)
    ENDIF
    ;사정처 타입의 설정
    TFLAG:703 = 1
    ;사정처 선택의 가능·불가
    TFLAG:704 = 1
    ;범용적인 처리는 이 함수로 실시한다
    ;(사정 게이지의 재계산이나 사정 경험의 상승과 공통 부분의 표시)
    CALL SAMEN_SHOOT
ENDIF

;사정에 관계없이 행해지는 처리는 여기로부터
STAIN:보지 |= STAIN:PLAYER:자지
STAIN:PLAYER:자지 |= STAIN:보지

SIF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
    TCVAR:12 += 7

;주인 경험 플래그
SIF ASSIPLAY == 0
    TFLAG:50 += (ABL:V감각 >= 3) ?  3 # 2

;봉사 쾌락 경험 입수 판정
TFLAG:100 |= 1

;조교자가 후타나리
SIF TALENT:PLAYER:121
    SOURCE:22 /= 2

;처녀 상실의 후에 페니스를 뽑고 있을까
TFLAG:101 = (TFLAG:101 == 2) ?  1 # 0
RETURN 1

;--------------------------------------------------
;명령 거부의 메시지
;--------------------------------------------------
@DENIAL_MESSAGE_COM_24
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
IF TEQUIP:68
    SIF PALAM:0 >= (PALAMLV:4 - 1) ||  PALAM:1 >= (PALAMLV:4 - 1) ||  PALAM:2 >= (PALAMLV:4 - 1) ||  PALAM:10 >= (PALAMLV:4 - 1)
        PRINT 지금 당장이라도 가버릴 것 같음에도 불구하고, 
    PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 명령을 거부했다….
ELSE
    PRINTFORML %CALLNAME:TARGET%에게 위에 올라타도록 명령해 보았지만 거부당했다.
    PRINTW 좀 더 조교가 필요한 것 같다.
ENDIF

;--------------------------------------------------
;조교시 메시지

;--------------------------------------------------
@TRAIN_MESSAGE_COM024
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
;애태우기플레이중에 자위계·기승위계·조수를 범하게 한다
IF TEQUIP:68
    IF TFLAG:699 == 0
        SIF PALAM:0 >= (PALAMLV:4 - 1) ||  PALAM:1 >= (PALAMLV:4 - 1) ||  PALAM:2 >= (PALAMLV:4 - 1) ||  PALAM:3 >= (PALAMLV:4 - 1)
            PRINT 당장이라도 가버릴 것 같은 
        PRINTFORML %조사선택(CALLNAME:TARGET, "가")% 좋아하는 대로 하게 하도록 했다….
    ELSE
        SIF PALAM:0 >= (PALAMLV:4 - 1) ||  PALAM:1 >= (PALAMLV:4 - 1) ||  PALAM:2 >= (PALAMLV:4 - 1) ||  PALAM:3 >= (PALAMLV:4 - 1)
            PRINT 더는 참을 수 없다듯이 
        PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 스스로 허리를 움직이기 시작했다….
    ENDIF
ENDIF

;전회의 조교도 섹스계 조교였던 경우로 조교 대상이 마음대로 커맨드를 실행하고 있는 경우
IF TFLAG:102 && (PREVCOM >= 20 && PREVCOM <= 24) && TFLAG:699 > 0
    PRINTFORML %조사선택(CALLNAME:PLAYER, "가")% 삽입한 채로 아무것도 하지 않으니, 만족하지 못한 %조사선택(CALLNAME:TARGET, "는")% 
    SIF TALENT:170 || TEQUIP:11 == 1
        PRINT 한층 더 쾌락을 탐욕스럽게 구하며 
    SIF TALENT:81
        PRINTFORM 피로를 호소하는 %조사선택(CALLNAME:PLAYER, "를")% 무시하며 
    SIF TALENT:157
        PRINT 질투의 증오에 미쳐 
    SIF TALENT:158 && TEQUIP:42 == 0
        PRINTL 망가진 듯이 웃어대며, 
    SIF PREVCOM >= 20 && PREVCOM <= 23
        PRINTFORM %조사선택(CALLNAME:PLAYER, "를")% 밀쳐 넘어뜨리고 위에 올라타 
    PRINTL 그대로 자신의 허리를 움직이기 시작했다….
    IF EXP:V경험 >= EXPLV:3
        PRINTL
        PRINTFORML 정말이지 지쳐버린 %조사선택(CALLNAME:PLAYER, "는")% 거의 움직이지 않는데도 구애 받지 않고, 
        PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
        SIF TALENT:128
            PRINT 작은 몸으로 열심히 
        SIF TALENT:112 || TALENT:113
            PRINT 커다란 가슴을 흔들어대며 
        PRINTL 남자를 범하는 쾌락에 몰두해, 계속 허리를 움직이고 있다….
        SIF TFLAG:699 == 3
            PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 완전히 페니스의 포로가 되어, 그대로 두면 말라 비틀어질 때까지 짜내길 멈추지 않을 것 같다….
    ENDIF
;조교 대상이 마음대로 커맨드를 실행하고 있는 경우
ELSEIF TFLAG:699 > 0
    PRINTFORML %조사선택(CALLNAME:PLAYER, "가")% 아무것도 하지 않자, 명령도 하지 않았는데 %조사선택(CALLNAME:TARGET, "는")% 
    SIF TALENT:170 || TEQUIP:11 == 1
        PRINT 격렬하게 페니스를 요구하며 
    SIF TALENT:81
        PRINTFORM 비웃는 듯한 조소\@(TEQUIP:42) ? 를 띠# 로 입가를 일그러뜨리 \@면서 
    SIF TALENT:157
        PRINT 질투에 미쳐 
    SIF TALENT:158 && TEQUIP:42 == 0
        PRINTL 망가진 듯이 웃어대며, 
    PRINTFORM %조사선택(CALLNAME:PLAYER, "를")% 밀어 넘어뜨리고, 스스로 페니스를 
    SIF PALAM:10 < PALAMLV:2
        PRINT 아직 젖지도 않은 
    IF TALENT:128
        PRINT 어린 균열
    ELSE
        PRINT 갈라진 틈
    ENDIF
    PRINTL 에 박은 그대로 눌렀다….
    IF EXP:V경험 >= EXPLV:3
        PRINTL
        PRINTFORML 갑작스런 일에 %조사선택(CALLNAME:PLAYER, "는")% 거의 움직이지 않는데도 구애 받지 않고 
        PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
        SIF TALENT:128
            PRINT 작은 몸으로 열심히 
        SIF TALENT:112 || TALENT:113
            PRINT 큰 가슴을 흔들면서 
        PRINTL 남자를 범하는 쾌락에 몰두해, 계속 허리를 움직이고 있다….
    ENDIF
;전회와 이번 조교자가 같아, 전회의 조교가 기승위이며, 뽑지 않는 플래그가 있는 경우
ELSEIF TFLAG:102 && PREVCOM == 24 && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 삽입된 채로 페니스를 단단히 조이며, 다시금 허리를 움직이기 시작했다….
    SIF TFLAG:101
        TFLAG:101 = 2
;같이 전회의 조교가 정상위나 후배위인 경우
ELSEIF TFLAG:102 && (PREVCOM == 20 || PREVCOM == 21) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    PRINTFORMW 질에서 빼지 않은 채로 %조사선택(CALLNAME:TARGET, "를")% 안아 올려 아래로부터 허리를 밀어 올렸다….
    SIF TFLAG:101
        TFLAG:101 = 2
;같이 전회의 조교가 대면좌위나 배면좌위인 경우
ELSEIF TFLAG:102 && (PREVCOM == 22 || PREVCOM == 23) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    PRINTFORMW %조사선택(CALLNAME:PLAYER, "는")% 삽입한 채로 누워, 아래로부터 허리를 밀어 올렸다….
ELSE
    IF EX:21
        PRINTFORM 참을성이 바닥났는지, %조사선택(CALLNAME:TARGET, "는")% 가쁜 숨을 내쉬며 %CALLNAME:PLAYER%에게 눌러댔
    ELSE
        PRINTFORM %조사선택(CALLNAME:TARGET, "는")% %CALLNAME:PLAYER%의 위에 올라 천천히 허리를 내렸
    ENDIF
    IF TALENT:128 && TALENT:처녀 == 1
        PRINTL 고, 남자를 모르는 어린 균열로 페니스를 삼켜 들었다….
    ELSEIF TALENT:처녀 == 1
        PRINTL 고, 남자를 모르는 보지로 페니스를 삼켜 들었다….
    ELSE
        PRINTL 다….
    ENDIF
    IF EXP:V경험 >= EXPLV:3
        PRINTL
        PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
        SIF TALENT:128
            PRINT 작은 몸으로 열심히 
        IF TALENT:112
            PRINT 커다란 가슴을 흔들어대며 
        ELSEIF TALENT:113
            PRINT 터질듯한 가슴을 흔들어대며 
        ENDIF
        SIF EX:21
            PRINT 탐닉하는 듯이 격렬하게 
        PRINT 자신의 허리를 움직이고 있다….
    ENDIF
    PRINTW
ENDIF

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_24, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:욕망
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:욕망
    TFLAG:240 += LOCAL:99 * 3
    RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF
;ABL:V감각
IF ABL:V감각
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:V감각
    TFLAG:240 += LOCAL:99 * 2
    RESULTS = %RESULTS% %ABLNAME:1%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF
;ABL:봉사 정신
IF ABL:봉사정신
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:봉사정신
    TFLAG:240 += LOCAL:99 * 4
    RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:정액 중독
IF ABL:정액중독
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:정액중독
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99})
ENDIF

;쾌락 각인
IF MARK:쾌락각인
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = MARK:쾌락각인
    TFLAG:240 += LOCAL:99 * 3
    RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;PALAM:윤활(의 부족)
IF PALAM:10 < PALAMLV:3
    RESULTS = %RESULTS% - 
    LOCAL:99 = 5
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% 윤활 부족({LOCAL:99})
ENDIF
;PALAM:욕정
GETPALAMLV PALAM:13, 5
LOCAL:2 = RESULT
IF LOCAL:2
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = LOCAL:2 * 3
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;부끄럼쟁이
IF TALENT:35
    RESULTS = %RESULTS% - 
    LOCAL:99 = 2
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;남자 싫다
IF TALENT:남성혐오 && TALENT:PLAYER:120
    RESULTS = %RESULTS% - 
    LOCAL:99 = 12
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;여자 증오
ELSEIF TALENT:여성혐오 && TALENT:PLAYER:120 == 0
    RESULTS = %RESULTS% - 
    LOCAL:99 = 12
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;쾌감의 부정
IF TALENT:71
    RESULTS = %RESULTS% - 
    LOCAL:99 = 5
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;연모
IF TALENT:150 && ASSIPLAY == 0
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 5
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF
;처녀
IF TALENT:처녀 == 1
    RESULTS = %RESULTS% - 
    LOCAL:99 = 20
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:0%({LOCAL:99})
ELSEIF EXP:V경험 < EXPLV:2
    RESULTS = %RESULTS% - 
    LOCAL:99 = 5
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %EXPNAME:0%부족({LOCAL:99})
ENDIF

;조교자가 후타나리
IF TALENT:PLAYER:121
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 4
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %조사처리(CALLNAME:PLAYER,"가")% %TALENTNAME:121%({LOCAL:99})
ENDIF

;미약
IF TEQUIP:11
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 6
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %ITEMNAME:61%({LOCAL:99})
ENDIF

;조교자의 P의 더러움
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)
;기승위에서는 더러움의 영향이 적은
TFLAG:242 /= 3

;서로불결
IF TFLAG:242
    RESULTS = %RESULTS% - 
    TFLAG:240 -= TFLAG:242
    RESULTS = %RESULTS% 서로불결
    ;악취 둔감
    IF TALENT:61
        RESULTS = %RESULTS%, %TALENTNAME:61%
    ELSEIF TALENT:62
        RESULTS = %RESULTS%, %TALENTNAME:62%
    ENDIF
    RESULTS = %RESULTS%({TFLAG:242})
    LOCAL:99 = 1
ENDIF

TFLAG:241 = 24

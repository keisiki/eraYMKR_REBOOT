;=============================================================================
;자궁탈주사
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE89
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:89 > 0
    RETURN 0
;강력 아이템·커맨드 무효시는 안 됨
SIF (FLAG:13 & 2) == 0
    RETURN 0
;주사계 커맨드를 유효하게 하지 않으면 안 됨
SIF (FLAG:15 & 64) == 0
    RETURN 0
;인체개조를 유효하게 하지 않으면 안 됨
SIF (FLAG:14 & 512) == 0
    RETURN 0
;주인이 금단의 지식이 없으면 불가
SIF TALENT:MASTER:56 == 0
    RETURN 0
;남자라면 안됨
SIF TALENT:120
    RETURN 0
;탈자궁이라면 안됨
SIF TALENT:3
    RETURN 0
;약물내성이라면 안 됨
SIF TALENT:44
    RETURN 0
;기계라면 안 됨
SIF TALENT:206
    RETURN 0
;기교 3 이하의 ASSI의 경우 주사할 수 없다(바늘 다루기라면 OK)
IF ASSIPLAY
    SIF ABL:ASSI:기교 <= 3 && TALENT:ASSI:53 == 0
        RETURN 0
ENDIF
;주사 세트와 질이완제를 가지고 있거나, 아이템 없음 설정이 아니면 안 됨
SIF (ITEM:53 == 0 || ITEM:72 == 0) && NOITEM == 0
    RETURN 0
;수면중은 불가
SIF EQUIP:14
    RETURN 0
;V계 장착도구 사용중은 불가
SIF TEQUIP:20
    RETURN 0
;한증과 암반 욕 이외의00목욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
    RETURN 0
;훈도시 착용중의 경우는 안 됨
SIF TEQUIP:59 == 17
    RETURN 0
;웨딩 드레스·고스로리 착용중의 경우는 안 됨
SIF TEQUIP:59 == 19 || TEQUIP:59 == 20
    RETURN 0
;알몸 앞치마, 학교수영복 플레이 중에는 불가
SIF TEQUIP:59 == 1 || TEQUIP:59 == 2
    RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
    RETURN 0
RETURN 1
;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM89
PRINTL 자궁탈주사
COM_NAME = 자궁탈주사
CALL TRAIN_MESSAGE_COM089

DOWNBASE:0 += 700
DOWNBASE:기력 += 1200

;질이완제 사용
IF NOITEM == 0
    ITEM:72 -= 1
    SIF FLAG:13 & 4 && MONEY:0 >= ITEMPRICE:72
        CALL FILL_PLURAL_ITEM, 72
ENDIF

SOURCE:1 = 500
SOURCE:25 = 5000
SOURCE:26 = 3000
SOURCE:31 = 7000

;ABL:순종을 본다
IF ABL:순종 == 0
    TIMES SOURCE:31 , 3.00
ELSEIF ABL:순종 == 1
    TIMES SOURCE:31 , 2.50
ELSEIF ABL:순종 == 2
    TIMES SOURCE:31 , 2.00
ELSEIF ABL:순종 == 3
    TIMES SOURCE:31 , 1.50
ELSEIF ABL:순종 == 4
    TIMES SOURCE:31 , 1.00
ELSE
    TIMES SOURCE:31 , 0.80
ENDIF

;ABL:V감각을 본다
IF ABL:V감각 == 0
    TIMES SOURCE:1 , 0.50
ELSEIF ABL:V감각 == 1
    TIMES SOURCE:1 , 1.00
ELSEIF ABL:V감각 == 2
    TIMES SOURCE:1 , 1.50
ELSEIF ABL:V감각 == 3
    TIMES SOURCE:1 , 2.00
    EX:1 += RAND:2
ELSEIF ABL:V감각 == 4
    TIMES SOURCE:1 , 2.50
    EX:1 += 1
ELSE
    TIMES SOURCE:1 , 3.00
    EX:1 += 2
ENDIF

;ABL:마조끼를 본다
IF ABL:마조끼 == 0
    TIMES SOURCE:24 , 0.80
    TIMES DOWNBASE:0 , 1.20
ELSEIF ABL:마조끼 == 1
    TIMES SOURCE:24 , 1.10
    TIMES DOWNBASE:0 , 1.10
ELSEIF ABL:마조끼 == 2
    TIMES SOURCE:24 , 1.30
    TIMES DOWNBASE:0 , 1.00
ELSEIF ABL:마조끼 == 3
    TIMES SOURCE:24 , 1.50
    TIMES DOWNBASE:0 , 0.90
ELSEIF ABL:마조끼 == 4
    TIMES SOURCE:24 , 1.70
    TIMES DOWNBASE:0 , 0.80
ELSE
    TIMES SOURCE:24 , 2.10
    TIMES DOWNBASE:0 , 0.70
ENDIF

;PALAM:고통을 본다
IF PALAM:16 < PALAMLV:1
    SOURCE:20 = 4000
ELSEIF PALAM:16 < PALAMLV:2
    SOURCE:20 = 4300
ELSEIF PALAM:16 < PALAMLV:3
    SOURCE:20 = 4600
ELSEIF PALAM:16 < PALAMLV:4
    SOURCE:20 = 5000
ELSE
    SOURCE:20 = 5500
ENDIF

;반항적
SIF TALENT:11
    TIMES SOURCE:20 , 1.10
;저항
SIF TALENT:34
    TIMES SOURCE:20 , 1.10
;음호
IF TALENT:172
    TIMES SOURCE:1 , 1.50
    TIMES SOURCE:31 , 1.50
    TIMES SOURCE:25 , 0.50
ENDIF
;V민감
IF TALENT:102
    IF ABL:V감각 <= 3
        TIMES SOURCE:1 , 1.80
    ELSEIF ABL:V감각 == 4
        TIMES SOURCE:1 , 2.00
        EX:1 += RAND:2
    ELSE
        TIMES SOURCE:1 , 2.20
        EX:1 += RAND:3
    ENDIF
;V둔감
ELSEIF TALENT:103
    IF ABL:V감각 <= 3
        TIMES SOURCE:1 , 0.70
        EX:1 -= (EX:1 > 1) ?  1 # 0
    ELSEIF ABL:V감각 == 4
        TIMES SOURCE:1 , 0.75
        EX:1 -= (EX:1 > 1) ?  1 # 0
    ELSE
        TIMES SOURCE:1 , 0.80
    ENDIF
ENDIF

;조교자에게 바늘 다루기
SIF TALENT:PLAYER:53
    TIMES SOURCE:20 , 0.90
;조교자에게 유치
SIF TALENT:PLAYER:131
    TIMES SOURCE:20 , 1.20

;레이센이나 에이린에 의한 사용
IF TEQUIP:90 == 0
    SIF NO:PLAYER == 28
        TIMES SOURCE:20 , 0.70
    SIF NO:PLAYER == 29
        TIMES SOURCE:20 , 0.50
ENDIF

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
    TCVAR:12 += 2
ENDIF

;탈자궁 획득
TALENT:탈자궁 = 1

;약물 경험
TCVAR:40 += 3

;이상 경험
TCVAR:50 += 1

;주인 경험 플래그
SIF ASSIPLAY == 0 && ABL:마조끼 >= 3
    TFLAG:50 += 1

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM089
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
PRINTFORM %CALLNAME:TARGET%의 
SIF TEQUIP:21
    PRINT 쿠스코로 벌려져 무방비가 된 
SIF TALENT:102
    PRINT 민감한 
PRINTL 자궁구에 주사바늘을 꽂았다….
PRINTL
PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 가장 예민한 기관에 주삿바늘이 꽃힌 격통과 미지의 약물이 퍼지는 감각에 
IF TALENT:172
    PRINT 왠지 모를 설렘이 섞인 공포에
ELSE
    PRINT 무시무시한 공포를 느끼면서
ENDIF
PRINTW  부르르 떨고 있다….
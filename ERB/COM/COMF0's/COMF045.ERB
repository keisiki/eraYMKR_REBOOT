;=============================================================================
;애널 조르기
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE45
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:45 > 0
    RETURN 0
;A조교 필터가 오프가 되지 않으면 안 됨
SIF FLAG:VA이상조교필터 & 2
    RETURN 0
;조교자에게 페니스가 없는, 한편(아이템 있음 설정이라면) 페니스 밴드도 없으면 안 됨
SIF EXIST_BAR(PLAYER) == 0 && ITEM:2 == 0 && NOITEM == 0
    RETURN 0
;A섹스의 산등성이다 리나의 것으로, [소인] 소유는 조교자도 [소인]이 아니면 불가
;그 이외의 경우는 주인이 금단의 지식 소유가 아니면 불가
;주인이 [금단의 지식] 소유가 아닐 때, 체격차이가 2 단계 이내나 조교자가 [금단의 지식] 소유가 아니면 안 됨
SIF TALENT:MASTER:금단의지식 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
    RETURN 0
;순종, 욕망, 노출버릇의 합계가 6이상 없으면 불가
SIF ABL:순종 + ABL:욕망 + ABL:노출증 < 6
    RETURN 0
;일정한 A경험이 필요
SIF EXP:A경험 < EXPLV:3
    RETURN 0
;그 조교중에 관장 플레이＋배설이 있었을 경우, 조교자가 더러운 무시 혹은 악취 둔감을 가지지 않으면 실행할 수 없다
SIF TFLAG:140 & 256 && TALENT:PLAYER:냄새둔감 == 0 && TALENT:PLAYER:불결무시 == 0
    RETURN 0
;수면중은 불가
SIF EQUIP:14
    RETURN 0
;실신중은 불가
SIF TFLAG:899 > 0
    RETURN 0
;A계 장착도구 사용중은 불가
SIF TEQUIP:25 || TEQUIP:26
    RETURN 0
;밧줄 사용 중에는 불가
SIF TEQUIP:40
    RETURN 0
;가시나무로 긴박중, 거미의 실로 긴박중은 불가
SIF TEQUIP:40 >= 5 && TEQUIP:40 <= 7
    RETURN 0
;00목욕탕 입욕중은 불가
SIF TEQUIP:55
    RETURN 0
;훈도시 착용중의 경우 조교자의 기교 4이상 필수
SIF TEQUIP:59 == 17 && ABL:PLAYER:기교 < 4
    RETURN 0
;조수가 훈도시 착용중의 경우 기교 4이상 필수
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17 && ABL:기교 < 4
    RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
    RETURN 0
;나체정식 중에는 안 됨
SIF TEQUIP:60
    RETURN 0
;시간정지 중에는 무리
SIF TEQUIP:71
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM45
PRINTL 애널 조르기

;-------------------------------------------------
;실행할 수 있을까의 판정
;-------------------------------------------------
IF TFLAG:699 == 0
    CALL COM_ORDER
    ;실행할 수 없다
    SIF RESULT == 0
        RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------
COM_NAME = 애널 조르기
CALL TRAIN_MESSAGE_COM045

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 5
DOWNBASE:기력 += 150

SOURCE:12 = 1000
SOURCE:22 = 100
SOURCE:31 = 1000

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    LOCAL:1 = 100
ELSEIF PALAM:13 < PALAMLV:2
    LOCAL:1 = 120
ELSEIF PALAM:13 < PALAMLV:3
    LOCAL:1 = 160
ELSEIF PALAM:13 < PALAMLV:4
    LOCAL:1 = 220
ELSEIF PALAM:13 < PALAMLV:9
    LOCAL:1 = 300
ELSE
    LOCAL:1 = 400
ENDIF

;ABL:A감각을 본다
IF ABL:A감각 == 0
    LOCAL:1 += 8
ELSEIF ABL:A감각 == 1
    LOCAL:1 += 16
ELSEIF ABL:A감각 == 2
    LOCAL:1 += 26
ELSEIF ABL:A감각 == 3
    LOCAL:1 += 34
ELSEIF ABL:A감각 == 4
    LOCAL:1 += 44
ELSE
    LOCAL:1 += 56
ENDIF

SOURCE:18 += PALAM:13 / 5
SOURCE:22 += ABL:욕망 * ABL:노출증 * LOCAL:1

;ABL:노출벽을 본다
IF ABL:노출증 == 0
    SOURCE:12 += 1500
    SOURCE:31 += 3000
ELSEIF ABL:노출증 == 1
    SOURCE:19 += (ABL:욕망 + ABL:노출증) * 10
    SOURCE:12 += 1250
    SOURCE:31 += 1500
ELSEIF ABL:노출증 == 2
    SOURCE:19 += (ABL:욕망 + ABL:노출증) * 20
    SOURCE:12 += 1000
    SOURCE:31 += 800
ELSEIF ABL:노출증 == 3
    SOURCE:19 += (ABL:욕망 + ABL:노출증) * 40
    SOURCE:12 += 750
    SOURCE:31 += 400
ELSEIF ABL:노출증 == 4
    SOURCE:19 += (ABL:욕망 + ABL:노출증) * 80
    SOURCE:12 += 500
    SOURCE:31 += 200
ELSE
    SOURCE:19 += (ABL:욕망 + ABL:노출증) * 140
    SOURCE:12 += 250
    SOURCE:31 += 100
ENDIF

IF TALENT:남자 == 0 && TALENT:PLAYER:남자 == 0
    TCVAR:12 += 2
ELSEIF TALENT:남자 && TALENT:PLAYER:남자
    TCVAR:13 += 2
ENDIF
;봉사 쾌락 경험 입수 판정
TFLAG:100 |= 1

RETURN 1

;--------------------------------------------------
;명령 거부의 메시지
;--------------------------------------------------
@DENIAL_MESSAGE_COM_45
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
PRINTFORML %CALLNAME:TARGET%에게 음란한 포즈로 애널을 애원해 보라고 명령했지만 거절당했다.
PRINTW 좀 더 조교가 필요한 것 같다.

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM045
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
IF TALENT:음란
    PRINTFORM 명령하지 않아도 %조사선택(CALLNAME:TARGET, "는")% 
ELSE
    PRINTFORM %CALLNAME:PLAYER%의 명령에 %조사선택(CALLNAME:TARGET, "는")% 
ENDIF
IF ABL:노출증 == 0 && TALENT:음란 == 0
    PRINTL 수치로 얼굴을 붉히며 저항해 봤지만
    PRINT 다시 한 번 명령하자 눈에 눈물이 가득한 채 
    IF TEQUIP:90
        PRINTFORMW 자신의 엉덩이를 %CALLNAME:PLAYER%에게 향하고 유혹하듯이 흔들기 시작했다….
    ELSE
        PRINTW 자신의 애널을 벌려 보였다….
    ENDIF
ELSEIF ABL:노출증 <= 1
    PRINT 부끄러워 하면서도 주춤주춤 엉덩이를 
    IF TEQUIP:90
        PRINTFORMW 흔들며 %조사선택(CALLNAME:PLAYER, "를")% 유혹했다….
    ELSE
        PRINTFORMW 벌리고 %조사선택(CALLNAME:PLAYER, "를")% 유혹했다….
    ENDIF
ELSEIF ABL:노출증 <= 3
    PRINTFORML 다리를 Ｍ자 형으로 벌리고 
    IF TEQUIP:90
        PRINTFORMW %CALLNAME:PLAYER%의 흥분을 부채질하듯이 항문을 보여주었다….
    ELSE
        PRINTFORMW 안쪽의 애널을 손가락으로 벌리고 %CALLNAME:PLAYER%의 흥분을 부채질했다….
    ENDIF
ELSEIF ABL:노출증 <= 4
    IF TEQUIP:90
        PRINTFORMW 촉수에 양 엉덩이가 넓게 벌려진 채로 %CALLNAME:PLAYER%에게 꿈틀거리는 항문이 훤히 보여졌다….
    ELSE
        PRINTW 개처럼 엎드려 엉덩이의 중심에 있는 항문을 보여줬다….
    ENDIF
ELSEIF TEQUIP:90
    PRINTL 촉수로 엉덩이를 벌려지면서 기대에 움찔대는 애널을 쳐들었다.
    PRINTW 그 모습은 마치 무언가를 삽입해주길 기대하는 것처럼 보였다….
ELSEIF ABL:노출증 >= 5 && TALENT:음고 == 0
    PRINTL 양 손으로 엉덩이를 벌리면서
    PRINTFORMW 기대에 꿈틀거리는 애널을 들이밀며 %조사선택(CALLNAME:PLAYER, "를")% 유혹하고 있다….
ELSE
    PRINTL 열기를 가득 띤 웃음을 지으면서 양 손으로 엉덩이를 벌리고
    PRINTFORMW 항문을 괴롭혀줄 기대에 움찔대는 애널을 들이밀며 %조사선택(CALLNAME:PLAYER, "를")% 유혹하고 있다….
ENDIF
PRINTL

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_45, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:욕망
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:욕망
    TFLAG:240 += LOCAL:99 * 2
    RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF
;ABL:A감각
IF ABL:A감각
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:A감각
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %ABLNAME:2%LV{LOCAL:99}({LOCAL:99})
ENDIF
;ABL:노출벽
IF ABL:노출증
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:노출증
    TFLAG:240 += LOCAL:99 * 2
    RESULTS = %RESULTS% %ABLNAME:14%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF
;ABL:BL기분＆TALENT:남자
IF TALENT:PLAYER:남자 && TALENT:남자
    SIF LOCAL:99
        RESULTS = %RESULTS% - 
    ;↓의 LOCAL:99의 기준(자위 5, 펠라/A애무 강제 10)
    LOCAL:99 = 15
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:120%같은 종류({LOCAL:99})
    IF ABL:BL끼
        LOCAL:99 = ABL:BL끼
        TFLAG:240 += LOCAL:99 * 4
        RESULTS = %RESULTS% + %ABLNAME:23%LV{LOCAL:99}({LOCAL:99 * 4})
    ENDIF
ENDIF
;ABL:정액 중독
IF ABL:정액중독
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:정액중독
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99})
ENDIF

;쾌락 각인
IF MARK:쾌락각인
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = MARK:쾌락각인
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99})
ENDIF

;PALAM:욕정
GETPALAMLV PALAM:13, 5
LOCAL:2 = RESULT
IF LOCAL:2
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = LOCAL:2 * 3
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;자제심
IF TALENT:자제심
    RESULTS = %RESULTS% - 
    LOCAL:99 = 5
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:20%({LOCAL:99})
ENDIF
;부끄럼쟁이
IF TALENT:부끄럼쟁이
    RESULTS = %RESULTS% - 
    LOCAL:99 = 5
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
;부끄럼없음
ELSEIF TALENT:부끄럼없음
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 3
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:36%({LOCAL:99})
ENDIF
;남자 싫다
IF TALENT:남성혐오 && TALENT:PLAYER:남자
    RESULTS = %RESULTS% - 
    LOCAL:99 = 12
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;여자 증오
ELSEIF TALENT:여성혐오 && TALENT:PLAYER:남자 == 0
    RESULTS = %RESULTS% - 
    LOCAL:99 = 12
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;쾌감에 솔직
IF TALENT:쾌감에솔직
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 5
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:70%({LOCAL:99})
;쾌감의 부정
ELSEIF TALENT:쾌감을부정
    RESULTS = %RESULTS% - 
    LOCAL:99 = 7
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;연모
IF TALENT:연모 && ASSIPLAY == 0
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 5
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF

;미약
IF TEQUIP:11
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 8
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %ITEMNAME:61%({LOCAL:99})
ENDIF

TFLAG:241 = 36

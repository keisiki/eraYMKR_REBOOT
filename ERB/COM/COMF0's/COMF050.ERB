;=============================================================================
;전신잡
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE50
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:50 > 0
    RETURN 0
;조교 대상이 조교자의 체격보다 충분히 작지 않으면 안 됨(금단의 지식은 무관계해)
SIF (BODYSIZE(PLAYER) - BODYSIZE(TARGET)) < 15
    RETURN 0
;조교자에게 페니스가 없으면 안 됨
SIF EXIST_BAR(PLAYER) == 0
    RETURN 0
;수면중은 불가
SIF EQUIP:14
    RETURN 0
;패닉중은 불가
SIF TFLAG:108 > 0
    RETURN 0
;실신중은 불가
SIF TFLAG:899 > 0
    RETURN 0
;쿠스코 대고 있으면(자) 안 됨
SIF TEQUIP:21
    RETURN 0
;밧줄 사용 중에는 불가
SIF TEQUIP:40
    RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
    RETURN 0
;조수가 훈도시 착용중의 경우는 안 됨
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17
    RETURN 0
;한증과 암반 욕 이외의00목욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
    RETURN 0
;나체정식 중에는 안 됨
SIF TEQUIP:60
    RETURN 0
;시간정지 중에는 무리
SIF TEQUIP:71
    RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM50
PRINTL 전신잡

;-------------------------------------------------
;실행할 수 있는가 판정
;-------------------------------------------------
IF TFLAG:699 == 0
    CALL COM_ORDER
    ;실행할 수 없다
    SIF RESULT == 0
        RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------
COM_NAME = 전신잡
CALL TRAIN_MESSAGE_COM050

;-------------------------------------------------
;사정 게이지 체크
;-------------------------------------------------
LOCAL = 0

;ABL:기교를 본다
IF ABL:기교 == 0
    LOCAL = 450
ELSEIF ABL:기교 == 1
    LOCAL = 1000
ELSEIF ABL:기교 == 2
    LOCAL = 1600
ELSEIF ABL:기교 == 3
    LOCAL = 2200
ELSEIF ABL:기교 == 4
    LOCAL = 2700
ELSE
    LOCAL = 3200
ENDIF

;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;봉사정신을 본다
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)

;조교자의 Ｃ감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
    BASE:PLAYER:사정 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 30
DOWNBASE:기력 += 100

SOURCE:31 = 400

;위쪽에서 계산한 더러움 데이터
SOURCE:30 = TFLAG:242*10 + 60

;ABL:Ｃ감각을 본다
IF ABL:C감각 == 0
    SOURCE:0 = 20
ELSEIF ABL:C감각 == 1
    SOURCE:0 = 120
ELSEIF ABL:C감각 == 2
    SOURCE:0 = 640
ELSEIF ABL:C감각 == 3
    SOURCE:0 = 1200
ELSEIF ABL:C감각 == 4
    SOURCE:0 = 2300
ELSE
    SOURCE:0 = 3400
ENDIF

;ABL:Ｂ감각을 본다
IF ABL:B감각 == 0
    SOURCE:3 = 20
ELSEIF ABL:B감각 == 1
    SOURCE:3 = 120
ELSEIF ABL:B감각 == 2
    SOURCE:3 = 640
ELSEIF ABL:B감각 == 3
    SOURCE:3 = 1200
ELSEIF ABL:B감각 == 4
    SOURCE:3 = 2300
ELSE
    SOURCE:3 = 3400
ENDIF

;ABL:봉사정신을 본다
IF ABL:봉사정신 == 0
    SOURCE:15 = 250
    SOURCE:16 = 50
    TIMES SOURCE:30 , 4.00
    SOURCE:22 = 40
ELSEIF ABL:봉사정신 == 1
    SOURCE:15 = 300
    SOURCE:16 = 100
    TIMES SOURCE:30 , 2.50
    SOURCE:22 = 160
ELSEIF ABL:봉사정신 == 2
    SOURCE:15 = 350
    SOURCE:16 = 200
    TIMES SOURCE:30 , 1.50
    SOURCE:22 = 700
ELSEIF ABL:봉사정신 == 3
    SOURCE:15 = 400
    SOURCE:16 = 300
    TIMES SOURCE:30 , 1.00
    SOURCE:22 = 1300
ELSEIF ABL:봉사정신 == 4
    SOURCE:15 = 450
    SOURCE:16 = 500
    TIMES SOURCE:30 , 0.50
    SOURCE:22 = 2200
ELSE
    SOURCE:15 = 500
    SOURCE:16 = 750
    TIMES SOURCE:30 , 0.10
    SOURCE:22 = 3100
ENDIF

;ABL:기교를 본다
IF ABL:기교 == 0
    TIMES SOURCE:15 , 0.50
    TIMES SOURCE:16 , 0.50
    TIMES SOURCE:22 , 0.50
ELSEIF ABL:기교 == 1
    TIMES SOURCE:15 , 0.80
    TIMES SOURCE:16 , 0.80
    TIMES SOURCE:22 , 0.80
ELSEIF ABL:기교 == 2
    TIMES SOURCE:15 , 1.00
    TIMES SOURCE:16 , 1.00
    TIMES SOURCE:22 , 1.00
ELSEIF ABL:기교 == 3
    TIMES SOURCE:15 , 1.20
    TIMES SOURCE:16 , 1.20
    TIMES SOURCE:22 , 1.50
ELSEIF ABL:기교 == 4
    TIMES SOURCE:15 , 1.50
    TIMES SOURCE:16 , 1.50
    TIMES SOURCE:22 , 2.50
ELSE
    TIMES SOURCE:15 , 2.00
    TIMES SOURCE:16 , 2.00
    TIMES SOURCE:22 , 4.00
ENDIF

;도착적
SIF TALENT:도착적
    TIMES SOURCE:16 , 1.80

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

IF TFLAG:91
;射精している
    TIMES SOURCE:15 , 3.00

    ;ABL:정액중독을 본다
    IF ABL:정액중독 == 0
        SOURCE:24 = 0
        TIMES SOURCE:16 , 2.00
        TIMES SOURCE:22, 2.00
    ELSEIF ABL:정액중독 == 1
        SOURCE:24 = 200
        TIMES SOURCE:16 , 2.50
        TIMES SOURCE:22, 1.60
    ELSEIF ABL:정액중독 == 2
        SOURCE:24 = 600
        TIMES SOURCE:16 , 3.00
        TIMES SOURCE:22, 1.00
    ELSEIF ABL:정액중독 == 3
        SOURCE:24 = 1500
        TIMES SOURCE:16 , 4.00
        TIMES SOURCE:22, 0.70
    ELSEIF ABL:정액중독 == 4
        SOURCE:24 = 3000
        TIMES SOURCE:16 , 5.00
        TIMES SOURCE:22, 0.40
    ELSE
        SOURCE:24 = 6000
        TIMES SOURCE:16 , 6.00
        TIMES SOURCE:22, 0.10
    ENDIF
ENDIF

;대량 사정
IF TFLAG:91 == 2
    TIMES SOURCE:24 , 2.00
    TIMES SOURCE:16 , 1.50

    TCVAR:PLAYER:3 += 2
    TCVAR:4 += 3
    PRINTL 대량사정

    ;精液汚れ
    STAIN:PLAYER:자지 |= 4
    STAIN:손 |= 4
    STAIN:자지 |= 4
    STAIN:보지 |= 4
    STAIN:가슴 |= 4
    STAIN:다리 |= 4
    STAIN:발 |= 4

    ;手で射精させたフラグ
    TFLAG:12 = TFLAG:91
;通常の射精
ELSEIF TFLAG:91 == 1
    TCVAR:PLAYER:3 += 1
    TCVAR:4 += 1
    PRINTL 사정

    ;精液汚れ
    STAIN:PLAYER:자지 |= 4
    STAIN:손 |= 4
    STAIN:자지 |= 4
    STAIN:보지 |= 4
    STAIN:가슴 |= 4
    STAIN:다리 |= 4
    STAIN:발 |= 4

    ;手で射精させたフラグ
    TFLAG:12 = TFLAG:91
ENDIF
BASE:PLAYER:사정 = LIMIT(BASE:PLAYER:사정 - EJAC*(TFLAG:91), 0, EJAC-1)

;노예의 体⇔조교자의 Ｐ의 불결이 이동
STAIN:손 |= STAIN:PLAYER:자지
STAIN:PLAYER:자지 |= STAIN:손
STAIN:자지 |= STAIN:PLAYER:자지
STAIN:PLAYER:자지 |= STAIN:자지
STAIN:보지 |= STAIN:PLAYER:자지
STAIN:PLAYER:자지 |= STAIN:보지
STAIN:가슴 |= STAIN:PLAYER:자지
STAIN:PLAYER:자지 |= STAIN:가슴
STAIN:다리 |= STAIN:PLAYER:자지
STAIN:PLAYER:자지 |= STAIN:다리
STAIN:발 |= STAIN:PLAYER:자지
STAIN:PLAYER:자지 |= STAIN:발

;LUNATIC以上は射精するたびに体力が減少
CALL EXHAUST_EJAC, PLAYER, TFLAG:91

IF TALENT:남자 == 0 && TALENT:PLAYER:남자 == 0
    TCVAR:12 += 4
ELSEIF TALENT:남자 && TALENT:PLAYER:남자
    TCVAR:13 += 4
ENDIF
;奉仕快楽経験入手判定
TFLAG:100 |= 1

;조교자가 후타나리
SIF TALENT:PLAYER:후타나리
    SOURCE:22 /= 2

RETURN 1

;--------------------------------------------------
;명령 거부 메시지
;--------------------------------------------------
@DENIAL_MESSAGE_COM_50
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:지문소거판정 == 1 || RESULT != 0
    RETURN 0

PRINTL
PRINTFORML %CALLNAME:TARGET%에게 몸 타기를 하도록 명령해보았지만 거절당했다.
PRINTW 좀 더 조교가 필요한 것 같다.

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM050
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:지문소거판정 == 1 || RESULT != 0
    RETURN 0

PRINTL
;未陥落で반발각인2以上の対象の場合
IF TALENT:연모 == 0 && TALENT:복종 == 0 && TALENT:음란 == 0 && MARK:반발각인 > 1
    PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 노골적으로 싫어하는 표정을 짓고 있지만 그래도 몸 전체를 사용하여 %CALLNAME:PLAYER%의 자지에 봉사하고 있다.
;助手プレイでなくて[연모]持ちの対象の場合
ELSEIF ASSIPLAY == 0 && TALENT:연모
    PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 사랑하는 %조사선택(CALLNAME:PLAYER, "를")% 위해서
    PRINTFORMW 몸 전체를 사용하여 %CALLNAME:PLAYER%의 자지에 봉사하고 있다.
;恥情が高まっている場合
ELSEIF PALAM:15 > PALAMLV:5
    PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 얼굴을 빨갛게 물들이면서
    PRINTFORMW 몸 전체를 사용하여 %CALLNAME:PLAYER%의 자지에 봉사하고 있다.
;그 외의 경우
ELSE
    PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 몸 전체를 사용해 %CALLNAME:MASTER%의 페니스에 봉사하고 있다….
ENDIF

;-------------------------------------------------
;고유 실행 판정
;-------------------------------------------------
@COM_ORDER_50, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:욕망
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:욕망
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99})
ENDIF
;ABL:봉사정신
IF ABL:봉사정신
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:봉사정신
    TFLAG:240 += LOCAL:99 * 4
    RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:정액중독
IF ABL:정액중독
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:정액중독
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99})
ENDIF

;쾌락각인
IF MARK:쾌락각인
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = MARK:쾌락각인
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99})
ENDIF

;PALAM:욕정
GETPALAMLV PALAM:13, 5
LOCAL:2 = RESULT
IF LOCAL:2
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = LOCAL:2
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;부끄럼쟁이
IF TALENT:부끄럼쟁이
    RESULTS = %RESULTS% - 
    LOCAL:99 = 1
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;男嫌い
IF TALENT:남성혐오 && TALENT:PLAYER:남자
    RESULTS = %RESULTS% - 
    LOCAL:99 = 5
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;女嫌い
ELSEIF TALENT:여성혐오 && TALENT:PLAYER:남자 == 0
    RESULTS = %RESULTS% - 
    LOCAL:99 = 5
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;냄새둔감
IF TALENT:냄새둔감
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 1
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:61%({LOCAL:99})
;냄새민감
ELSEIF TALENT:냄새민감
    RESULTS = %RESULTS% - 
    LOCAL:99 = 3
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:62%({LOCAL:99})
ENDIF
;헌신적
IF TALENT:헌신적
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 6
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:63%({LOCAL:99})
ENDIF
;쾌감의 부정
IF TALENT:쾌감을부정
    RESULTS = %RESULTS% - 
    LOCAL:99 = 1
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;연모
IF TALENT:연모 && ASSIPLAY == 0
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 5
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF

;조교자가 후타나리
IF TALENT:PLAYER:후타나리
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 8
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %조사처리(CALLNAME:PLAYER,"가")% %TALENTNAME:121%({LOCAL:99})
ENDIF

;조교자의 Ｐ의 더러움
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)
;全身だから汚れがもろに影響する
TFLAG:242 *= 2

;불결 있음
IF TFLAG:242
    RESULTS = %RESULTS% - 
    TFLAG:240 -= TFLAG:242
    RESULTS = %RESULTS% 불결 있음
    ;냄새둔감
    IF TALENT:냄새둔감
        RESULTS = %RESULTS%%TALENTNAME:61%
    ELSEIF TALENT:냄새민감
        RESULTS = %RESULTS%%TALENTNAME:62%
    ENDIF
    RESULTS = %RESULTS%({TFLAG:242})
    LOCAL:99 = 1
ENDIF

TFLAG:241 = 34

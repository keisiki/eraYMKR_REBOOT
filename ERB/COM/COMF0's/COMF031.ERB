;=============================================================================
;후배위 애널
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE31
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:31 > 0
    RETURN 0
;A조교 필터가 오프가 되지 않으면 안 됨
SIF FLAG:2 & 2
    RETURN 0
;조교자에게 페니스가 없는, 한편(아이템 있음 설정이라면) 페니스 밴드도 없으면 안 됨
SIF EXIST_BAR(PLAYER) == 0 && ITEM:2 == 0 && NOITEM == 0
    RETURN 0
;주인이 [금단의 지식] 소유가 아닐 때, 체격차이가 2 단계 이내나 조교자가 [금단의 지식] 소유가 아니면 안 됨
SIF TALENT:MASTER:56 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
    RETURN 0
;윤활 부족해, 순종 4 미만·레즈끼(BL끼) 4 미만의 ASSI의 경우, 조교 대상이 동성이라면 실행할 수 없다(새드나 새드끼 Lv3 이상, 또는 망신이나 복종이 있다면 OK)
IF PALAM:10 < PALAMLV:2 && ASSIPLAY
    SIF (ABL:ASSI:순종 < 4 || ((TALENT:120 == 0 && TALENT:ASSI:120 == 0 && ABL:ASSI:레즈끼 < 4) || (TALENT:120 && TALENT:ASSI:120 && ABL:ASSI:BL끼 < 4))) && TALENT:ASSI:81 == 0 && ABL:ASSI:새드끼 < 3 && TALENT:ASSI:151 == 0 && TALENT:ASSI:160 == 0
        RETURN 0
ENDIF
;일정한 A경험이 필요
SIF EXP:A경험 < EXPLV:3
    RETURN 0
;그 조교중에 관장 플레이＋배설이 있었을 경우, 조교자가 더러운 무시 혹은 악취 둔감을 가지지 않으면 실행할 수 없다
SIF TFLAG:140 & 256 && TALENT:PLAYER:61 == 0 && TALENT:PLAYER:64 == 0
    RETURN 0
;A계 장착도구 사용중은 불가
SIF TEQUIP:25 || TEQUIP:26
    RETURN 0
;쿠스코 삽입 중에는 불가
SIF TEQUIP:21
    RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
    RETURN 0
;목욕탕 플레이중은 플레이 매트가 없으면 안 됨
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
    RETURN 0
;한증과 암반 욕 이외의00목욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
    RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
    RETURN 0
;훈도시 착용중의 경우 조교자의 기교 4이상 필수
SIF TEQUIP:59 == 17 && ABL:PLAYER:기교 < 4
    RETURN 0
;조수가 훈도시 착용중의 경우 기교 4이상 필수
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17 && ABL:기교 < 4
    RETURN 0
;나체정식 중에는 안 됨
SIF TEQUIP:60
    RETURN 0
;촉수 조교중일 시 주인의 촉수사역Lv2 이하는 안 됨
SIF TEQUIP:90 && ABL:MASTER:촉수사역 <= 2
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM31

;전회의 조교가 둘이서 범하는 것이라고 하고 둘이서 범하는에
TFLAG:132 = 0
IF PREVCOM == 610
    CALL EXCOM_ABLE610
    IF RESULT == 1
        TFLAG:132 = 1
        JUMP COM610
    ENDIF
;조교자가 전회:조수로 이번:주인인가, 전회:주인으로 이번:조수 때
ELSEIF (ASSIPLAY && TFLAG:133 == 0) || (ASSIPLAY == 0 && TFLAG:133)
    ;전회의 조교가 정상위이거나 정상위 애널이거나 펠라치오이거나 이라마치오일 때 둘이서 범하기로
    IF GROUPMATCH(PREVCOM, 20, 30, 41, 230)
        CALL EXCOM_ABLE610
        SIF RESULT == 1
            JUMP COM610
    ENDIF
ENDIF
;전회와 이번 조교자 같고, 가슴 애무 혹은 후배위 애널 애무에의 경우, 후배위 애널 애무에
SIF PREVCOM == 3 || PREVCOM == 618 && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    JUMP COM618

PRINTL 후배위 애널
COM_NAME = 후배위 애널
CALL TRAIN_MESSAGE_COM031

;-------------------------------------------------
;사정 게이지 체크
;-------------------------------------------------
LOCAL = 0

;ABL:기교를 본다
IF ABL:기교 == 0
    LOCAL = 2700
ELSEIF ABL:기교 == 1
    LOCAL = 2800
ELSEIF ABL:기교 == 2
    LOCAL = 2900
ELSEIF ABL:기교 == 3
    LOCAL = 3100
ELSEIF ABL:기교 == 4
    LOCAL = 3200
ELSE
    LOCAL = 3300
ENDIF

;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;욕망을 본다
LOCAL = EJACULATIONCHECK_ABL_11_1(TARGET, LOCAL)
;윤활을 본다
LOCAL = EJACULATIONCHECK_PALAM_10_4(TARGET, LOCAL)

;조교자의 C감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
    BASE:PLAYER:2 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 100
DOWNBASE:1 += 150

SOURCE:12 = 800

;ABL:A감각을 본다
IF ABL:A감각 == 0
    SOURCE:2 = 10
    SOURCE:11 = 10
    SOURCE:22 = 100
ELSEIF ABL:A감각 == 1
    SOURCE:2 = 40
    SOURCE:11 = 20
    SOURCE:22 = 700
ELSEIF ABL:A감각 == 2
    SOURCE:2 = 500
    SOURCE:11 = 80
    SOURCE:22 = 1500
ELSEIF ABL:A감각 == 3
    SOURCE:2 = 1200
    SOURCE:11 = 180
    SOURCE:22 = 3000
ELSEIF ABL:A감각 == 4
    SOURCE:2 = 1900
    SOURCE:11 = 400
    SOURCE:22 = 5000
ELSE
    SOURCE:2 = 2400
    SOURCE:11 = 680
    SOURCE:22 = 8000
ENDIF

;EXP:A경험을 본다
IF EXP:A경험 < EXPLV:1
    TIMES SOURCE:2 , 0.10
    SOURCE:20 = 18000
    DOWNBASE:0 += 60
    DOWNBASE:1 += 100
ELSEIF EXP:A경험 < EXPLV:2
    TIMES SOURCE:2 , 0.30
    SOURCE:20 = 10000
    DOWNBASE:0 += 50
    DOWNBASE:1 += 80
ELSEIF EXP:A경험 < EXPLV:3
    TIMES SOURCE:2 , 0.50
    SOURCE:20 = 4500
    DOWNBASE:0 += 40
    DOWNBASE:1 += 50
ELSEIF EXP:A경험 < EXPLV:4
    TIMES SOURCE:2 , 1.00
    SOURCE:20 = 1500
    DOWNBASE:0 += 30
    DOWNBASE:1 += 40
ELSEIF EXP:A경험 < EXPLV:5
    TIMES SOURCE:2 , 1.40
    SOURCE:20 = 700
    DOWNBASE:0 += 20
    DOWNBASE:1 += 20
ELSE
    TIMES SOURCE:2 , 1.60
    SOURCE:20 = 300
    DOWNBASE:0 += 0
    DOWNBASE:1 += 0
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
    TIMES SOURCE:2 , 0.40
    SOURCE:20 += 10000
ELSEIF PALAM:10 < PALAMLV:2
    TIMES SOURCE:2 , 0.80
    SOURCE:20 += 3600
ELSEIF PALAM:10 < PALAMLV:3
    TIMES SOURCE:2 , 1.00
    SOURCE:20 += 1200
ELSEIF PALAM:10 < PALAMLV:4
    TIMES SOURCE:2 , 1.40
    SOURCE:20 += 200
ELSE
    TIMES SOURCE:2 , 1.80
    SOURCE:20 += 100
ENDIF

;조교자가[남자]
IF TALENT:PLAYER:120
    TIMES SOURCE:2 , 2.50
;조교자가[후타나리]
ELSEIF TALENT:PLAYER:121
    TIMES SOURCE:22 , 1.50
    TIMES SOURCE:31 , 1.60
ENDIF

;[작은체형]
SIF TALENT:128
    TIMES SOURCE:20 , 2.00

;정조관념
IF TALENT:30
    IF EXP:A경험 == 0
        TIMES SOURCE:11 , 1.20
        SOURCE:32 = 500
    ELSE
        TIMES SOURCE:11 , 1.20
        SOURCE:32 = 0
    ENDIF
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    TIMES SOURCE:2 , 0.60
    TIMES SOURCE:22 , 0.60
ELSEIF PALAM:13 < PALAMLV:2
    TIMES SOURCE:2 , 0.80
    TIMES SOURCE:22 , 0.80
ELSEIF PALAM:13 < PALAMLV:3
    TIMES SOURCE:2 , 1.00
    TIMES SOURCE:22 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
    TIMES SOURCE:2 , 1.20
    TIMES SOURCE:22 , 1.20
ELSE
    TIMES SOURCE:2 , 1.40
    TIMES SOURCE:22 , 1.40
ENDIF

;ABL:순종을 본다
IF ABL:순종 == 0
    TIMES SOURCE:11 , 0.60
    TIMES SOURCE:32 , 2.00
ELSEIF ABL:순종 == 1
    TIMES SOURCE:11 , 0.80
    TIMES SOURCE:32 , 1.50
ELSEIF ABL:순종 == 2
    TIMES SOURCE:11 , 1.00
    TIMES SOURCE:32 , 1.00
ELSEIF ABL:순종 == 3
    TIMES SOURCE:11 , 1.20
    TIMES SOURCE:32 , 0.80
ELSEIF ABL:순종 == 4
    TIMES SOURCE:11 , 1.40
    TIMES SOURCE:32 , 0.60
ELSE
    TIMES SOURCE:11 , 1.60
    TIMES SOURCE:32 , 0.30
ENDIF

;A민감, 둔감을 본다
;쾌A자체의 체크는 다음에 정리해 준다
IF TALENT:105
    TIMES SOURCE:20 , 1.50
    TIMES SOURCE:22 , 1.50
ELSEIF TALENT:104
    TIMES SOURCE:20 , 0.60
    TIMES SOURCE:22 , 0.60
ENDIF

SIF EXP:A경험 >= EXPLV:3
    TIMES SOURCE:2 , 1.50

TCVAR:1 += 3
SIF ASSIPLAY == 0 && EXIST_BAR(MASTER)
    CFLAG:174 += 3

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;사정시의 처리
IF TFLAG:91 >= 1
    ;커맨드 마다 고유의 처리는 이 아래에 쓴다

    ;사정처 타입의 설정
    TFLAG:703 = 2
    ;사정처 선택의 가능·불가
    TFLAG:704 = 1
    ;범용적인 처리는 이 함수로 실시한다
    ;(사정 게이지의 재계산이나 사정 경험의 상승과 공통 부분의 표시)
    CALL SAMEN_SHOOT
ENDIF
;노예의 A⇔조교자의 P의 불결이 이동
STAIN:4 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:4

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
    TCVAR:12 += 7
ELSEIF TALENT:120 && TALENT:PLAYER:120
    TCVAR:13 += 7
ENDIF
;주인 경험 플래그
SIF ASSIPLAY == 0
    TFLAG:50 += (ABL:A감각 >= 3) ?  2 # 1

;처녀 상실의 후에 페니스를 뽑고 있을까
TFLAG:101 = (TFLAG:101 == 2) ?  1 # 0
RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM031
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
;전회와 이번 조교자가 같아, 전회의 조교가 후배위 애널이며, 뽑지 않는 플래그가 있는 경우
IF TFLAG:102 && PREVCOM == 31 && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    ;애태우기플레이
    IF TEQUIP:68
        PRINTFORM %CALLNAME:TARGET%의 
        SIF EX:22
            PRINT 오므린 채로 떨고 있는 
        PRINTW 항문에 삽입한 채로, 움직이지 않고 엉덩이를 만졌다….
    ;조교자가 남자나 후타나리이다
    ELSEIF EXIST_PENIS(PLAYER)
        PRINTFORMW %CALLNAME:TARGET%의 항문의 감촉을 맛보면서, 다시 허리를 움직이기 시작했다….
    ELSE
    ;페니스 밴드를 사용하고 있는 경우
        IF RAND:2 == 0
            PRINTFORMW %CALLNAME:TARGET%의 장에서 뽑지 않고, 다시 허리를 움직이기 시작했다….
        ELSE
            PRINTFORMW %CALLNAME:TARGET%의 항문에 뿌리까지 삽입한 채로 다시 허리를 움직이기 시작했다….
        ENDIF
    ENDIF
    SIF TFLAG:101
        TFLAG:101 = 2
;같이 전회의 조교가 정상위 애널인 경우
ELSEIF TFLAG:102 && PREVCOM == 30 && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    PRINTFORM 빼지 않은 채로 %CALLNAME:TARGET%의 뒤로 돌았다. 
    IF TEQUIP:68
        PRINTW
    ELSE
        PRINTW 그리고 다시금 허리를 움직이기 시작했다….
    ENDIF
    SIF TFLAG:101
        TFLAG:101 = 2
;같이 전회의 조교가 대면좌위 애널, 배면좌위 애널, 기승위 애널인 경우
ELSEIF TFLAG:102 && (PREVCOM == 32 || PREVCOM == 33 || PREVCOM == 34) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    PRINTFORM 삽입한 채로 %조사선택(CALLNAME:TARGET, "를")% \@ !(TFLAG:899 || EQUIP:14) ? 엎드리게 해서 눕힌 뒤 네 발로 기게 했 # 눕혔 \@
    SIF TEQUIP:68 == 0
        PRINT 다. 그리고 다시 허리를 움직이기 시작했 
    PRINTW 다….
    SIF TFLAG:101
        TFLAG:101 = 2
ELSE
    IF RAND:2 == 0
        PRINTFORM 뒤로부터 %CALLNAME:TARGET%의 
        SIF PALAM:10 >= PALAMLV:4
            PRINT 점액투성이가 되어있는 
        PRINTW 항문의 가장 깊은 곳까지 찔러 넣었다….
    ELSE
        PRINTFORM %CALLNAME:TARGET%의 
        SIF ABL:A감각 >= 3
            PRINT 충분히 개발된 
        IF TEQUIP:68
            PRINTW 애널에 끝만 삽입했다….
        ELSE
            PRINTW 애널에 뿌리까지 꽂아댔다….
        ENDIF
    ENDIF
ENDIF

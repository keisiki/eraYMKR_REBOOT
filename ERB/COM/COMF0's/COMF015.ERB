;=============================================================================
;유두캡
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE15
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:15 > 0
    RETURN 0
;강력 아이템·커맨드 무효시는 안 됨
SIF (FLAG:13 & 2) == 0
    RETURN 0
;한증과 암반 욕 이외의00목욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
    RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
    RETURN 0
;해제는 언제라도 가능
SIF TEQUIP:35 == 1
    RETURN 1
;아이템을 가지고 있는지, 아이템 없음 설정이 아니면 안 됨
SIF ITEM:20 == 0 && NOITEM == 0
    RETURN 0
;조교 대상이 [소인]의 경우는 주인이 [금단의 지식] 소유가 아니면 안 됨
SIF TALENT:127 && TALENT:MASTER:56 == 0
    RETURN 0
;ASSI의 경우, 기교 3이상이 아니면 사용할 수 없다.
IF ASSIPLAY
    SIF ABL:ASSI:기교 < 3
        RETURN 0
ENDIF
;다른 B계 장착도구 사용중은 불가
SIF (TEQUIP:35 && TEQUIP:35 != 1) || TEQUIP:36
    RETURN 0
;샤워 중에는 불가
SIF TEQUIP:54
    RETURN 0
;나체정식 중에는 안 됨
SIF TEQUIP:60
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;----------------------------------------------------------
;유두캡(SELECTCOM=15), 촉수 유두 고문(SELECTCOM=504)
;----------------------------------------------------------
;촉수 유두 고문은 COMF500.ERB로부터 JUMP 해 온다.
@COM15
IF TEQUIP:90
    PRINTL 촉수 유두 고문
    COM_NAME = 촉수 유두 고문
ELSE
    COM_TOOL_NAME = 유두캡
    IF FLAG:1100 & 1
        ;技師パッチ有効なら特殊な名前チェック
        FLAG:1110 = 20
        CALL NAMECHECK_CUSTOMTOOL
        SIF STR:4 == "유두캡"
            STR:4 = 클립 로터
    ELSE

        COM_TOOL_CALLNAME = 클립 로터
    ENDIF
    PRINTFORML %COM_TOOL_NAME%
    COM_NAME = 유두캡
ENDIF
CALL TRAIN_MESSAGE_COM15

DOWNBASE:0 += 20
DOWNBASE:1 += 70

SOURCE:12 = 120
SOURCE:31 = 70

;ABL:B감각을 본다
IF ABL:B감각 == 0
    SOURCE:3 = 100
ELSEIF ABL:B감각 == 1
    SOURCE:3 = 300
ELSEIF ABL:B감각 == 2
    SOURCE:3 = 800
ELSEIF ABL:B감각 == 3
    SOURCE:3 = 1500
ELSEIF ABL:B감각 == 4
    SOURCE:3 = 2300
ELSE
    SOURCE:3 = 2900
ENDIF

;모유 체질 때는, 분유중독을 본다
IF TALENT:134
    IF ABL:분유중독 == 0
        SOURCE:24 += 0
        TIMES SOURCE:3 , 1.00
        TIMES SOURCE:31 , 1.00
    ELSEIF ABL:분유중독 == 1
        SOURCE:24 += 20
        TIMES SOURCE:3 , 1.05
        TIMES SOURCE:31 , 1.10
    ELSEIF ABL:분유중독 == 2
        SOURCE:24 += 80
        TIMES SOURCE:3 , 1.10
        TIMES SOURCE:31 , 1.15
    ELSEIF ABL:분유중독 == 3
        SOURCE:24 += 200
        TIMES SOURCE:3 , 1.15
        TIMES SOURCE:31 , 1.20
    ELSEIF ABL:분유중독 == 4
        SOURCE:24 += 500
        TIMES SOURCE:3 , 1.20
        TIMES SOURCE:31 , 1.60
    ELSE
        SOURCE:24 += 1000
        TIMES SOURCE:3 , 1.40
        TIMES SOURCE:31 , 2.00
    ENDIF
ENDIF

;B민감·B둔감
IF TALENT:106
    TIMES SOURCE:3 , 1.50
ELSEIF TALENT:107
    TIMES SOURCE:3 , 0.60
ENDIF
;큰 가슴·폭유
IF TALENT:112 || TALENT:113
    TIMES SOURCE:3 , 1.50
ENDIF
;조교자가［유아］또는［유치］또는［유아 퇴행］
SIF TALENT:PLAYER:130 || TALENT:PLAYER:131 || TALENT:PLAYER:159
    TIMES SOURCE:3 , 1.20

IF (FLAG:1100 & 1) && TEQUIP:90 == 0
    ;技師パッチ有効なら道具使用時ソースチェックを呼ぶ
    ;FLAG 1110:道具番号 1111:改善タイプ 1112:SOURCEの実値 
    FLAG:1110 = 20
    FLAG:1111 = 0
    FLAG:1112 = SOURCE:3
    CALL SOURCECHECK_CUSTOMTOOL
    SOURCE:3 = FLAG:1120
ENDIF

;유두캡의 착탈
IF TEQUIP:35
    TEQUIP:35 = 0
ELSE
    IF TEQUIP:90
        STAIN:5 |= 2
        STAIN:5 |= 4
        STAIN:5 |= 64
    ENDIF
    TEQUIP:35 = 1
ENDIF

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
    TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:120
    TCVAR:13 += 1
ENDIF

;커맨드 속성：촉수
IF TEQUIP:90
    TFLAG:98 = 8
;커맨드 속성：도구(조교자)
ELSE
    TFLAG:98 = 1
ENDIF
;촉수 경험 플래그
TFLAG:90 = 0
RETURN 1

;-------------------------------------------------
;유두캡 장비중
;-------------------------------------------------
@EQUIP_COM15
IF TEQUIP:90
    PRINTL <촉수 유두 고문 중>
ELSE
    COM_TOOL_NAME = 유두캡
    IF FLAG:1100 & 1
        ;技師パッチ有効なら特殊な名前チェック
        FLAG:1110 = 20
        CALL NAMECHECK_CUSTOMTOOL
    ENDIF
    PRINTFORML <%COM_TOOL_NAME% 장비 중>
ENDIF

DOWNBASE:0 += 5
DOWNBASE:1 += 20

;ABL:B감각을 본다
IF ABL:B감각 == 0
    LOCAL = 40
ELSEIF ABL:B감각 == 1
    LOCAL = 120
ELSEIF ABL:B감각 == 2
    LOCAL = 250
ELSEIF ABL:B감각 == 3
    LOCAL = 450
ELSEIF ABL:B감각 == 4
    LOCAL = 600
ELSE
    LOCAL = 750
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    TIMES LOCAL , 0.80
ELSEIF PALAM:13 < PALAMLV:2
    TIMES LOCAL , 0.90
ELSEIF PALAM:13 < PALAMLV:3
    TIMES LOCAL , 1.00
ELSEIF PALAM:13 < PALAMLV:4
    TIMES LOCAL , 1.10
ELSE
    TIMES LOCAL , 1.20
ENDIF

;ABL:순종을 본다
IF ABL:순종 == 0
    TIMES LOCAL , 0.80
ELSEIF ABL:순종 == 1
    TIMES LOCAL , 0.90
ELSEIF ABL:순종 == 2
    TIMES LOCAL , 1.00
ELSEIF ABL:순종 == 3
    TIMES LOCAL , 1.10
ELSEIF ABL:순종 == 4
    TIMES LOCAL , 1.20
ELSE
    TIMES LOCAL , 1.30
ENDIF

IF (FLAG:1100 & 1) && TEQUIP:90 == 0
    ;技師パッチ有効なら道具使用時ソースチェックを呼ぶ
    ;FLAG 1110:道具番号 1111:改善タイプ 1112:SOURCEの実値 
    FLAG:1110 = 20
    FLAG:1111 = 0
    FLAG:1112 = LOCAL
    CALL SOURCECHECK_CUSTOMTOOL
    LOCAL = FLAG:1120
ENDIF

SOURCE:3 += LOCAL
SOURCE:12 += 50
SOURCE:18 += 50
;촉수 경험 플래그
SIF TEQUIP:90
    TFLAG:90 += 1

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM15
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

;토리코 모드에서의 직접 호출 대비
COM_TOOL_NAME = 유두캡
IF FLAG:1100 & 1
    ;技師パッチ有効なら特殊な名前チェック
    FLAG:1110 = 20
    CALL NAMECHECK_CUSTOMTOOL
    SIF STR:4 == "유두캡"
        STR:4 = 클립 로터
ELSE

    COM_TOOL_CALLNAME = 클립 로터
ENDIF

PRINTL
IF TEQUIP:90
    IF TEQUIP:35
        IF ABL:촉수중독 > 2
            PRINTFORML 촉수는 %CALLNAME:TARGET%의 유두를 놓았다.
            PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 조금 아쉬운 표정을 하고 있다.
        ELSEIF MARK:반발각인 > 1
            PRINTFORML 촉수는 %CALLNAME:TARGET%의 유두에서 떨어졌다.
            PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 촉수로 엉망이 된 가슴을 보고 있다.
        ELSE
            PRINTFORMW 촉수는 %CALLNAME:TARGET%의 유두로부터 멀어졌다….
        ENDIF
    ELSE
        IF ABL:촉수중독 > 2
            PRINTFORML 끈적끈적한 촉수를 %조사선택(CALLNAME:TARGET, "는")% 스스로 유두로 이끌었다.
            PRINTFORMW 촉수에 의한 리드미컬한 자극을 %조사선택(CALLNAME:TARGET, "는")% 마음껏 만끽하고 있다….
        ELSEIF MARK:반발각인 > 1
            PRINTFORML 세세하게 나뉘어진 촉수가 %CALLNAME:TARGET%의 유두를 만지작거리기 시작했다.
            PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 어떻게든 촉수를 피하려고 몸을 배배 꼬고 있다.
        ELSE
            PRINTFORMW 세세하게 나뉘어진 촉수가 %CALLNAME:TARGET%의 유두를 만지작거리기 시작했다….
        ENDIF
    ENDIF
ELSE
    IF TEQUIP:35
        IF MARK:반발각인 > 1
            PRINTFORMW %조사선택(CALLNAME:PLAYER, "가")% 손을 뻗기도 전에, %조사선택(CALLNAME:TARGET, "는")% %조사선택(COM_TOOL_NAME, "를")% 벗어 던졌다….
        ELSEIF TALENT:153 && ASSIPLAY == 0
            PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% \@(TALENT:106) ? 민감한  # \@%CALLNAME:TARGET%의 유두로부터 %조사선택(COM_TOOL_NAME, "를")% 떼어냈다.
            PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 다음에 어디를 괴롭혀줄지를 기대하면서 %조사선택(CALLNAME:PLAYER, "를")% 뜨거운 시선으로 보고 있다….
        ELSE
            PRINTFORMW %조사선택(CALLNAME:PLAYER, "는")% \@(TALENT:106) ? 민감한  # \@%CALLNAME:TARGET%의 유두로부터 %조사선택(COM_TOOL_NAME, "를")% 떼어냈다….
        ENDIF
    ELSE
        IF MARK:반발각인 > 1
            PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% 싫어서 버둥대는 %조사선택(CALLNAME:TARGET, "를")% 억지로 눌렀다.
            PRINTFORMW 그리고, %CALLNAME:TARGET%의 유두에 %조사선택(COM_TOOL_CALLNAME, "를")% 붙였다….
        ELSE
            PRINTFORM %조사선택(CALLNAME:PLAYER,"는")% 
            SIF TALENT:106
                PRINT 민감한 
            PRINTFORMW %CALLNAME:TARGET%의 유두에 %조사선택(COM_TOOL_CALLNAME, "를")% 붙였다….
        ENDIF
    ENDIF
ENDIF

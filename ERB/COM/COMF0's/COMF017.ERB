;=============================================================================
;전극 오나홀
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE17
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:17 > 0
    RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
    RETURN 0
;목욕탕 플레이중, 00목욕탕 입욕 중에는 불가
SIF TEQUIP:53 || TEQUIP:55
    RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
    RETURN 0
;훈도시 착용중의 경우는 안 됨
SIF TEQUIP:59 == 17
    RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
    RETURN 0
;해제는 언제라도 가능
SIF TEQUIP:30 == 6
    RETURN 1
;조교 대상으로 페니스가 없으면 안 됨
SIF EXIST_BAR(TARGET) == 0
    RETURN 0
;아이템을 가지고 있는지, 아이템 없음 설정이 아니면 안 됨
SIF ITEM:17 == 0 && NOITEM == 0
    RETURN 0
;조교 대상이 [소인]의 경우는 주인이 [금단의 지식] 소유가 아니면 안 됨
SIF TALENT:127 && TALENT:MASTER:금단의지식 == 0
    RETURN 0
;ASSI의 경우, 기교 3이상이 아니면 사용할 수 없다.
IF ASSIPLAY
    SIF ABL:ASSI:기교 < 3
        RETURN 0
ENDIF
;다른 C계 장착도구 사용중은 불가
SIF TEQUIP:30 && TEQUIP:30 != 6
    RETURN 0
;콘돔 장착 중에는 불가
SIF TEQUIP:31
    RETURN 0
;샤워 중에는 불가
SIF TEQUIP:54
    RETURN 0
;알몸 앞치마 중에는 불가
SIF TEQUIP:59 == 1
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;----------------------------------------------------------------
;전극 오나홀(SELECTCOM=17), 촉수 방전 페니스 고문(SELECTCOM=515)
;----------------------------------------------------------------
;촉수 방전 페니스 고문은 COMF500.ERB로부터 JUMP 해 온다.
@COM17
IF TEQUIP:90
    PRINTL 촉수 방전 페니스 고문
    COM_NAME = 촉수 방전 페니스 고문
ELSE
    COM_TOOL_NAME = 전극 오나홀
    IF FLAG:1100 & 1
        ;技師パッチ有効なら特殊な名前チェック
        FLAG:1110 = 17
        CALL NAMECHECK_CUSTOMTOOL
    ELSE

        COM_TOOL_CALLNAME = %COM_TOOL_NAME%
    ENDIF
    PRINTFORML %COM_TOOL_NAME%
    COM_NAME = 전극 오나홀
ENDIF
CALL TRAIN_MESSAGE_COM17

DOWNBASE:0 += 20
DOWNBASE:기력 += 160

SOURCE:12 = 120
SOURCE:31 = 470

;ABL:C감각을 본다
IF ABL:C감각 == 0
    SOURCE:0 = 500
    SOURCE:20 = 1000
ELSEIF ABL:C감각 == 1
    SOURCE:0 = 1000
    SOURCE:20 = 1000
ELSEIF ABL:C감각 == 2
    SOURCE:0 = 2250
    SOURCE:20 = 1000
ELSEIF ABL:C감각 == 3
    SOURCE:0 = 4000
    SOURCE:20 = 1200
ELSEIF ABL:C감각 == 4
    SOURCE:0 = 6000
    SOURCE:20 = 1500
ELSE
    SOURCE:0 = 7500
    SOURCE:20 = 1800
ENDIF

IF FLAG:1100 & 1
    ;技師パッチ有効なら道具使用時ソースチェックを呼ぶ
    ;FLAG 1110:道具番号 1111:改善タイプ 1112:SOURCEの実値 
    FLAG:1110 = 17
    FLAG:1111 = 1
    FLAG:1112 = SOURCE:0
    CALL SOURCECHECK_CUSTOMTOOL
    SOURCE:0 = FLAG:1120
    FLAG:1112 = SOURCE:22
    CALL SOURCECHECK_CUSTOMTOOL
    SOURCE:22 = FLAG:1120
    FLAG:1111 = 2
    FLAG:1112 = SOURCE:20
    CALL SOURCECHECK_CUSTOMTOOL
    SOURCE:20 = FLAG:1120
ENDIF

;전극 오나홀의 착탈
IF TEQUIP:30
    TEQUIP:30 = 0
ELSE
    IF TEQUIP:90
        STAIN:보지 |= 2
        STAIN:보지 |= 4
        TEQUIP:30 = 6
    ENDIF
    TEQUIP:30 = 6
ENDIF

IF TALENT:120 == 0 && TALENT:PLAYER:남자 == 0
    TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:남자
    TCVAR:13 += 1
ENDIF
SIF PLAYER == MASTER && TEQUIP:30 == 6
    TCVAR:41 += (TALENT:120) ?  1 # 2

;커맨드 속성：촉수
IF TEQUIP:90
    TFLAG:98 = 8
;커맨드 속성：도구(조교자)
ELSE
    TFLAG:98 = 1
ENDIF
;촉수 경험 플래그
TFLAG:90 = 0
RETURN 1

;-------------------------------------------------
;전극 오나홀 장비중
;-------------------------------------------------
@EQUIP_COM17
IF TEQUIP:90
    PRINTL <촉수 방전 페니스 고문 중>
ELSE
    COM_TOOL_NAME = 전극 오나홀
    IF FLAG:1100 & 1
        ;技師パッチ有効なら特殊な名前チェック
        FLAG:1110 = 17
        CALL NAMECHECK_CUSTOMTOOL
    ENDIF
    PRINTFORML <%COM_TOOL_NAME% 장비 중>
ENDIF

DOWNBASE:0 += 10
DOWNBASE:기력 += 40

;ABL:C감각을 본다
IF ABL:C감각 == 0
    LOCAL = 100
    LOCAL:1 = 1000
ELSEIF ABL:C감각 == 1
    LOCAL = 300
    LOCAL:1 = 1000
ELSEIF ABL:C감각 == 2
    LOCAL = 625
    LOCAL:1 = 1000
ELSEIF ABL:C감각 == 3
    LOCAL = 1125
    LOCAL:1 = 1200
ELSEIF ABL:C감각 == 4
    LOCAL = 1500
    LOCAL:1 = 1500
ELSE
    LOCAL = 1875
    LOCAL:1 = 1800
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    TIMES LOCAL , 0.80
ELSEIF PALAM:13 < PALAMLV:2
    TIMES LOCAL , 0.90
ELSEIF PALAM:13 < PALAMLV:3
    TIMES LOCAL , 1.00
ELSEIF PALAM:13 < PALAMLV:4
    TIMES LOCAL , 1.10
ELSE
    TIMES LOCAL , 1.20
ENDIF

;ABL:순종을 본다
IF ABL:순종 == 0
    TIMES LOCAL , 0.80
ELSEIF ABL:순종 == 1
    TIMES LOCAL , 0.90
ELSEIF ABL:순종 == 2
    TIMES LOCAL , 1.00
ELSEIF ABL:순종 == 3
    TIMES LOCAL , 1.10
ELSEIF ABL:순종 == 4
    TIMES LOCAL , 1.20
ELSE
    TIMES LOCAL , 1.30
ENDIF

IF TEQUIP:90 == 0 && (FLAG:1100 & 1)
    ;技師パッチ有効なら道具使用時ソースチェックを呼ぶ
    ;FLAG 1110:道具番号 1111:改善タイプ 1112:SOURCEの実値 
    FLAG:1110 = 17
    FLAG:1111 = 1
    FLAG:1112 = LOCAL
    CALL SOURCECHECK_CUSTOMTOOL
    LOCAL = FLAG:1120
    FLAG:1111 = 3
    FLAG:1112 = LOCAL:1
    CALL SOURCECHECK_CUSTOMTOOL
    LOCAL:1 = FLAG:1120
ENDIF

SOURCE:0 += LOCAL
SOURCE:20 += LOCAL:1
SOURCE:12 += 50
SOURCE:18 += 50
;촉수 경험 플래그
SIF TEQUIP:90
    TFLAG:90 += 1

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM17
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
IF TEQUIP:90
    IF TEQUIP:30
        IF ABL:촉수중독 > 2
            PRINTFORML 촉수는 방전을 멈추고 %CALLNAME:TARGET%의 페니스를 해방했다.
            PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 자극의 여운에 잠기면서도 아직 부족한 것 같다….
        ELSEIF MARK:반발각인 > 1
            PRINTFORML 촉수는 방전을 그만두어 %CALLNAME:TARGET%의 페니스를 해방했다.
            PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 촉수에 의해 더럽혀진 페니스를 곤란한 표정으로 바라보고 있다….
        ELSE
            PRINTFORMW 촉수는 방전을 그만두어 %CALLNAME:TARGET%의 페니스를 해방했다….
        ENDIF
    ELSE
        IF ABL:촉수중독 > 2
            PRINTFORML 불쾌하게 구불구불 휜 촉수가 %CALLNAME:TARGET%의 페니스에 감겨 방전하며 훑어대기 시작했다.
            PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 그 강렬한 자극에 전신으로 기쁨을 표현하고 있다….
        ELSEIF MARK:반발각인 > 1
            PRINTFORML 불쾌하게 구불구불 휜 촉수가 %CALLNAME:TARGET%의 페니스에 감겨 방전하며 훑어 내기 시작했다.
            PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 어떻게든 자극을 견디려고 하며 \@(TEQUIP:42) ? 몸을 부들부들 떨어댔다 # 허덕이며 신음했다 \@….
        ELSE
            PRINTFORMW 불쾌하게 구불구불 휜 촉수가 %CALLNAME:TARGET%의 페니스에 감겨 방전하면서 훑어 내기 시작했다….
        ENDIF
    ENDIF
ELSE
    IF TEQUIP:30
        IF MARK:반발각인 > 1
            PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% 전격에 의한 자극으로 휘청거리고 있는 %조사선택(CALLNAME:TARGET, "로")%부터 %조사선택(COM_TOOL_NAME, "를")% 뺐다.
            PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 숨을 정돈하면서 %조사선택(CALLNAME:PLAYER, "를")% 멍한 표정으로 쳐다봤다….
        ELSEIF TALENT:153 && ASSIPLAY == 0
            PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%의 페니스로부터 %조사선택(COM_TOOL_NAME, "를")% 뺐다.
            PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 다음엔 어디를 괴롭혀 줄 지 기대하면서 %조사선택(CALLNAME:PLAYER, "를")% 뜨거운 시선으로 바라보았다….
        ELSE
            PRINTFORMW %조사선택(CALLNAME:PLAYER, "는")% %조사선택(CALLNAME:TARGET, "로")%부터 %조사선택(COM_TOOL_NAME, "를")% 뺐다….
        ENDIF
    ELSE
        IF MARK:반발각인 > 1
            PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% 싫어서 버둥대는 %조사선택(CALLNAME:TARGET, "를")% 억눌렀다.
            PRINTFORMW 그리고, %CALLNAME:TARGET%의 페니스를 %COM_TOOL_CALLNAME%에 삽입했다….
        ELSE
            PRINTFORMW %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%의 페니스를 %COM_TOOL_CALLNAME%에 삽입했다….
        ENDIF
    ENDIF
ENDIF

;=============================================================================
;스마타
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE43
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:43 > 0
    RETURN 0
;조교자에게 페니스가 없으면 안 됨
SIF EXIST_BAR(PLAYER) == 0
    RETURN 0
;주인이 [금단의 지식] 소유가 아닐 때, 체격차이가 2 단계 이내나 조교자가 [금단의 지식] 소유가 아니면 안 됨
SIF TALENT:MASTER:금단의지식 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
    RETURN 0
;조교 대상으로 페니스가 붙어 있으면(자) 안 됨
SIF EXIST_BAR(TARGET)
    RETURN 0
;윤활이 높지 않으면 안 됨
SIF PALAM:10 < 2000
    RETURN 0
;수면중은 불가
SIF EQUIP:14
    RETURN 0
;패닉중은 불가
SIF TFLAG:108 > 0
    RETURN 0
;실신중은 불가
SIF TFLAG:899 > 0
    RETURN 0
;V계 장착도구 사용중은 불가
SIF TEQUIP:20 || TEQUIP:21
    RETURN 0
;밧줄 사용 중에는 불가
SIF TEQUIP:40
    RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
    RETURN 0
;목욕탕 플레이중은 플레이 매트가 없으면 안 됨
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
    RETURN 0
;한증과 암반 욕 이외의00목욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
    RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
    RETURN 0
;훈도시 착용중의 경우 조교자의 기교 4이상 필수
SIF TEQUIP:59 == 17 && ABL:PLAYER:기교 < 4
    RETURN 0
;조수가 훈도시 착용중의 경우는 안 됨
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17
    RETURN 0
;나체정식 중에는 안 됨
SIF TEQUIP:60
    RETURN 0
;시간정지 중에는 무리
SIF TEQUIP:71
    RETURN 0
;촉수 조교중일 시 주인의 촉수사역Lv2 이하는 안 됨
SIF TEQUIP:90 && ABL:MASTER:촉수사역 <= 2
    RETURN 0
;인어는 안됨
SIF TALENT:인어 == 1
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM43

;조교자가 주인으로, 전회의 조교가 조개 대면시키고(조수)인가 둘이서 가랑이라면 둘이서 가랑이에
SIF ASSIPLAY == 0 && ((TFLAG:133 && PREVCOM == 301) || PREVCOM == 305) && TFLAG:699 == 0
    JUMP COM305
;조교자가 주인의 경우 여기서 샌드위치 재즈 악단 가능한가 어떤가를 우선 조사한다
IF ASSIPLAY == 0 && ASSI >= 0 && TFLAG:157
    CALL JUDGE_TRAIN_PAIR, TARGET, ASSI
    ;샌드위치 편성이 있는 경우만 COMABLE를 호출해 조사한다
    IF TFLAG:175 != 0
        CALL EXCOM_ABLE629
        ;샌드위치 COMABLE도 OK라면 샌드위치에 GO!
        SIF RESULT == 1
            JUMP COM629
    ENDIF
ENDIF
PRINTL 스마타

;-------------------------------------------------
;실행할 수 있을까의 판정
;-------------------------------------------------
IF TFLAG:699 == 0
    CALL COM_ORDER
    ;실행할 수 없다
    SIF RESULT == 0
        RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------
COM_NAME = 스마타
CALL TRAIN_MESSAGE_COM043

;-------------------------------------------------
;사정 게이지 체크
;-------------------------------------------------
LOCAL = 0

;ABL:기교를 본다
IF ABL:기교 == 0
    LOCAL = 500
ELSEIF ABL:기교 == 1
    LOCAL = 1100
ELSEIF ABL:기교 == 2
    LOCAL = 2000
ELSEIF ABL:기교 == 3
    LOCAL = 3000
ELSEIF ABL:기교 == 4
    LOCAL = 3900
ELSE
    LOCAL = 4600
ENDIF

;윤활을 본다
LOCAL = EJACULATIONCHECK_PALAM_10_5(TARGET, LOCAL)
;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;봉사 정신을 본다
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)

;조교자의 C감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
    BASE:PLAYER:사정 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 40
DOWNBASE:기력 += 130

SOURCE:14 = 1000
SOURCE:22 = 1200
SOURCE:31 = 400

;위 쪽으로 계산한 더러운 데이터
SOURCE:30 = TFLAG:242*10 + 60

;ABL:봉사 정신을 본다
IF ABL:봉사정신 == 0
    SOURCE:15 = 200
    SOURCE:16 = 100
    TIMES SOURCE:30 , 4.00
ELSEIF ABL:봉사정신 == 1
    SOURCE:15 = 250
    SOURCE:16 = 180
    TIMES SOURCE:30 , 2.50
ELSEIF ABL:봉사정신 == 2
    SOURCE:15 = 300
    SOURCE:16 = 250
    TIMES SOURCE:30 , 1.50
ELSEIF ABL:봉사정신 == 3
    SOURCE:15 = 350
    SOURCE:16 = 350
    TIMES SOURCE:30 , 1.00
ELSEIF ABL:봉사정신 == 4
    SOURCE:15 = 400
    SOURCE:16 = 500
    TIMES SOURCE:30 , 0.50
ELSE
    SOURCE:15 = 450
    SOURCE:16 = 800
    TIMES SOURCE:30 , 0.10
ENDIF

;ABL:기교를 본다
IF ABL:기교 == 0
    TIMES SOURCE:15 , 0.70
    TIMES SOURCE:16 , 0.70
ELSEIF ABL:기교 == 1
    TIMES SOURCE:15 , 0.90
    TIMES SOURCE:16 , 0.90
ELSEIF ABL:기교 == 2
    TIMES SOURCE:15 , 1.00
    TIMES SOURCE:16 , 1.00
ELSEIF ABL:기교 == 3
    TIMES SOURCE:15 , 1.20
    TIMES SOURCE:16 , 1.20
ELSEIF ABL:기교 == 4
    TIMES SOURCE:15 , 1.40
    TIMES SOURCE:16 , 1.40
ELSE
    TIMES SOURCE:15 , 1.60
    TIMES SOURCE:16 , 1.60
ENDIF

;ABL:C감각을 본다
IF ABL:C감각 == 0
    SOURCE:0 = 0
ELSEIF ABL:C감각 == 1
    SOURCE:0 = 10
ELSEIF ABL:C감각 == 2
    SOURCE:0 = 50
ELSEIF ABL:C감각 == 3
    SOURCE:0 = 200
ELSEIF ABL:C감각 == 4
    SOURCE:0 = 600
ELSE
    SOURCE:0 = 2000
ENDIF

;PALAM:윤활
IF PALAM:10 < PALAMLV:1
    TIMES SOURCE:0 , 0.30
    TIMES SOURCE:15 , 0.60
ELSEIF PALAM:10 < PALAMLV:2
    TIMES SOURCE:0 , 0.60
    TIMES SOURCE:15 , 0.80
ELSEIF PALAM:10 < PALAMLV:3
    TIMES SOURCE:0 , 1.00
    TIMES SOURCE:15 , 1.00
ELSEIF PALAM:10 < PALAMLV:4
    TIMES SOURCE:0 , 1.50
    TIMES SOURCE:15 , 1.20
ELSEIF PALAM:10 < PALAMLV:5
    TIMES SOURCE:0 , 2.00
    TIMES SOURCE:15 , 1.40
ELSE
    TIMES SOURCE:0 , 2.50
    TIMES SOURCE:15 , 1.60
ENDIF

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;사정시의 처리
IF TFLAG:91 >= 1
    ;커맨드 마다 고유의 처리는 이 아래에 쓴다
    TIMES SOURCE:15 , 2.00

    ;ABL:정액 중독을 본다
    IF ABL:정액중독 == 0
        SOURCE:24 = 0
        TIMES SOURCE:16 , 1.50
        TIMES SOURCE:22, 1.40
    ELSEIF ABL:정액중독 == 1
        SOURCE:24 = 200
        TIMES SOURCE:16 , 2.00
        TIMES SOURCE:22, 1.00
    ELSEIF ABL:정액중독 == 2
        SOURCE:24 = 400
        TIMES SOURCE:16 , 2.50
        TIMES SOURCE:22, 0.80
    ELSEIF ABL:정액중독 == 3
        SOURCE:24 = 700
        TIMES SOURCE:16 , 3.00
        TIMES SOURCE:22, 0.50
    ELSEIF ABL:정액중독 == 4
        SOURCE:24 = 1000
        TIMES SOURCE:16 , 4.00
        TIMES SOURCE:22, 0.20
    ELSE
        SOURCE:24 = 1500
        TIMES SOURCE:16 , 5.00
        TIMES SOURCE:22, 0.00
    ENDIF

    ;사정처 타입의 설정
    TFLAG:703 = 5
    ;사정처 선택의 가능·불가
    TFLAG:704 = 1
    ;범용적인 처리는 이 함수로 실시한다
    ;(사정 게이지의 재계산이나 사정 경험의 상승과 공통 부분의 표시)
    CALL SAMEN_SHOOT
ENDIF

;사정에 관계없이 행해지는 처리는 여기로부터
;노예의 V⇔조교자의 P의 불결이 이동
STAIN:보지 |= STAIN:PLAYER:자지
STAIN:PLAYER:자지 |= STAIN:보지

IF TALENT:남자 == 0 && TALENT:PLAYER:남자 == 0
    TCVAR:12 += 7
ELSEIF TALENT:남자 && TALENT:PLAYER:남자
    TCVAR:13 += 7
ENDIF

SIF PLAYER == MASTER
    TCVAR:41 += 1

;주인 경험 플래그
SIF ASSIPLAY == 0
    TFLAG:50 += 1
;봉사 쾌락 경험 입수 판정
TFLAG:100 |= 1

;조교자가 후타나리
SIF TALENT:PLAYER:후타나리
    SOURCE:22 /= 2

RETURN 1

;--------------------------------------------------
;명령 거부의 메시지
;--------------------------------------------------
@DENIAL_MESSAGE_COM_43
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
PRINTFORML %CALLNAME:TARGET%에게 가랑이로 비비도록 명령했지만 거절당했다.
PRINTW 좀 더 조교가 필요한 것 같다.

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM043
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
IF TFLAG:699 > 0
    PRINTFORML %조사선택(CALLNAME:MASTER, "가")% 명령하지도 않았는데 %조사선택(CALLNAME:TARGET, "는")% 스스로 
    SIF TALENT:음란 || TEQUIP:11 == 1
        PRINT 참을 수 없다는 듯한 표정을 하곤 
    SIF TALENT:새드
        PRINTFORM 욕정을 부채질하는 듯한 시선을 %CALLNAME:MASTER%에게 보내곤 
    SIF TALENT:질투
        PRINT 질투의 원념을 보이듯이 
    SIF TALENT:광기 && TEQUIP:42 == 0
        PRINT 미쳐버린 듯한 웃음소리를 내며 
ELSE
    PRINTFORM %조사선택(CALLNAME:TARGET, "는")% %CALLNAME:PLAYER%의 페니스를 가랑이에 끼우고 
    IF TALENT:연모 && TALENT:음란 && PALAM:13 > PALAMLV:5
        PRINTFORM 쾌락에 완전히 빠진 표정으로 %조사선택(CALLNAME:PLAYER, "를")% 바라보면서 
    ELSEIF TALENT:연모 && PALAM:13 > PALAMLV:5
        PRINT 지금 당장이라도 녹아버릴 것 같은 표정을 지으면서 
    ELSEIF TALENT:음란 && PALAM:13 > PALAMLV:5
        PRINT 애원하는 듯한 눈빛으로 
    ELSEIF PALAM:13 > PALAMLV:4
        PRINTFORM \@(TEQUIP:42) ? 쾌락을 마음껏 만끽하면서 # 입가에서 침을 흘리면서 \@ 
    ELSEIF TALENT:연모 && TALENT:음란
        PRINTFORM 도취한 듯한 눈동자로 %조사선택(CALLNAME:PLAYER, "를")% 바라보면서 
    ELSEIF TALENT:연모
        PRINT 행복한 듯한 표정을 짓고서 
    ELSEIF TALENT:음란
        PRINT 음란한 눈빛으로 도발하면서 
    ELSEIF PALAM:13 > PALAMLV:5 && PALAM:14 > PALAMLV:4 && TALENT:복종 == 0 && MARK:반발각인 > 1
        PRINT 하고 싶지 않지만 몸이 멋대로 움직여서 억울해하며 
    ELSEIF TALENT:복종 == 0 && MARK:반발각인 > 1
        PRINT 싫어하는 표정을 지으며 
    ELSEIF TALENT:부끄럼없음
        PRINT 대담하게 허리를 움직이면서 
    ELSEIF TALENT:감정결여
        PRINT 새빨갛게 상기된 얼굴로 
    ELSE
        PRINT 부끄러운 듯이 
    ENDIF
ENDIF
IF TALENT:작은체형
    PRINT 어린 균열을 
ELSE
    PRINT 음순을 
ENDIF
PRINTW 페니스에 비벼대고 있다….

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_43, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:욕망
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:욕망
    TFLAG:240 += LOCAL:99 * 2
    RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF
;ABL:봉사 정신
IF ABL:봉사정신
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:봉사정신
    TFLAG:240 += LOCAL:99 * 4
    RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:정액 중독
IF ABL:정액중독
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:정액중독
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99})
ENDIF

;쾌락 각인
IF MARK:쾌락각인
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = MARK:쾌락각인
    TFLAG:240 += LOCAL:99 * 2
    RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF

;PALAM:욕정
GETPALAMLV PALAM:13, 5
LOCAL:2 = RESULT
IF LOCAL:2
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = LOCAL:2 * 3
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;부끄럼쟁이
IF TALENT:부끄럼쟁이
    RESULTS = %RESULTS% - 
    LOCAL:99 = 1
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;남자 싫다
IF TALENT:남성혐오 && TALENT:PLAYER:남자
    RESULTS = %RESULTS% - 
    LOCAL:99 = 7
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;여자 증오
ELSEIF TALENT:여성혐오 && TALENT:PLAYER:남자 == 0
    RESULTS = %RESULTS% - 
    LOCAL:99 = 7
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;쾌감의 부정
IF TALENT:쾌감을부정
    RESULTS = %RESULTS% - 
    LOCAL:99 = 3
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;연모
IF TALENT:연모 && ASSIPLAY == 0
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 3
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF

;조교자가 후타나리
IF TALENT:PLAYER:후타나리
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 8
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %조사처리(CALLNAME:PLAYER,"가")% %TALENTNAME:121%({LOCAL:99})
ENDIF

;미약
IF TEQUIP:11
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 6
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %ITEMNAME:61%({LOCAL:99})
ENDIF

;조교자의 P의 더러움
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)
;가랑이에서는 더러움의 영향이 적은
TFLAG:242 /= 3

;서로불결
IF TFLAG:242
    RESULTS = %RESULTS% - 
    TFLAG:240 -= TFLAG:242
    RESULTS = %RESULTS% 서로불결
    ;악취 둔감
    IF TALENT:냄새둔감
        RESULTS = %RESULTS%, %TALENTNAME:61%
    ELSEIF TALENT:냄새민감
        RESULTS = %RESULTS%, %TALENTNAME:62%
    ENDIF
    RESULTS = %RESULTS%({TFLAG:242})
    LOCAL:99 = 1
ENDIF

TFLAG:241 = 20
;실행치가 75이상때는 샌드위치의 체크 온
TFLAG:157 = (TFLAG:240 >= 75) ?  1 # 0

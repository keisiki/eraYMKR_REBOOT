;=============================================================================
;헤어잡
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE48
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:48 > 0
    RETURN 0
;조교자에게 페니스가 없으면 안 됨
SIF EXIST_BAR(PLAYER) == 0
    RETURN 0
;수면중은 불가
SIF EQUIP:14
    RETURN 0
;패닉중은 불가
SIF TFLAG:108 > 0
    RETURN 0
;실신중은 불가
SIF TFLAG:899 > 0
    RETURN 0
;밧줄 사용 중에는 목분리시만 가능
SIF TEQUIP:40 && TALENT:목분리 == 0
    RETURN 0
;승마 중에는 불가
SIF TEQUIP:43 && TALENT:목분리 == 0
    RETURN 0
;조수가 훈도시 착용중의 경우는 안 됨
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17
    RETURN 0
;00목욕탕 입욕 중에는 불가
SIF TEQUIP:55
    RETURN 0
;시간정지 중에는 무리
SIF TEQUIP:71
    RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM48
PRINTL 헤어잡

;-------------------------------------------------
;실행할 수 있는가 판정
;-------------------------------------------------
IF TFLAG:699 == 0
    CALL COM_ORDER
    ;실행할 수 없다
    SIF RESULT == 0
        RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------
COM_NAME = 헤어잡
CALL TRAIN_MESSAGE_COM048

;-------------------------------------------------
;사정 게이지 체크
;-------------------------------------------------
LOCAL = 0

;ABL:조교자의 기교를 본다
IF ABL:PLAYER:기교 == 0
    LOCAL = 1200
ELSEIF ABL:PLAYER:기교 == 1
    LOCAL = 1700
ELSEIF ABL:PLAYER:기교 == 2
    LOCAL = 2300
ELSEIF ABL:PLAYER:기교 == 3
    LOCAL = 3000
ELSEIF ABL:PLAYER:기교 == 4
    LOCAL = 3600
ELSE
    LOCAL = 4200
ENDIF

;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;봉사정신을 본다
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)
;정액중독을 본다
LOCAL = EJACULATIONCHECK_ABL_31_1(TARGET, LOCAL)

;조교자의 Ｃ감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
    BASE:PLAYER:사정 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 40
DOWNBASE:기력 += 180

SOURCE:22 = 2000
SOURCE:31 = 600

;위쪽에서 계산한 더러움 데이터
SOURCE:30 = TFLAG:242*40 + 100

;ABL:봉사정신을 본다
IF ABL:봉사정신 == 0
    SOURCE:15 = 420
    SOURCE:16 = 150
    SOURCE:32 = 90
    TIMES SOURCE:30 , 4.00
ELSEIF ABL:봉사정신 == 1
    SOURCE:15 = 500
    SOURCE:16 = 300
    SOURCE:32 = 65
    TIMES SOURCE:30 , 2.50
ELSEIF ABL:봉사정신 == 2
    SOURCE:15 = 580
    SOURCE:16 = 600
    SOURCE:32 = 40
    TIMES SOURCE:30 , 1.50
ELSEIF ABL:봉사정신 == 3
    SOURCE:15 = 660
    SOURCE:16 = 900
    SOURCE:32 = 15
    TIMES SOURCE:30 , 1.00
ELSEIF ABL:봉사정신 == 4
    SOURCE:15 = 740
    SOURCE:16 = 1500
    TIMES SOURCE:30 , 0.50
ELSE
    SOURCE:15 = 820
    SOURCE:16 = 2200
    TIMES SOURCE:30 , 0.10
ENDIF

;ABL:기교를 본다
IF ABL:기교 == 0
    TIMES SOURCE:15 , 0.50
    TIMES SOURCE:16 , 0.30
ELSEIF ABL:기교 == 1
    TIMES SOURCE:15 , 0.80
    TIMES SOURCE:16 , 0.50
ELSEIF ABL:기교 == 2
    TIMES SOURCE:15 , 1.00
    TIMES SOURCE:16 , 0.80
ELSEIF ABL:기교 == 3
    TIMES SOURCE:15 , 1.20
    TIMES SOURCE:16 , 1.00
ELSEIF ABL:기교 == 4
    TIMES SOURCE:15 , 1.50
    TIMES SOURCE:16 , 1.20
ELSE
    TIMES SOURCE:15 , 2.00
    TIMES SOURCE:16 , 1.50
ENDIF

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;사정시의 처리
IF TFLAG:91 >= 1
    ;コマンドごとに固有の処理はこの下に書く
    TIMES SOURCE:15 , 3.00

    ;ABL:정액중독을 본다
    IF ABL:정액중독 == 0
        SOURCE:24 = 0
        TIMES SOURCE:16 , 2.00
        TIMES SOURCE:22, 4.00
    ELSEIF ABL:정액중독 == 1
        SOURCE:24 = 500
        TIMES SOURCE:16 , 3.00
        TIMES SOURCE:22, 3.00
    ELSEIF ABL:정액중독 == 2
        SOURCE:24 = 1200
        TIMES SOURCE:16 , 4.00
        TIMES SOURCE:22, 2.50
    ELSEIF ABL:정액중독 == 3
        SOURCE:24 = 3000
        TIMES SOURCE:16 , 6.00
        TIMES SOURCE:22, 2.00
    ELSEIF ABL:정액중독 == 4
        SOURCE:24 = 6000
        TIMES SOURCE:16 , 9.00
        TIMES SOURCE:22, 1.50
    ELSE
        SOURCE:24 = 12000
        TIMES SOURCE:16 , 15.00
        TIMES SOURCE:22, 1.00
    ENDIF

    ;射精先タイプの設定
    TFLAG:703 = 23
    ;射精先選択の可・不可
    TFLAG:704 = 0
    ;汎用的な処理はこの関数で行う
    ;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
    CALL SAMEN_SHOOT

    ;精液汚れ
    STAIN:발 |= 4
ENDIF

;対象の髪의 더러움⇔조교자의 Ｐ의 더러움が相互に移動
STAIN:발 |= STAIN:PLAYER:자지
STAIN:PLAYER:자지 |= STAIN:발

IF TALENT:남자 == 0 && TALENT:PLAYER:남자 == 0
    TCVAR:12 += 7
ELSEIF TALENT:남자 && TALENT:PLAYER:남자
    TCVAR:13 += 7
ENDIF
;주인 경험 플래그
SIF ASSIPLAY == 0 && EXP:펠라경험 >= EXPLV:3
    TFLAG:50 += 1

;조교자가 후타나리
SIF TALENT:PLAYER:후타나리
    SOURCE:22 /= 2
;奉仕快楽経験入手判定
TFLAG:100 |= 1

RETURN 1

;--------------------------------------------------
;명령 거부 메시지
;--------------------------------------------------
@DENIAL_MESSAGE_COM_48
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
IF ASSIPLAY
    PRINTFORML %CALLNAME:TARGET%에게 %CALLNAME:PLAYER%의 페니스를 머리카락으로 얽히게 해서 문지르도록 명령해 보았지만 거절당했다.
ELSE
    PRINTFORML %CALLNAME:TARGET%에게 자신의 페니스를 머리카락으로 얽히게 해서 문지르도록 명령해 보았지만 거절당했다.
ENDIF
PRINTW 좀 더 조교가 필요한 것 같다.

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM048
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
    PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%에게 다가와 머리카락을 자기 페니스에 휘감고 부비기 시작했다.
    PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 눈앞의 행위에 
IF TALENT:음란 && ABL:정액중독 >= 3
    PRINTFORMW \@(TEQUIP:42) ? 멍한 표정으로 # 입을 헤 벌린 채로 \@ %조사선택(CALLNAME:PLAYER, "가")% 자위하는 걸 보고 있다.
ELSEIF TALENT:연모 == 0 && TALENT:복종 == 0 && MARK:반발각인 > 1
    PRINTFORMW 몸을 맡기면서도 굴욕과 분노의 표정을 %CALLNAME:PLAYER%에게 보여주고 있다.
ELSEIF ABL:정액중독 == 0 || ABL:기교 <= 1
    PRINTW 고개를 돌렸다….
ELSEIF ABL:정액중독 <= 3 || ABL:기교 <= 3
    PRINTFORMW 수줍어 하면서도, %CALLNAME:PLAYER%의 자위를 힐끔 힐끔 보고 있다.
ELSEIF ASSIPLAY == 0 && (TALENT:친애 || TALENT:상애 || TALENT:예속)
    PRINTL 별다른 내색을 보이지 않지만
    PRINTFORMW 언제든지 %CALLNAME:PLAYER%의 정액을 받을 수 있도록 페니스를 주시하고 있다….
ELSE
    PRINTFORMW 사정을 기대하듯이 %조사선택(CALLNAME:PLAYER, "를")% 지켜보고 있다….
ENDIF
PRINTL

;-------------------------------------------------
;고유 실행 판정
;-------------------------------------------------
@COM_ORDER_48, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:욕망
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:욕망
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99})
ENDIF
;ABL:봉사정신
IF ABL:봉사정신
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:봉사정신
    TFLAG:240 += LOCAL:99 * 4
    RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:정액중독
IF ABL:정액중독
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:정액중독
    TFLAG:240 += LOCAL:99 * 3
    RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;쾌락각인
IF MARK:쾌락각인
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = MARK:쾌락각인
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99})
ENDIF

;PALAM:욕정
GETPALAMLV PALAM:13, 5
LOCAL:2 = RESULT
IF LOCAL:2
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = LOCAL:2
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;부끄럼쟁이
IF TALENT:부끄럼쟁이
    RESULTS = %RESULTS% - 
    LOCAL:99 = 1
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;男嫌い
IF TALENT:남성혐오 && TALENT:PLAYER:남자
    RESULTS = %RESULTS% - 
    LOCAL:99 = 12
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;女嫌い
ELSEIF TALENT:여성혐오 && TALENT:PLAYER:남자 == 0
    RESULTS = %RESULTS% - 
    LOCAL:99 = 12
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;냄새둔감
IF TALENT:냄새둔감
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 1
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:61%({LOCAL:99})
;냄새민감
ELSEIF TALENT:냄새민감
    RESULTS = %RESULTS% - 
    LOCAL:99 = 3
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:62%({LOCAL:99})
ENDIF
;헌신적
IF TALENT:헌신적
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 6
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:63%({LOCAL:99})
ENDIF
;쾌감의 부정
IF TALENT:쾌감을부정
    RESULTS = %RESULTS% - 
    LOCAL:99 = 1
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;연모
IF TALENT:연모 && ASSIPLAY == 0
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 5
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF

;조교자가 후타나리
IF TALENT:PLAYER:후타나리
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 8
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %조사처리(CALLNAME:PLAYER,"가")% %TALENTNAME:121%({LOCAL:99})
ENDIF

;조교자의 Ｐ의 더러움
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)

;불결 있음
IF TFLAG:242
    RESULTS = %RESULTS% - 
    TFLAG:240 -= TFLAG:242
    RESULTS = %RESULTS% 불결 있음
    ;냄새둔감
    IF TALENT:냄새둔감
        RESULTS = %RESULTS%%TALENTNAME:61%
    ELSEIF TALENT:냄새민감
        RESULTS = %RESULTS%%TALENTNAME:62%
    ENDIF
    RESULTS = %RESULTS%({TFLAG:242})
    LOCAL:99 = 1
ENDIF

TFLAG:241 = 22

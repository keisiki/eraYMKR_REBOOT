;=============================================================================
;좌약(질)
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE64
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:64 > 0
    RETURN 0
;V조교 필터가 오프가 되지 않으면 안 됨
SIF FLAG:VA이상조교필터 & 1
    RETURN 0
;아이템을 가지고 있는지, 아이템 없음 설정이 아니면 안 됨
SIF ITEM:63 == 0 && NOITEM == 0
    RETURN 0
;처녀는 안 됨
SIF TALENT:처녀 == 1
    RETURN 0
;남자라면 안 됨
SIF TALENT:120
    RETURN 0
;윤활 부족해, 순종 4 미만·레즈끼(BL끼) 4 미만의 ASSI의 경우, 조교 대상이 동성이라면 실행할 수 없다(새드나 새드끼 Lv3 이상, 또는 망신이나 복종이 있다면 OK)
IF PALAM:10 < PALAMLV:2 && ASSIPLAY
    SIF (ABL:ASSI:순종 < 4 || ((TALENT:120 == 0 && TALENT:ASSI:120 == 0 && ABL:ASSI:레즈끼 < 4) || (TALENT:120 && TALENT:ASSI:120 && ABL:ASSI:BL끼 < 4))) && TALENT:ASSI:81 == 0 && ABL:ASSI:새드끼 < 3 && TALENT:ASSI:151 == 0 && TALENT:ASSI:160 == 0
        RETURN 0
ENDIF
;V계 장착도구 사용중은 불가
SIF TEQUIP:20 || TEQUIP:21
    RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
    RETURN 0
;한증과 암반 욕 이외의00목욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
    RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
    RETURN 0
;훈도시 착용중의 경우 조교자의 기교 4이상 필수
SIF TEQUIP:59 == 17 && ABL:PLAYER:기교 < 4
    RETURN 0
;학교수영복 플레이 중에는 불가
SIF TEQUIP:59 == 2
    RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;약물내성 보유면 효과가 감소(삽입시는 빼고)
@COM64
IF TEQUIP:15 & 2
    PRINTL 좌약(질) 추가 삽입
    COM_NAME = 좌약(질) 추가 삽입
ELSE
    PRINTL 좌약(질) 삽입
    COM_NAME = 좌약(질) 삽입
ENDIF

;調合知識があれば消費を抑える
IF TALENT:MASTER:조합지식 || (ASSI >= 0 && TALENT:ASSI:조합지식)
    DOWNBASE:0 += 6
    DOWNBASE:기력 += 22
ELSE
    DOWNBASE:0 += 11
    DOWNBASE:기력 += 60
ENDIF

SOURCE:12 = 350
SOURCE:31 = 225

;ABL:Ｖ감각을 본다
IF ABL:V감각 == 0
    SOURCE:1 = 10
    SOURCE:22 = 50
ELSEIF ABL:V감각 == 1
    SOURCE:1 = 50
    SOURCE:22 = 75
ELSEIF ABL:V감각 == 2
    SOURCE:1 = 100
    SOURCE:22 = 150
ELSEIF ABL:V감각 == 3
    SOURCE:1 = 250
    SOURCE:22 = 300
ELSEIF ABL:V감각 == 4
    SOURCE:1 = 400
    SOURCE:22 = 500
ELSE
    SOURCE:1 = 750
    SOURCE:22 = 800
ENDIF

;EXP:V경험을 본다
IF EXP:V경험 < EXPLV:1
    SOURCE:20 = 500
    TIMES SOURCE:1 , 0.20
    TIMES SOURCE:22 , 0.20
ELSEIF EXP:V경험 < EXPLV:2
    SOURCE:20 = 400
    TIMES SOURCE:1 , 0.50
    TIMES SOURCE:22 , 0.50
ELSEIF EXP:V경험 < EXPLV:3
    SOURCE:20 = 300
    TIMES SOURCE:1 , 1.00
    TIMES SOURCE:22 , 1.00
ELSEIF EXP:V경험 < EXPLV:4
    SOURCE:20 = 200
    TIMES SOURCE:1 , 1.20
    TIMES SOURCE:22 , 1.20
ELSEIF EXP:V경험 < EXPLV:5
    SOURCE:20 = 100
    TIMES SOURCE:1 , 1.60
    TIMES SOURCE:22 , 1.60
ELSE
    SOURCE:20 = 50
    TIMES SOURCE:1 , 1.80
    TIMES SOURCE:22 , 1.80
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
    TIMES SOURCE:1 , 0.10
    TIMES SOURCE:20 , 3.00
    TIMES SOURCE:22 , 0.10
ELSEIF PALAM:10 < PALAMLV:2
    TIMES SOURCE:1 , 0.20
    TIMES SOURCE:20 , 2.00
    TIMES SOURCE:22 , 0.20
ELSEIF PALAM:10 < PALAMLV:3
    TIMES SOURCE:1 , 0.60
    TIMES SOURCE:20 , 1.00
    TIMES SOURCE:22 , 0.60
ELSEIF PALAM:10 < PALAMLV:4
    TIMES SOURCE:1 , 1.00
    TIMES SOURCE:20 , 0.50
    TIMES SOURCE:22 , 1.00
ELSEIF PALAM:10 >= PALAMLV:4
    TIMES SOURCE:1 , 2.00
    TIMES SOURCE:20 , 0.10
    TIMES SOURCE:22 , 2.00
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    TIMES SOURCE:1 , 0.30
    TIMES SOURCE:22 , 0.30
ELSEIF PALAM:13 < PALAMLV:2
    TIMES SOURCE:1 , 0.60
    TIMES SOURCE:22 , 0.60
ELSEIF PALAM:13 < PALAMLV:3
    TIMES SOURCE:1 , 1.00
    TIMES SOURCE:22 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
    TIMES SOURCE:1 , 1.30
    TIMES SOURCE:22 , 1.30
ELSEIF PALAM:13 >= PALAMLV:4
    TIMES SOURCE:1 , 1.60
    TIMES SOURCE:22 , 1.60
ENDIF

;V민감둔감을 본다
;快Ｖ自体のチェックは後でまとめてやる
IF TALENT:102
    TIMES SOURCE:22 , 1.50
    TIMES SOURCE:31 , 1.50
ELSEIF TALENT:103
    TIMES SOURCE:22 , 0.60
    TIMES SOURCE:31 , 0.60
ENDIF

;처녀에다 정조관념（ただし処女にはできないようにするので死にIF）
SIF TALENT:처녀 == 1 && TALENT:30
    SOURCE:22 /= 3

CALL TRAIN_MESSAGE_COM064
TEQUIP:15 |= 2

TCVAR:0 += 2

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
    TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:120
    TCVAR:13 += 2
ENDIF

IF NOITEM == 0
    ITEM:63 -= 1
    SIF FLAG:아이템확장 & 8 && MONEY:0 >= ITEMPRICE:63
        CALL FILL_PLURAL_ITEM, 63
ENDIF
RETURN 1

;-------------------------------------------------
;질 좌약 효과 발휘 중
;-------------------------------------------------
@EQUIP_COM64
PRINTL <질 좌약 효과 발휘 중>

DOWNBASE:0 += 55
DOWNBASE:기력 += 33

LOCAL:1 = 300
LOCAL:2 = 100

;ABL:Ｖ감각을 본다
IF ABL:V감각 == 0
    LOCAL = 5
ELSEIF ABL:V감각 == 1
    LOCAL = 10
ELSEIF ABL:V감각 == 2
    LOCAL = 20
ELSEIF ABL:V감각 == 3
    LOCAL = 50
ELSEIF ABL:V감각 == 4
    LOCAL = 100
ELSE
    LOCAL = 180
ENDIF

;V민감둔감을 본다
;快Ｖ自体のチェックは後でまとめてやる
IF TALENT:102
    TIMES LOCAL:1 , 1.50
    TIMES LOCAL:2 , 1.50
ELSEIF TALENT:103
    TIMES LOCAL:1 , 0.60
    TIMES LOCAL:2 , 0.60
ENDIF
;약독내성
IF TALENT:44
    TIMES LOCAL , 0.50
    TIMES LOCAL:1 , 0.50
    TIMES LOCAL:2 , 1.50
ENDIF

SOURCE:1 += LOCAL
SOURCE:22 += LOCAL:1
SOURCE:31 += LOCAL:2

TCVAR:0 += 1

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
    TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:120
    TCVAR:13 += 2
ENDIF
RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM064
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
IF TEQUIP:15 & 2
    PRINTFORML 좌약의 효과로 느슨해져 버린 %CALLNAME:TARGET%의 질에 
    PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% 다시 새로운 좌약을 집어넣었다.
ELSE
    PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%의 질에 탄환과 같은 형태의 좌약을 집어넣었다.
ENDIF

PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
IF EXP:V경험 < 20
    PRINTL 공포로 얼굴을 일그러뜨리고
    PRINTFORM 어떻게든 %CALLNAME:PLAYER%의 손에서 피하려고 날뛰고 있다….
ELSEIF EXP:V경험 < 40
    PRINT 질에서 쳐 올라오는 이물감에 굴욕과 혐오의 표정을 하고 있다….
ELSEIF EXP:V경험 < 75
    PRINTL 싫어하고 있었지만, 별다른 저항은 하지 않고
    PRINT 어른스럽게 좌약을 받아들이고 있다….
ELSEIF EXP:V경험 < 100
    PRINT 질을 통과되었을때의 감촉을 참을 수 없는지, 몸을 떨고 있다….
ELSEIF EXP:V경험 < 140
    PRINT 좌약이 들어오는 느낌이 좋은 듯하다….
ELSEIF EXP:V경험 < 180
    PRINT 약을 삽입한 후에도 허리를 흔들며 더욱 쾌감을 쫓고 있다….
ELSE
    PRINTL 만족할 수 없는지 약을 더욱 더 안쪽에 밀어 넣기 위해
    PRINT 스스로의 손가락을 질안에 쑤셔 넣고 있다….
ENDIF
PRINTW

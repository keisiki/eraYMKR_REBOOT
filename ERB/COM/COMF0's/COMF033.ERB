;=============================================================================
;배면좌위 애널
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE33
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:33 > 0
    RETURN 0
;A조교 필터가 오프가 되지 않으면 안 됨
SIF FLAG:VA이상조교필터 & 2
    RETURN 0
;조교자에게 페니스가 없는, 한편(아이템 있음 설정이라면) 페니스 밴드도 없으면 안 됨
SIF EXIST_BAR(PLAYER) == 0 && ITEM:2 == 0 && NOITEM == 0
    RETURN 0
;주인이 [금단의 지식] 소유가 아닐 때, 체격차이가 2 단계 이내나 조교자가 [금단의 지식] 소유가 아니면 안 됨
SIF TALENT:MASTER:금단의지식 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
    RETURN 0
;윤활 부족해, 순종 4 미만·레즈끼(BL끼) 4 미만의 ASSI의 경우, 조교 대상이 동성이라면 실행할 수 없다(새드나 새드끼 Lv3 이상, 또는 망신이나 복종이 있다면 OK)
IF PALAM:10 < PALAMLV:2 && ASSIPLAY
    SIF (ABL:ASSI:순종 < 4 || ((TALENT:남자 == 0 && TALENT:ASSI:남자 == 0 && ABL:ASSI:레즈끼 < 4) || (TALENT:남자 && TALENT:ASSI:남자 && ABL:ASSI:BL끼 < 4))) && TALENT:ASSI:새드 == 0 && ABL:ASSI:새드끼 < 3 && TALENT:ASSI:망신 == 0 && TALENT:ASSI:복종 == 0
        RETURN 0
ENDIF
;일정한 A경험이 필요
SIF EXP:A경험 < EXPLV:3
    RETURN 0
;그 조교중에 관장 플레이＋배설이 있었을 경우, 조교자가 더러운 무시 혹은 악취 둔감을 가지지 않으면 실행할 수 없다
SIF TFLAG:140 & 256 && TALENT:PLAYER:냄새둔감 == 0 && TALENT:PLAYER:불결무시 == 0
    RETURN 0
;A계 장착도구 사용중은 불가
SIF TEQUIP:25 || TEQUIP:26
    RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
    RETURN 0
;목욕탕 플레이중은 플레이 매트가 없으면 안 됨
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
    RETURN 0
;한증과 암반 욕 이외의00목욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
    RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
    RETURN 0
;훈도시 착용중의 경우 조교자의 기교 4이상 필수
SIF TEQUIP:59 == 17 && ABL:PLAYER:기교 < 4
    RETURN 0
;조수가 훈도시 착용중의 경우 기교 4이상 필수
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17 && ABL:기교 < 4
    RETURN 0
;나체정식 중에는 안 됨
SIF TEQUIP:60
    RETURN 0
;촉수 조교중일 시 주인의 촉수사역Lv2 이하는 안 됨
SIF TEQUIP:90 && ABL:MASTER:촉수사역 <= 2
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM33
PRINTL 배면좌위 애널
COM_NAME = 배면좌위 애널
CALL TRAIN_MESSAGE_COM033

;-------------------------------------------------
;사정 게이지 체크
;-------------------------------------------------
LOCAL = 0

;ABL:기교를 본다
IF ABL:기교 == 0
    LOCAL = 900
ELSEIF ABL:기교 == 1
    LOCAL = 1100
ELSEIF ABL:기교 == 2
    LOCAL = 1300
ELSEIF ABL:기교 == 3
    LOCAL = 1500
ELSEIF ABL:기교 == 4
    LOCAL = 1700
ELSE
    LOCAL = 1900
ENDIF

;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_2(TARGET, LOCAL)
;욕망을 본다
LOCAL = EJACULATIONCHECK_ABL_11_1(TARGET, LOCAL)
;윤활을 본다
LOCAL = EJACULATIONCHECK_PALAM_10_2(TARGET, LOCAL)

;조교자의 C감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
    BASE:PLAYER:사정 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 70
DOWNBASE:기력 += 90

SOURCE:12 = 200

;ABL:A감각을 본다
IF ABL:A감각 == 0
    SOURCE:2 += 10
    SOURCE:11 += 10
    SOURCE:22 += 80
ELSEIF ABL:A감각 == 1
    SOURCE:2 += 30
    SOURCE:11 += 130
    SOURCE:22 += 300
ELSEIF ABL:A감각 == 2
    SOURCE:2 += 200
    SOURCE:11 += 500
    SOURCE:22 += 700
ELSEIF ABL:A감각 == 3
    SOURCE:2 += 500
    SOURCE:11 += 1000
    SOURCE:22 += 1500
ELSEIF ABL:A감각 == 4
    SOURCE:2 += 900
    SOURCE:11 += 1500
    SOURCE:22 += 2600
ELSE
    SOURCE:2 += 1400
    SOURCE:11 += 2000
    SOURCE:22 += 4000
ENDIF

;EXP:A경험을 본다
IF EXP:A경험 < EXPLV:1
    TIMES SOURCE:2 , 0.10
    SOURCE:20 = 18000
    DOWNBASE:0 += 40
    DOWNBASE:기력 += 60
ELSEIF EXP:A경험 < EXPLV:2
    TIMES SOURCE:2 , 0.30
    SOURCE:20 = 10000
    DOWNBASE:0 += 30
    DOWNBASE:기력 += 50
ELSEIF EXP:A경험 < EXPLV:3
    TIMES SOURCE:2 , 0.50
    SOURCE:20 = 4500
    DOWNBASE:0 += 20
    DOWNBASE:기력 += 40
ELSEIF EXP:A경험 < EXPLV:4
    TIMES SOURCE:2 , 1.00
    SOURCE:20 = 1500
    DOWNBASE:0 += 10
    DOWNBASE:기력 += 30
ELSEIF EXP:A경험 < EXPLV:5
    TIMES SOURCE:2 , 1.40
    SOURCE:20 = 700
    DOWNBASE:0 += 0
    DOWNBASE:기력 += 20
ELSE
    TIMES SOURCE:2 , 1.60
    SOURCE:20 = 300
    DOWNBASE:0 += 0
    DOWNBASE:기력 += 0
ENDIF

;ABL:B감각을 본다
IF ABL:B감각 == 0
    SOURCE:3 = 50
    SOURCE:11 += 50
ELSEIF ABL:B감각 == 1
    SOURCE:3 = 200
    SOURCE:11 += 200
ELSEIF ABL:B감각 == 2
    SOURCE:3 = 500
    SOURCE:11 += 400
ELSEIF ABL:B감각 == 3
    SOURCE:3 = 800
    SOURCE:11 += 600
ELSEIF ABL:B감각 == 4
    SOURCE:3 = 1300
    SOURCE:11 += 1000
ELSE
    SOURCE:3 = 1800
    SOURCE:11 += 1400
ENDIF

;ABL:C감각을 본다
IF ABL:C감각 == 0
    SOURCE:0 = 40
ELSEIF ABL:C감각 == 1
    SOURCE:0 = 160
ELSEIF ABL:C감각 == 2
    SOURCE:0 = 500
ELSEIF ABL:C감각 == 3
    SOURCE:0 = 900
ELSEIF ABL:C감각 == 4
    SOURCE:0 = 1400
ELSE
    SOURCE:0 = 2100
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
    TIMES SOURCE:2 , 0.40
    SOURCE:20 += 10000
ELSEIF PALAM:10 < PALAMLV:2
    TIMES SOURCE:2 , 0.80
    SOURCE:20 += 3600
ELSEIF PALAM:10 < PALAMLV:3
    TIMES SOURCE:2 , 1.00
    SOURCE:20 += 1200
ELSEIF PALAM:10 < PALAMLV:4
    TIMES SOURCE:2 , 1.40
    SOURCE:20 += 200
ELSE
    TIMES SOURCE:2 , 1.80
    SOURCE:20 += 100
ENDIF

;조교자가[남자]
IF TALENT:PLAYER:남자
    TIMES SOURCE:2 , 2.50
;조교자가[후타나리]
ELSEIF TALENT:PLAYER:후타나리
    TIMES SOURCE:22 , 1.50
    TIMES SOURCE:31 , 1.60
ENDIF

;[작은체형]
SIF TALENT:작은체형
    TIMES SOURCE:20 , 2.00

;정조관념
IF TALENT:정조관념
    IF EXP:A경험 == 0
        TIMES SOURCE:11 , 1.20
        SOURCE:32 = 500
    ELSE
        TIMES SOURCE:11 , 1.20
        SOURCE:32 = 0
    ENDIF
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    TIMES SOURCE:2 , 0.80
    TIMES SOURCE:0 , 0.80
    TIMES SOURCE:11 , 0.60
ELSEIF PALAM:13 < PALAMLV:2
    TIMES SOURCE:2 , 1.00
    TIMES SOURCE:0 , 1.00
    TIMES SOURCE:11 , 1.00
ELSEIF PALAM:13 < PALAMLV:3
    TIMES SOURCE:2 , 1.20
    TIMES SOURCE:0 , 1.10
    TIMES SOURCE:11 , 1.50
ELSEIF PALAM:13 < PALAMLV:4
    TIMES SOURCE:2 , 1.40
    TIMES SOURCE:0 , 1.20
    TIMES SOURCE:11 , 2.00
ELSE
    TIMES SOURCE:2 , 1.60
    TIMES SOURCE:0 , 1.30
    TIMES SOURCE:11 , 2.60
ENDIF

;ABL:순종을 본다
IF ABL:순종 == 0
    TIMES SOURCE:2 , 1.50
    TIMES SOURCE:11 , 1.00
    TIMES SOURCE:32 , 2.00
ELSEIF ABL:순종 == 1
    TIMES SOURCE:2 , 1.50
    TIMES SOURCE:11 , 1.30
    TIMES SOURCE:32 , 1.80
ELSEIF ABL:순종 == 2
    TIMES SOURCE:2 , 1.50
    TIMES SOURCE:11 , 1.50
    TIMES SOURCE:32 , 1.60
ELSEIF ABL:순종 == 3
    TIMES SOURCE:2 , 1.80
    TIMES SOURCE:11 , 1.90
    TIMES SOURCE:32 , 1.40
ELSEIF ABL:순종 == 4
    TIMES SOURCE:2 , 2.10
    TIMES SOURCE:11 , 2.20
    TIMES SOURCE:32 , 1.20
ELSE
    TIMES SOURCE:2 , 2.50
    TIMES SOURCE:11 , 2.60
    TIMES SOURCE:32 , 1.00
ENDIF

;플레이어의 ABL:기교를 본다
IF ABL:PLAYER:기교 == 0
    SOURCE:11 += 100
    SOURCE:19 = 0
    LOCAL:1 = 0
ELSEIF ABL:PLAYER:기교 == 1
    SOURCE:11 += 150
    SOURCE:19 += 50
    LOCAL:1 = 0
ELSEIF ABL:PLAYER:기교 == 2
    SOURCE:11 += 200
    SOURCE:19 += 100
    LOCAL:1 = 0
ELSEIF ABL:PLAYER:기교 == 3
    SOURCE:11 += 300
    SOURCE:19 += 150
    LOCAL:1 = 50
ELSEIF ABL:PLAYER:기교 == 4
    SOURCE:11 += 500
    SOURCE:19 += 250
    LOCAL:1 = 100
ELSE
    SOURCE:11 += 800
    SOURCE:19 += 400
    LOCAL:1 = 300
ENDIF

SOURCE:0 += LOCAL:1

SIF EXP:A경험 >= EXPLV:3
    SOURCE:2 += LOCAL:1

;TALENT:연모를 본다
SIF TALENT:연모
    TIMES SOURCE:11 , 1.50

TCVAR:1 += 2
SIF ASSIPLAY == 0 && EXIST_BAR(MASTER)
    CFLAG:주인에게A바친회수 += 2

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;사정시의 처리
IF TFLAG:91 >= 1
    ;커맨드 마다 고유의 처리는 이 아래에 쓴다
    IF ABL:순종 >= 3 && ABL:욕망 >= 3 && FLAG:조교시텍스트표시
        PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 등을 뒤로 돌리고 %CALLNAME:PLAYER%에게 힘없이 기댔다….
        PRINTL
    ENDIF

    ;사정처 타입의 설정
    TFLAG:703 = 2
    ;사정처 선택의 가능·불가
    TFLAG:704 = 1
    ;범용적인 처리는 이 함수로 실시한다
    ;(사정 게이지의 재계산이나 사정 경험의 상승과 공통 부분의 표시)
    CALL SAMEN_SHOOT
ENDIF

;사정에 관계없이 행해지는 처리는 여기로부터
;노예의 A⇔조교자의 P의 불결이 이동
STAIN:애널 |= STAIN:PLAYER:자지
STAIN:PLAYER:자지 |= STAIN:애널

IF TALENT:남자 == 0 && TALENT:PLAYER:남자 == 0
    TCVAR:12 += 7
ELSEIF TALENT:남자 && TALENT:PLAYER:남자
    TCVAR:13 += 7
ENDIF
;주인 경험 플래그
SIF ASSIPLAY == 0
    TFLAG:50 += (ABL:V감각 >= 3) ?  2 # 1

;처녀 상실의 후에 페니스를 뽑고 있을까
TFLAG:101 = (TFLAG:101 == 2) ?  1 # 0
RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM033
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:지문소거판정 == 1 || RESULT != 0
    RETURN 0

PRINTL
;전회와 이번 조교자가 같아, 전회의 조교가 배면좌위 애널이며, 뽑지 않는 플래그가 있는 경우
IF TFLAG:102 && PREVCOM == 33 && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    PRINTFORM %조사선택(CALLNAME:TARGET, "를")% 뒤에서 꼭 껴안아 
    IF TEQUIP:68 == 0
        PRINTFORM 흔들리는 %CALLNAME:TARGET%의 허리를 억눌렀다….
    ELSE
        PRINTFORM %CALLNAME:TARGET%의 몸을 흔들기 시작했다….
    ENDIF
    PRINTW
;같이 전회의 조교가 후배위 애널이나 기승위 애널인 경우
ELSEIF TFLAG:102 && (PREVCOM == 31 || PREVCOM == 34) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    PRINT 장에서 
    ;조교자가 남자나 후타나리이다
    IF EXIST_PENIS(PLAYER)
        PRINT 페니스를 
    ELSE
        PRINT 딜도를 
    ENDIF
    PRINTFORM 빼지 않고 %CALLNAME:TARGET%의 몸을 등에서 껴안았다. 
    SIF TEQUIP:68 == 0
        PRINT 그리고 몸을 서서히 흔들기 시작했다.
    PRINTW
;같이 전회의 조교가 정상위 애널이나 대면좌위 애널인 경우
ELSEIF TFLAG:102 && (PREVCOM == 30 || PREVCOM == 32) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    PRINTFORM 삽입한 채로 뒤로 향하게 한 %CALLNAME:TARGET%의 몸을 
    IF PREVCOM == 30
        PRINT 안아 올려 
    ELSE
        PRINT 꽉 껴안아 
    ENDIF
    SIF TEQUIP:68 == 0
        PRINTFORM 격렬하게 허리를 흔들어 
    PRINTW 주었다….
ELSE
    PRINTFORM %CALLNAME:TARGET%의 몸을 안아 올려 
    SIF PALAM:10 >= PALAMLV:4
        PRINT 점액 투성이가 된 
    PRINTW 항문에, 기다려 마지않는 물건을 천천히 삽입했다….
ENDIF

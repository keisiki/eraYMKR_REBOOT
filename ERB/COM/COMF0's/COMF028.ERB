;=============================================================================
;배면입위
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE28
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:28 > 0
    RETURN 0
;V조교 필터가 오프가 되지 않으면 안 됨
SIF FLAG:2 & 1
    RETURN 0
;조교자가 조수로 그 조수가 남자가 아닌 경우, 이상계 조교 필터가 오프가 되지 않으면 안 됨
SIF FLAG:2 & 4 && ASSIPLAY && TALENT:PLAYER:120 == 0
    RETURN 0
;조교자에게 페니스가 없는, 한편(아이템 있음 설정이라면) 페니스 밴드도 없으면 안 됨
SIF EXIST_BAR(PLAYER) == 0 && ITEM:2 == 0 && NOITEM == 0
    RETURN 0
;처녀는 안 됨
SIF TALENT:처녀 == 1
    RETURN 0
;남자라면 안 됨
SIF TALENT:120
    RETURN 0
;주인이 [금단의 지식] 소유가 아닐 때, 체격차이가 2 단계 이내나 조교자가 [금단의 지식] 소유가 아니면 안 됨
SIF TALENT:MASTER:56 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
    RETURN 0
;처녀로, 순종 5 미만·레즈끼 5 미만의 ASSI의 경우, 조교 대상이 동성이라면 실행할 수 없다(새드나 새드끼 Lv3 이상, 또는 망신이나 복종이 있다면 OK)
IF TALENT:처녀 == 1 && ASSIPLAY
    SIF (ABL:ASSI:순종 < 5 || (TALENT:120 == 0 && TALENT:ASSI:120 == 0 && ABL:ASSI:레즈끼 < 5)) && TALENT:ASSI:81 == 0 && ABL:ASSI:새드끼 < 3 && TALENT:ASSI:151 == 0 && TALENT:ASSI:160 == 0
        RETURN 0
ENDIF
;윤활 부족해, 순종 4 미만·레즈끼 4 미만의 ASSI의 경우, 조교 대상이 동성이라면 실행할 수 없다(새드나 새드끼 Lv3 이상, 또는 망신이나 복종이 있다면 OK)
IF PALAM:10 < PALAMLV:2 && ASSIPLAY
    SIF (ABL:ASSI:순종 < 4 || (TALENT:120 == 0 && TALENT:ASSI:120 == 0 && ABL:ASSI:레즈끼 < 4)) && TALENT:ASSI:81 == 0 && ABL:ASSI:새드끼 < 3 && TALENT:ASSI:151 == 0 && TALENT:ASSI:160 == 0
        RETURN 0
ENDIF
;수면중은 불가
SIF EQUIP:14
    RETURN 0
;실신중은 불가
SIF TFLAG:899 > 0
    RETURN 0
;V계 장착도구 사용중은 불가
SIF TEQUIP:20 || TEQUIP:21
    RETURN 0
;A계 장착도구 사용중은 불가
SIF TEQUIP:25 || TEQUIP:26
    RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
    RETURN 0
;목욕탕 플레이중은 플레이 매트가 없으면 안 됨
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
    RETURN 0
;한증과 암반 욕 이외의00목욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
    RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
    RETURN 0
;훈도시 착용중의 경우 조교자의 기교 4이상 필수
SIF TEQUIP:59 == 17 && ABL:PLAYER:기교 < 4
    RETURN 0
;조수가 훈도시 착용중의 경우 기교 4이상 필수
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17 && ABL:기교 < 4
    RETURN 0
;애태우기플레이 중에는 불가
SIF TEQUIP:68
    RETURN 0
;나체정식 중에는 안 됨
SIF TEQUIP:60
    RETURN 0
;촉수 조교중일 시 주인의 촉수사역Lv2 이하는 안 됨
SIF TEQUIP:90 && ABL:MASTER:촉수사역 <= 2
    RETURN 0
;인어는 안됨
SIF TALENT:229 == 1
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM28
IF NO:TARGET == 73 || NO:TARGET == 77
    PRINTL 불단 뒤집기
ELSE
    PRINTL 배면입위
ENDIF

;-------------------------------------------------
;실행할 수 있을까의 판정
;-------------------------------------------------
IF TFLAG:699 == 0
    CALL COM_ORDER
    ;실행할 수 없다
    SIF RESULT == 0
        RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------
IF NO:TARGET == 73 || NO:TARGET == 77
    COM_NAME = 불단 뒤집기
ELSE
    COM_NAME = 배면입위
ENDIF
CALL TRAIN_MESSAGE_COM028

;V경험을 수반하는 커맨드의 플래그(처녀막 재생 대응)
TFLAG:1 = 1
SIF TALENT:처녀 && TALENT:150 && ASSIPLAY == 0
    TFLAG:2 = TALENT:처녀

;-------------------------------------------------
;사정 게이지 체크
;-------------------------------------------------
LOCAL = 0

;ABL:기교를 본다
IF ABL:기교 == 0
    LOCAL = 1000
ELSEIF ABL:기교 == 1
    LOCAL = 1300
ELSEIF ABL:기교 == 2
    LOCAL = 1700
ELSEIF ABL:기교 == 3
    LOCAL = 2200
ELSEIF ABL:기교 == 4
    LOCAL = 3000
ELSE
    LOCAL = 4500
ENDIF

;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_3(TARGET, LOCAL)
;욕망을 본다
LOCAL = EJACULATIONCHECK_ABL_11_1(TARGET, LOCAL)
;윤활을 본다
LOCAL = EJACULATIONCHECK_PALAM_10_2(TARGET, LOCAL)

;조교자의 C감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
    BASE:PLAYER:2 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 100
DOWNBASE:1 += 200

SOURCE:12 = 350

;ABL:V감각을 본다
IF ABL:V감각 == 0
    SOURCE:1 = 50
    SOURCE:11 = 25
ELSEIF ABL:V감각 == 1
    SOURCE:1 = 150
    SOURCE:11 = 75
ELSEIF ABL:V감각 == 2
    SOURCE:1 = 300
    SOURCE:11 = 125
ELSEIF ABL:V감각 == 3
    SOURCE:1 = 600
    SOURCE:11 = 175
ELSEIF ABL:V감각 == 4
    SOURCE:1 = 1000
    SOURCE:11 = 300
ELSE
    SOURCE:1 = 1500
    SOURCE:11 = 425
ENDIF

;EXP:V경험을 본다
IF EXP:0 < EXPLV:1
    TIMES SOURCE:1 , 0.20
    SOURCE:20 = 3000
    SOURCE:12 += 6000
    SOURCE:31 = 60000
ELSEIF EXP:0 < EXPLV:2
    TIMES SOURCE:1 , 0.60
    SOURCE:20 = 240
ELSEIF EXP:0 < EXPLV:3
    TIMES SOURCE:1 , 1.00
    SOURCE:20 = 30
ELSEIF EXP:0 < EXPLV:4
    TIMES SOURCE:1 , 1.20
    SOURCE:20 = 5
ELSEIF EXP:0 < EXPLV:5
    TIMES SOURCE:1 , 1.40
    SOURCE:20 = 0
ELSE
    TIMES SOURCE:1 , 1.50
    SOURCE:20 = 0
ENDIF

;ABL:B감각을 본다
IF ABL:B감각 == 0
    SOURCE:3 = 50
    LOCAL:1 = 50
ELSEIF ABL:B감각 == 1
    SOURCE:3 = 200
    LOCAL:1 = 200
ELSEIF ABL:B감각 == 2
    SOURCE:3 = 500
    LOCAL:1 = 400
ELSEIF ABL:B감각 == 3
    SOURCE:3 = 800
    LOCAL:1 = 600
ELSEIF ABL:B감각 == 4
    SOURCE:3 = 1300
    LOCAL:1 = 1000
ELSE
    SOURCE:3 = 1800
    LOCAL:1 = 1400
ENDIF

;음유
IF TALENT:174
    TIMES SOURCE:3 , 1.20
    TIMES LOCAL:1 , 1.20
ENDIF

SOURCE:11 += LOCAL:1

;ABL:C감각을 본다
IF ABL:C감각 == 0
    SOURCE:0 = 40
ELSEIF ABL:C감각 == 1
    SOURCE:0 = 160
ELSEIF ABL:C감각 == 2
    SOURCE:0 = 500
ELSEIF ABL:C감각 == 3
    SOURCE:0 = 900
ELSEIF ABL:C감각 == 4
    SOURCE:0 = 1400
ELSE
    SOURCE:0 = 2100
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
    TIMES SOURCE:1 , 0.40
    SOURCE:20 += 600
    TIMES SOURCE:20 , 2.60
ELSEIF PALAM:10 < PALAMLV:2
    TIMES SOURCE:1 , 0.70
    SOURCE:20 += 180
    TIMES SOURCE:20 , 1.00
ELSEIF PALAM:10 < PALAMLV:3
    TIMES SOURCE:1 , 1.00
    TIMES SOURCE:20 , 0.50
ELSEIF PALAM:10 < PALAMLV:4
    TIMES SOURCE:1 , 1.20
    TIMES SOURCE:20 , 0.20
ELSE
    TIMES SOURCE:1 , 1.60
    TIMES SOURCE:20 , 0.10
ENDIF

;조교자가[남자]
SIF TALENT:PLAYER:120
    TIMES SOURCE:1 , 2.50

;[작은체형]
SIF TALENT:128
    TIMES SOURCE:20 , 2.00

;정조관념
IF TALENT:30
    IF TALENT:처녀 == 1
        TIMES SOURCE:11 , 0.60
        SOURCE:32 = 10000
    ELSE
        TIMES SOURCE:11 , 0.60
        SOURCE:32 = 1000
    ENDIF
;정조무구애
ELSEIF TALENT:31
    IF TALENT:처녀 == 1
        TIMES SOURCE:11 , 0.60
        SOURCE:32 = 300
    ENDIF
ELSE
    SIF TALENT:처녀 == 1
        SOURCE:32 = 3000
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    TIMES SOURCE:0 , 0.80
    TIMES SOURCE:1 , 0.80
    TIMES SOURCE:11 , 0.60
ELSEIF PALAM:13 < PALAMLV:2
    TIMES SOURCE:0 , 1.00
    TIMES SOURCE:1 , 1.00
    TIMES SOURCE:11 , 1.00
ELSEIF PALAM:13 < PALAMLV:3
    TIMES SOURCE:0 , 1.10
    TIMES SOURCE:1 , 1.20
    TIMES SOURCE:11 , 1.50
ELSEIF PALAM:13 < PALAMLV:4
    TIMES SOURCE:0 , 1.20
    TIMES SOURCE:1 , 1.40
    TIMES SOURCE:11 , 2.00
ELSE
    TIMES SOURCE:0 , 1.30
    TIMES SOURCE:1 , 1.60
    TIMES SOURCE:11 , 2.60
ENDIF

;ABL:순종을 본다
IF ABL:순종 == 0
    TIMES SOURCE:1 , 1.50
    TIMES SOURCE:11 , 1.00
    TIMES SOURCE:32 , 2.00
ELSEIF ABL:순종 == 1
    TIMES SOURCE:1 , 1.50
    TIMES SOURCE:11 , 1.30
    TIMES SOURCE:32 , 1.80
ELSEIF ABL:순종 == 2
    TIMES SOURCE:1 , 1.50
    TIMES SOURCE:11 , 1.50
    TIMES SOURCE:32 , 1.60
ELSEIF ABL:순종 == 3
    TIMES SOURCE:1 , 1.80
    TIMES SOURCE:11 , 1.90
    TIMES SOURCE:32 , 1.40
ELSEIF ABL:순종 == 4
    TIMES SOURCE:1 , 2.10
    TIMES SOURCE:11 , 2.20
    TIMES SOURCE:32 , 1.20
ELSE
    TIMES SOURCE:1 , 2.50
    TIMES SOURCE:11 , 2.60
    TIMES SOURCE:32 , 1.00
ENDIF

;플레이어의 ABL:기교를 본다
IF ABL:PLAYER:기교 == 0
    SOURCE:11 += 100
    SOURCE:19 = 0
    LOCAL:1 = 0
ELSEIF ABL:PLAYER:기교 == 1
    SOURCE:11 += 150
    SOURCE:19 += 50
    LOCAL:1 = 0
ELSEIF ABL:PLAYER:기교 == 2
    SOURCE:11 += 200
    SOURCE:19 += 100
    LOCAL:1 = 0
ELSEIF ABL:PLAYER:기교 == 3
    SOURCE:11 += 300
    SOURCE:19 += 150
    LOCAL:1 = 50
ELSEIF ABL:PLAYER:기교 == 4
    SOURCE:11 += 500
    SOURCE:19 += 250
    LOCAL:1 = 100
ELSE
    SOURCE:11 += 800
    SOURCE:19 += 400
    LOCAL:1 = 300
ENDIF

SOURCE:0 += LOCAL:1

SIF EXPLV:0 >= 3
    SOURCE:1 += LOCAL:1

;TALENT:연모를 본다
SIF TALENT:150
    TIMES SOURCE:11 , 2.00

;불단 뒤집기 보너스(가슴에의 공격력 저하의 변해, 사랑 증가해, 욕정 추가도 있다)
IF NO:TARGET == 73 || NO:TARGET == 77
    TIMES SOURCE:3 , 0.90
    TIMES SOURCE:11 , 1.10
    SOURCE:18 += 25
ENDIF

;전회의 조교가 손가락으로 V를 어루만지는이었다고 나무로, 반발 각인 없을 때
IF PREVCOM == 605 && MARK:9 <= 0
    TIMES SOURCE:20 , 0.80
    TIMES SOURCE:32 , 0.80
ENDIF

TCVAR:0 += 1
IF PLAYER == MASTER
    TCVAR:42 += 1
    SIF EXIST_BAR(MASTER)
        CFLAG:173 += 1
ENDIF

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;사정시의 처리
IF TFLAG:91 >= 1
    ;커맨드 마다 고유의 처리는 이 아래에 쓴다
    IF ABL:순종 >= 3 && ABL:욕망 >= 3 && FLAG:10
        PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 등을 뒤로 돌리고 %CALLNAME:PLAYER%에게 힘없이 기댔다….
        PRINTL
    ENDIF

    ;사정처 타입의 설정
    TFLAG:703 = 1
    ;사정처 선택의 가능·불가
    TFLAG:704 = 0
    ;범용적인 처리는 이 함수로 실시한다
    ;(사정 게이지의 재계산이나 사정 경험의 상승과 공통 부분의 표시)
    CALL SAMEN_SHOOT
ENDIF

;사정에 관계없이 행해지는 처리는 여기로부터
;노예의 V⇔조교자의 P의 불결이 이동
STAIN:3 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:3

SIF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
    TCVAR:12 += 7

;주인 경험 플래그
SIF ASSIPLAY == 0
    TFLAG:50 += (ABL:V감각 >= 3) ?  2 # 1

;처녀 상실의 후에 페니스를 뽑고 있을까
TFLAG:101 = (TFLAG:101 == 2) ?  1 # 0
RETURN 1

;--------------------------------------------------
;명령 거부의 메시지
;--------------------------------------------------
@DENIAL_MESSAGE_COM_28
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
PRINTFORML %조사선택(CALLNAME:PLAYER, "가")% %CALLNAME:TARGET%의 뒤에서 질에 삽입하려고 했지만 거절당했다.
PRINTW 좀 더 조교가 필요한 것 같다.

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM028
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
;전회와 이번 조교자가 같아, 전회의 조교가 배면입위이며, 뽑지 않는 플래그가 있는 경우
IF TFLAG:102 && PREVCOM == 28 && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    PRINTFORMW %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%의 질 내에 페니스를 삽입한 채로 허리를 계속 흔들었다….
;전회와 이번 조교자가 같아, 전회의 조교가 배면좌위이며, 뽑지 않는 플래그가 있는 경우
ELSEIF TFLAG:102 && PREVCOM == 23 && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    PRINTFORM %조사선택(CALLNAME:TARGET, "를")% 뒤에서 꼭 껴안아 
    PRINTFORM 흔들리는 %CALLNAME:TARGET%의 허리를 억누르고 
    PRINTW 일어섰다….
;같이 전회의 조교가 후배위나 기승위인 경우
ELSEIF TFLAG:102 && (PREVCOM == 21 || PREVCOM == 24) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    PRINTFORM 질에서 페니스를 빼지 않은 채로 %CALLNAME:TARGET%의 몸을 등에서 껴안고, 
    PRINTW 몸을 흔들면서 일어섰다….
;같이 전회의 조교가 정상위나 대면좌위인 경우
ELSEIF TFLAG:102 && (PREVCOM == 20 || PREVCOM == 22) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    PRINTFORM 삽입한 채로 반대로 향하게 하여, %CALLNAME:TARGET%의 몸을 
    IF PREVCOM == 20
        PRINT 안아 올리고 
    ELSE
        PRINT 꽉 껴안고 
    ENDIF
    PRINTW 격렬하게 허리를 흔들면서 일어섰다….
ELSE
    PRINTFORM %CALLNAME:TARGET%의 몸을 안아 올려 %조사선택(CALLNAME:PLAYER, "는")% 선 채로 뒤로부터 
    SIF PALAM:10 >= PALAMLV:4
        PRINT 젖어서 번들거리는 
    SIF EX:21
        PRINT 안타깝게 벌름거리는 
    IF TALENT:128
        PRINT 어린 균열에 
    ELSE
        PRINT 보지에 
    ENDIF
    PRINTW 삽입했다….
ENDIF

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_28, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:욕망
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:욕망
    TFLAG:240 += LOCAL:99 * 3
    RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF
;ABL:V감각
IF ABL:V감각
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:V감각
    TFLAG:240 += LOCAL:99 * 2
    RESULTS = %RESULTS% %ABLNAME:1%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF
;ABL:봉사 정신
IF ABL:봉사정신
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:봉사정신
    TFLAG:240 += LOCAL:99 * 4
    RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:정액 중독
IF ABL:정액중독
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:정액중독
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99})
ENDIF

;쾌락 각인
IF MARK:1
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = MARK:1
    TFLAG:240 += LOCAL:99 * 3
    RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;PALAM:윤활(의 부족)
IF PALAM:10 < PALAMLV:3
    RESULTS = %RESULTS% - 
    LOCAL:99 = 5
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% 윤활 부족({LOCAL:99})
ENDIF
;PALAM:욕정
GETPALAMLV PALAM:13, 5
LOCAL:2 = RESULT
IF LOCAL:2
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = LOCAL:2 * 3
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;부끄럼쟁이
IF TALENT:35
    RESULTS = %RESULTS% - 
    LOCAL:99 = 2
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;남자 싫다
IF TALENT:남성혐오 && TALENT:PLAYER:120
    RESULTS = %RESULTS% - 
    LOCAL:99 = 12
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;여자 증오
ELSEIF TALENT:여성혐오 && TALENT:PLAYER:120 == 0
    RESULTS = %RESULTS% - 
    LOCAL:99 = 12
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;쾌감의 부정
IF TALENT:71
    RESULTS = %RESULTS% - 
    LOCAL:99 = 5
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;연모
IF TALENT:150 && ASSIPLAY == 0
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 5
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF
;처녀
IF TALENT:처녀 == 1
    RESULTS = %RESULTS% - 
    LOCAL:99 = 20
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:0%({LOCAL:99})
ELSEIF EXP:0 < EXPLV:2
    RESULTS = %RESULTS% - 
    LOCAL:99 = 5
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %EXPNAME:0%부족({LOCAL:99})
ENDIF

;조교자가 후타나리
IF TALENT:PLAYER:121
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 4
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %조사처리(CALLNAME:PLAYER,"가")% %TALENTNAME:121%({LOCAL:99})
ENDIF

;미약
IF TEQUIP:11
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 6
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %ITEMNAME:61%({LOCAL:99})
ENDIF

;조교자의 P의 더러움
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)
;배면입위에서는 더러움의 영향이 적은
TFLAG:242 /= 3

;서로불결
IF TFLAG:242
    RESULTS = %RESULTS% - 
    TFLAG:240 -= TFLAG:242
    RESULTS = %RESULTS% 서로불결
    ;악취 둔감
    IF TALENT:61
        RESULTS = %RESULTS%, %TALENTNAME:61%
    ELSEIF TALENT:62
        RESULTS = %RESULTS%, %TALENTNAME:62%
    ENDIF
    RESULTS = %RESULTS%({TFLAG:242})
    LOCAL:99 = 1
ENDIF

TFLAG:241 = 26

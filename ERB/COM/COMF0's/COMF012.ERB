;=============================================================================
;바이브
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE12
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:12 > 0
    RETURN 0
;V조교 필터가 오프가 되지 않으면 안 됨
SIF FLAG:VA이상조교필터 & 1
    RETURN 0
;V경험이 낮은 동안에 촉수 조교중이라고, 이상계 조교 필터가 오프가 되지 않으면 안돼
SIF FLAG:VA이상조교필터 & 4 && EXP:V경험 < EXPLV:1 && TEQUIP:90
    RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
    RETURN 0
;한증과 암반 욕 이외의00목욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
    RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
    RETURN 0
;훈도시 착용중의 경우 조교자의 기교 4이상 필수
SIF TEQUIP:59 == 17 && ABL:PLAYER:기교 < 4
    RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
    RETURN 0
;해제는 언제라도 가능
SIF TEQUIP:20 == 1
    RETURN 1
;아이템을 가지고 있는지, 아이템 없음 설정이 아니면 안 됨
SIF ITEM:3 == 0 && NOITEM == 0
    RETURN 0
;남자라면 안 됨
SIF TALENT:120
    RETURN 0
;조교 대상이 [소인]의 경우는 주인이 [금단의 지식] 소유가 아니면 안 됨
SIF TALENT:127 && TALENT:MASTER:금단의지식 == 0
    RETURN 0
;처녀로, 순종 5 미만·레즈끼 5 미만의 ASSI의 경우, 조교 대상이 동성이라면 실행할 수 없다(새드나 새드끼 Lv3 이상, 또는 망신이나 복종이 있다면 OK)
IF TALENT:처녀 == 1 && ASSIPLAY
    SIF (ABL:ASSI:순종 < 5 || (TALENT:120 == 0 && TALENT:ASSI:남자 == 0 && ABL:ASSI:레즈끼 < 5)) && TALENT:ASSI:새드 == 0 && ABL:ASSI:새드끼 < 3 && TALENT:ASSI:151 == 0 && TALENT:ASSI:160 == 0
        RETURN 0
ENDIF
;윤활 부족해, 순종 4 미만·레즈끼 4 미만의 ASSI의 경우, 조교 대상이 동성이라면 실행할 수 없다(새드나 새드끼 Lv3 이상, 또는 망신이나 복종이 있다면 OK)
IF PALAM:10 < PALAMLV:2 && ASSIPLAY
    SIF (ABL:ASSI:순종 < 4 || (TALENT:120 == 0 && TALENT:ASSI:남자 == 0 && ABL:ASSI:레즈끼 < 4)) && TALENT:ASSI:새드 == 0 && ABL:ASSI:새드끼 < 3 && TALENT:ASSI:151 == 0 && TALENT:ASSI:160 == 0
        RETURN 0
ENDIF
;다른 V계 장착도구 사용중은 불가
SIF (TEQUIP:20 && TEQUIP:20 != 1) || TEQUIP:21
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;-------------------------------------------------
;바이브(SELECTCOM=12), 촉수 삽입(SELECTCOM=501)
;-------------------------------------------------
;촉수 삽입은 COMF500.ERB로부터 JUMP 해 온다.
@COM12
IF TEQUIP:90
    PRINTL 촉수 삽입
    COM_NAME = 촉수 삽입
ELSE
    COM_TOOL_NAME = 바이브
    IF FLAG:1100 & 1
        ;技師パッチ有効なら特殊な名前チェック
        FLAG:1110 = 3
        CALL NAMECHECK_CUSTOMTOOL
    ELSE

        COM_TOOL_CALLNAME = %COM_TOOL_NAME%
    ENDIF
    PRINTFORML %COM_TOOL_NAME%
    COM_NAME = 바이브레이터
ENDIF
CALL TRAIN_MESSAGE_COM12

;V경험을 수반하는 커맨드의 플래그(처녀막 재생 대응)
TFLAG:1 = 1

DOWNBASE:0 += 30
DOWNBASE:기력 += 100

;ABL:V감각을 본다
IF ABL:V감각 == 0
    SOURCE:1 = 80
ELSEIF ABL:V감각 == 1
    SOURCE:1 = 250
ELSEIF ABL:V감각 == 2
    SOURCE:1 = 600
ELSEIF ABL:V감각 == 3
    SOURCE:1 = 1000
ELSEIF ABL:V감각 == 4
    SOURCE:1 = 1300
ELSE
    SOURCE:1 = 1700
ENDIF

;EXP:V경험을 본다
IF EXP:V경험 < EXPLV:1
    TIMES SOURCE:1 , 0.20
    SOURCE:20 = 5500
    SIF TEQUIP:90
        TCVAR:50 += 1
ELSEIF EXP:V경험 < EXPLV:2
    TIMES SOURCE:1 , 0.60
    SOURCE:20 = 300
ELSEIF EXP:V경험 < EXPLV:3
    TIMES SOURCE:1 , 1.00
    SOURCE:20 = 50
ELSEIF EXP:V경험 < EXPLV:4
    TIMES SOURCE:1 , 1.20
    SOURCE:20 = 10
ELSEIF EXP:V경험 < EXPLV:5
    TIMES SOURCE:1 , 1.40
    SOURCE:20 = 0
ELSE
    TIMES SOURCE:1 , 1.60
    SOURCE:20 = 0
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
    TIMES SOURCE:1 , 0.10
    SOURCE:20 += 1000
    TIMES SOURCE:20 , 3.00
ELSEIF PALAM:10 < PALAMLV:2
    TIMES SOURCE:1 , 0.40
    SOURCE:20 += 400
    TIMES SOURCE:20 , 1.00
ELSEIF PALAM:10 < PALAMLV:3
    TIMES SOURCE:1 , 1.00
    TIMES SOURCE:20 , 0.50
ELSEIF PALAM:10 < PALAMLV:4
    TIMES SOURCE:1 , 1.40
    TIMES SOURCE:20 , 0.20
ELSE
    TIMES SOURCE:1 , 1.80
    TIMES SOURCE:20 , 0.10
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    TIMES SOURCE:1 , 0.80
ELSEIF PALAM:13 < PALAMLV:2
    TIMES SOURCE:1 , 0.90
ELSEIF PALAM:13 < PALAMLV:3
    TIMES SOURCE:1 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
    TIMES SOURCE:1 , 1.10
ELSE
    TIMES SOURCE:1 , 1.20
ENDIF

;ABL:순종을 본다
IF ABL:순종 == 0
    TIMES SOURCE:1 , 0.80
ELSEIF ABL:순종 == 1
    TIMES SOURCE:1 , 0.90
ELSEIF ABL:순종 == 2
    TIMES SOURCE:1 , 1.00
ELSEIF ABL:순종 == 3
    TIMES SOURCE:1 , 1.10
ELSEIF ABL:순종 == 4
    TIMES SOURCE:1 , 1.20
ELSE
    TIMES SOURCE:1 , 1.30
ENDIF

;[작은체형]
SIF TALENT:128
    TIMES SOURCE:20 , 2.00

IF (FLAG:1100 & 1) && TEQUIP:90 == 0
    ;技師パッチ有効なら道具使用時ソースチェックを呼ぶ
    ;FLAG 1110:道具番号 1111:改善タイプ 1112:SOURCEの実値 
    FLAG:1110 = 3
    FLAG:1111 = 0
    FLAG:1112 = SOURCE:1
    CALL SOURCECHECK_CUSTOMTOOL
    SOURCE:1 = FLAG:1120
ENDIF

;바이브의 착탈
LOCAL = 1
IF TEQUIP:20
    IF TEQUIP:90
        IF TEQUIP:20 == 2
            LOCAL = 2
        ELSEIF TEQUIP:20 == 3
            LOCAL = 3
        ENDIF
        SIF TEQUIP:93
            CALL EGGPLAY_OUT
    ENDIF
    TEQUIP:20 = 0
ELSE
    IF TEQUIP:90
        STAIN:보지 |= 2
        STAIN:보지 |= 4
        STAIN:보지 |= 64
    ENDIF
    TEQUIP:20 = 1
ENDIF

TCVAR:0 += LOCAL

IF TALENT:120 == 0 && TALENT:PLAYER:남자 == 0
    TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:남자
    TCVAR:13 += 1
ENDIF

;커맨드 속성：촉수
IF TEQUIP:90
    TFLAG:98 = 8
;커맨드 속성：도구(조교자)
ELSE
    TFLAG:98 = 1
ENDIF
;촉수 경험 플래그
TFLAG:90 = 0
RETURN 1

;-------------------------------------------------
;바이브 삽입중
;-------------------------------------------------
@EQUIP_COM12
IF TEQUIP:90
    PRINTL <촉수 삽입 중>
ELSE
    COM_TOOL_NAME = 바이브
    IF FLAG:1100 & 1
        ;技師パッチ有効なら特殊な名前チェック
        FLAG:1110 = 3
        CALL NAMECHECK_CUSTOMTOOL
    ENDIF
    PRINTFORML <%COM_TOOL_NAME% 삽입 중>
ENDIF

DOWNBASE:0 += 10
DOWNBASE:기력 += 50

;ABL:V감각을 본다
IF ABL:V감각 == 0
    LOCAL = 40
ELSEIF ABL:V감각 == 1
    LOCAL = 120
ELSEIF ABL:V감각 == 2
    LOCAL = 300
ELSEIF ABL:V감각 == 3
    LOCAL = 500
ELSEIF ABL:V감각 == 4
    LOCAL = 650
ELSE
    LOCAL = 850
ENDIF

;EXP:V경험을 본다
;처녀는 있을 수 없다
IF EXP:V경험 < EXPLV:2
    TIMES LOCAL , 0.60
    LOCAL:1 = 150
ELSEIF EXP:V경험 < EXPLV:3
    TIMES LOCAL , 1.00
    LOCAL:1 = 20
ELSEIF EXP:V경험 < EXPLV:4
    TIMES LOCAL , 1.20
    LOCAL:1 = 0
ELSEIF EXP:V경험 < EXPLV:5
    TIMES LOCAL , 1.40
    LOCAL:1 = 0
ELSE
    TIMES LOCAL , 1.60
    LOCAL:1 = 0
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
    TIMES LOCAL , 0.10
    LOCAL:1 += 400
    TIMES LOCAL:1 , 3.00
    LOCAL:2 += 1000
ELSEIF PALAM:10 < PALAMLV:2
    TIMES LOCAL , 0.40
    LOCAL:1 += 150
    TIMES LOCAL:1 , 1.00
    LOCAL:2 += 200
ELSEIF PALAM:10 < PALAMLV:3
    TIMES LOCAL , 1.00
    TIMES LOCAL:1 , 0.50
    LOCAL:2 += 80
ELSEIF PALAM:10 < PALAMLV:4
    TIMES LOCAL , 1.40
    TIMES LOCAL:1 , 0.20
ELSE
    TIMES LOCAL , 1.80
    TIMES LOCAL:1 , 0.10
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    TIMES LOCAL , 0.80
ELSEIF PALAM:13 < PALAMLV:2
    TIMES LOCAL , 0.90
ELSEIF PALAM:13 < PALAMLV:3
    TIMES LOCAL , 1.00
ELSEIF PALAM:13 < PALAMLV:4
    TIMES LOCAL , 1.10
ELSE
    TIMES LOCAL , 1.20
ENDIF

;ABL:순종을 본다
IF ABL:순종 == 0
    TIMES LOCAL , 0.80
ELSEIF ABL:순종 == 1
    TIMES LOCAL , 0.90
ELSEIF ABL:순종 == 2
    TIMES LOCAL , 1.00
ELSEIF ABL:순종 == 3
    TIMES LOCAL , 1.10
ELSEIF ABL:순종 == 4
    TIMES LOCAL , 1.20
ELSE
    TIMES LOCAL , 1.30
ENDIF

;정조관념
IF TALENT:30
    ;처녀는 있을 수 없다
    TIMES LOCAL:2, 3.00
ENDIF
;[작은체형]
SIF TALENT:128
    TIMES LOCAL:1 , 2.00
    
IF (FLAG:1100 & 1) && TEQUIP:90 == 0
    ;技師パッチ有効なら道具使用時ソースチェックを呼ぶ
    ;FLAG 1110:道具番号 1111:改善タイプ 1112:SOURCEの実値 
    FLAG:1110 = 3
    FLAG:1111 = 0
    FLAG:1112 = LOCAL
    CALL SOURCECHECK_CUSTOMTOOL
    LOCAL = FLAG:1120
ENDIF

SOURCE:1 += LOCAL
SOURCE:20 += LOCAL:1
SOURCE:22 += LOCAL:2

;자위 실행시는 경험＋1
IF SELECTCOM == 7 || SELECTCOM == 124 || SELECTCOM == 125 || SELECTCOM == 127 || SELECTCOM == 182 || SELECTCOM == 205 || SELECTCOM == 628
    TCVAR:0 += 3
ELSE
    TCVAR:0 += 2
ENDIF

;촉수 경험 플래그
SIF TEQUIP:90
    TFLAG:90 += 1

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM12
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
IF TEQUIP:90
    IF TEQUIP:20 == 0
        PRINTFORMW 세세한 돌기에 덮인 촉수가 %CALLNAME:TARGET%의 성기를 비벼대며 기어들어가고 있다….
    ELSEIF TEQUIP:20 == 2
        PRINTFORMW %CALLNAME:TARGET%의 질 내의 가장 민감한 곳을 더듬고 있던 촉수가 뽑혀졌다….
    ELSEIF TEQUIP:20 == 3
        PRINTFORMW %CALLNAME:TARGET%의 자궁을 휘젓고 있던 촉수가 뽑혀졌다….
    ELSEIF TEQUIP:20 == 7
        PRINTFORMW %CALLNAME:TARGET%의 애액을 탐욕에 채집하고 있던 촉수가 뽑혀졌다….
    ELSE
        PRINTFORMW 촉수가 %CALLNAME:TARGET%의 성기로부터 뽑혀졌다….
    ENDIF
ELSE
    IF TEQUIP:20
        PRINTFORML %조사선택(CALLNAME:TARGET, "로")%부터 %조사선택(COM_TOOL_NAME, "를")% 천천히 뽑아 냈다.
        IF EQUIP:14 == 0 && TFLAG:899 == 0
            IF EXP:V경험 > 200 && TALENT:150
                PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 질 내의 상실감에 참기 힘들어 눈물\@(TEQUIP:42) ? #과 군침\@ 을 흘리고 
            ELSEIF EXP:V경험 > 150 && TALENT:150
                PRINTFORM %CALLNAME:TARGET%의 질 내에 모여 있던 점액이 긁어내 져 방울져 떨어져, 웅덩이를 만들고 
            ELSEIF EXP:V경험 > 120 && TALENT:150
                PRINTFORM %조사선택(COM_TOOL_NAME, "가")% 뽑아진 %CALLNAME:TARGET%의 육 벽에서 툭툭 애액이 물방울지고 
            ELSEIF EXP:V경험 > 70 && TALENT:150
                PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 머뭇머뭇 가랑이를 만지며 %조사선택(CALLNAME:PLAYER, "를")% 보고 
            ELSEIF EXP:V경험 > 40 && TALENT:150
                PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 열이 가득한 \@(TEQUIP:42) ? 시선을 향하고 # 한숨을 토하고 \@ 
            ELSEIF EXP:V경험 > 20
                PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 느슨해진 질구를 당겼다 놓았다 하고 
            ELSE
                PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 마음이 놓인 것 같은 표정을 하고 
            ENDIF
            PRINTW 있다….
        ENDIF
    ELSE
        PRINTFORM %CALLNAME:TARGET%의 
        SIF PALAM:10 >= PALAMLV:5
            PRINT 잔뜩 
        SIF PALAM:10 >= PALAMLV:3
            PRINT 점액을 머금은 
        IF TALENT:128
            PRINT 어린 균열에 
        ELSE
            PRINT 보지에 
        ENDIF
        PRINTFORML %COM_TOOL_CALLNAME%의 끝을 꼭 대고 비볐다….

        IF EQUIP:14 == 0 && TFLAG:899 == 0
            PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
            IF TALENT:처녀 && TALENT:150 && TALENT:170
                PRINT 자신이 처녀라는 것을 잊은 것 듯이 웃으며 
            ELSEIF TALENT:처녀 && TALENT:150
                PRINTFORM 조금 외로운 것 같은 눈으로 %조사선택(CALLNAME:PLAYER, "를")% 보고 
            ELSEIF TALENT:처녀 && TALENT:170
                PRINT 처녀인 것도 상관없이, 허리를 흔들며 애원하고 
            ELSEIF TALENT:처녀 && TALENT:27
                PRINT 무엇인가를 단념한 표정으로 눈에 어렴풋이 눈물을 머금고 
            ELSEIF TALENT:처녀
                PRINT 부들부들 떨며, 상실의 고통에 두려워하고 
            ELSEIF EXP:V경험 > 200 && TALENT:150
                PRINT 삽입의 기대감 만으로도 덜덜 허리를 흔들며 느끼고 
            ELSEIF EXP:V경험 > 150 && TALENT:150
                PRINTFORM 애처로운 눈빛으로 %조사선택(CALLNAME:PLAYER, "를")% 를 보며 스스로 음순을 문지르고 
            ELSEIF EXP:V경험 > 120 && TALENT:150
                PRINT 황홀한 표정으로 기다릴 수 없다며 허리를 굽히고 
            ELSEIF EXP:V경험 > 70 && TALENT:150
                PRINTFORM 욕구에 가득한 눈으로 %조사선택(CALLNAME:PLAYER, "를")% 보고 
            ELSEIF EXP:V경험 > 40 && TALENT:150
                PRINTFORM %조사선택(COM_TOOL_NAME, "가")% 스치는 감촉에 도취된 \@(TEQUIP:42) ? 표정을 보이고 # 것 처럼 한숨을 쉬고 \@ 
            ELSEIF EXP:V경험 > 0 && TALENT:150
                PRINTFORM %조사선택(COM_TOOL_NAME, "가")% 스치는 감촉에 몸을 꼬며 애액을 흘리고 
            ELSE
                PRINTFORM %조사선택(COM_TOOL_NAME, "가")% 스치는 감촉에 육벽을 움찔거리고 
            ENDIF
            PRINTL 있다.
            PRINT 그런 
        ELSE
            PRINT 그리고, 아직 깨어나지 않은 
        ENDIF
        PRINTFORM %CALLNAME:TARGET%의 
        SIF PALAM:10 >= PALAMLV:4
            PRINT 젖어있는 
        PRINTFORMW 성기에 %조사선택(COM_TOOL_CALLNAME, "를")% 꽂았다….
    ENDIF
ENDIF

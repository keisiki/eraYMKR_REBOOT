;=============================================================================
;애태우기 플레이
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE120
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:120 > 0
    RETURN 0
;Ｃ계 확장 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 1) == 0
    RETURN 0
;조교자에게 기교3이상 없으면 안 됨(새드 · 소악마라면 OK)
SIF ABL:PLAYER:12 < 3 && TALENT:PLAYER:81 == 0 && TALENT:PLAYER:93 == 0
    RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;eraSQ의 「이라고 하한」참고
;다만 밸런스 조정을 위해 대폭 약체화
@COM120
PRINTL 애태우기 플레이
COM_NAME = 애태우기플레이
CALL TRAIN_MESSAGE_COM120

DOWNBASE:0 += 10
DOWNBASE:1 += 50

SOURCE:18 = 120
SOURCE:22 = 70
SOURCE:31 = 70

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
    TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:120
    TCVAR:13 += 1
ENDIF

;애태우기플레이의 on/off
IF TEQUIP:68
    TEQUIP:68 = 0
ELSE
    TEQUIP:68 = 1
ENDIF
RETURN 1

;--------------------------------------------------
;애태우기플레이중
;--------------------------------------------------
@EQUIP_COM120
TFLAG:99 = 0
;자위·애널 자위·가슴 자위·기승 유방 자위
IF SELECTCOM == 7 || SELECTCOM == 182 || SELECTCOM == 205 || SELECTCOM == 613
    TFLAG:99 = 1
;기승위·기승위 애널·가랑이·더블 가랑이·거품 춤·짝 맞추기 놀이·조수를 범하게 하는(페니스 있어)·조수의 A를 범하게 하는(페니스 있어)·정상위 시키는·후배위 시키는·A정상위 시키는·A 후배위 시킨다
ELSEIF SELECTCOM == 24 || SELECTCOM == 34 || SELECTCOM == 43 || SELECTCOM == 301 || SELECTCOM == 305 || ((SELECTCOM == 307 || SELECTCOM == 308) && (TALENT:120 || TALENT:121)) || SELECTCOM == 352 || SELECTCOM == 323 || SELECTCOM == 324 || SELECTCOM == 332 || SELECTCOM == 333
    TFLAG:99 = 2
;시리코다마 빼기
ELSEIF SELECTCOM == 402
    TFLAG:99 = 3
;주사
ELSEIF (SELECTCOM >= 80 && SELECTCOM < 90)
    TFLAG:99 = 4
;충·슬라임·뱀장어 목욕탕
ELSEIF TEQUIP:55
    TFLAG:99 = 6
;사정 멈춤 해제에서의 사정
ELSEIF SELECTCOM == 191 && TEQUIP:69 == 0 && BASE:2 >= MAXBASE:2
    TFLAG:99 = 7
;초조하게 할 수 없을 것 같은 장비 아이템(주관)
;바이브래이터·애널 바이브래이터·클리캡·유두캡·착유기·오나호르·전극 오나호르·관장·A전극·B전극·V전극·거봉·군고구마·고드름
ELSEIF TEQUIP:20 || TEQUIP:21 || TEQUIP:25 || TEQUIP:26 || TEQUIP:30 || TEQUIP:32 || TEQUIP:35
    TFLAG:99 = 8
ENDIF
IF TFLAG:99
    CALL EQUIP_MESSAGE_COM120
    PRINTL <애태우기 플레이를 종료합니다>
    TEQUIP:68 = 0
    RETURN 0
ENDIF

PRINTL <애태우기 플레이 중>

DOWNBASE:0 += 25
DOWNBASE:1 += 40

LOCAL:1 = 160

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    TIMES LOCAL:1 , 0.80
ELSEIF PALAM:13 < PALAMLV:2
    TIMES LOCAL:1 , 0.90
ELSEIF PALAM:13 < PALAMLV:3
    TIMES LOCAL:1 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
    TIMES LOCAL:1 , 1.10
ELSE
    TIMES LOCAL:1 , 1.20
ENDIF

;ABL:순종을 본다
IF ABL:순종 == 0
    TIMES LOCAL:1 , 0.80
ELSEIF ABL:순종 == 1
    TIMES LOCAL:1 , 0.90
ELSEIF ABL:순종 == 2
    TIMES LOCAL:1 , 1.00
ELSEIF ABL:순종 == 3
    TIMES LOCAL:1 , 1.10
ELSEIF ABL:순종 == 4
    TIMES LOCAL:1 , 1.20
ELSE
    TIMES LOCAL:1 , 1.30
ENDIF

TIMES SOURCE:0 , 0.25
TIMES SOURCE:1 , 0.25
TIMES SOURCE:2 , 0.25
TIMES SOURCE:3 , 0.25
SOURCE:18 += 50
SOURCE:22 += LOCAL:1
SOURCE:31 += 50

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
    TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:120
    TCVAR:13 += 1
ENDIF
;촉수 경험 플래그
SIF TEQUIP:90
    TFLAG:90 += 1

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM120
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
IF TEQUIP:68
    IF PALAM:0 >= (PALAMLV:4 - 1) ||  PALAM:1 >= (PALAMLV:4 - 1) ||  PALAM:2 >= (PALAMLV:4 - 1) ||  PALAM:3 >= (PALAMLV:4 - 1)
        PRINTFORMW %조사선택(CALLNAME:PLAYER, "는")% %조사선택(CALLNAME:TARGET, "를")% 가버리게 했다….
    ELSE
        PRINTFORMW %조사선택(CALLNAME:PLAYER, "는")% %조사선택(CALLNAME:TARGET, "를")% 애태우는 걸 멈췄다….
    ENDIF
ELSE
    PRINTFORMW %조사선택(CALLNAME:PLAYER, "는")% %조사선택(CALLNAME:TARGET, "를")% 애태웠다….
ENDIF

;--------------------------------------------------
;애태우기플레이중 메시지
;--------------------------------------------------
@EQUIP_MESSAGE_COM120

[IF ENABLE_KOJO_EQUIP_MESSAGE]
CALL KOJO_EQUIP_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0
[ENDIF]

SELECTCASE TFLAG:99
    CASE 1
        PRINTL
        PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 자신의 손으로 쾌락을 탐하고 있다….
    CASE 2
        PRINTL
        PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 스스로 허리를 흔들며 쾌락을 탐하고 있다….
    CASE 3
        PRINTL
        PRINTL 쾌감을 주지 않고 엉덩이구슬을 빼내는 건 어려워 보인다….
    CASE 4
        PRINTL
        PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 약물이 가져오는 쾌감에 울며 소리지르고 있다. 이래서는 무리다….
    CASE 5
        PRINTL
        PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 축적된 쾌락에 단번에 습격 당해 덜덜 경련하고 있다.
        PRINTL 아무것도 하지 않아도 절정해버릴 것 같다. 이래서는 무리다….
    CASE 6
        PRINTL
        IF TEQUIP:55 == 1
            PRINT 음충이
        ELSEIF TEQUIP:55 == 2
            PRINT 뱀장어가
        ELSEIF TEQUIP:55 == 4
            PRINT 슬라임이
        ELSEIF TEQUIP:55 == 11
            PRINT 털 구슬이
        ELSE
            PRINT 욕조내의 액체가
        ENDIF
        PRINTFORML  %조사선택(CALLNAME:TARGET, "를")% 계속 자극하고 있다. 이래서는 무리다….
    CASE 7
        PRINTL
        PRINTFORML 띠가 풀린 %CALLNAME:TARGET%의 페니스로부터 애태울 틈도 없이 정액이 퓩퓩 하고 분출되고 있다….
    CASE 8
        PRINTL
        PRINTFORML 도구가 %조사선택(CALLNAME:TARGET, "를")% 계속 자극하고 있다. 이래서는 무리다….
ENDSELECT

;=============================================================================
;애널 면봉
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE181
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:181 > 0
    RETURN 0
;Ａ계 확장 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:커맨드확장 & 4) == 0
    RETURN 0
;Ａ조교 필터가 오프되어 있지 않으면 불가
SIF FLAG:VA이상조교필터 & 2
    RETURN 0
;그 조교 중에 관장 플레이＋배설이 있었을 경우, 조교자가 汚れ無視もしくは汚臭鈍感を持たないなら実行できない
SIF TFLAG:140 & 256 && TALENT:PLAYER:61 == 0 && TALENT:PLAYER:64 == 0
    RETURN 0
;아이템을 가지고 있거나, 아이템 없음 설정이 아니면 불가
SIF ITEM:79 == 0 && NOITEM == 0
    RETURN 0
;조교 대상이 [소인]일 경우에는 주인이 금단의 지식 보유자가 아니면 불가
SIF TALENT:127 && TALENT:MASTER:56 == 0
    RETURN 0
;Ａ계 장착구 사용 중에는 불가
SIF TEQUIP:25 || TEQUIP:26
    RETURN 0
;욕실 플레이 중에는 플레이 매트가 없으면 불가
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
    RETURN 0
;샤워 중에는 불가
SIF TEQUIP:54
    RETURN 0
;○○목욕탕 입욕 중에는 불가
SIF TEQUIP:55
    RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
    RETURN 0
;훈도시 착용 중일 경우 조교자의 기교4이상 필수
SIF TEQUIP:59 == 17 && ABL:PLAYER:기교 < 4
    RETURN 0
;새댁 플레이 중에는 불가
SIF TEQUIP:59 == 1
    RETURN 0
;학교수영복 플레이 중에는 불가
SIF TEQUIP:59 == 2
    RETURN 0
;나체정식 중에는 불가
SIF TEQUIP:60
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM181
PRINTL 애널 면봉
COM_NAME = 애널 면봉
CALL TRAIN_MESSAGE_COM181

DOWNBASE:0 += 20
DOWNBASE:기력 += 60

SOURCE:12 = 300
SOURCE:31 = 200

;ABL:A감각을 본다
IF ABL:A감각 == 0
    SOURCE:2 = 10
    SOURCE:22 = 150
ELSEIF ABL:A감각 == 1
    SOURCE:2 = 50
    SOURCE:22 = 250
ELSEIF ABL:A감각 == 2
    SOURCE:2 = 250
    SOURCE:22 = 400
ELSEIF ABL:A감각 == 3
    SOURCE:2 = 600
    SOURCE:22 = 700
ELSEIF ABL:A감각 == 4
    SOURCE:2 = 1200
    SOURCE:22 = 1300
ELSE
    SOURCE:2 = 1800
    SOURCE:22 = 2000
ENDIF

;EXP:A쾌락 경험을 본다
IF EXP:A쾌락경험 < EXPLV:1
    TIMES SOURCE:2 , 0.20
    TIMES SOURCE:22 , 0.20
    SOURCE:20 = 100
ELSEIF EXP:A쾌락경험 < EXPLV:2
    TIMES SOURCE:2 , 0.50
    TIMES SOURCE:22 , 0.50
    SOURCE:20 = 50
ELSEIF EXP:A쾌락경험 < EXPLV:3
    TIMES SOURCE:2 , 1.00
    TIMES SOURCE:22 , 0.80
    SOURCE:20 = 0
ELSEIF EXP:A쾌락경험 < EXPLV:4
    TIMES SOURCE:2 , 1.20
    TIMES SOURCE:22 , 1.00
    SOURCE:20 = 0
ELSEIF EXP:A쾌락경험 < EXPLV:5
    TIMES SOURCE:2 , 1.60
    TIMES SOURCE:22 , 1.20
    SOURCE:20 = 0
ELSE
    TIMES SOURCE:2 , 1.80
    TIMES SOURCE:22 , 1.50
    SOURCE:20 = 0
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
    TIMES SOURCE:2 , 0.10
    TIMES SOURCE:20 , 1.50
ELSEIF PALAM:10 < PALAMLV:2
    TIMES SOURCE:2 , 0.20
ELSEIF PALAM:10 < PALAMLV:3
    TIMES SOURCE:2 , 0.60
    TIMES SOURCE:20 , 0.80
ELSEIF PALAM:10 < PALAMLV:4
    TIMES SOURCE:20 , 0.50
ELSE
    TIMES SOURCE:2 , 2.00
    TIMES SOURCE:20 , 0.10
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    TIMES SOURCE:2 , 0.50
ELSEIF PALAM:13 < PALAMLV:2
    TIMES SOURCE:2 , 0.80
ELSEIF PALAM:13 < PALAMLV:3
    TIMES SOURCE:2 , 1.20
ELSEIF PALAM:13 < PALAMLV:4
    TIMES SOURCE:2 , 1.50
ELSE
    TIMES SOURCE:2 , 1.80
ENDIF

;음고
SIF TALENT:173
    TIMES SOURCE:2 , 1.20

;A민감, 둔감을 본다
;쾌A자체의 체크는 다음에 정리해 준다
IF TALENT:104
    TIMES SOURCE:12 , 1.50
    TIMES SOURCE:20 , 1.50
    TIMES SOURCE:22 , 1.50
ELSEIF TALENT:105
    TIMES SOURCE:12 , 0.60
    TIMES SOURCE:20 , 0.60
    TIMES SOURCE:22 , 0.60
ENDIF

;처녀로 정조관념
SIF TALENT:처녀 == 1 && TALENT:30
    TIMES SOURCE:22 , 2.00

;면봉 1개 소비
IF NOITEM == 0
    ITEM:79 -= 1
    SIF FLAG:아이템확장 & 16 && MONEY:0 >= ITEMPRICE:79
        CALL FILL_PLURAL_ITEM, 79
ENDIF

TCVAR:1 += 1

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
    TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:120
    TCVAR:13 += 1
ENDIF

;커맨드 속성：도구(조교자)
TFLAG:98 = 1
RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM181
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
PRINTFORM %조사선택(CALLNAME:PLAYER, "는")% 면봉을 %CALLNAME:TARGET%의 
SIF EXP:A경험 <= 10
    PRINT 단단히 닫혀있는 
PRINTL 항문에 대고, 쑤욱 밀어 넣어 보았다.
PRINT 면봉이 
;이미 욕정을 하고 있는 경우
IF PALAM:13 > PALAMLV:4
    IF TALENT:170 || TALENT:180
        PRINT 추잡한 자극을 기다려 마지않는 
    ELSEIF TALENT:35
        PRINT 욕정과 치정의 상승효과에 당황하고 있는 
    ENDIF
;아직 그다지 욕정하고 있지 않는 경우
ELSE
    IF TALENT:170 || TALENT:180
        PRINT 쾌락에 굶주린 
    ELSEIF TALENT:35
        PRINT 부끄러움에 몸을 배배 꼬는 
    ENDIF
ENDIF
PRINTFORMW %CALLNAME:TARGET%의 항문 안을 비틀며 들어가고 있다….

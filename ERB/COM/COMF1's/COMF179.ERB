;=============================================================================
;애널 바벨
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE179
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:179 > 0
    RETURN 0
;Ａ계 확장 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:커맨드확장 & 4) == 0
    RETURN 0
;Ａ조교 필터가 오프되어 있지 않으면 불가
SIF FLAG:VA이상조교필터 & 2
    RETURN 0
;대상의 A확장경험이 0일 때 이상계 조교 필터가 오프되어 있지 않으면 불가
SIF FLAG:VA이상조교필터 & 4 && EXP:A확장경험 == 0
    RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
    RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
    RETURN 0
;훈도시 착용 중일 경우는 안 됨
SIF TEQUIP:59 == 17
    RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
    RETURN 0
;해제는 언제든 가능
SIF TEQUIP:25 == 10
    RETURN 1
;아이템을 가지고 있거나, 아이템 없음 설정이 아니면 불가
SIF ITEM:4 == 0 && NOITEM == 0
    RETURN 0
;조교 대상이 [소인]일 경우에는 주인이 금단의 지식 보유자가 아니면 불가
SIF TALENT:127 && TALENT:MASTER:56 == 0
    RETURN 0
;그 조교 중에 관장 플레이＋배설이 있었을 경우, 조교자가 汚れ無視もしくは汚臭鈍感を持たないなら実行できない
SIF TFLAG:140 & 256 && TALENT:PLAYER:61 == 0 && TALENT:PLAYER:64 == 0
    RETURN 0
;A경험이 50미만이면 못 함
SIF EXP:A경험 < 50
    RETURN 0
;기계면 불가
SIF TALENT:206
    RETURN 0
;학교수영복 플레이 중에는 불가
SIF TEQUIP:59 == 2
    RETURN 0
;쿠스코 삽입 중에는 불가
SIF TEQUIP:21
    RETURN 0
;그밖의 Ａ계 장착구 사용 중에는 불가
SIF TEQUIP:25 || TEQUIP:26
    RETURN 0
;한증탕과 암반욕 외의 ○○ 욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;보지 바벨을 애널에 전용한 것.
;환타지이므로, 현실에 그렇게 말하지는 하지 말아 주세요
@COM179
PRINTL 애널 바벨
COM_NAME = 애널 바벨
CALL TRAIN_MESSAGE_COM179

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 130
DOWNBASE:기력 += 150

SOURCE:12 = 350
SOURCE:20 = 480
SOURCE:24 = 500
SOURCE:31 = 850

;ABL:A감각을 본다
IF ABL:A감각 == 0
    SOURCE:2 = 10
    SOURCE:22 = 100
ELSEIF ABL:A감각 == 1
    SOURCE:2 = 30
    SOURCE:22 = 500
ELSEIF ABL:A감각 == 2
    SOURCE:2 = 300
    SOURCE:22 = 1000
ELSEIF ABL:A감각 == 3
    SOURCE:2 = 600
    SOURCE:22 = 2000
ELSEIF ABL:A감각 == 4
    SOURCE:2 = 900
    SOURCE:22 = 3000
ELSE
    SOURCE:2 = 1200
    SOURCE:22 = 6000
ENDIF

;EXP:A경험을 본다
IF EXP:A경험 < EXPLV:1
    TIMES SOURCE:2 , 0.10
    SOURCE:20 += 12000
ELSEIF EXP:A경험 < EXPLV:2
    TIMES SOURCE:2 , 0.30
    SOURCE:20 += 6000
ELSEIF EXP:A경험 < EXPLV:3
    TIMES SOURCE:2 , 0.50
    SOURCE:20 += 2000
ELSEIF EXP:A경험 < EXPLV:4
    TIMES SOURCE:2 , 1.00
    SOURCE:20 += 1200
ELSEIF EXP:A경험 < EXPLV:5
    TIMES SOURCE:2 , 1.40
    SOURCE:20 += 600
ELSE
    TIMES SOURCE:2 , 1.60
    SOURCE:20 += 200
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
    TIMES SOURCE:2 , 0.40
    SOURCE:20 += 10000
ELSEIF PALAM:10 < PALAMLV:2
    TIMES SOURCE:2 , 0.80
    SOURCE:20 += 3600
ELSEIF PALAM:10 < PALAMLV:3
    TIMES SOURCE:2 , 1.00
    SOURCE:20 += 1200
ELSEIF PALAM:10 < PALAMLV:4
    TIMES SOURCE:2 , 1.40
    SOURCE:20 += 200
ELSE
    TIMES SOURCE:1 , 1.80
    SOURCE:20 += 100
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    TIMES SOURCE:2 , 0.60
    TIMES SOURCE:22 , 0.60
ELSEIF PALAM:13 < PALAMLV:2
    TIMES SOURCE:2 , 0.80
    TIMES SOURCE:22 , 0.80
ELSEIF PALAM:13 < PALAMLV:3
    TIMES SOURCE:2 , 1.00
    TIMES SOURCE:22 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
    TIMES SOURCE:2 , 1.20
    TIMES SOURCE:22 , 1.20
ELSE
    TIMES SOURCE:2 , 1.40
    TIMES SOURCE:22 , 1.40
ENDIF

;[작은체형]
SIF TALENT:128
    TIMES SOURCE:20 , 2.00

;A민감, 둔감을 본다
;쾌A자체의 체크는 다음에 정리해 준다
IF TALENT:105
    TIMES SOURCE:20 , 1.50
    TIMES SOURCE:22 , 1.50
    TIMES SOURCE:31 , 1.50
ELSEIF TALENT:104
    TIMES SOURCE:20 , 0.60
    TIMES SOURCE:22 , 0.60
    TIMES SOURCE:31 , 0.60
ENDIF

;처녀로 정조관념
SIF TALENT:처녀 == 1 && TALENT:30
    SOURCE:22 /= 3

TCVAR:1 += 4
SIF EXP:A확장경험 == 0 
    TCVAR:50 += 1
TCVAR:33 += 1

;커맨드 속성：도구(조교자)
TFLAG:98 = 1

;애널 바벨의 착탈
IF TEQUIP:25
    TEQUIP:25 = 0
ELSE
    TEQUIP:25 = 10
ENDIF
RETURN 1

;--------------------------------------------------
;애널 바벨 삽입중
;--------------------------------------------------
@EQUIP_COM179
PRINTL <애널 바벨 삽입 중>

DOWNBASE:0 += 45
DOWNBASE:기력 += 85

;ABL:A감각을 본다
IF ABL:A감각 == 0
    LOCAL = 10
    LOCAL:1 = 100
    SOURCE:11 += 10
ELSEIF ABL:A감각 == 1
    LOCAL = 20
    LOCAL:1 = 500
    SOURCE:11 += 30
ELSEIF ABL:A감각 == 2
    LOCAL = 200
    LOCAL:1 = 1000
    SOURCE:11 += 100
ELSEIF ABL:A감각 == 3
    LOCAL = 400
    LOCAL:1 = 2000
    SOURCE:11 += 200
ELSEIF ABL:A감각 == 4
    LOCAL = 700
    LOCAL:1 = 3000
    SOURCE:11 += 450
ELSE
    LOCAL = 1000
    LOCAL:1 = 6000
    SOURCE:11 += 750
ENDIF

;EXP:A경험을 본다
IF EXP:A경험 < EXPLV:1
    TIMES LOCAL , 0.10
    LOCAL:2 = 12000
ELSEIF EXP:A경험 < EXPLV:2
    TIMES LOCAL , 0.30
    LOCAL:2 = 6000
ELSEIF EXP:A경험 < EXPLV:3
    TIMES LOCAL , 0.50
    LOCAL:2 = 2000
ELSEIF EXP:A경험 < EXPLV:4
    TIMES LOCAL , 1.00
    LOCAL:2 = 1200
ELSEIF EXP:A경험 < EXPLV:5
    TIMES LOCAL , 1.40
    LOCAL:2 = 600
ELSE
    TIMES LOCAL , 1.60
    LOCAL:2 = 200
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    TIMES LOCAL , 0.60
    TIMES LOCAL:1 , 0.60
ELSEIF PALAM:13 < PALAMLV:2
    TIMES LOCAL , 0.80
    TIMES LOCAL:1 , 0.80
ELSEIF PALAM:13 < PALAMLV:3
    TIMES LOCAL , 1.00
    TIMES LOCAL:1 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
    TIMES LOCAL , 1.20
    TIMES LOCAL:1 , 1.20
ELSE
    TIMES LOCAL , 1.40
    TIMES LOCAL:1 , 1.40
ENDIF

;[작은체형]
SIF TALENT:128
    TIMES LOCAL:2 , 2.00

;A민감, 둔감을 본다
;쾌A자체의 체크는 다음에 정리해 준다
IF TALENT:105
    TIMES LOCAL , 1.50
    TIMES LOCAL:1 , 1.50
    TIMES LOCAL:2 , 1.50
ELSEIF TALENT:104
    TIMES LOCAL , 0.60
    TIMES LOCAL:1 , 0.60
    TIMES LOCAL:2 , 0.60
ENDIF

;처녀로 정조관념
SIF TALENT:처녀 == 1 && TALENT:30
    LOCAL:1 /= 3

SOURCE:21 += LOCAL
SOURCE:22 += LOCAL:1
SOURCE:20 += LOCAL:2
SOURCE:31 += LOCAL:1

TCVAR:1 += 3

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
    TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:120
    TCVAR:13 += 1
ENDIF

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM179
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
IF TEQUIP:25
    IF MARK:반발각인 > 1
        PRINTFORMW %조사선택(CALLNAME:PLAYER, "가")% 미처 손을 쓰기도 전에, %조사선택(CALLNAME:TARGET, "는")% 활짝 벌려진 항문에서 바벨을 뽑아 던져버렸다….
    ELSE
        PRINTFORMW %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%의 벌려진 항문에서 바벨을 뽑아 주었다….
    ENDIF
ELSE
    IF MARK:반발각인 > 1
        PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% 싫어서 버둥대는 %조사선택(CALLNAME:TARGET, "를")% 억눌렀다.
        PRINTFORM 그리고, %CALLNAME:TARGET%의 
        SIF PALAM:10 >= PALAMLV:4
            PRINT 점액 투성이가 된 
        PRINTW 항문에 바벨을 삽입해 주었다….
    ELSE
        PRINTFORM %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%의 
        SIF PALAM:10 >= PALAMLV:4
            PRINT 점액 투성이가 된 
        PRINTW 항문에 바벨을 삽입해 주었다….
    ENDIF
ENDIF

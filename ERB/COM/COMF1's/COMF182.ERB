;=============================================================================
;애널 자위
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE182
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:182 > 0
    RETURN 0
;Ａ계 확장 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 4) == 0
    RETURN 0
;순종4미만 · 레즈끼(BL끼) 4 미만의 ASSI일 경우, 조교 대상이 동성이라면 실행할 수 없다(소악마か망신か복종ならＯＫ)
IF ASSIPLAY
    SIF (ABL:ASSI:순종 < 4 || ((TALENT:120 == 0 && TALENT:ASSI:120 == 0 && ABL:ASSI:레즈끼 < 4) || (TALENT:120 && TALENT:ASSI:120 && ABL:ASSI:BL끼 < 4))) && TALENT:ASSI:93 == 0 && TALENT:ASSI:151 == 0 && TALENT:ASSI:160 == 0
        RETURN 0
ENDIF
;公開自慰を行ったことがない場合のビデオ撮影中は, 이상계 조교 필터가 오프되어 있지 않으면 불가
SIF FLAG:2 & 4 && (CFLAG:21 & 1) == 0 && TEQUIP:50
    RETURN 0
;Ａ조교 필터가 오프되어 있지 않으면 불가
SIF FLAG:2 & 2
    RETURN 0
;수면 중에는 불가
SIF EQUIP:14
    RETURN 0
;실신 중에는 불가
SIF TFLAG:899 > 0
    RETURN 0
;바이브, 쿠스코, 애널바이브, 애널비즈以外のＶ계 장착구, Ａ계 장착구 사용 중에는 불가
SIF TEQUIP:20 > 1 || TEQUIP:25 == 2 || TEQUIP:25 == 3 || TEQUIP:25 == 4 || TEQUIP:25 > 5 || TEQUIP:26
    RETURN 0
;밧줄 사용 중에는 불가
SIF TEQUIP:40
    RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
    RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
    RETURN 0
;훈도시 착용 중일 경우 기교4이상 필수
SIF TEQUIP:59 == 17 && ABL:기교 < 4
    RETURN 0
;욕실 플레이 중에는 플레이 매트가 없으면 불가
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
    RETURN 0
;샤워 중에는 불가
SIF TEQUIP:54
    RETURN 0
;○○목욕탕 입욕 중에는 불가
SIF TEQUIP:55
    RETURN 0
;女体盛り中はダメ
SIF TEQUIP:60
    RETURN 0
;시간정지 중에는 무리
SIF TEQUIP:71
    RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
    RETURN 0
;시체는 안됨
SIF TALENT:226 == 1
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;애널 바이브래이터, 비디오 촬영때에 내용 변화
;비디오 촬영중이라면 공개 애널 자위
;애널 바이브래이터 장착시라면 바이브래이터 애널 자위
;애널 비즈 장착시라면 비즈 애널 자위
@COM182
LOCALS = 
SIF TEQUIP:50
    LOCALS = %LOCALS%공개
IF TEQUIP:25 == 1
    LOCALS = %LOCALS%바이브래이터
ELSEIF TEQUIP:25 == 5
    LOCALS = %LOCALS%비즈
ENDIF
LOCALS = %LOCALS%애널 자위
PRINTFORML %LOCALS%

;-------------------------------------------------
;실행할 수 있을까의 판정
;-------------------------------------------------
IF TFLAG:699 == 0
    CALL COM_ORDER
    ;실행할 수 없다
    SIF RESULT == 0
        RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------
COM_NAME = %LOCALS%
CALL TRAIN_MESSAGE_COM182

DOWNBASE:0 += 5
DOWNBASE:1 += 50

SOURCE:31 = 400
LOCAL:1 = 0
LOCAL:2 = 0
LOCAL:3 = 0

IF TEQUIP:50
    SOURCE:18 = 100
    SOURCE:19 = 50
ENDIF

;ABL:A감각을 본다
IF ABL:A감각 == 0
    SOURCE:2 = 7
    SOURCE:12 = 2000
    SOURCE:22 = 500
ELSEIF ABL:A감각 == 1
    SOURCE:2 = 25
    SOURCE:12 = 2300
    SOURCE:22 = 800
ELSEIF ABL:A감각 == 2
    SOURCE:2 = 150
    SOURCE:12 = 2600
    SOURCE:22 = 1200
ELSEIF ABL:A감각 == 3
    SOURCE:2 = 350
    SOURCE:12 = 2900
    SOURCE:22 = 1900
ELSEIF ABL:A감각 == 4
    SOURCE:2 = 550
    SOURCE:12 = 3200
    SOURCE:22 = 2500
ELSE
    SOURCE:2 = 500
    SOURCE:12 = 3500
    SOURCE:22 = 3000
ENDIF

;EXP:A경험을 본다
IF EXP:A경험 < EXPLV:1
    TIMES LOCAL:2 , 0.50
    LOCAL:3 += 500
ELSEIF EXP:A경험 < EXPLV:2
    TIMES LOCAL:2 , 1.00
    LOCAL:3 += 75
ELSEIF EXP:A경험 < EXPLV:3
    TIMES LOCAL:2 , 1.10
    LOCAL:3 += 10
ELSEIF EXP:A경험 < EXPLV:4
    TIMES LOCAL:2 , 1.20
    LOCAL:3 += 0
ELSEIF EXP:A경험 < EXPLV:5
    TIMES LOCAL:2 , 1.40
    LOCAL:3 += 0
ELSE
    TIMES LOCAL:2 , 1.60
    LOCAL:3 += 0
ENDIF

;애널 바이브래이터 삽입중을 확인·계산
IF TEQUIP:25
    DOWNBASE:0 += 30
    DOWNBASE:1 += 80

    ;ABL:A감각을 본다
    IF ABL:A감각 == 0
        LOCAL:2 += 40
        LOCAL:1 += 150
    ELSEIF ABL:A감각 == 1
        LOCAL:2 += 120
        LOCAL:1 += 400
    ELSEIF ABL:A감각 == 2
        LOCAL:2 += 300
        LOCAL:1 += 700
    ELSEIF ABL:A감각 == 3
        LOCAL:2 += 500
        LOCAL:1 += 900
    ELSEIF ABL:A감각 == 4
        LOCAL:2 += 650
        LOCAL:1 += 1000
    ELSE
        LOCAL:2 += 850
        LOCAL:1 += 1200
    ENDIF

    ;EXP:A경험을 본다
    IF EXP:A경험 < EXPLV:1
        TIMES LOCAL:2 , 0.50
        LOCAL:3 += 500
    ELSEIF EXP:A경험 < EXPLV:2
        TIMES LOCAL:2 , 1.00
        LOCAL:3 += 75
    ELSEIF EXP:A경험 < EXPLV:3
        TIMES LOCAL:2 , 1.10
        LOCAL:3 += 10
    ELSEIF EXP:A경험 < EXPLV:4
        TIMES LOCAL:2 , 1.20
        LOCAL:3 += 0
    ELSEIF EXP:A경험 < EXPLV:5
        TIMES LOCAL:2 , 1.40
        LOCAL:3 += 0
    ELSE
        TIMES LOCAL:2 , 1.60
        LOCAL:3 += 0
    ENDIF
    ;PALAM:윤활을 본다
    IF PALAM:10 < PALAMLV:1
        TIMES LOCAL:2 , 0.40
        LOCAL:3 += 800
    ELSEIF PALAM:10 < PALAMLV:2
        TIMES LOCAL:2 , 0.80
        LOCAL:3 += 500
    ELSEIF PALAM:10 < PALAMLV:3
        TIMES LOCAL:2 , 1.00
        LOCAL:3 += 300
    ELSEIF PALAM:10 < PALAMLV:4
        TIMES LOCAL:2 , 1.40
        LOCAL:3 += 120
    ELSE
        TIMES LOCAL:2 , 1.80
        LOCAL:3 += 100
    ENDIF

    ;PALAM:욕정을 본다
    IF PALAM:13 < PALAMLV:1
        TIMES LOCAL:2 , 0.80
    ELSEIF PALAM:13 < PALAMLV:2
        TIMES LOCAL:2 , 0.90
    ELSEIF PALAM:13 < PALAMLV:3
        TIMES LOCAL:2 , 1.00
    ELSEIF PALAM:13 < PALAMLV:4
        TIMES LOCAL:2 , 1.10
    ELSE
        TIMES LOCAL:2 , 1.20
    ENDIF

    ;ABL:순종을 본다
    IF ABL:순종 == 0
        TIMES LOCAL:1 , 0.80
        TIMES LOCAL:2 , 0.80
    ELSEIF ABL:순종 == 1
        TIMES LOCAL:1 , 0.90
        TIMES LOCAL:2 , 0.90
    ELSEIF ABL:순종 == 2
        TIMES LOCAL:1 , 1.00
        TIMES LOCAL:2 , 1.00
    ELSEIF ABL:순종 == 3
        TIMES LOCAL:1 , 1.10
        TIMES LOCAL:2 , 1.10
    ELSEIF ABL:순종 == 4
        TIMES LOCAL:1 , 1.20
        TIMES LOCAL:2 , 1.20
    ELSE
        TIMES LOCAL:1 , 1.30
        TIMES LOCAL:2 , 1.30
    ENDIF

    ;정조관념
    SIF TALENT:30
        TIMES LOCAL:3, 3.00
    ;[작은체형]
    SIF TALENT:128
        TIMES LOCAL:3 , 2.00
ENDIF

;A민감, 둔감을 본다
IF TALENT:105
    TIMES LOCAL:3 , 1.50
    TIMES LOCAL:1 , 1.50
ELSEIF TALENT:104
    TIMES LOCAL:3 , 0.60
    TIMES LOCAL:1 , 0.60
ENDIF

;한 번 단독으로 계산
SOURCE:22 += LOCAL:1

SOURCE:2 += LOCAL:2
SOURCE:20 += LOCAL:3

;ABL:기교를 본다
IF ABL:기교 == 0
    SOURCE:15 = 100
    TIMES SOURCE:2 , 0.30
ELSEIF ABL:기교 == 1
    SOURCE:15 = 160
    TIMES SOURCE:2 , 0.70
ELSEIF ABL:기교 == 2
    SOURCE:15 = 220
    TIMES SOURCE:2 , 1.00
ELSEIF ABL:기교 == 3
    SOURCE:15 = 280
    TIMES SOURCE:2 , 1.20
ELSEIF ABL:기교 == 4
    SOURCE:15 = 340
    TIMES SOURCE:2 , 1.40
ELSE
    SOURCE:15 = 400
    TIMES SOURCE:2 , 1.60
ENDIF

;ABL:자위 중독을 본다
IF ABL:자위중독 == 0
    SOURCE:24 = 0
    TIMES SOURCE:2 , 1.00
ELSEIF ABL:자위중독 == 1
    SOURCE:24 = 100
    TIMES SOURCE:2 , 1.10
ELSEIF ABL:자위중독 == 2
    SOURCE:24 = 300
    TIMES SOURCE:2 , 1.20
ELSEIF ABL:자위중독 == 3
    SOURCE:24 = 800
    TIMES SOURCE:2 , 1.30
ELSEIF ABL:자위중독 == 4
    SOURCE:24 = 1500
    TIMES SOURCE:2 , 1.50
ELSE
    SOURCE:24 = 2500
    TIMES SOURCE:2 , 1.70
ENDIF

;공개 때는, ABL:노출벽을 본다
IF TEQUIP:50
    IF ABL:노출증 == 0
        SOURCE:24 += 0
        TIMES SOURCE:2 , 1.00
        TIMES SOURCE:12 , 1.00
    ELSEIF ABL:노출증 == 1
        SOURCE:24 += 100
        TIMES SOURCE:2 , 1.10
        TIMES SOURCE:12 , 1.20
    ELSEIF ABL:노출증 == 2
        SOURCE:24 += 300
        TIMES SOURCE:2 , 1.20
        TIMES SOURCE:12 , 1.40
    ELSEIF ABL:노출증 == 3
        SOURCE:24 += 800
        TIMES SOURCE:2 , 1.30
        TIMES SOURCE:12 , 1.60
    ELSEIF ABL:노출증 == 4
        SOURCE:24 += 1500
        TIMES SOURCE:2 , 1.50
        TIMES SOURCE:12 , 2.00
    ELSE
        SOURCE:24 += 2500
        TIMES SOURCE:2 , 1.70
        TIMES SOURCE:12 , 3.00
    ENDIF
ENDIF

;비디오 촬영시는 경험에 플러스
IF TEQUIP:50
    TCVAR:1 += 1
    TCVAR:10 += 2
    TCVAR:11 += 2
ELSE
    TCVAR:1 += 1
    TCVAR:10 += 1
    TCVAR:11 += 1
ENDIF
IF TEQUIP:50 && (CFLAG:21 & 1) == 0
    TCVAR:50 += 1
    CFLAG:21 |= 1
ENDIF

;노예의 손가락⇔노예의 A의 불결이 이동
STAIN:1 |= STAIN:4
STAIN:4 |= STAIN:1

;촬영시의 조교 내용 체크
SIF TEQUIP:25
    TFLAG:122 = 992

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
    TCVAR:12 += 3
ELSEIF TALENT:120 && TALENT:PLAYER:120
    TCVAR:13 += 3
ENDIF

;노출 쾌락 경험 입수 판정
TFLAG:100 |= 2

RETURN 1

;--------------------------------------------------
;명령 거부의 메시지
;--------------------------------------------------
@DENIAL_MESSAGE_COM_182
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
IF TEQUIP:68
    SIF PALAM:0 >= (PALAMLV:4 - 1) ||  PALAM:1 >= (PALAMLV:4 - 1) ||  PALAM:2 >= (PALAMLV:4 - 1) ||  PALAM:3 >= (PALAMLV:4 - 1)
        PRINT 지금 당장이라도 가버릴 것 같으면서도, 
    PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 명령을 거부했다….
ELSE
    PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 자신의 항문으로 자위하는 것을 거절했다. 좀 더 조교가 필요할 것 같다.
ENDIF

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM182
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
;애태우기플레이중에 자위계·기승위계·조수를 범하게 한다
IF TEQUIP:68
    SIF PALAM:0 >= (PALAMLV:4 - 1) ||  PALAM:1 >= (PALAMLV:4 - 1) ||  PALAM:2 >= (PALAMLV:4 - 1) ||  PALAM:3 >= (PALAMLV:4 - 1)
        PRINT 지금 당장이라도 가버릴 것 같은 
    PRINTFORMW %조사선택(CALLNAME:TARGET, "가")% 좋아하는 대로 하게 두었다….
ENDIF
IF TFLAG:699 > 0
    PRINTFORML %조사선택(CALLNAME:MASTER, "가")% 아무것도 명령하지 않았는데도 %조사선택(CALLNAME:TARGET, "는")% 
    SIF TALENT:170 || TEQUIP:11 == 1
        PRINT 참을 수 없다는 표정으로 
    SIF TALENT:81
        PRINTFORM 유혹하는 듯한 시선을 %CALLNAME:MASTER%에게 보내며 
    SIF TALENT:157
        PRINT 질투심에 사로잡혀 
    SIF TALENT:158 && TEQUIP:42 == 0
        PRINT 제정신이 아닌 것 같은 웃음소리를 내며 
    PRINTW 항문 자위의 쾌감에 잠겨 있다….
ELSE
    IF EX:22
        PRINTFORM 기다리고 있는 %CALLNAME:TARGET%에게 자위를 허락하자 음란하게 \@(TEQUIP:42) ? 표정을 지으며 # 입술을 깨물며\@, 
    ELSEIF TFLAG:240 < 50
        PRINTFORM 자위를 명령하자 %조사선택(CALLNAME:TARGET, "는")% 
        SIF TALENT:16
            PRINT 프라이드 높음을 보이며 
        SIF TALENT:11
            PRINT 반항적인 태도로 
        PRINT 거부했지만 다시 명령하자 어쩔 수 없이 
    ELSEIF TFLAG:240 < 80
        PRINTFORML 조교의 성과가 나왔는지, %조사선택(CALLNAME:TARGET, "는")% 명령에 따라,
    ELSE
        PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 자위의 명령에도 싫어하는 기색을 보이지 않고, 완전히 길들여진 \@(TALENT:120) ? 짐승 # 돼지\@의 표정으로,
    ENDIF
    SIF TEQUIP:50
        PRINT 비디오 카메라의 앞에서 
    SIF ABL:노출증 > 3
        PRINT 보여주듯이 
    IF TEQUIP:21 && TEQUIP:25 == 1
        PRINT 보지를 쿠스코로 연 채로 애널바이브를 스스로 넣고 빼면서 
    ELSEIF TEQUIP:21 && TEQUIP:25 == 5
        PRINT 보지를 쿠스코로 연 채로 애널 비즈를 스스로 넣고 빼면서 
    ELSEIF TEQUIP:21
        PRINT 보지를 쿠스코로 연 채로 애널을 스스로의 손가락으로 만지작거리며 
    ELSEIF TEQUIP:25 == 1
        PRINT 꽂혀있는 애널바이브를 스스로 넣고 빼면서 
    ELSEIF TEQUIP:25 == 5
        PRINT 꽂혀있는 애널비즈를 스스로 넣고 빼면서 
    ELSE
        PRINT 자신의 손으로 애널을 만지작거리며 
    ENDIF
    PRINTW 쾌감을 맛보고 있다….
    IF TEQUIP:20 == 1 && TEQUIP:25 == 1 && ABL:C감각 >= 5 && ABL:V감각 >= 5 && ABL:A감각 >= 5 && ABL:B감각 >= 5 && PALAM:13 >= PALAMLV:4 && PREVCOM == 182
        PRINTL
        PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 군침을 흘리며 몸을 경련 하\@(TEQUIP:42) ? 고 # 면서 비명을 지르고\@ 있다.
        PRINTW 아무래도 절정이 멈추지 않아 계속해서 가고 있는 것 같다….
    ELSEIF TEQUIP:25 == 1 && ABL:C감각 >= 5 && ABL:A감각 >= 5 && ABL:B감각 >= 5 && PALAM:13 >= PALAMLV:4 && PREVCOM == 182
        PRINTL
        PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 퍼덕퍼덕 몸을 경련하면서, 항문에 바이브를 찔러 넣고 있다.
        PRINTW 아무래도 장벽을 긁힐 때 마다 절정하고 있는 것 같다….
    ELSEIF TEQUIP:25 == 5 && ABL:C감각 >= 5 && ABL:A감각 >= 5 && ABL:B감각 >= 5 && PALAM:13 >= PALAMLV:4 && PREVCOM == 182
        PRINTL
        PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 부들부들 몸을 떨며, 애널비즈를 쑤셔대고 있다.
        PRINTW 아무래도 구슬이 빠질 때 마다 절정하고 있는 것 같다….
    ELSEIF ABL:C감각 >= 5 && ABL:B감각 >= 5 && ABL:자위중독 >= 3 && PALAM:13 >= PALAMLV:4 && PREVCOM == 182
        PRINTL
        PRINTFORML %조사선택(CALLNAME:TARGET, "는")% \@(TEQUIP:42) ? # 교성을 지르며\@ 일심 불란하게 자신의 애널을 쑤시고 있다.
        PRINTW 멈추고 싶어도 손이 멋대로 움직이고 있는 것 같다….
    ENDIF
ENDIF

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_182, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:욕망
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:욕망
    TFLAG:240 += LOCAL:99 * 3
    RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF
;ABL:노출벽
IF ABL:노출증
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:노출증
    TFLAG:240 += LOCAL:99 * 4
    RESULTS = %RESULTS% %ABLNAME:14%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:자위 중독
IF ABL:자위중독
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = ABL:자위중독
    TFLAG:240 += LOCAL:99 * 3
    RESULTS = %RESULTS% %ABLNAME:30%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;쾌락 각인
IF MARK:1
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = MARK:1
    TFLAG:240 += LOCAL:99 * 3
    RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;PALAM:욕정
GETPALAMLV PALAM:13, 5
LOCAL:2 = RESULT
IF LOCAL:2
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = LOCAL:2 * 3
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;자제심
IF TALENT:20
    RESULTS = %RESULTS% - 
    LOCAL:99 = 5
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:20%({LOCAL:99})
ENDIF
;부끄럼쟁이
IF TALENT:35
    RESULTS = %RESULTS% - 
    LOCAL:99 = 5
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
;부끄럼없음
ELSEIF TALENT:36
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 2
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:36%({LOCAL:99})
ENDIF
;자위하기 쉽다
IF TALENT:60
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 5
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:60%({LOCAL:99})
ENDIF
;쾌감에 솔직
IF TALENT:70
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 5
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:70%({LOCAL:99})
;쾌감의 부정
ELSEIF TALENT:71
    RESULTS = %RESULTS% - 
    LOCAL:99 = 5
    TFLAG:240 -= LOCAL:99
    RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF

;미약
IF TEQUIP:11
    SIF LOCAL:99
        RESULTS = %RESULTS% + 
    LOCAL:99 = 8
    TFLAG:240 += LOCAL:99
    RESULTS = %RESULTS% %ITEMNAME:61%({LOCAL:99})
ENDIF

;난이도 상승
;공개로+10, 애널 바이브래이터로―5, 비즈로―5
TFLAG:241 = 48
SIF TEQUIP:50
    TFLAG:241 += 10
SIF TEQUIP:25 == 1
    TFLAG:241 -= 5
SIF TEQUIP:25 == 5
    TFLAG:241 -= 5

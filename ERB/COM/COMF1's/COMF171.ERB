;=============================================================================
;애널 전극
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE171
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:171 > 0
    RETURN 0
;Ａ계 확장 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 4) == 0
    RETURN 0
;Ａ조교 필터가 오프되어 있지 않으면 불가
SIF FLAG:2 & 2
    RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
    RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
    RETURN 0
;훈도시 착용 중일 경우는 안 됨
SIF TEQUIP:59 == 17
    RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
    RETURN 0
;해제는 언제든 가능
SIF TEQUIP:25 == 4
    RETURN 1
;그 조교 중에 관장 플레이＋배설이 있었을 경우, 조교자가 汚れ無視もしくは汚臭鈍感を持たないなら実行できない
SIF TFLAG:140 & 256 && TALENT:PLAYER:61 == 0 && TALENT:PLAYER:64 == 0
    RETURN 0
;아이템을 가지고 있거나, 아이템 없음 설정이 아니면 불가
SIF ITEM:14 == 0 && NOITEM == 0
    RETURN 0
;조교 대상이 [소인]일 경우에는 주인이 금단의 지식 보유자가 아니면 불가
SIF TALENT:127 && TALENT:MASTER:56 == 0
    RETURN 0
;기계면 불가
SIF TALENT:206
    RETURN 0
;쿠스코 삽입 중에는 불가
SIF TEQUIP:21
    RETURN 0
;그밖의 Ａ계 장착구 사용 중에는 불가
SIF (TEQUIP:25 && TEQUIP:25 != 4) || TEQUIP:26
    RETURN 0
;욕실 플레이 중, ○○ 욕탕 입욕 중에는 불가
SIF TEQUIP:53 || TEQUIP:55
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM171
COM_TOOL_NAME = 전극
IF FLAG:1100 & 1
    ;技師パッチ有効なら特殊な名前チェック
    FLAG:1110 = 14
    CALL NAMECHECK_CUSTOMTOOL
    IF COM_TOOL_NAME != "전극"
        PRINTFORML 애널 %COM_TOOL_NAME% 
    ELSE
        PRINTL 애널 전극
    ENDIF 
ELSE
    COM_TOOL_CALLNAME = %COM_TOOL_NAME%
    PRINTL 애널 전극
ENDIF
COM_NAME = 애널 전극
CALL TRAIN_MESSAGE_COM171

DOWNBASE:0 += 100
DOWNBASE:기력 += 150

SOURCE:31 = 1200

;ABL:A감각을 본다
IF ABL:A감각 == 0
    SOURCE:2 = 200
    SOURCE:20 = 1000
    SOURCE:22 = 1000
ELSEIF ABL:A감각 == 1
    SOURCE:2 = 500
    SOURCE:20 = 1000
    SOURCE:22 = 2000
ELSEIF ABL:A감각 == 2
    SOURCE:2 = 900
    SOURCE:20 = 1000
    SOURCE:22 = 3000
ELSEIF ABL:A감각 == 3
    SOURCE:2 = 1700
    SOURCE:20 = 1200
    SOURCE:22 = 5000
ELSEIF ABL:A감각 == 4
    SOURCE:2 = 2200
    SOURCE:20 = 1500
    SOURCE:22 = 7000
ELSE
    SOURCE:2 = 3400
    SOURCE:20 = 1800
    SOURCE:22 = 11000
ENDIF

;EXP:A경험을 본다
IF EXP:A경험 < EXPLV:1
    TIMES SOURCE:2 , 0.50
    SOURCE:20 += 2000
ELSEIF EXP:A경험 < EXPLV:2
    TIMES SOURCE:2 , 1.00
    SOURCE:20 += 300
ELSEIF EXP:A경험 < EXPLV:3
    TIMES SOURCE:2 , 1.10
    SOURCE:20 += 50
ELSEIF EXP:A경험 < EXPLV:4
    TIMES SOURCE:2 , 1.20
    SOURCE:20 += 10
ELSEIF EXP:A경험 < EXPLV:5
    TIMES SOURCE:2 , 1.40
    SOURCE:20 += 0
ELSE
    TIMES SOURCE:2 , 1.60
    SOURCE:20 += 0
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
    TIMES SOURCE:2 , 0.40
    SOURCE:20 += 800
ELSEIF PALAM:10 < PALAMLV:2
    TIMES SOURCE:2 , 0.80
    SOURCE:20 += 500
ELSEIF PALAM:10 < PALAMLV:3
    TIMES SOURCE:2 , 1.00
    SOURCE:20 += 300
ELSEIF PALAM:10 < PALAMLV:4
    TIMES SOURCE:2 , 1.40
    SOURCE:20 += 120
ELSE
    TIMES SOURCE:2 , 1.80
    SOURCE:20 += 100
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    TIMES SOURCE:2 , 0.80
ELSEIF PALAM:13 < PALAMLV:2
    TIMES SOURCE:2 , 0.90
ELSEIF PALAM:13 < PALAMLV:3
    TIMES SOURCE:2 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
    TIMES SOURCE:2 , 1.10
ELSE
    TIMES SOURCE:2 , 1.20
ENDIF

;ABL:순종을 본다
IF ABL:순종 == 0
    TIMES SOURCE:2 , 0.80
ELSEIF ABL:순종 == 1
    TIMES SOURCE:2 , 0.90
ELSEIF ABL:순종 == 2
    TIMES SOURCE:2 , 1.00
ELSEIF ABL:순종 == 3
    TIMES SOURCE:2 , 1.10
ELSEIF ABL:순종 == 4
    TIMES SOURCE:2 , 1.20
ELSE
    TIMES SOURCE:2 , 1.30
ENDIF

;[작은체형]
SIF TALENT:128
    TIMES SOURCE:20 , 2.00

;A민감, 둔감을 본다
;쾌A자체의 체크는 다음에 정리해 준다
IF TALENT:105
    TIMES SOURCE:20 , 1.50
    TIMES SOURCE:22 , 1.50
    TIMES SOURCE:31 , 1.50
ELSEIF TALENT:104
    TIMES SOURCE:20 , 0.60
    TIMES SOURCE:22 , 0.60
    TIMES SOURCE:31 , 0.60
ENDIF
;처녀로 정조관념
SIF TALENT:처녀 == 1 && TALENT:30
    SOURCE:22 /= 3

IF FLAG:1100 & 1
    ;技師パッチ有効なら道具使用時ソースチェックを呼ぶ
    ;FLAG 1110:道具番号 1111:改善タイプ 1112:SOURCEの実値 
    FLAG:1110 = 14
    FLAG:1111 = 1
    FLAG:1112 = SOURCE:2
    CALL SOURCECHECK_CUSTOMTOOL
    SOURCE:2 = FLAG:1120
    FLAG:1111 = 2
    FLAG:1112 = SOURCE:20
    CALL SOURCECHECK_CUSTOMTOOL
    SOURCE:20 = FLAG:1120
    FLAG:1111 = 1
    FLAG:1112 = SOURCE:22
    CALL SOURCECHECK_CUSTOMTOOL
    SOURCE:22 = FLAG:1120
ENDIF

TCVAR:1 += 5

;커맨드 속성：도구(조교자)
TFLAG:98 = 1

;전극의 착탈
IF TEQUIP:25
    TEQUIP:25 = 0
ELSE
    TEQUIP:25 = 4
ENDIF
;촉수 경험 플래그
TFLAG:90 = 0
RETURN 1

;--------------------------------------------------
;애널 전극 삽입중
;--------------------------------------------------
@EQUIP_COM171
COM_TOOL_NAME = 전극
IF FLAG:1100 & 1
    ;技師パッチ有効なら特殊な名前チェック
    FLAG:1110 = 14
    CALL NAMECHECK_CUSTOMTOOL
    IF COM_TOOL_NAME != "전극"
        PRINTFORM <애널 %COM_TOOL_NAME% 
    ELSE
        PRINT <애널 전극 
    ENDIF 
ELSE
    PRINT <애널 전극 
ENDIF
PRINTL 삽입 중>

DOWNBASE:0 += 80
DOWNBASE:기력 += 120

;ABL:A감각을 본다
IF ABL:A감각 == 0
    LOCAL = 250
    LOCAL:1 = 1000
ELSEIF ABL:A감각 == 1
    LOCAL = 600
    LOCAL:1 = 2000
ELSEIF ABL:A감각 == 2
    LOCAL = 1200
    LOCAL:1 = 3000
ELSEIF ABL:A감각 == 3
    LOCAL = 1900
    LOCAL:1 = 5000
ELSEIF ABL:A감각 == 4
    LOCAL = 2500
    LOCAL:1 = 8000
ELSE
    LOCAL = 3900
    LOCAL:1 = 12000
ENDIF

;EXP:A경험을 본다
IF EXP:A경험 < EXPLV:1
    TIMES LOCAL , 0.50
    LOCAL:2 = 2000
ELSEIF EXP:A경험 < EXPLV:2
    TIMES LOCAL , 1.00
    LOCAL:2 = 300
ELSEIF EXP:A경험 < EXPLV:3
    TIMES LOCAL , 1.10
    LOCAL:2 = 50
ELSEIF EXP:A경험 < EXPLV:4
    TIMES LOCAL , 1.20
    LOCAL:2 = 10
ELSEIF EXP:A경험 < EXPLV:5
    TIMES LOCAL , 1.40
    LOCAL:2 = 0
ELSE
    TIMES LOCAL , 1.60
    LOCAL:2 = 0
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
    TIMES LOCAL , 0.40
    LOCAL:2 += 800
ELSEIF PALAM:10 < PALAMLV:2
    TIMES LOCAL , 0.80
    LOCAL:2 += 500
ELSEIF PALAM:10 < PALAMLV:3
    TIMES LOCAL , 1.00
    LOCAL:2 += 300
ELSEIF PALAM:10 < PALAMLV:4
    TIMES LOCAL , 1.40
    LOCAL:2 += 120
ELSE
    TIMES LOCAL , 1.80
    LOCAL:2 += 100
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    TIMES LOCAL , 0.80
ELSEIF PALAM:13 < PALAMLV:2
    TIMES LOCAL , 0.90
ELSEIF PALAM:13 < PALAMLV:3
    TIMES LOCAL , 1.00
ELSEIF PALAM:13 < PALAMLV:4
    TIMES LOCAL , 1.10
ELSE
    TIMES LOCAL , 1.20
ENDIF

;ABL:순종을 본다
IF ABL:순종 == 0
    TIMES LOCAL , 0.80
ELSEIF ABL:순종 == 1
    TIMES LOCAL , 0.90
ELSEIF ABL:순종 == 2
    TIMES LOCAL , 1.00
ELSEIF ABL:순종 == 3
    TIMES LOCAL , 1.10
ELSEIF ABL:순종 == 4
    TIMES LOCAL , 1.20
ELSE
    TIMES LOCAL , 1.30
ENDIF

;[작은체형]
SIF TALENT:128
    TIMES LOCAL:2 , 2.00

;A민감, 둔감을 본다
;쾌A자체의 체크는 다음에 정리해 준다
IF TALENT:105
    TIMES LOCAL , 1.50
    TIMES LOCAL:1 , 1.50
    TIMES LOCAL:2 , 1.50
ELSEIF TALENT:104
    TIMES LOCAL , 0.60
    TIMES LOCAL:1 , 0.60
    TIMES LOCAL:2 , 0.60
ENDIF

;처녀로 정조관념
SIF TALENT:처녀 == 1 && TALENT:30
    LOCAL:1 /= 3

IF FLAG:1100 & 1
    ;技師パッチ有効なら道具使用時ソースチェックを呼ぶ
    ;FLAG 1110:道具番号 1111:改善タイプ 1112:SOURCEの実値 
    FLAG:1110 = 14
    FLAG:1111 = 1
    FLAG:1112 = LOCAL
    CALL SOURCECHECK_CUSTOMTOOL
    LOCAL = FLAG:1120
    FLAG:1111 = 2
    FLAG:1112 = LOCAL:2
    CALL SOURCECHECK_CUSTOMTOOL
    LOCAL:2 = FLAG:1120
    FLAG:1111 = 1
    FLAG:1112 = LOCAL:1
    CALL SOURCECHECK_CUSTOMTOOL
    LOCAL:1 = FLAG:1120
ENDIF

SOURCE:2 += LOCAL
SOURCE:22 += LOCAL:1
SOURCE:20 += LOCAL:2
SOURCE:31 += 1200

TCVAR:1 += 5

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
    TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:120
    TCVAR:13 += 1
ENDIF
;촉수 경험 플래그
SIF TEQUIP:90
    TFLAG:90 += 1

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM171
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
;전극의 착탈
IF TEQUIP:25
    IF MARK:반발각인 > 1
        PRINTFORMW %조사선택(CALLNAME:PLAYER, "가")% 미처 손을 쓰기도 전에, %조사선택(CALLNAME:TARGET, "는")% 항문에서 %조사선택(COM_TOOL_NAME, "를")% 뽑아 던져버렸다….
    ELSE
        PRINTFORMW %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%의 항문에서 %조사선택(COM_TOOL_NAME, "를")% 뽑아 주었다….
    ENDIF
ELSE
    IF MARK:반발각인 > 1
        PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% 싫어서 버둥대는 %조사선택(CALLNAME:TARGET, "를")% 억눌렀다.
        PRINTFORM 그리고, %CALLNAME:TARGET%의 
        SIF PALAM:10 >= PALAMLV:4
            PRINT 점액 투성이가 된 
        PRINTFORMW 항문에 %조사선택(COM_TOOL_CALLNAME, "를")% 삽입해 스위치를 넣었다….
    ELSE
        PRINTFORM %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%의 
        SIF PALAM:10 >= PALAMLV:4
            PRINT 점액 투성이가 된 
        PRINTFORMW 항문에 %조사선택(COM_TOOL_CALLNAME, "를")% 삽입해 스위치를 넣었다….
    ENDIF
ENDIF

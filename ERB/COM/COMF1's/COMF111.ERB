;=============================================================================
;방뇨
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE111
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:111 > 0
    RETURN 0
;利尿剤使用中かおもらし癖ありかある程度尿意を催していないとダメ
SIF TEQUIP:12 == 0 && TALENT:오줌싸개버릇 == 0 && BASE:뇨의 <= MAXBASE:뇨의 / 2
    RETURN 0
;기계면 불가
SIF TALENT:206
    RETURN 0
;순종3이상でないとダメ(お風呂場ならＯＫ)
IF TEQUIP:53 == 0
    SIF ABL:순종 < 3
        RETURN 0
ENDIF
;한증탕과 암반욕 외의 ○○ 욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
    RETURN 0
;수면 중에는 불가
SIF EQUIP:14
    RETURN 0
;실신 중에는 불가
SIF TFLAG:899 > 0
    RETURN 0
;射精止め中はダメ
SIF TEQUIP:69
    RETURN 0
;Ｕ계 장착구 사용 중에는 불가
SIF TEQUIP:32
    RETURN 0
;시체는 안됨
SIF TALENT:226 == 1
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM111
;주되들 해 버릇로 이뇨제가 없는 또는 주되들 해 버릇 없음 또한 순종 4이상 욕망 3이상 마조끼 3이상은 강제 방뇨로 변경
SIF (TALENT:오줌싸개버릇 == 0 && ABL:순종 >= 4 && ABL:욕망 >= 3 && ABL:마조끼 >= 3) || (TALENT:오줌싸개버릇 && TEQUIP:12 == 0)
    JUMP COM620

PRINTL 방뇨
COM_NAME = 방뇨
CALL TRAIN_MESSAGE_COM111

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 10
DOWNBASE:기력 += 200

SOURCE:31 = 5000
SOURCE:16 = 3000

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
    SOURCE:12 = 1800
ELSEIF PALAM:13 < PALAMLV:2
    SOURCE:12 = 1900
ELSEIF PALAM:13 < PALAMLV:3
    SOURCE:12 = 2000
ELSEIF PALAM:13 < PALAMLV:4
    SOURCE:12 = 2100
ELSE
    SOURCE:12 = 2200
ENDIF

;ABL:노출벽을 본다
IF ABL:노출증 == 0
    SOURCE:22 = 6000
ELSEIF ABL:노출증 == 1
    SOURCE:22 = 5000
ELSEIF ABL:노출증 == 2
    SOURCE:22 = 4000
ELSEIF ABL:노출증 == 3
    SOURCE:22 = 3000
ELSEIF ABL:노출증 == 4
    SOURCE:22 = 2000
ELSE
    SOURCE:22 = 1000
ENDIF

;ABL:마조끼를 본다
IF ABL:마조끼 == 0
    TIMES SOURCE:12 , 0.80
    SOURCE:19 = 100
    SOURCE:18 = 100
ELSEIF ABL:마조끼 == 1
    TIMES SOURCE:12 , 1.00
    SOURCE:19 = 200
    SOURCE:18 = 300
ELSEIF ABL:마조끼 == 2
    TIMES SOURCE:12 , 1.20
    SOURCE:19 = 400
    SOURCE:18 = 700
ELSEIF ABL:마조끼 == 3
    TIMES SOURCE:12 , 1.40
    SOURCE:19 = 700
    SOURCE:18 = 1200
ELSEIF ABL:마조끼 == 4
    TIMES SOURCE:12 , 1.50
    SOURCE:19 = 1100
    SOURCE:18 = 1800
ELSE
    TIMES SOURCE:12 , 1.70
    SOURCE:19 = 1500
    SOURCE:18 = 2500
ENDIF

;장소를 본다
IF TEQUIP:52
    TIMES SOURCE:31 , 2.50
ELSEIF TEQUIP:53
    TIMES SOURCE:31 , 0.50
ELSEIF TEQUIP:56 == 2 || (TEQUIP:56 == 1 && TALENT:205 == 0)
    TIMES SOURCE:31 , 1.50
    ;필연적으로 흡혈귀 소유만, 지문으로부터 전혀 신경쓰지 않은 것은 아닌 것 같아서.
ELSEIF TEQUIP:56 == 1
    TIMES SOURCE:31 , 1.20
ENDIF

LOCAL:1 = 100
;EXP:방뇨경험을 본다
IF EXP:방뇨경험 < EXPLV:1
    TIMES LOCAL:1 , 3.00
ELSEIF EXP:방뇨경험 < EXPLV:2
    TIMES LOCAL:1 , 2.50
ELSEIF EXP:방뇨경험 < EXPLV:3
    TIMES LOCAL:1 , 2.00
ELSEIF EXP:방뇨경험 < EXPLV:4
    TIMES LOCAL:1 , 1.00
ELSEIF EXP:방뇨경험 < EXPLV:5
    TIMES LOCAL:1 , 0.80
ELSE
    TIMES LOCAL:1 , 0.60
ENDIF

;반항적
SIF TALENT:11
    TIMES LOCAL:1 , 2.00
;프라이드 높다
IF TALENT:16
    TIMES LOCAL:1 , 2.00
;프라이드 낮다
ELSEIF TALENT:17
    TIMES LOCAL:1 , 0.80
ENDIF
;자제심
SIF TALENT:20
    TIMES LOCAL:1 , 1.50
;감정결여
SIF TALENT:22
    TIMES LOCAL:1 , 0.60
;튀고 싶어함
SIF TALENT:28
    TIMES LOCAL:1 , 0.80
;억압
SIF TALENT:32
    TIMES LOCAL:1 , 3.00
;저항
SIF TALENT:34
    TIMES LOCAL:1 , 3.00
;부끄럼쟁이
IF TALENT:35
    TIMES LOCAL:1 , 3.50
;부끄럼없음
ELSEIF TALENT:36
    TIMES LOCAL:1 , 0.50
ENDIF

SOURCE:25 += LOCAL:1 * 3

;겁쟁이
SIF TALENT:10
    TIMES LOCAL:1 , 1.20

SOURCE:31 += LOCAL:1 * 5
SOURCE:32 += LOCAL:1 * 2

;군고구마 도시락 패치 전용 이벤트(황금수 입수)
SIF FLAG:SHOP이벤트확장 & 8 && BASE:뇨의 > 3000
    CALL SWEET_POTATO_GW

TCVAR:6 += 2
;방뇨 레벨 증가
IF TALENT:오줌싸개버릇 == 0
    LOCAL:1 = 1 + ((EXP:방뇨경험 + TCVAR:6) / 10)
    LOCAL:1 = MIN(LOCAL:1 * 2, 10)
    CFLAG:31 += LOCAL:1
ENDIF

IF TALENT:120 == 0 && TALENT:PLAYER:남자 == 0
    TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:남자
    TCVAR:13 += 1
ENDIF
;촉수 경험 플래그
SIF TEQUIP:90
    TFLAG:90 += 1

TEQUIP:12 = 0
TFLAG:32 = 1
EX:12 += 1
NOWEX:12 += 1
BASE:뇨의 = 0

;노출 쾌락 경험 입수 판정
TFLAG:100 |= 2

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM111
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:조교시텍스트표시 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
;전회와 이번 조교자가 같아, 조교 대상이 여성으로 전회의 조교가 안면기승위 혹은 안면기승위 애널인 경우
IF TALENT:120 == 0 && (PREVCOM == 126 || PREVCOM == 187) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
    PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% %CALLNAME:PLAYER%에게 걸쳐진 상태로 수치심에 뺨을 붉히면서 방뇨하기 시작했다….
ELSEIF TEQUIP:58
    PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 기저귀 위로부터 비소가 자극되어 참지 못하고 기저귀에 오줌을 싸버렸다….
ELSEIF TEQUIP:52
    PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 수치와 굴욕감에 눈물을 머금으며, 풀숲 속에서 방뇨했다….
ELSEIF TEQUIP:56 && TALENT:205 == 0
    PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 거울에 비치는 자신을 보지 않으려 하며, 주저앉아 방뇨하기 시작했다….
ELSEIF TEQUIP:56
    PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 자신의 모습이 비치지 않는데도, 거울을 보지 않으려 하며 방뇨하기 시작했다….
ELSEIF TALENT:128 && (TALENT:110 || TALENT:111)
    PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% %CALLNAME:PLAYER%에게 어린 아이와 같이 양 다리를 움켜 쥐고 쪼그려 앉아 방뇨했다….
ELSE
    PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 수치심에 뺨을 붉히면서, 그 자리에 주저앉아 넣어 방뇨하기 시작했다….
ENDIF

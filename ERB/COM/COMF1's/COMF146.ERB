;=============================================================================
;요도 삽입
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE146
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:146 > 0
    RETURN 0
;Ｃ계 확장 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 1) == 0
    RETURN 0
;요도개발경험이 5이하일 경우, 이상계 조교 필터가 오프되어 있지 않으면 불가
SIF FLAG:2 & 4 && EXP:요도개발경험 <= 5
    RETURN 0
;조교자에게 페니스가 없고, かつ(아이템なし設定でなければ)페니스밴드도 없으면 불가
SIF EXIST_BAR(PLAYER) == 0 && ITEM:2 == 0 && NOITEM == 0
    RETURN 0
;주인이 [금단의 지식]보유가 아닐 경우, 体格差が2段階以内か조교자가 [금단의 지식]보유가 아니면 불가
SIF TALENT:MASTER:56 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
    RETURN 0
;처녀で, 순종5미만 · 레즈끼5미만의 ASSI일 경우, 조교 대상이 동성이라면 실행할 수 없다(새드거나 새드끼Lv3이상, 또는 망신이나 복종이 있다면 ＯＫ)
IF TALENT:처녀 == 1 && ASSIPLAY
    SIF (ABL:ASSI:순종 < 5 || (TALENT:120 == 0 && TALENT:ASSI:120 == 0 && ABL:ASSI:레즈끼 < 5)) && TALENT:ASSI:81 == 0 && ABL:ASSI:새드끼 < 3 && TALENT:ASSI:151 == 0 && TALENT:ASSI:160 == 0
        RETURN 0
ENDIF
;윤활 부족이고, 순종4미만 · 레즈끼(BL끼) 4 미만의 ASSI일 경우, 조교 대상이 동성이라면 실행할 수 없다(새드거나 새드끼Lv3이상, 또는 망신이나 복종이 있다면 ＯＫ)
IF PALAM:10 < PALAMLV:2 && ASSIPLAY
    SIF (ABL:ASSI:순종 < 4 || ((TALENT:120 == 0 && TALENT:ASSI:120 == 0 && ABL:ASSI:레즈끼 < 4) || (TALENT:120 && TALENT:ASSI:120 && ABL:ASSI:BL끼 < 4))) && TALENT:ASSI:81 == 0 && ABL:ASSI:새드끼 < 3 && TALENT:ASSI:151 == 0 && TALENT:ASSI:160 == 0
        RETURN 0
ENDIF
;ASSI일 경우, 기교4이상이 아니면 쓸 수 없다。
IF ASSIPLAY
    SIF ABL:ASSI:기교 < 4
        RETURN 0
ENDIF
;승마 중에는 불가
SIF TEQUIP:43
    RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
    RETURN 0
;훈도시 착용 중일 경우는 안 됨
SIF TEQUIP:59 == 17
    RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
    RETURN 0
;페니스가 있을 경우 불가
SIF (TALENT:120 || TALENT:121)
    RETURN 0
;체력이 1000미만ではダメ
SIF BASE:0 < 1000
    RETURN 0
;기력이 500미만일 때는 불가
SIF BASE:기력 < 500
    RETURN 0
;U확장경험 적으면 안 됨
SIF EXP:U확장경험 < 5
    RETURN 0
;쿠스코 사용 중에는 불가
SIF TEQUIP:21
    RETURN 0
;Ｕ계 장착구 사용 중에는 불가
SIF TEQUIP:32
    RETURN 0
;욕실 플레이 중에는 플레이 매트가 없으면 불가
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
    RETURN 0
;샤워 중에는 불가
SIF TEQUIP:54
    RETURN 0
;○○목욕탕 입욕 중에는 불가
SIF TEQUIP:55
    RETURN 0
;학교수영복 플레이 중에는 불가
SIF TEQUIP:59 == 2
    RETURN 0
;나체정식 중에는 불가
SIF TEQUIP:60
    RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;정상위를 마개조라고 하는 이름의 개악이라고 하는 원패치의 사양을
;한층 더 마개조했으므로 원의 모습이 없는 orz
@COM146
PRINTL 요도 삽입
COM_NAME = 요도 삽입
CALL TRAIN_MESSAGE_COM146

;-------------------------------------------------
;사정 게이지 체크
;-------------------------------------------------
LOCAL = 0

;ABL:기교를 본다
IF ABL:기교 == 0
    LOCAL = 1500
ELSEIF ABL:기교 == 1
    LOCAL = 1600
ELSEIF ABL:기교 == 2
    LOCAL = 1800
ELSEIF ABL:기교 == 3
    LOCAL = 2000
ELSEIF ABL:기교 == 4
    LOCAL = 2400
ELSE
    LOCAL = 3000
ENDIF

;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;욕망을 본다
LOCAL = EJACULATIONCHECK_ABL_11_1(TARGET, LOCAL)
;윤활을 본다
LOCAL = EJACULATIONCHECK_PALAM_10_1(TARGET, LOCAL)

;조교자의 C감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
    BASE:PLAYER:사정 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
LOCAL:1 = 240

;EXP:U확장 경험을 본다
IF EXP:U확장경험 < EXPLV:1
    TIMES LOCAL:1, 3.00
    SOURCE:20 = 5500
ELSEIF EXP:U확장경험 < EXPLV:2
    TIMES LOCAL:1, 2.50
    SOURCE:20 = 300
ELSEIF EXP:U확장경험 < EXPLV:3
    TIMES LOCAL:1, 2.00
    SOURCE:20 = 50
ELSEIF EXP:U확장경험 < EXPLV:4
    TIMES LOCAL:1, 1.00
    SOURCE:20 = 10
ELSEIF EXP:U확장경험 < EXPLV:5
    TIMES LOCAL:1, 0.80
    SOURCE:20 = 0
ELSE
    TIMES LOCAL:1, 0.60
    SOURCE:20 = 0
ENDIF

;겁쟁이
SIF TALENT:10
    TIMES LOCAL:1, 2.00
;감정결여
SIF TALENT:22
    TIMES LOCAL:1, 0.60
;[작은체형]
SIF TALENT:128
    TIMES LOCAL:1, 2.00

DOWNBASE:0 += LOCAL:1
DOWNBASE:기력 += LOCAL:1 * 2

SOURCE:12 = 800
SOURCE:31 = 2000

;ABL:C감각을 본다
IF ABL:C감각 == 0
    SOURCE:0 = 80
    SOURCE:11 = 150
    SOURCE:22 = 300
ELSEIF ABL:C감각 == 1
    SOURCE:0 = 250
    SOURCE:11 = 250
    SOURCE:22 = 800
ELSEIF ABL:C감각 == 2
    SOURCE:0 = 600
    SOURCE:11 = 350
    SOURCE:22 = 1400
ELSEIF ABL:C감각 == 3
    SOURCE:0 = 1000
    SOURCE:11 = 500
    SOURCE:22 = 1800
ELSEIF ABL:C감각 == 4
    SOURCE:0 = 1300
    SOURCE:11 = 700
    SOURCE:22 = 2100
ELSE
    SOURCE:0 = 1700
    SOURCE:11 = 1000
    SOURCE:22 = 2400
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
    TIMES SOURCE:0 , 0.40
    SOURCE:20 += 1600
ELSEIF PALAM:10 < PALAMLV:2
    TIMES SOURCE:0 , 0.80
    SOURCE:20 += 1000
ELSEIF PALAM:10 < PALAMLV:3
    TIMES SOURCE:0 , 1.00
    SOURCE:20 += 600
ELSEIF PALAM:10 < PALAMLV:4
    TIMES SOURCE:0 , 1.40
    SOURCE:20 += 250
ELSE
    TIMES SOURCE:0 , 1.80
    SOURCE:20 += 120
ENDIF

;EXP:요도 개발 경험을 본다
IF EXP:요도개발경험 < EXPLV:1
    TIMES SOURCE:0 , 0.20
    SOURCE:20 += 8000
ELSEIF EXP:요도개발경험 < EXPLV:2
    TIMES SOURCE:0 , 0.60
    SOURCE:20 += 3600
ELSEIF EXP:요도개발경험 < EXPLV:3
    TIMES SOURCE:0 , 1.00
    SOURCE:20 += 1200
ELSEIF EXP:요도개발경험 < EXPLV:4
    TIMES SOURCE:0 , 1.20
    SOURCE:20 += 300
ELSEIF EXP:요도개발경험 < EXPLV:5
    TIMES SOURCE:0 , 1.40
    SOURCE:20 += 30
ELSE
    TIMES SOURCE:0 , 1.60
    SOURCE:20 += 0
ENDIF

;ABL:순종을 본다
IF ABL:순종 == 0
    TIMES SOURCE:0 , 0.80
ELSEIF ABL:순종 == 1
    TIMES SOURCE:0 , 0.90
ELSEIF ABL:순종 == 2
    TIMES SOURCE:0 , 1.00
ELSEIF ABL:순종 == 3
    TIMES SOURCE:0 , 1.10
ELSEIF ABL:순종 == 4
    TIMES SOURCE:0 , 1.20
ELSE
    TIMES SOURCE:0 , 1.30
ENDIF

;남자
SIF TALENT:120
    TIMES SOURCE:20 , 4.50
;[작은체형]
SIF TALENT:128
    TIMES SOURCE:20 , 2.00
;요도광
SIF TALENT:175
    TIMES SOURCE:0 , 1.20

;C민감, 둔감을 본다
;쾌C자체의 체크는 다음에 정리해 준다
IF TALENT:101
    TIMES SOURCE:20 , 1.50
    TIMES SOURCE:22 , 1.50
    TIMES SOURCE:31 , 1.50
ELSEIF TALENT:100
    TIMES SOURCE:20 , 0.60
    TIMES SOURCE:22 , 0.60
    TIMES SOURCE:31 , 0.60
ENDIF

;처녀로 정조관념
SIF TALENT:처녀 == 1 && TALENT:30
    SOURCE:22 /= 3

SIF EXP:요도개발경험 <= 5
    TCVAR:50 += 1

;방뇨 레벨 증가
CFLAG:31 += 3
SIF BASE:뇨의
    BASE:뇨의 = 0
TCVAR:46 += 3
TCVAR:34 += 2

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;사정시의 처리
IF TFLAG:91 >= 1
    ;커맨드 마다 고유의 처리는 이 아래에 쓴다
    IF ABL:순종 >= 3 && ABL:욕망 >= 3 && FLAG:10
        PRINTFORML %조사선택(CALLNAME:TARGET, "는")% %CALLNAME:PLAYER%에게 다리를 감아 허리를 조여왔다…. 
        PRINTFORML  
    ENDIF

    ;사정처 타입의 설정
    TFLAG:703 = 19
    ;사정처 선택의 가능·불가
    TFLAG:704 = 1
    ;범용적인 처리는 이 함수로 실시한다
    ;(사정 게이지의 재계산이나 사정 경험의 상승과 공통 부분의 표시)
    CALL SAMEN_SHOOT
ENDIF

;사정에 관계없이 행해지는 처리는 여기로부터
IF TALENT:120 == 0
    ;노예의 V⇔조교자의 P의 불결이 이동
    STAIN:보지 |= STAIN:PLAYER:자지
    STAIN:PLAYER:자지 |= STAIN:보지
ELSE
    ;노예의 P⇔조교자의 P의 불결이 이동
    STAIN:자지 |= STAIN:PLAYER:자지
    STAIN:PLAYER:자지 |= STAIN:자지
ENDIF

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
    TCVAR:12 += 7
ELSEIF TALENT:120 && TALENT:PLAYER:120
    TCVAR:13 += 7
ENDIF
;주인 경험 플래그
SIF ASSIPLAY == 0
    TFLAG:50 += (ABL:V감각 >= 3) ?  2 # 1

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM146
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
    RETURN 0

PRINTL
PRINTFORM %조사선택(CALLNAME:PLAYER, "는")% 
;애태우기플레이
IF TEQUIP:68
    PRINTFORM %CALLNAME:TARGET%의 
    SIF EX:20
        PRINT 펄떡펄떡 경련하는 
    PRINT 허리를 억눌러 요도에 
    IF EXIST_PENIS(PLAYER)
        PRINTW 페니스를 삽입한 채로 작게 비볐다….
    ELSE
        PRINTW 페니스 밴드의 끄트머리를 가볍게 삽입했다….
    ENDIF
;조교자가[남자]나[후타나리]이다
ELSEIF EXIST_PENIS(PLAYER)
    PRINTFORMW %CALLNAME:TARGET%의 요도내의 감촉을 맛보면서, 안쪽 깊이 페니스를 찔러 넣었다….
;페니스 밴드를 사용하고 있는 경우
ELSE
    PRINTFORMW %CALLNAME:TARGET%의 요도내에 페니스 밴드의 돌기부를 삽입해 자극을 계속 주었다….
ENDIF

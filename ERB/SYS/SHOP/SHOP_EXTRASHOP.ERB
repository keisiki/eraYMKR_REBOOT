;=============================================================================
;특별한 도구 구입 처리
;=============================================================================
@BUY_SPECIAL_ITEM
DRAWLINE
PRINTL 무엇을 구입하겠습니까?
PRINTFORML 소지금:{MONEY}원
DRAWLINE
CALL ARRANGE_PRINT_ITEM
DRAWLINE
;-----------------------------------------------
SIF (FLAG:지하실개조 & 1) == 0 && FLAG:커맨드확장 & 16 && (TALENT:MASTER:81 || TALENT:MASTER:158 || TALENT:MASTER:177)
    PRINTLC [00] 비밀 지하실(50만원)
IF FLAG:커맨드확장 & 16 && FLAG:지하실개조 & 1
    SIF (FLAG:지하실개조 & 2) == 0
        PRINTLC [01] 삼각목마(1만원)
    SIF (FLAG:지하실개조 & 4) == 0
        PRINTLC [02] 말뚝(2666원)
    SIF (FLAG:지하실개조 & 8) == 0
        PRINTLC [03] 나이프(6000원)
    SIF (FLAG:지하실개조 & 16) == 0
        PRINTLC [04] 손도끼(1만원)
    SIF (FLAG:지하실개조 & 32) == 0
        PRINTLC [05] 아이언메이든(8만원)
ENDIF
;(名称が決まらない……)
;SIF (FLAG:XX & X) == 0 && FLAG:커맨드확장 & 16 && (TALENT:MASTER:81 || TALENT:MASTER:158 || TALENT:MASTER:177)
;    PRINTLC [19] 우리(25만원)
;-----------------------------------------------
IF FLAG:커맨드확장 & 16 && FLAG:지하실개조 & 1
    LOCAL:2 = BRAND_SLAVE_CHECK()
    SIF LOCAL:2 & 1
        PRINTLC [20] 클리토리스 링(10만원)
    SIF LOCAL:2 & 2
        PRINTLC [21] 노예 관리용 정조대(25만원)
    SIF LOCAL:2 & 4
        PRINTLC [22] 벨리 쥬얼(30만원)
    SIF LOCAL:2 & 8
        PRINTLC [23] 본디지(25만원)
    SIF LOCAL:2 & 16
        PRINTLC [24] 라비아 피어스(15만원)
    SIF LOCAL:2 & 32
        PRINTLC [25] 니플 피어스(20만원)
ENDIF
;-----------------------------------------------
SIF FLAG:커맨드확장 & 8192 && (FLAG:보유한동물 & 1) == 0
    PRINTLC [40] 동물 우리(10만원)
IF FLAG:커맨드확장 & 8192 && FLAG:보유한동물 & 1
    SIF (FLAG:보유한동물 & 2) == 0
        PRINTLC [41] 개(5만원)
    SIF (FLAG:보유한동물 & 4) == 0
        PRINTLC [42] 돼지(20만원)
    SIF (FLAG:보유한동물 & 8) == 0
        PRINTLC [43] 말(40만원)
ENDIF
;-----------------------------------------------
SIF (FLAG:정원개조 & 1) == 0
    PRINTLC [60] 정원(40만원)
IF FLAG:정원개조 & 1
    SIF (FLAG:정원개조 & 2) == 0
        PRINTLC [61] 돌장식(1만원)
    SIF (FLAG:정원개조 & 4) == 0
        PRINTLC [62] 대나무 물받이(1만원)
    SIF (FLAG:정원개조 & 8) == 0
        PRINTLC [63] 다실(15만원)
    SIF (FLAG:정원개조 & 16) == 0
        PRINTLC [64] 연못(20만원)
    SIF (FLAG:정원개조 & 32) == 0 && FLAG:정원개조 & 16
        PRINTLC [65] 물고기(1만원)
    SIF (FLAG:정원개조 & 64) == 0
        PRINTLC [66] 분수(10만원)
    SIF (FLAG:정원개조 & 128) == 0
        PRINTLC [67] 연못장식(10만원)
    SIF (FLAG:정원개조 & 256) == 0 && FLAG:정원개조 & 128
        PRINTLC [68] 나무(2만원)
ENDIF
;-----------------------------------------------
SIF (FLAG:허브농원 & 1) == 0
    PRINTLC [80] 허브농장(50만원)
IF FLAG:허브농원 & 1
    SIF (FLAG:허브농원 & 2) == 0
        PRINTLC [81] 허브개발시설(40만원)
    SIF (FLAG:허브농원 & 4) == 0
        PRINTLC [82] 허브생산시설(30만원)
ENDIF
;-----------------------------------------------
;[プールパッチ]FLAF:89をお借りします
SIF (FLAG:89 & 1) == 0
    PRINTLC [90] 풀장(40만원)
IF FLAG:89 & 1
    SIF (FLAG:89 & 2) == 0
        PRINTLC [91] 워터슬라이더(30만원)
    SIF (FLAG:89 & 4) == 0
        PRINTLC [92] 다이빙대(30만원)
ENDIF
;-----------------------------------------------
IF FLAG:자택증축 < 20
    PRINTLC [98] 토지(5000원)
ELSEIF GETBIT(FLAG:리부트모드설정,2) && FLAG:자택 >= 2
    PRINTLC [98] 토지(5만원)
ENDIF
IF FLAG:자택 == 0
    PRINTLC [99] 호화저택(10만원)
ELSEIF FLAG:자택 == 1
    PRINTLC [99] 작은 성(450만원)
ELSEIF FLAG:자택 == 2
    PRINTLC [99] 숨겨진 마을(700만원)
ENDIF
SIF LINEISEMPTY()
    PRINT 살 게 없습니다.
PRINTL 
PRINTL [100] 돌아가기
DRAWLINE

$INPUT_LOOP
INPUT
IF RESULT == 100
    RETURN 0
ELSEIF RESULT == 0 && (FLAG:지하실개조 & 1) == 0 && FLAG:커맨드확장 & 16 && (TALENT:MASTER:81 || TALENT:MASTER:158 || TALENT:MASTER:177)
    LOCAL:1 = 500000
ELSEIF RESULT == 1 && (FLAG:지하실개조 & 2) == 0 && FLAG:커맨드확장 & 16 && FLAG:지하실개조 & 1
    LOCAL:1 = 10000
ELSEIF RESULT == 2 && (FLAG:지하실개조 & 4) == 0 && FLAG:커맨드확장 & 16 && FLAG:지하실개조 & 1
    LOCAL:1 = 2666
ELSEIF RESULT == 3 && (FLAG:지하실개조 & 8) == 0 && FLAG:커맨드확장 & 16 && FLAG:지하실개조 & 1
    LOCAL:1 = 6000
ELSEIF RESULT == 4 && (FLAG:지하실개조 & 16) == 0 && FLAG:커맨드확장 & 16 && FLAG:지하실개조 & 1
    LOCAL:1 = 10000
ELSEIF RESULT == 5 && (FLAG:지하실개조 & 32) == 0 && FLAG:커맨드확장 & 16 && FLAG:지하실개조 & 1
    LOCAL:1 = 80000

ELSEIF RESULT == 20 && FLAG:커맨드확장 & 16 && FLAG:지하실개조 & 1 && LOCAL:2 & 1
    LOCAL:1 = 100000
ELSEIF RESULT == 21 && FLAG:커맨드확장 & 16 && FLAG:지하실개조 & 1 && LOCAL:2 & 2
    LOCAL:1 = 250000
ELSEIF RESULT == 22 && FLAG:커맨드확장 & 16 && FLAG:지하실개조 & 1 && LOCAL:2 & 4
    LOCAL:1 = 300000
ELSEIF RESULT == 23 && FLAG:커맨드확장 & 16 && FLAG:지하실개조 & 1 && LOCAL:2 & 8
    LOCAL:1 = 250000
ELSEIF RESULT == 24 && FLAG:커맨드확장 & 16 && FLAG:지하실개조 & 1 && LOCAL:2 & 16
    LOCAL:1 = 150000
ELSEIF RESULT == 25 && FLAG:커맨드확장 & 16 && FLAG:지하실개조 & 1 && LOCAL:2 & 32
    LOCAL:1 = 200000

ELSEIF RESULT == 40 && FLAG:커맨드확장 & 8192 && (FLAG:보유한동물 & 1) == 0
    LOCAL:1 = 100000
ELSEIF RESULT == 41 && FLAG:커맨드확장 & 8192 && FLAG:보유한동물 & 1 && (FLAG:보유한동물 & 2) == 0
    LOCAL:1 = 50000
ELSEIF RESULT == 42 && FLAG:커맨드확장 & 8192 && FLAG:보유한동물 & 1 && (FLAG:보유한동물 & 4) == 0
    LOCAL:1 = 200000
ELSEIF RESULT == 43 && FLAG:커맨드확장 & 8192 && FLAG:보유한동물 & 1 && (FLAG:보유한동물 & 8) == 0
    LOCAL:1 = 400000

ELSEIF RESULT == 60 && (FLAG:정원개조 & 1) == 0
    LOCAL:1 = 400000
ELSEIF RESULT == 61 && FLAG:정원개조 & 1 && (FLAG:정원개조 & 2) == 0
    LOCAL:1 = 10000
ELSEIF RESULT == 62 && FLAG:정원개조 & 1 && (FLAG:정원개조 & 4) == 0
    LOCAL:1 = 10000
ELSEIF RESULT == 63 && FLAG:정원개조 & 1 && (FLAG:정원개조 & 8) == 0
    LOCAL:1 = 150000
ELSEIF RESULT == 64 && FLAG:정원개조 & 1 && (FLAG:정원개조 & 16) == 0
    LOCAL:1 = 200000
ELSEIF RESULT == 65 && FLAG:정원개조 & 1 && FLAG:정원개조 & 16 && (FLAG:정원개조 & 32) == 0
    LOCAL:1 = 10000
ELSEIF RESULT == 66 && FLAG:정원개조 & 1 && (FLAG:정원개조 & 64) == 0
    LOCAL:1 = 100000
ELSEIF RESULT == 67 && FLAG:정원개조 & 1 && (FLAG:정원개조 & 128) == 0
    LOCAL:1 = 100000
ELSEIF RESULT == 68 && FLAG:정원개조 & 1 && FLAG:정원개조 & 128 && (FLAG:정원개조 & 256) == 0
    LOCAL:1 = 20000

ELSEIF RESULT == 80 && (FLAG:허브농원 & 1) == 0
    LOCAL:1 = 500000
ELSEIF RESULT == 81 && FLAG:허브농원 & 1 && (FLAG:허브농원 & 2) == 0
    LOCAL:1 = 400000
ELSEIF RESULT == 82 && FLAG:허브농원 & 1 && (FLAG:허브농원 & 4) == 0
    LOCAL:1 = 300000

;[プールパッチ]
ELSEIF RESULT == 90 && (FLAG:89 & 1) == 0
    LOCAL:1 = 400000
ELSEIF RESULT == 91 && FLAG:89 & 1 && (FLAG:89 & 2) == 0
    LOCAL:1 = 300000
ELSEIF RESULT == 92 && FLAG:89 & 1 && (FLAG:89 & 4) == 0
    LOCAL:1 = 300000

ELSEIF RESULT == 98 
    IF FLAG:자택증축 < 20
        LOCAL:1 = 5000
    ELSEIF GETBIT(FLAG:리부트모드설정,2) && FLAG:자택 >= 2
        LOCAL:1 = 50000
    ELSE
        GOTO INPUT_LOOP
    ENDIF
ELSEIF RESULT == 99 && FLAG:자택 == 0
    LOCAL:1 = 100000
ELSEIF RESULT == 99 && FLAG:자택 == 1
    LOCAL:1 = 4500000
ELSEIF RESULT == 99 && FLAG:자택 == 2
    LOCAL:1 = 7000000
ELSE
    GOTO INPUT_LOOP
ENDIF
LOCAL = RESULT
IF MONEY < LOCAL:1
    PRINTW 자금이 부족합니다.
    DRAWLINE
    RESTART
ENDIF

MONEY -= LOCAL:1
PRINT ＜
IF LOCAL == 0
    PRINT 비밀 지하실
    FLAG:지하실개조 |= 1
ELSEIF LOCAL == 1
    PRINT 삼각목마
    FLAG:지하실개조 |= 2
ELSEIF LOCAL == 2
    PRINT 말뚝
    FLAG:지하실개조 |= 4
ELSEIF LOCAL == 3
    PRINT 나이프
    FLAG:지하실개조 |= 8
ELSEIF LOCAL == 4
    PRINT 나타
    FLAG:지하실개조 |= 16
ELSEIF LOCAL == 5
    PRINT 아이언메이든
    FLAG:지하실개조 |= 32

ELSEIF LOCAL == 20
    PRINT 클리토리스 링
ELSEIF LOCAL == 21
    PRINT 노예 관리용 정조대
ELSEIF LOCAL == 22
    PRINT 벨리 쥬얼
ELSEIF LOCAL == 23
    PRINT 본디지
ELSEIF LOCAL == 24
    PRINT 라비아 피어스
ELSEIF LOCAL == 25
    PRINT 니플 피어스

ELSEIF LOCAL == 40
    PRINT 동물 우리
    FLAG:보유한동물 |= 1
ELSEIF LOCAL == 41
    PRINT 개
    FLAG:보유한동물 |= 2
ELSEIF LOCAL == 42
    PRINT 돼지
    FLAG:보유한동물 |= 4
ELSEIF LOCAL == 43
    PRINT 말
    FLAG:보유한동물 |= 8

ELSEIF LOCAL == 60
    PRINT 정원
    FLAG:정원개조 |= 1
ELSEIF LOCAL == 61
    PRINT 돌장식
    FLAG:정원개조 |= 2
ELSEIF LOCAL == 62
    PRINT 대나무 물받이
    FLAG:정원개조 |= 4
ELSEIF LOCAL == 63
    PRINT 다실
    FLAG:정원개조 |= 8
ELSEIF LOCAL == 64
    PRINT 연못
    FLAG:정원개조 |= 16
ELSEIF LOCAL == 65
    PRINT 물고기
    FLAG:정원개조 |= 32
ELSEIF LOCAL == 66
    PRINT 분수
    FLAG:정원개조 |= 64
ELSEIF LOCAL == 67
    PRINT 연못 장식
    FLAG:정원개조 |= 128
ELSEIF LOCAL == 68
    PRINT 나무
    FLAG:정원개조 |= 256

ELSEIF LOCAL == 80
    PRINT 허브농장
    FLAG:허브농원 |= 1
ELSEIF LOCAL == 81
    PRINT 허브 개발 시설
    FLAG:허브농원 |= 2
ELSEIF LOCAL == 82
    PRINT 허브 생산 시설
    FLAG:허브농원 |= 4

;[プールパッチ]
ELSEIF LOCAL == 90
    PRINT 풀장
    FLAG:89 |= 1
ELSEIF LOCAL == 91
    PRINT 워터슬라이더
    FLAG:89 |= 2
ELSEIF LOCAL == 92
    PRINT 다이빙대
    FLAG:89 |= 4

ELSEIF LOCAL == 98
    PRINTL 토지를 구입했습니다.>
    IF FLAG:자택증축 < 20
        FLAG:자택증축 += 1
    ELSEIF GETBIT(FLAG:리부트모드설정,2) && FLAG:자택 >= 2
        FLAG:94 += 3
    ENDIF
ELSEIF LOCAL == 99
    IF FLAG:자택 == 0
        PRINTL 호화저택을 건설했습니다.＞
        FLAG:92 = 20
    ELSEIF FLAG:자택 == 1
        PRINTL 집을 팔고 작은 성을 지었습니다＞
        FLAG:92 = 50
    ELSE
        PRINTL 성을 중심으로 숨겨진 마을을 건설했습니다＞
        FLAG:92 = 200
    ENDIF
    IF FLAG:자택증축
        MONEY += FLAG:자택증축 * 1000
        FLAG:자택증축 /= 2
    ENDIF
    FLAG:자택 += 1
ENDIF
IF LOCAL == 0
    PRINTW 을 개장했습니다＞
ELSEIF LOCAL >= 20 && LOCAL < 30
    PRINTW 을(를) 구입했습니다＞
    CALL BRAND_SLAVE_EQUIPMENT, LOCAL:1, LOCAL
ELSEIF LOCAL >= 41 && LOCAL < 50
    PRINTW 을(를) 구입해 동물 우리를 만들었습니다＞
ELSEIF LOCAL == 60 || LOCAL == 64 || LOCAL == 67
    PRINTW 을(를) 조성했습니다＞
ELSEIF LOCAL == 61 || LOCAL == 62 || LOCAL == 66
    PRINTW 을(를) 설치했습니다＞
ELSEIF LOCAL == 63 || LOCAL == 81 || LOCAL == 82
    PRINTW 을(를) 건설했습니다＞
ELSEIF LOCAL == 64
    PRINTW 를 구입해 연못에 뿌렸습니다＞
ELSEIF LOCAL == 68
    PRINTW 를 심었습니다＞
ELSEIF LOCAL == 80
    PRINTW 을 개장했습니다＞
ELSEIF LOCAL == 98
    PRINTW ＜노예구입 가능 숫자가 늘어났습니다＞
    SIF FLAG:자택증축 >= 20 && (!GETBIT(FLAG:리부트모드설정,2) || FLAG:자택 < 2)
        PRINTW ＜더 이상 토지를 구입할 수는 없습니다＞
ELSEIF LOCAL == 99
    ;주인이 家屋敷を購入したときの現在の調教対象のイベント口上
    SIF TARGET >= 0
        CALL SELF_KOJO, TARGET, 239
    PRINTW ＜노예구입 가능 숫자가 대폭으로 늘어났습니다＞
ELSE
    PRINTW 을(를) 구입했습니다＞
ENDIF
DRAWLINE
RESTART

;=============================================================================
;기능계 아이템 구입 처리
;=============================================================================
@BUY_SKILL
;価格計算のためのローカル変数をリセット
VARSET LOCAL, 0
DRAWLINE
PRINTFORML %CALLNAME:MASTER%에게 스킬을 부가시킬 수 있습니다.
PRINTL 무엇을 구입하겠습니까?
PRINTFORML 소지금:{MONEY}원
DRAWLINE
;주인의 現在の기교 Lvが5未満なら기교 Lvを習得できる
IF ABL:MASTER:기교 < 5
    LOCAL:10 = BUY_SKILL_MASTER_TECH_PRICE()
    PRINTFORMLC [10] 【기교 Lv{ABL:MASTER:기교 + 1}】(%COM_NAME%원)
ENDIF
;주인의 현재 요리기능 Lv이 5 미만이고 도시락 · 라이브 ON이라면 요리기능 Lv을 습득할 수 있을지도 모른다
IF ABL:MASTER:요리기능 < 5 && FLAG:SHOP이벤트확장 & 16
    LOCAL:11 = BUY_SKILL_MASTER_COOKING_PRICE()
    SIF LOCAL:11 > 0
        PRINTFORMLC [11] 【요리기능 Lv{ABL:MASTER:요리기능 + 1}】(%COM_NAME%원)
ELSEIF ABL:MASTER:요리기능 >= 5 && TALENT:MASTER:193 == 0 && TALENT:MASTER:194 == 0 && FLAG:SHOP이벤트확장 & 16
    LOCAL:11 = BUY_SKILL_MASTER_COOK_PRICE()
    SIF LOCAL:11 > 0
        PRINTFORMLC [11] 【요리잘함】({LOCAL:11/10000}만원)
ELSEIF ABL:MASTER:요리기능 >= 5 && TALENT:MASTER:193 && TALENT:MASTER:194 == 0 && FLAG:SHOP이벤트확장 & 16
    LOCAL:11 = BUY_SKILL_MASTER_CHEF_PRICE()
    SIF LOCAL:11 > 0
        PRINTFORMLC [11] 【조리명인】({LOCAL:11/10000}만원)
ENDIF
;주인의 現在の촬영기능 Lvが5未満でカメラかビデオカメラかアイテムなし設定で撮影コマンド拡張ＯＮなら촬영기능 Lvを習得できるかもしれない
IF ABL:MASTER:촬영기능 < 5 && (ITEM:50 || ITEM:51 || NOITEM) && FLAG:커맨드확장 & 128
    LOCAL:12 = BUY_SKILL_MASTER_CAMERA_PRICE()
    SIF LOCAL:12 > 0
        PRINTFORMLC [12] 【촬영기능 Lv{ABL:MASTER:촬영기능 + 1}】(%COM_NAME%원)
ELSEIF ABL:MASTER:촬영기능 >= 5 && TALENT:MASTER:196 == 0 && (ITEM:50 || ITEM:51 || NOITEM) && FLAG:커맨드확장 & 128
    LOCAL:12 = BUY_SKILL_MASTER_REPORTER_PRICE()
    SIF LOCAL:12 > 0
        PRINTFORMLC [12] 【보도자】({LOCAL:12/10000}만원)
ENDIF
;주인의 現在の歌唱技能Lvが5未満でマイクかアイテムなし設定で弁当・ライブＯＮなら歌唱技能Lvを習得できるかもしれない
IF ABL:MASTER:가창기능 < 5 && (ITEM:52 || NOITEM) && FLAG:SHOP이벤트확장 & 16
    LOCAL:13 = BUY_SKILL_MASTER_SINGING_PRICE()
    SIF LOCAL:13 > 0
        PRINTFORMLC [13] 【가창기능 Lv{ABL:MASTER:가창기능 + 1}】(%COM_NAME%원)
ELSEIF ABL:MASTER:가창기능 >= 5 && TALENT:MASTER:191 == 0 && (ITEM:52 || NOITEM) && FLAG:SHOP이벤트확장 & 16
    LOCAL:13 = BUY_SKILL_MASTER_SINGER_PRICE()
    SIF LOCAL:13 > 0
        PRINTFORMLC [13] 【음감】({LOCAL:13/10000}만원)
ENDIF
;주인의 現在の調教師Lvが5未満なら調教師Lvを習得できるかもしれない
IF ABL:MASTER:조교사Lv < 5
    LOCAL:14 = BUY_SKILL_MASTER_TRAINER_PRICE()
    SIF LOCAL:14 > 0
        PRINTFORMLC [14] 【조교사 Lv{ABL:MASTER:조교사Lv + 1}】(%COM_NAME%원)
ENDIF
;주인이 [サド]でないなら[サド]を習得できる
IF TALENT:MASTER:81 == 0
    LOCAL:20 = BUY_SKILL_MASTER_SADIST_PRICE()
    PRINTFORMLC [20] 【새드】({LOCAL:20/10000}만원)
ENDIF
;주인이 [마조]でないなら[마조]を習得できる
IF TALENT:MASTER:82 == 0
    LOCAL:21 = BUY_SKILL_MASTER_MASOIST_PRICE()
    PRINTFORMLC [21] 【마조】({LOCAL:21/10000}만원)
ENDIF
;주인이 [汚れ無視]でないなら[汚れ無視]を習得できる
IF TALENT:MASTER:64 == 0
    LOCAL:22 = BUY_SKILL_DISREGARDSTAIN_PRICE()
    PRINTFORMLC [22] 【불결무시】({LOCAL:22/10000}만원)
ENDIF
;주인이 [オトコ]か[ふたなり]であり, [早漏]でないなら[早漏]を習得できる
SIF TALENT:MASTER:132 == 0 && (TALENT:MASTER:120 ||TALENT:MASTER:121)
    PRINTFORMLC [23] \@(TALENT:MASTER:133)?【지루】 소거(1만원) # 【조루】(1만원)\@
;주인이 [オトコ]か[ふたなり]であり, [遅漏]でないなら[遅漏]を習得できる
SIF TALENT:MASTER:133 == 0 && (TALENT:MASTER:120 ||TALENT:MASTER:121)
    PRINTFORMLC [24] \@(TALENT:MASTER:132)?【조루】 소거(1만원) # 【지루】(1만원)\@
;주인이 [촉수사역술]を持っていないなら, [촉수사역술]を習得できる
SIF TALENT:MASTER:149 == 0
    PRINTLC [25] 【촉수사역술】(1만원)
;주인이 [금단의 지식]を持っていないなら, [금단의 지식]を習得できる
IF TALENT:MASTER:금단의지식 == 0
    LOCAL:26 = BUY_SKILL_MASTER_FORBIDDEN_PRICE()
    PRINTFORMLC [26] 【금단의 지식】({(LOCAL:26)/10000}만원)
ENDIF
;CHALLENGE나 EXTRA모드고 주인의 조교사 레벨이 5 이상에다 [혀놀림]でないなら[혀놀림]を習得できる
IF (FLAG:게임모드 == 2 || FLAG:게임모드 >= 50 ) && ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:혀놀림 == 0
    LOCAL:30 = BUY_SKILL_MASTER_EXTALENT52_PRICE()
    PRINTFORMLC [30] 【혀놀림】({LOCAL:30/10000}만원)
ENDIF
;CHALLENGE나 EXTRA모드고 주인의 조교사 레벨이 5 이상에다 [소악마]でないなら[소악마]を習得できる
IF (FLAG:게임모드 == 2 || FLAG:게임모드 >= 50 ) && ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:93 == 0
    LOCAL:31 = BUY_SKILL_MASTER_EXTALENT93_PRICE()
    PRINTFORMLC [31] 【소악마】({LOCAL:31/10000}만원)
ENDIF
;CHALLENGE나 EXTRA모드고 주인의 조교사 레벨이 5 이상에다 [매혹]でないなら[매혹]を習得できる
IF (FLAG:게임모드 == 2 || FLAG:게임모드 >= 50 ) && ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:91 == 0
    LOCAL:32 = BUY_SKILL_MASTER_EXTALENT91_PRICE()
    PRINTFORMLC [32] 【매혹】({LOCAL:32/10000}만원)
ENDIF
;CHALLENGE나 EXTRA모드고 주인의 조교사 레벨이 5 이상에다 [바느질]でないなら[바느질]を習得できる
IF (FLAG:게임모드 == 2 || FLAG:게임모드 >= 50 ) && ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:바느질 == 0
    LOCAL:33 = BUY_SKILL_MASTER_EXTALENT53_PRICE()
    PRINTFORMLC [33] 【바느질】({LOCAL:33/10000}만원)
ENDIF
;CHALLENGE나 EXTRA모드고 주인의 조교사 레벨이 5 이상에다 [절벽가슴격추왕][대유도]でないなら[절벽가슴격추왕]を習得できる
IF (FLAG:게임모드 == 2 || FLAG:게임모드 >= 50 ) && ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:96 == 0 && TALENT:MASTER:97 == 0
    LOCAL:34 = BUY_SKILL_MASTER_EXTALENT97_PRICE()
    PRINTFORMLC [34] 【절벽가슴격추왕】({LOCAL:34/10000}만원)
ENDIF
;CHALLENGE나 EXTRA모드고 주인의 조교사 레벨이 5 이상에다 [절벽가슴격추왕][대유도]でないなら[대유도]を習得できる
IF (FLAG:게임모드 == 2 || FLAG:게임모드 >= 50 ) && ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:96 == 0 && TALENT:MASTER:97 == 0
    LOCAL:35 = BUY_SKILL_MASTER_EXTALENT96_PRICE()
    PRINTFORMLC [35] 【대유도】({LOCAL:35/10000}만원)
ENDIF
;주인의 조교사 레벨이 5 이상에다 [淫具知識]でないなら[淫具知識]を習得できる
IF ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:음구지식 == 0
    LOCAL:36 = BUY_SKILL_MASTER_EXTALENT58_PRICE()
    PRINTFORMLC [36] 【음구지식】({LOCAL:36/10000}만원)
ENDIF
;주인의 조교사 레벨이 5 이상에다 [教育者]でないなら[教育者]を習得できる
IF ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:126 == 0
    LOCAL:37 = BUY_SKILL_MASTER_EXTALENT126_PRICE()
    PRINTFORMLC [37] 【교육자】({LOCAL:37/10000}만원)
ENDIF
;주인의 조교사 레벨이 5 이상에다 [家電使役知識]でないなら[家電使役知識]を習得できる
IF ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:189 == 0
    LOCAL:38 = BUY_SKILL_MASTER_EXTALENT189_PRICE()
    PRINTFORMLC [38] 【가전제품지식】({LOCAL:38/10000}만원)
ENDIF
;주인의 조교사 레벨이 5 이상에다 [공작명인]でないなら[공작명인]を習得できる
IF ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:195 == 0
    LOCAL:39 = BUY_SKILL_MASTER_EXTALENT197_PRICE()
    PRINTFORMLC [39] 【공작명인】({LOCAL:39/10000}만원)
ENDIF
;주인의 조교사 레벨이 5 이상에다 [마술기능]でないなら[마술기능]を習得できる
IF ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:197 == 0
    LOCAL:40 = BUY_SKILL_MASTER_EXTALENT197_PRICE()
    PRINTFORMLC [40] 【마술기능】({LOCAL:40/10000}만원)
ENDIF
;주인의 조교사 레벨이 5 이상에다 [超常目算]でないなら[超常目算]を習得できる
IF ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:198 == 0
    LOCAL:41 = BUY_SKILL_MASTER_EXTALENT198_PRICE()
    PRINTFORMLC [41] 【초상계산】({LOCAL:41/10000}만원)
ENDIF
IF TALENT:MASTER:998 == 0
    LOCAL:42 = BUY_SKILL_MASTER_EXTALENT998_PRICE()
    PRINTFORMLC [42] 【Debug】({LOCAL:42/10000}만원)
ENDIF
IF TALENT:MASTER:998 == 1
    LOCAL:42 = BUY_SKILL_MASTER_EXTALENT998_PRICE()
    PRINTFORMLC [42] 【Debug】 폐기({LOCAL:42/10000}만원)
ENDIF
;SIF TALENT:MASTER: == 0
;    PRINT [27]【】(원) 
SIF LINEISEMPTY()
    PRINT 구입할 게 없습니다.
PRINTL 
PRINTL [100] - 돌아가기
DRAWLINE

$INPUT_LOOP_SELECT_SKILL
INPUT
IF RESULT == 100
    RETURN 0
ELSEIF RESULT == 10 && ABL:MASTER:기교 < 5
    PRINTFORML 【기교 Lv{ABL:MASTER:기교 + 1}】：%CALLNAME:MASTER%의 기교 Lv이 하나 상승합니다.
    LOCAL:1 = LOCAL:10
ELSEIF RESULT == 11 && ABL:MASTER:요리기능 < 5 && LOCAL:11 > 0 && FLAG:SHOP이벤트확장 & 16
    PRINTFORML 【요리기능 Lv{ABL:MASTER:요리기능 + 1}】：%CALLNAME:MASTER%의 요리기능 Lv이 하나 상승합니다.
    LOCAL:1 = LOCAL:11
ELSEIF RESULT == 11 && ABL:MASTER:요리기능 >= 5 && TALENT:MASTER:193 == 0 && TALENT:MASTER:194 == 0 && LOCAL:11 > 0 && FLAG:SHOP이벤트확장 & 16
    PRINTFORML 【요리잘함】：%CALLNAME:MASTER%의 요리 실력이 능숙해집니다.
    LOCAL:1 = LOCAL:11
ELSEIF RESULT == 11 && ABL:MASTER:요리기능 >= 5 && TALENT:MASTER:193 && TALENT:MASTER:194 == 0 && LOCAL:11 > 0 && FLAG:SHOP이벤트확장 & 16
    PRINTFORML 【조리명인】：%CALLNAME:MASTER%의 요리 실력이 한층 더 능숙해집니다.
    LOCAL:1 = LOCAL:11
ELSEIF RESULT == 12 && ABL:MASTER:촬영기능 < 5 && LOCAL:12 > 0 && (ITEM:50 || ITEM:51 || NOITEM) && FLAG:커맨드확장 & 128
    PRINTFORML 【촬영기능 Lv{ABL:MASTER:촬영기능 + 1}】：%CALLNAME:MASTER%의 촬영기능 Lv이 하나 상승합니다.
    LOCAL:1 = LOCAL:12
ELSEIF RESULT == 12 && ABL:MASTER:촬영기능 >= 5 && TALENT:MASTER:196 == 0 && LOCAL:12 > 0 && (ITEM:50 || ITEM:51 || NOITEM) && FLAG:커맨드확장 & 128
    PRINTFORML 【보도자】：%CALLNAME:MASTER%의 촬영 실력이 능숙해집니다.
    LOCAL:1 = LOCAL:12
ELSEIF RESULT == 13 && ABL:MASTER:가창기능 < 5 && LOCAL:13 > 0 && (ITEM:52 || NOITEM) && FLAG:SHOP이벤트확장 & 16
    PRINTFORML 【가창기능 Lv{ABL:MASTER:가창기능 + 1}】：%CALLNAME:MASTER%의 가창기능 Lv이 하나 상승합니다.
    LOCAL:1 = LOCAL:13
ELSEIF RESULT == 13 && ABL:MASTER:가창기능 >= 5 && TALENT:MASTER:191 == 0 && LOCAL:13 > 0 && (ITEM:52 || NOITEM) && FLAG:SHOP이벤트확장 & 16
    PRINTFORML 【음감】：%CALLNAME:MASTER%의 노래 실력이 능숙해집니다.
    LOCAL:1 = LOCAL:13
ELSEIF RESULT == 14 && ABL:MASTER:조교사Lv < 5 && LOCAL:14 > 0
    PRINTFORML 【조교사Lv{ABL:MASTER:조교사Lv + 1}】：%CALLNAME:MASTER%의 조교사 LV이 하나 상승합니다.
    LOCAL:1 = LOCAL:14

ELSEIF RESULT == 20 && TALENT:MASTER:81 == 0
    PRINTFORML 【사도】：조교중, %조사처리(CALLNAME:MASTER,"가")% 하는 SM계 조교 커맨드의 효과가 상승합니다.
    LOCAL:1 = LOCAL:20
ELSEIF RESULT == 21 && TALENT:MASTER:82 == 0
    PRINTFORML 【마조】：조교중, 노예의 「선도」 파라미터 상승이 쉬워집니다.
    LOCAL:1 = LOCAL:21
ELSEIF RESULT == 22 && TALENT:MASTER:64 == 0
    PRINTFORML 【불결무시】：조교중, %조사처리(CALLNAME:MASTER,"가")% 불결함을 신경쓰지 않게 됩니다.
    LOCAL:1 = LOCAL:22
ELSEIF RESULT == 23 && TALENT:MASTER:132 == 0 && (TALENT:MASTER:120 ||TALENT:MASTER:121)
    PRINTFORML \@(TALENT:MASTER:133) ? 【지루】소거 # 【조루】\@：조교중, %CALLNAME:MASTER%의 사정이 빨라집니다.
    LOCAL:1 = 10000
ELSEIF RESULT == 24 && TALENT:MASTER:133 == 0 && (TALENT:MASTER:120 ||TALENT:MASTER:121)
    PRINTFORML \@(TALENT:MASTER:132) ? 【조루】소거 # 【지루】\@：조교중, %CALLNAME:MASTER%의 사정이 느려집니다.
    LOCAL:1 = 10000
ELSEIF RESULT == 25 && TALENT:MASTER:149 == 0
    PRINTFORML 【촉수사역술】：조교중, 촉수를 소환할 수 있게 됩니다.
    LOCAL:1 = 10000
ELSEIF RESULT == 26 && TALENT:MASTER:금단의지식 == 0
    PRINTFORML 【금단의 지식】：금기라 일컬어지는 다수의 두려운 지식을 익히게 됩니다.
    LOCAL:1 = LOCAL:26

ELSEIF RESULT == 30 && (FLAG:게임모드 == 2 || FLAG:게임모드 >= 50) && ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:혀놀림 == 0
    PRINTFORML 【혀놀림】：조교시 %CALLNAME:MASTER%의 혀놀림이 유연해집니다.
    LOCAL:1 = LOCAL:30
ELSEIF RESULT == 31 && (FLAG:게임모드 == 2 || FLAG:게임모드 >= 50) && ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:93 == 0
    PRINTFORML 【소악마】：%조사처리(CALLNAME:MASTER,"가")% 조교시 명령을 잘 듣습니다.
    LOCAL:1 = LOCAL:31
ELSEIF RESULT == 32 && (FLAG:게임모드 == 2 || FLAG:게임모드 >= 50) && ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:91 == 0
    PRINTFORML 【매혹】：%조사처리(CALLNAME:MASTER,"가")% 조교시 명령을 잘 듣습니다.
    LOCAL:1 = LOCAL:32
ELSEIF RESULT == 33 && (FLAG:게임모드 == 2 || FLAG:게임모드 >= 50) && ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:바느질 == 0
    PRINTFORML 【바느질】：조교시 %CALLNAME:MASTER%의 바느질이 잘 됩니다.
    LOCAL:1 = LOCAL:33
ELSEIF RESULT == 34 && (FLAG:게임모드 == 2 || FLAG:게임모드 >= 50) && ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:96 == 0 && TALENT:MASTER:97 == 0
    PRINTFORML 【절벽가슴격추왕】：%조사처리(CALLNAME:MASTER,"가")% 절벽 캐릭터를 잘 조교합니다.
    LOCAL:1 = LOCAL:34
ELSEIF RESULT == 35 && (FLAG:게임모드 == 2 || FLAG:게임모드 >= 50) && ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:96 == 0 && TALENT:MASTER:97 == 0
    PRINTFORML 【대유도】：%조사처리(CALLNAME:MASTER,"가")% 거유 캐릭터를 잘 조교합니다.
    LOCAL:1 = LOCAL:35
ELSEIF RESULT == 36 && ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:음구지식 == 0
    PRINTFORML 【음구지식】：%조사처리(CALLNAME:MASTER,"가")% 도구를 이용한 조교시 명령을 잘 듣습니다.
    LOCAL:1 = LOCAL:36
ELSEIF RESULT == 37 && ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:126 == 0
    PRINTFORML 【교육자】：%조사처리(CALLNAME:MASTER,"가")% 공방작업을 할 수 있습니다.
    LOCAL:1 = LOCAL:37
ELSEIF RESULT == 38 && ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:189 == 0
    PRINTFORML 【가전제품지식】：%조사처리(CALLNAME:MASTER,"가")% 가전제품을 잘 이용하게 됩니다.
    LOCAL:1 = LOCAL:38
ELSEIF RESULT == 39 && ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:195 == 0
    PRINTFORML 【공작명인】：%조사처리(CALLNAME:MASTER,"가")% 특수한 공방작업을 할 수 있습니다.
    LOCAL:1 = LOCAL:39
ELSEIF RESULT == 40 && ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:197 == 0
    PRINTFORML 【마술기능】：%조사처리(CALLNAME:MASTER,"가")% 마법을 사용한 의식을 할 수 있게 됩니다.
    LOCAL:1 = LOCAL:40
ELSEIF RESULT == 41 && ABL:MASTER:조교사Lv >= 5 && TALENT:MASTER:198 == 0
    PRINTFORML 【초상계산】：%조사처리(CALLNAME:MASTER,"가")% 어림짐작으로 값 추측이 됩니다.
    LOCAL:1 = LOCAL:41
ELSEIF RESULT == 42 && TALENT:MASTER:998 == 0
    PRINTFORML 【Debug】：%조사처리(CALLNAME:MASTER,"가")% Debug를 습득합니다. 하라는 버그는 안 잡고!
    LOCAL:1 = LOCAL:42
ELSEIF RESULT == 42 && TALENT:MASTER:998 == 1
    PRINTFORML 【Debug】：%조사처리(CALLNAME:MASTER,"가")% Debug를 버립니다. 하라는 버그는 안 잡고!
    LOCAL:1 = LOCAL:42
;ELSEIF RESULT == 27
;    LOCAL:1 = 0
ELSE
    GOTO INPUT_LOOP_SELECT_SKILL
ENDIF
LOCAL = RESULT

PRINTL 구입하시겠습니까?
PRINTL [0] 예
PRINTL [1] 아니오

$INPUT_LOOP_BUY_SKILL_YN
INPUT
IF RESULT == 0 && MONEY < LOCAL:1
    PRINTW 자금이 부족합니다.
    RESTART
ELSEIF RESULT == 0
    MONEY -= LOCAL:1
ELSEIF RESULT == 1
    RETURN 0
ELSE
    GOTO INPUT_LOOP_BUY_SKILL_YN
ENDIF

;能力アップアイテム【기교 Lv】(주인전용)
IF LOCAL == 10
    ABL:MASTER:기교 += 1
    PRINTFORMW ＜%CALLNAME:MASTER%의 기교 레벨이 1 올라갔습니다.　현재 기교 레벨：{ABL:MASTER:기교}＞
;能力アップアイテム【요리능력Lv】(주인전용)
ELSEIF LOCAL == 11 && ABL:MASTER:요리기능 < 5 
    ABL:MASTER:요리기능 += 1
    PRINTFORMW ＜%CALLNAME:MASTER%의 요리기능 레벨이 1 올라갔습니다.　현재 요리기능 레벨:{ABL:MASTER:요리기능}＞
;소질 아이템 【요리잘함】
ELSEIF LOCAL == 11 && ABL:MASTER:요리기능 >= 5 && TALENT:MASTER:193 == 0 && TALENT:MASTER:194 == 0
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [요리잘함]을 배웠다＞
    TALENT:MASTER:192 = 0
    TALENT:MASTER:193 = 1
;소질 아이템 【요리잘함】
ELSEIF LOCAL == 11 && ABL:MASTER:요리기능 >= 5 && TALENT:MASTER:193 && TALENT:MASTER:194 == 0
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [조리명인]이 되었다＞
    TALENT:MASTER:193 = 0
    TALENT:MASTER:194 = 1
;能力アップアイテム【촬영능력Lv】(주인전용)
ELSEIF LOCAL == 12 && ABL:MASTER:촬영기능 < 5 
    ABL:MASTER:촬영기능 += 1
    PRINTFORMW ＜%CALLNAME:MASTER%의 촬영기능 레벨이 1 올라갔습니다.　현재 촬영기능 레벨:{ABL:MASTER:촬영기능}＞
;소질 아이템 【보도자】
ELSEIF LOCAL == 12 && ABL:MASTER:촬영기능 >= 5 && TALENT:MASTER:196 == 0
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [보도자]가 되었다＞
    TALENT:MASTER:196 = 1
;能力アップアイテム【가창능력Lv】(주인전용)
ELSEIF LOCAL == 13 && ABL:MASTER:가창기능 < 5 
    ABL:MASTER:가창기능 += 1
    PRINTFORMW ＜%CALLNAME:MASTER%의 가창기능 레벨이 1 올라갔습니다.　현재 가창기능 레벨:{ABL:MASTER:가창기능}＞
;소질 아이템 【음감】
ELSEIF LOCAL == 13 && ABL:MASTER:가창기능 >= 5 && TALENT:MASTER:191 == 0
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [음감]을 배웠다＞
    TALENT:MASTER:190 = 0
    TALENT:MASTER:191 = 1
;能力アップアイテム【조교사Lv】(주인전용)
ELSEIF LOCAL == 14
    ABL:MASTER:조교사Lv += 1
    PRINTFORMW ＜%CALLNAME:MASTER%의 조교사 레벨이 1 올라갔습니다.　현재 조교사 레벨:{ABL:MASTER:조교사Lv}＞

;소질 아이템 【サド】
ELSEIF LOCAL == 20
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [새드]를 배웠다＞
    TALENT:MASTER:81 = 1
;소질 아이템 【마조】
ELSEIF LOCAL == 21
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [마조]를 배웠다＞
    TALENT:MASTER:82 = 1
;소질 아이템 【汚れ無視】
ELSEIF LOCAL == 22
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [불결무시]를 배웠다＞
    TALENT:MASTER:64 = 1
;소질 아이템 【早漏】
ELSEIF LOCAL == 23
    ;もし[遅漏]があればそれが消えるだけで[早漏]は習得できない
    IF TALENT:MASTER:133
        PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [지루]를 잃었다＞
        TALENT:MASTER:133 = 0
    ELSE
        PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [조루]를 얻었다＞
        TALENT:MASTER:132 = 1
    ENDIF
    MAXBASE:MASTER:사정 = CALC_GAUGE2_COMMON(MASTER)
;소질 아이템 【遅漏】
ELSEIF LOCAL == 24
    ;もし[早漏]があればそれが消えるだけで[遅漏]は習得できない
    IF TALENT:MASTER:132
        PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [조루]를 잃었다＞
        TALENT:MASTER:132 = 0
    ELSE
        PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [지루]를 얻었다＞
        TALENT:MASTER:133 = 1
    ENDIF
    MAXBASE:MASTER:사정 = CALC_GAUGE2_COMMON(MASTER)
;소질 아이템 【촉수사역술】
ELSEIF LOCAL == 25
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [촉수사역술]을 배웠다＞
    TALENT:MASTER:149 = 1
;소질 아이템 【금단의 지식】
ELSEIF LOCAL == 26
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [금단의 지식]을 배웠다＞
    TALENT:MASTER:금단의지식 = 1
;소질 아이템 【혀놀림】
ELSEIF LOCAL == 30
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [혀놀림]을 배웠다＞
    TALENT:MASTER:혀놀림 = 1
;소질 아이템 【소악마】
ELSEIF LOCAL == 31
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [소악마]를 배웠다＞
    TALENT:MASTER:93 = 1
;소질 아이템 【매혹】
ELSEIF LOCAL == 32
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [매혹]을 배웠다＞
    TALENT:MASTER:91 = 1
;소질 아이템 【바느질】
ELSEIF LOCAL == 33
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [바느질]을 배웠다＞
    TALENT:MASTER:바느질 = 1
;소질 아이템 【절벽가슴격추왕】
ELSEIF LOCAL == 34
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [절벽가슴격추왕]을 배웠다＞
    TALENT:MASTER:97 = 1
;소질 아이템 【대유도】
ELSEIF LOCAL == 35
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [대유도]를 배웠다＞
    TALENT:MASTER:96 = 1
;소질 아이템 【음구지식】
ELSEIF LOCAL == 36
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [음구지식]을 배웠다＞
    TALENT:MASTER:음구지식 = 1
;소질 아이템 【교육자】
ELSEIF LOCAL == 37
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [교육자]가 되었다＞
    TALENT:MASTER:126 = 1
;소질 아이템 【가전제품지식】
ELSEIF LOCAL == 38
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [가전제품지식]을 배웠다＞
    TALENT:MASTER:189 = 1
;소질 아이템 【공작명인】
ELSEIF LOCAL == 39
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [공작명인]이 되었다＞
    TALENT:MASTER:195 = 1
;소질 아이템 【마술기능】
ELSEIF LOCAL == 40
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [마술기능]을 배웠다＞
    TALENT:MASTER:197 = 1
;소질 아이템 【초상계산】
ELSEIF LOCAL == 41
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [초상계산]을 배웠다＞
    TALENT:MASTER:198 = 1
;Debug 습득
ELSEIF LOCAL == 42 && TALENT:MASTER:998 == 0
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [Debug]를 배웠다＞
    TALENT:MASTER:998 = 1
;Debug 폐기
ELSEIF LOCAL == 42 && TALENT:MASTER:998 == 1
    PRINTFORMW ＜%조사처리(CALLNAME:MASTER,"가")% [Debug]를 잃었다＞
    TALENT:MASTER:998 = 0
ENDIF
RESTART

;-----------------------------------------------
;【기교 Lv】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_TECH_PRICE
#FUNCTION
;주인의 現在の기교 Lvを価格計算基礎に変換する
LOCAL = (ABL:MASTER:기교 <= 0) ? 2 # ABL:MASTER:기교
LOCAL:2 = 5000 * LOCAL
LOCALS:0 = 
LOCALS:1 = 
SIF LOCAL:2 >= 10000
    LOCALS:0 = {(LOCAL:2)/10000}만
SIF (LOCAL:2)%10000 > 0
    LOCALS:1 = {(LOCAL:2)%10000}
COM_NAME = % LOCALS:0 + LOCALS:1 %
RETURNF LOCAL:2

;-----------------------------------------------
;【요리기능 Lv】의 가격 계산 처리
;-----------------------------------------------
;LOCAL 요리기능 Lvを上昇させるのに必要な調理経験の最低値
@BUY_SKILL_MASTER_COOKING_PRICE
#FUNCTION
LOCAL:2 = 0
;주인의 現在の요리기능 Lvを見る
IF ABL:MASTER:요리기능 == 0
    LOCAL = 5
ELSEIF ABL:MASTER:요리기능 == 1
    LOCAL = 20
ELSEIF ABL:MASTER:요리기능 == 2
    LOCAL = 100
ELSEIF ABL:MASTER:요리기능 == 3
    LOCAL = 250
ELSE
    LOCAL = 500
ENDIF
;주인이 料理苦手のときはより経験必要
SIF TALENT:MASTER:192
    TIMES LOCAL , 1.20
;주인의 調理経験不足なら上昇不可
SIF EXP:MASTER:조리경험 < LOCAL
    RETURNF 0

LOCAL:1 = ABL:MASTER:요리기능
LOCAL:2 = 10000 * (1 + LOCAL:1)
COM_NAME = {(LOCAL:2)/10000}만
RETURNF LOCAL:2

;-----------------------------------------------
;【촬영기능 Lv】의 가격 계산 처리
;-----------------------------------------------
;LOCAL 촬영기능 Lvを上昇させるのに必要な撮影経験の最低値
@BUY_SKILL_MASTER_CAMERA_PRICE
#FUNCTION
LOCAL:2 = 0
;주인의 現在の촬영기능 Lvを見る
IF ABL:MASTER:촬영기능 == 0
    LOCAL = 10
ELSEIF ABL:MASTER:촬영기능 == 1
    LOCAL = 30
ELSEIF ABL:MASTER:촬영기능 == 2
    LOCAL = 60
ELSEIF ABL:MASTER:촬영기능 == 3
    LOCAL = 100
ELSE
    LOCAL = 150
ENDIF
;주인의 撮影経験不足なら上昇不可
SIF EXP:MASTER:촬영경험 < LOCAL
    RETURNF 0
;주인의 現在の촬영기능 Lvが０以下のときの価格計算基礎
IF ABL:MASTER:촬영기능 <= 0
    LOCAL:1 = 2
ELSE
    LOCAL:1 = ABL:MASTER:촬영기능
ENDIF
LOCAL:2 = 30000 * LOCAL:1
COM_NAME = {(LOCAL:2)/10000}만
RETURNF LOCAL:2

;-----------------------------------------------
;【歌唱技能Lv】의 가격 계산 처리
;-----------------------------------------------
;LOCAL 歌唱技能Lvを上昇させるのに必要な歌唱経験の最低値
@BUY_SKILL_MASTER_SINGING_PRICE
#FUNCTION
LOCAL:2 = 0
;주인의 現在の歌唱技能Lvを見る
IF ABL:MASTER:가창기능 == 0
    LOCAL = 40
ELSEIF ABL:MASTER:가창기능 == 1
    LOCAL = 100
ELSEIF ABL:MASTER:가창기능 == 2
    LOCAL = 160
ELSEIF ABL:MASTER:가창기능 == 3
    LOCAL = 320
ELSE
    LOCAL = 500
ENDIF
;주인이 音痴ならより経験が必要
SIF TALENT:MASTER:190
    TIMES LOCAL , 1.10
;주인의 歌唱経験不足なら上昇不可
SIF EXP:MASTER:가창경험 < LOCAL
    RETURNF 0

LOCAL:1 = ABL:MASTER:가창기능
LOCAL:2 = 10000 * (1 + LOCAL:1)
COM_NAME = {(LOCAL:2)/10000}만
RETURNF LOCAL:2

;-----------------------------------------------
;【調教師Lv】의 가격 계산 처리
;-----------------------------------------------
;LOCAL   全部の周回を含めた売却した人数の累計
;LOCAL:1 現在のプレイで恋慕or淫乱をつけた人数
;……に対応
@BUY_SKILL_MASTER_TRAINER_PRICE
#FUNCTION
LOCAL:3 = 0
;주인의 現在の調教師Lvを見る
IF ABL:MASTER:조교사Lv == 0
    LOCAL = 5
    LOCAL:1 = 5
ELSEIF ABL:MASTER:조교사Lv == 1
    LOCAL = 25
    LOCAL:1 = 10
ELSEIF ABL:MASTER:조교사Lv == 2
    LOCAL = 50
    LOCAL:1 = 25
ELSEIF ABL:MASTER:조교사Lv == 3
    LOCAL = 100
    LOCAL:1 = 50
ELSE
    LOCAL = 200
    LOCAL:1 = 100
ENDIF
;売却人数または現在周回中に陥落させた人数のどちらかが足りない場合は上昇不可
SIF FLAG:매각총계 < LOCAL || CFLAG:MASTER:98 < LOCAL:1
    RETURNF 0
LOCAL:2 = ABL:MASTER:조교사Lv * ABL:MASTER:조교사Lv
LOCAL:3 = 2000 * (1 + LOCAL:2)
LOCALS:0 = 
LOCALS:1 = 
SIF LOCAL:3 >= 10000
    LOCALS:0 = {(LOCAL:3)/10000}만
SIF (LOCAL:3)%10000 > 0
    LOCALS:1 = {(LOCAL:3)%10000}
COM_NAME = % LOCALS:0 + LOCALS:1 %
RETURNF LOCAL:3

;-----------------------------------------------
;【새드】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_SADIST_PRICE
#FUNCTION
LOCALS:1 = 
;주인이 마조かドＭ
IF TALENT:MASTER:82 || TALENT:MASTER:178
    LOCAL = 75000
;주인의 마조っ気がLv3以上
ELSEIF ABL:MASTER:마조끼 >= 3
    LOCAL = 60000
ELSE
    LOCAL = 50000
ENDIF
SIF LOCAL >= 10000
    LOCALS = {(LOCAL)/10000}만
SIF (LOCAL)%10000 > 0
    LOCALS:1 = {(LOCAL)%10000}
COM_NAME = % LOCALS + LOCALS:1 %
RETURNF LOCAL

;-----------------------------------------------
;【마조】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_MASOIST_PRICE
#FUNCTION
LOCALS:1 = 
;주인이 サドかドＳ
IF TALENT:MASTER:81 || TALENT:MASTER:177
    LOCAL = 75000
;주인의 サドっ気がLv3以上
ELSEIF ABL:MASTER:새드끼 >= 3
    LOCAL = 60000
ELSE
    LOCAL = 50000
ENDIF
SIF LOCAL >= 10000
    LOCALS = {(LOCAL)/10000}만
SIF (LOCAL)%10000 > 0
    LOCALS:1 = {(LOCAL)%10000}
COM_NAME = % LOCALS + LOCALS:1 %
RETURNF LOCAL

;-----------------------------------------------
;【汚れ無視】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_DISREGARDSTAIN_PRICE
#FUNCTION
;주인이 汚臭敏感
IF TALENT:MASTER:62
    LOCAL = 30000
;주인이 汚臭鈍感
ELSEIF TALENT:MASTER:61
    LOCAL = 10000
ELSE
    LOCAL = 20000
ENDIF
RETURNF LOCAL

;-----------------------------------------------
;【금단의 지식】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_FORBIDDEN_PRICE
#FUNCTION
;주인의 調教師レベルを見る
IF ABL:MASTER:조교사Lv == 0
    LOCAL = 1000000
ELSEIF ABL:MASTER:조교사Lv == 1
    LOCAL = 500000
ELSEIF ABL:MASTER:조교사Lv == 2
    LOCAL = 250000
ELSE
    LOCAL = 100000
ENDIF
;自制心＋保守的な人はちょっぴり覚えづらい
SIF TALENT:MASTER:자제심 && TALENT:MASTER:보수적
    LOCAL += 10000
RETURNF LOCAL

;-----------------------------------------------
;【요리잘함】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_COOK_PRICE
#FUNCTION
LOCAL = 100000
RETURNF LOCAL

;-----------------------------------------------
;【조리명인】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_CHEF_PRICE
#FUNCTION
LOCAL = 200000
RETURNF LOCAL

;-----------------------------------------------
;【보도자】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_REPORTER_PRICE
#FUNCTION
LOCAL = 500000
RETURNF LOCAL

;-----------------------------------------------
;【음감】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_SINGER_PRICE
#FUNCTION
LOCAL = 100000
RETURNF LOCAL

;-----------------------------------------------
;【혀놀림】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_EXTALENT52_PRICE
#FUNCTION
LOCAL = 3000000
RETURNF LOCAL

;-----------------------------------------------
;【소악마】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_EXTALENT93_PRICE
#FUNCTION
LOCAL = 4000000
RETURNF LOCAL

;-----------------------------------------------
;【매혹】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_EXTALENT91_PRICE
#FUNCTION
LOCAL = 5000000
RETURNF LOCAL

;-----------------------------------------------
;【바느질】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_EXTALENT53_PRICE
#FUNCTION
LOCAL = 2000000
RETURNF LOCAL

;-----------------------------------------------
;【절벽가슴격추왕】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_EXTALENT97_PRICE
#FUNCTION
LOCAL = 8000000
RETURNF LOCAL

;-----------------------------------------------
;【대유도】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_EXTALENT96_PRICE
#FUNCTION
LOCAL = 8000000
RETURNF LOCAL

;-----------------------------------------------
;【음구지식】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_EXTALENT58_PRICE
#FUNCTION
LOCAL = 3000000
RETURNF LOCAL

;-----------------------------------------------
;【교육자】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_EXTALENT126_PRICE
#FUNCTION
LOCAL = 2000000
RETURNF LOCAL

;-----------------------------------------------
;【가전제품지식】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_EXTALENT189_PRICE
#FUNCTION
LOCAL = 3000000
RETURNF LOCAL

;-----------------------------------------------
;【공작명인】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_EXTALENT195_PRICE
#FUNCTION
LOCAL = 4000000
RETURNF LOCAL

;-----------------------------------------------
;【마술기능】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_EXTALENT197_PRICE
#FUNCTION
LOCAL = 4000000
RETURNF LOCAL

;-----------------------------------------------
;【초상계산】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_EXTALENT198_PRICE
#FUNCTION
LOCAL = 4000000
RETURNF LOCAL

;-----------------------------------------------
;【디버그】의 가격 계산 처리
;-----------------------------------------------
@BUY_SKILL_MASTER_EXTALENT998_PRICE
#FUNCTION
;LOCAL = 100
LOCAL = 0
RETURNF LOCAL

;=============================================================================
;烙印キャラ用特殊装備購入処理
;=============================================================================
;-----------------------------------------------
;烙印キャラ用特殊装備購入可能かの判定
;-----------------------------------------------
@BRAND_SLAVE_CHECK
#FUNCTION
LOCAL:1 = 0
FOR LOCAL, 0, CHARANUM
    ;主人は除外
    SIF LOCAL == MASTER
        CONTINUE
    ;その場にいないキャラは排除
    SIF CFLAG:LOCAL:4
        CONTINUE
    ;[烙印]必須
    SIF TALENT:LOCAL:161 == 0
        CONTINUE
    ;クリトリスリング
    SIF (CFLAG:LOCAL:42 & 1) == 0 && TALENT:LOCAL:120 == 0 && TALENT:LOCAL:121 == 0 && EXP:LOCAL:C조율경험 >= 20
        LOCAL:1 |= 1
    ;奴隷管理用貞操帯
    SIF (CFLAG:LOCAL:42 & 2) == 0 && TALENT:LOCAL:처녀 == 0
        LOCAL:1 |= 2
    ;ベリージュエル
    SIF (CFLAG:LOCAL:42 & 4) == 0 && ABL:LOCAL:노출증 >= 3 && ABL:LOCAL:새드끼 >= 3 && ABL:LOCAL:마조끼 >= 3
        LOCAL:1 |= 4
    ;ボンデージ
    SIF (CFLAG:LOCAL:42 & 8) == 0 && MARK:LOCAL:고통각인 >= 3 && ABL:LOCAL:노출증 >= 3 && ABL:LOCAL:마조끼 >= 3
        LOCAL:1 |= 8
    ;ラビアピアス
    SIF (CFLAG:LOCAL:42 & 16) == 0 && TALENT:LOCAL:120 == 0 && EXP:LOCAL:V확장경험 >= 20 && EXP:LOCAL:V조율경험 >= 20
        LOCAL:1 |= 16
    ;ニプルピアス
    SIF (CFLAG:LOCAL:42 & 32) == 0 && ABL:LOCAL:B감각 >= 3 && EXP:LOCAL:B쾌락경험 >= 20 && EXP:LOCAL:유선개발경험 >= 20
        LOCAL:1 |= 32
NEXT
RETURNF LOCAL:1

;-----------------------------------------------
;烙印キャラ用特殊装備の装着処理
;-----------------------------------------------
;ARG   = 付けさせる特殊装備の値段
;ARG:1 = 付けさせる特殊装備の種類
@BRAND_SLAVE_EQUIPMENT, ARG, ARG:1
;表示させるキャラを抽出（LOCAL:2に人数）
VARSET LOCAL, 0
CALLF CLEAR_LIST
REPEAT CHARANUM
    MARK:COUNT:98 = 0
    ;主人は除外
    SIF COUNT == 0
        CONTINUE
    ;その場にいないキャラは排除
    SIF CFLAG:COUNT:4
        CONTINUE
    ;[烙印]必須
    SIF TALENT:COUNT:161 == 0
        CONTINUE

    ;クリトリスリング
    IF ARG:1 == 20
        ;[オトコ]・[ふたなり]は装着不可能
        SIF EXIST_PENIS(COUNT)
            CONTINUE
        ;Ｃ調律経験が20未満だとダメ
        SIF EXP:COUNT:C조율경험 < 20
            CONTINUE
        ;既に装着している場合はダメ
        SIF CFLAG:COUNT:42 & 1
            CONTINUE
    ;奴隷管理用貞操帯
    ELSEIF ARG:1 == 21
        ;女性用のはバイブ付きなので[処女]は装着不可能
        SIF TALENT:COUNT:처녀
            CONTINUE
        ;既に装着している場合はダメ
        SIF CFLAG:COUNT:42 & 2
            CONTINUE
    ;ベリージュエル
    ELSEIF ARG:1 == 22
        ;露出癖・サドっ気・마조っ気全て3以上必要
        SIF ABL:COUNT:노출증 < 3 || ABL:COUNT:새드끼 < 3 || ABL:COUNT:마조끼 < 3
            CONTINUE
        ;既に装着している場合はダメ
        SIF CFLAG:COUNT:42 & 4
            CONTINUE
    ;ボンデージ
    ELSEIF ARG:1 == 23
        ;苦痛刻印・露出癖・마조っ気全て3以上必要
        SIF MARK:COUNT:고통각인 < 3 || ABL:COUNT:노출증 < 3 || ABL:COUNT:마조끼 < 3
            CONTINUE
        ;既に装着している場合はダメ
        SIF CFLAG:COUNT:42 & 8
            CONTINUE
    ;ラビアピアス
    ELSEIF ARG:1 == 24
        ;[オトコ]は装着不可能
        SIF TALENT:COUNT:120
            CONTINUE
        ;Ｖ拡張経験・Ｖ調律経験のどちらかが20未満だとダメ
        SIF EXP:COUNT:V확장경험 < 20 || EXP:COUNT:V조율경험 < 20
            CONTINUE
        ;既に装着している場合はダメ
        SIF CFLAG:COUNT:42 & 16
            CONTINUE
    ;ニプルピアス
    ELSEIF ARG:1 == 25
        ;Ｂ感覚3以上必要
        SIF ABL:COUNT:B감각 < 3
            CONTINUE
        ;Ｂ快楽経験・乳腺開発経験のどちらかが20未満だとダメ
        SIF EXP:COUNT:B쾌락경험 < 20 || EXP:COUNT:유선개발경험 < 20
            CONTINUE
        ;既に装着している場合はダメ
        SIF CFLAG:COUNT:42 & 32
            CONTINUE
    ENDIF

    CALLF SET_LIST, LOCAL:2, COUNT
    LOCAL:2 += 1
    MARK:COUNT:98 = 1
REND
LOCAL:1 = (LOCAL:2 - 1) / 20

$PRINT_LIST
DRAWLINE
PRINTFORML 누구에게 채웁니까? ＜page.{LOCAL+1}＞
CALL USE_ITEM_LIST, LOCAL
DRAWLINE
PRINTFORMLC \@(LOCAL <= 0) ? %" " * 16% # [1001] 이전 페이지\@
PRINTLC [1000] 돌아간다
PRINTFORMLC \@(LOCAL >= LOCAL:1) ? %" " * 16% # [1009] 다음 페이지\@

$INPUT_LOOP_0
INPUT
IF RESULT == 1000
    ;返金処理
    MONEY += ARG
    RETURN 0
ELSEIF RESULT == 1001 && LOCAL > 0
    LOCAL -= 1
    GOTO PRINT_LIST
ELSEIF RESULT == 1009 && LOCAL < LOCAL:1
    LOCAL += 1
    GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT >= CHARANUM || MARK:RESULT:98 == 0
    GOTO INPUT_LOOP_0
ENDIF
LOCAL:3 = TARGET
TARGET = RESULT
PRINTFORM %CALLNAME:TARGET%
IF ARG:1 == 20
    PRINTL 의 음핵에 클리토리스링을 장착하겠습니까?
    PRINTL ※한 번 장착하면 두 번 다시 벗길 수 없게 됩니다.
    PRINTL ※또한 장착한 자는 이후 후타나리로 만들 수 없게 됩니다.
ELSEIF ARG:1 == 21
    PRINT 에게 노예관리용 정조대
    SIF TALENT:120 == 0
        PRINT (바이브 부착형)
    PRINTL 를 채우겠습니까?
    PRINTL ※한 번 장착하면 조교중 이외엔 벗길 수 없게 됩니다.
    PRINTL ※또한 장착한 자는 이후 조교 이외엔 자위할 수 없게 됩니다.
ELSEIF ARG:1 == 22
    PRINTL 의 배꼽에 벨리 쥬얼을 장착하겠습니까?
    PRINTL ※한 번 장착하면 두 번 다시 벗길 수 없게 됩니다.
    PRINTL ※또한 장착한 자는 체력 성장이 저하됩니다.
ELSEIF ARG:1 == 23
    PRINTL 의 속옷으로 본디지를 입히겠습니까?
    PRINTL ※한 번 장착하면 조교중 이외엔 벗길 수 없게 됩니다.
    PRINTL ※또한 장착한 자는 체력 성장이 저하됩니다.
ELSEIF ARG:1 == 24
    PRINTL 의 보지에 라비아피어스를 장착하겠습니까？
    PRINTL ※한 번 장착하면 두 번 다시 벗길 수 없게 됩니다.
    PRINTL ※또한 장착한 자는 Ｖ 계열의 쾌락을 느끼기 힘들게 됩니다.
ELSEIF ARG:1 == 25
    PRINTL 의 유두에 니플피어스를 장착하겠습니까?
    PRINTL ※한 번 장착하면 두 번 다시 벗길 수 없게 됩니다.
    PRINTL ※또한 장착한 자는 Ｂ 계열의 쾌락을 느끼기 힘들게 됩니다.
ENDIF
PRINTLC [0] 네
PRINTLC [1] 아니오
PRINTL 
$INPUT_LOOP_1
INPUT
IF RESULT == 1
    ;返金処理
    MONEY += ARG
    TARGET = LOCAL:3
    RETURN 0
ELSEIF RESULT != 0
    GOTO INPUT_LOOP_1
ENDIF

TRYCALLFORM BRAND_EQUIPMENT_{ARG:1},ARG
PRINTL 
TARGET = LOCAL:3

;-----------------------------------------------
;クリトリスリング
;-----------------------------------------------
@BRAND_EQUIPMENT_20, ARG
;지문 컷
IF CFLAG:99 == 1
    CALL SELF_KOJO, TARGET, 230, ARG
ELSE
PRINTFORML %조사처리(CALLNAME:MASTER,"는")% %CALLNAME:TARGET%의 다리를 Ｍ자 모양으로 벌리게 해 놓곤,
PRINTW 오똑 부풀어 오른 음핵에 은색 클리토리스링을 채우기 위해 구멍을 뚫었다.
PRINTFORML 클리토리스링을 착용한 %CALLNAME:TARGET%의 음핵은
PRINTL 안쪽을 파고든 침으로부터 미약한 자극을 끊임없이 받음과 동시에,
PRINTW 링의 죄어드는 힘에 의해 의사와는 관계 없이 계속 발기되고 있다…….
    PRINTL 
    CALL SELF_KOJO, TARGET, 230, ARG
ENDIF
;苦痛快楽経験
CALL COMMON_UP_EXP,TARGET, 30, 5, 0
;Ｃ調律経験
CALL COMMON_UP_EXP,TARGET, 41, 1, 0
;快Ｃの珠
CALL COMMON_UP_JUEL, TARGET, 0, (ABL:C감각 * 100), 0
PRINTL 
PRINTFORMW ―――%CALLNAME:TARGET%의 클리토리스는 %CALLNAME:MASTER%의 것이 되었습니다―――
PRINTFORMW …………
PRINTFORMW ………
PRINTFORMW ……
PRINTFORMW ＜%조사처리(CALLNAME:TARGET,"는")% 후타나리가 될 수 없게 되었습니다＞
CFLAG:42 |= 1

;-----------------------------------------------
;奴隷管理用貞操帯
;-----------------------------------------------
@BRAND_EQUIPMENT_21, ARG
;지문 컷
IF CFLAG:99 == 1
    CALL SELF_KOJO, TARGET, 231, ARG
ELSE
PRINTFORML %조사처리(CALLNAME:MASTER,"는")% %CALLNAME:TARGET%의 다리를 Ｍ자로 벌리게 해놓곤,
IF TALENT:120 == 0
    PRINTL 우선 흑색 광택을 뿜는 딜도를 보짓속에 꽃아넣었다.
    PRINTFORMW 요염한 신음을 지르고 있는 %CALLNAME:TARGET%의 엉덩이에 %조사처리(CALLNAME:MASTER,"는")% 
    PRINTFORML 딜도가 %CALLNAME:TARGET%의 질내에 완전히 다 파묻힌 것을 확인하고는 
    PRINT 이번엔 
ENDIF
PRINTFORML 애널플러그를 가지고 와선 %CALLNAME:TARGET%의 국문에 깊숙히 박았다.
PRINTW 그리고 전용 펌프를 꺼내와 절대 맘대로 벗을 수 없도록 플러그를 부풀려놨다.
PRINTFORMW 마개가 빠지지 않는 것을 확인하고는, %조사처리(CALLNAME:MASTER,"는")% 중앙에 집게가 붙은 가늘고 긴 투명관을 꺼내어 한쪽 끝은 애널플러그에 연결하고, 또 한쪽 끝을 %CALLNAME:TARGET%의 요도구 안쪽에 꽃아넣었다.
PRINTFORML 관 끝이 방광에 도달할 때까지 밀어넣은 그 순간, 갈 곳을 찾아 헤매던 %CALLNAME:TARGET%의 오줌은
PRINTW 요도에 깊숙히까지 파고들어 있는 관으로 흘러나와 집게로 막은 데에서 멈췄다.
PRINTFORMW 가볍게 관을 뽑았다가 꽃았다가 해서 %조사처리(CALLNAME:MASTER,"는")% 관이 간단히 빠지지 않는다는 걸 확인하고는, 갑자기 오줌을 막고 있던 집게를 풀었다.
PRINTFORML %CALLNAME:TARGET%의 오줌은 둑이 터진 듯이 관을 타고 흘러 애널 플러그를 통해 직장 속으로 흘러들어갔다.
PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 의도하지 않은 방뇨와 자신의 오줌으로 관장되는 감각에 형용할 수 없는 소리를 지르며 허리를 격렬하게 흔들어댔다….
PRINTFORML 마지막으로 %조사처리(CALLNAME:MASTER,"는")% 날뛰고 있는 %조사처리(CALLNAME:TARGET,"를")% 짓누르고는, 그 요염한 골반에 검은 가죽제 정조대를 장착하곤 자신이 벗을 수 없게 자물쇠를 채웠다.
PRINTFORMW 그리고 %CALLNAME:TARGET%에게 허가 없이 배설이나 자위하는 것을 금지한다고 말한 뒤 방을 나섰다…….
    PRINTL 
    CALL SELF_KOJO, TARGET, 231, ARG
ENDIF
;Ｖ経験
SIF TALENT:120 ==0
    CALL COMMON_UP_EXP,TARGET, 0, 1, 0
;Ａ経験
CALL COMMON_UP_EXP,TARGET, 1, 1, 0
;放尿経験
CALL COMMON_UP_EXP,TARGET, 6, 1, 0
;尿道開発経験
CALL COMMON_UP_EXP,TARGET, 46, 1, 0
PRINTL 
PRINTFORMW ―――%조사처리(CALLNAME:TARGET,"는")% 배설행위를 맘대로 할 수 없게 되었습니다―――
PRINTFORMW …………
PRINTFORMW ………
PRINTFORMW ……
PRINTFORMW ＜%CALLNAME:TARGET%의 성행위와 배설행위는 %CALLNAME:MASTER%의 관리하에 놓였습니다＞
CFLAG:42 |= 2

;-----------------------------------------------
;ベリージュエル
;-----------------------------------------------
@BRAND_EQUIPMENT_22, ARG
;지문 컷
IF CFLAG:99 == 1
    CALL SELF_KOJO, TARGET, 232, ARG
ELSE
PRINTFORML %조사처리(CALLNAME:MASTER,"는")% 하얗고 요염한 빛을 띈 진주같은 구슬을 집어들어선 
PRINTFORMW %CALLNAME:TARGET%의 배꼽에 댔다.
PRINTL 진주를 가볍게 눌렀더니, 마치 거기에 살갗이 없는 것처럼 하얀 구슬은 
PRINTFORMW 아무 어려움 없이 %CALLNAME:TARGET%의 배꼽에 파묻혀 고정되어 버렸다….
PRINTFORML %조사처리(CALLNAME:MASTER,"는")% 반신을 %CALLNAME:TARGET%의 체내에 파묻은 진주를 한 번 쓰다듬었다.
PRINTFORMW 그러자 마치 전기가 온몸을 타고 도는 것처럼 %조사처리(CALLNAME:TARGET,"는")% 몸을 떨었다…….
    PRINTL 
    CALL SELF_KOJO, TARGET, 232, ARG
ENDIF
PRINTL 
PRINTFORMW ―――%조사처리(CALLNAME:TARGET,"는")% 보석의 마력에 의해 소모가 억제되었습니다―――
PRINTFORMW …………
PRINTFORMW ………
PRINTFORMW ……
PRINTFORMW ＜%CALLNAME:TARGET%의 몸 상태 관리는 %조사처리(CALLNAME:MASTER,"가")% 장악했습니다＞
CFLAG:42 |= 4

;-----------------------------------------------
;ボンデージ
;-----------------------------------------------
;たまたま覗きこんだ3D少女総合7スレ目の>>455を見て
;ボンデージを下着の代わりに日常的に装着して緊縛経験を稼ぐものを思いついてしまった
;……が, 本人はボンデージ属性を持っていないためか, 地の文の完成具合は微妙
@BRAND_EQUIPMENT_23, ARG
;지문 컷
IF CFLAG:99 == 1
    CALL SELF_KOJO, TARGET, 233, ARG
ELSE
PRINTFORML %조사처리(CALLNAME:MASTER,"는")% 검은 광택이 나는 가죽제 본디지를 집어들어선, 
PRINTFORMW 속옷 대신 삼아 %CALLNAME:TARGET%에게 입혔다.
PRINTFORML 가슴을 노출시킨 디자인의 그 옷은 %CALLNAME:TARGET%의 수치심을 부채질함과 동시에
PRINTW 미치 일체화된 것같은 밀착감에 의해 정신적인 종속감을 더했다…….
    PRINTL 
    CALL SELF_KOJO, TARGET, 233, ARG
ENDIF
PRINTL 
PRINTFORMW ―――%조사처리(CALLNAME:TARGET,"는")% 항시 긴박상태에 처해졌습니다―――
PRINTFORMW …………
PRINTFORMW ………
PRINTFORMW ……
PRINTFORMW ＜%CALLNAME:TARGET%의 정신은 %CALLNAME:MASTER%에 의해 구속되었습니다＞
CFLAG:42 |= 8

;-----------------------------------------------
;ラビアピアス
;-----------------------------------------------
;ピアスパッチより「ラビアピアス装着」の地の文を一部流用
@BRAND_EQUIPMENT_24, ARG
;지문 컷
IF CFLAG:99 == 1
    CALL SELF_KOJO, TARGET, 234, ARG
ELSE
PRINTFORM %조사처리(CALLNAME:MASTER,"는")% %CALLNAME:TARGET%의 
SIF TALENT:102
    PRINT 민감한 
PRINTW 바기나에 용서 없이 침을 꽃아넣곤 피어스를 채웠다….
PRINTFORML %CALLNAME:TARGET%의 보지에 채워진 피어스는 어두운 등불 아래서도 
PRINTW 예리한 광채를 발하고 있었다.
PRINTL 성기에 피어스가 장착된 배덕감에서인지, 그게 아니면 주인의 것이 되었다는 충족감에서인지
PRINTFORMW 피어스로 일그러져 있는 %CALLNAME:TARGET%의 보지는 서서히 애액에 젖기 시작했다…….
    PRINTL 
    CALL SELF_KOJO, TARGET, 234, ARG
ENDIF
;苦痛快楽経験
CALL COMMON_UP_EXP,TARGET, 30, 10, 0
;快Ｖの珠
CALL COMMON_UP_JUEL, TARGET, 1, (ABL:V감각 * 100), 0
PRINTL 
PRINTFORMW ―――%CALLNAME:TARGET%의 바기나는 %CALLNAME:MASTER%의 것이 되었습니다―――
PRINTFORMW …………
PRINTFORMW ………
PRINTFORMW ……
PRINTFORMW ＜%조사처리(CALLNAME:TARGET,"는")% 보지 벌리기에 약간의 저항을 느끼게 되었습니다＞
CFLAG:42 |= 16

;-----------------------------------------------
;ニプルピアス
;-----------------------------------------------
;ピアスパッチより「ニプルピアス装着」の地の文を一部流用
@BRAND_EQUIPMENT_25, ARG
;지문 컷
IF CFLAG:99 == 1
    CALL SELF_KOJO, TARGET, 235, ARG
ELSE
PRINTFORM %조사처리(CALLNAME:MASTER,"는")% %CALLNAME:TARGET%의 
SIF TALENT:106
    PRINT 민감한 
PRINTW 유두에 용서 없이 침을 꽃아넣고는, 피어스를 채웠다….
PRINTL 그리고 품 속에서 한쪽 끝에 은색 방울이 달린 작은 실을 꺼내선, 
PRINTFORMW 그걸 %CALLNAME:TARGET%의 유두에 채운 피어스에 매달았다.
PRINTL 방울의 무게 때문에 잡아당겨져 유두엔 끝없이 자극이 주어지고 있었다.
PRINTL 억지로 발기된 유두 끝에 연결된 방울이 음탕해 보이는 외견과는 반대로
PRINTFORMW 몹시 맑고 고운 소리를 내서 오히려 %CALLNAME:TARGET%의 수치심을 부채질했다…….
    PRINTL 
    CALL SELF_KOJO, TARGET, 235, ARG
ENDIF
;苦痛快楽経験
CALL COMMON_UP_EXP,TARGET, 30, 10, 0
;快Ｂの珠
CALL COMMON_UP_JUEL, TARGET, 3, (ABL:B감각 * 100), 0
PRINTL 
PRINTFORMW ―――%CALLNAME:TARGET%의 유두는 %CALLNAME:MASTER%의 것이 되었습니다―――
PRINTFORMW …………
PRINTFORMW ………
PRINTFORMW ……
PRINTFORMW ＜%조사처리(CALLNAME:TARGET,"는")% 평생 유두의 장식을 달고 살게 되었습니다＞
CFLAG:42 |= 32

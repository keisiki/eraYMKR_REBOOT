;=============================================================================
;년월일 관련의 처리
;=============================================================================
;--------------------------------------------------
;게임 개시 또는 주차 플레이 개시의 초기 설정
;--------------------------------------------------
@CALENDAR_INIT
;년
DAY:4 = 1
;월
DAY:5 = 1
;날짜
DAY:6 = 1
;요일 1=일요일～7=토요일
DAY:7 = 1
;윤년인가 아닌가? 1=윤년 0=평년
DAY:8 = 0

;--------------------------------------------------
;올해는 윤년인가 아닌가를 판단한다
;--------------------------------------------------
@CALENDAR_LEAP
DAY:8 = ((DAY:4 % 4 == 0) && (DAY:4 % 100 != 0) || (DAY:4 % 400 == 0)) ? 1 # 0

;--------------------------------------------------
;データ読み込み時などの場合の再設定
;--------------------------------------------------
@CALENDAR_CALC,ARG
VARSET LOCAL,0
;曜日を先に求める
DAY:7 = 1 + ARG % 7

;以下年月日を計算
LOCAL = ARG
DAY:4 = 1
LOCAL:12 = 0
;4000000年(1460970000日）ごとにまず計算
IF LOCAL > 1460970000
    LOCAL:12 += LOCAL / 1460970000
    LOCAL = LOCAL % 1460970000
ENDIF
DAY:4 += 4000000 * LOCAL:12
LOCAL:11 = 0
;次に40000年(14609700日）ごとに計算
IF LOCAL > 14609700
    LOCAL:11 += LOCAL / 14609700
    LOCAL = LOCAL % 14609700
ENDIF
DAY:4 += 40000 * LOCAL:11
LOCAL:10 = 0
;400年（146097日）ごとに繰り返すのがグレゴリオ暦
IF LOCAL > 146097
    LOCAL:10 += LOCAL / 146097
    LOCAL = LOCAL % 146097
ENDIF
DAY:4 += 400 * LOCAL:10
LOCAL:4 = 0
LOCAL:8 = 0
IF LOCAL % 146097 == 0 && DAY < 146098
    DAY:4 += 399
    DAY:5 = 12
    DAY:6 = 31
    RETURN 1
ENDIF
$NEXT_YEAR
IF LOCAL > (365 + LOCAL:8) 
    LOCAL:4 ++
    LOCAL -= (LOCAL:8) ? 366 # 365
    LOCAL:8 = (LOCAL:4 % 100 != 0 && LOCAL:4 % 4 == 0) ? 1 # 0
    GOTO NEXT_YEAR
ENDIF
DAY:4 += LOCAL:4
LOCAL:5 = 1
LOCAL:6 = 0
IF LOCAL <= 0
    DAY:4 --
    DAY:5 = 12
    DAY:6 = 31
    CALL CALENDAR_LEAP
    RETURN 1
ENDIF
CALL CALENDAR_LEAP
$NEXT_DAY
LOCAL --
LOCAL:6 ++
IF LOCAL > 0
    IF LOCAL:5 == 1 || LOCAL:5 == 3 || LOCAL:5 == 5 || LOCAL:5 == 7 || LOCAL:5 == 8 || LOCAL:5 == 10 || LOCAL:5 == 12
        IF LOCAL:6 > 30
            LOCAL:5 ++
            LOCAL:6 = 0
        ENDIF
    ELSEIF LOCAL:5 == 4 || LOCAL:5 == 6 || LOCAL:5 == 9 || LOCAL:5 == 11
        IF LOCAL:6 > 29
            LOCAL:5 ++
            LOCAL:6 = 0
        ENDIF
    ELSEIF DAY:8
        IF LOCAL:6 > 28
            LOCAL:5 ++
            LOCAL:6 = 0
        ENDIF
    ELSE
        IF LOCAL:6 > 27
            LOCAL:5 ++
            LOCAL:6 = 0
        ENDIF
    ENDIF
    GOTO NEXT_DAY
ENDIF
DAY:5 = LOCAL:5
DAY:6 = LOCAL:6

;--------------------------------------------------
;１日経過
;--------------------------------------------------
@CALENDAR_CHECK
CALL CALENDAR_LEAP
;まずは曜日処理
DAY:7 = (DAY:7 < 7) ? (DAY:7 + 1) # 1
;日数処理
DAY:6 ++
IF DAY:5 == 1 || DAY:5 == 3 || DAY:5 == 5 || DAY:5 == 7 || DAY:5 == 8 || DAY:5 == 10 || DAY:5 == 12
    IF DAY:6 == 32
        DAY:6 = 1
        DAY:5 ++
        IF DAY:5 == 13
            DAY:5 = 1
            DAY:4 ++
        ENDIF
    ENDIF
ELSEIF DAY:5 == 4 || DAY:5 == 6 || DAY:5 == 9 || DAY:5 == 11
    IF DAY:6 == 31
        DAY:6 = 1
        DAY:5 ++
    ENDIF
ELSEIF DAY:6 == (DAY:8 == 0) ? 29 # 30
    DAY:6 = 1
    DAY:5 ++
ENDIF

;--------------------------------------------------
;年月日と曜日の表示処理
;--------------------------------------------------
;ARG=0：計算省略、ARG=1：日付の計算実行
@CALENDAR_PRINT, ARG
SIF DAY:7 == 0 && ARG == 1
    CALL CALENDAR_CALC,DAY

VARSET LOCALS,""
SPLIT "閏,일,월,화,수,목,금,토",",",LOCALS
STR:3 = %LOCALS:(DAY:7)%

PRINTFORM 　{DAY:4}년 {DAY:5}월 {DAY:6}일 %STR:3%요일

;--------------------------------------------------
;キャラごとの記念日をチェックする処理
;--------------------------------------------------
@ANNIVERSARY_CHECK
;現在の年月日をローカル変数に入れておく
LOCAL:1 = DAY:4
LOCAL:2 = DAY:5
LOCAL:3 = DAY:6

FOR LOCAL, 0, CHARANUM
    ;失踪中のは除外する
    SIF CFLAG:LOCAL:조교관외 == 1
        CONTINUE
    ;主人の記念日はいまのところない
    SIF LOCAL == MASTER
        CONTINUE
    SIF 대상_제외됨(LOCAL)
        CONTINUE
    ;記念日フラグをクリアにする
    CFLAG:LOCAL:기념일플래그 = 0

    ;購入されて…
    IF CFLAG:LOCAL:구입한날 > 0
        CALL CALENDAR_CALC,CFLAG:LOCAL:구입한날
        ;月日を比較する
        IF LOCAL:2 == DAY:5 && LOCAL:3 == DAY:6
            PRINTFORML %CALLNAME:LOCAL%를(을) 구입한지 {LOCAL:1 - DAY:4}년 되는 해입니다.
            CFLAG:LOCAL:기념일플래그 |= 1
        ENDIF
    ENDIF
    ;連れてこられて…
    IF CFLAG:LOCAL:끌려온날 > 0
        CALL CALENDAR_CALC,CFLAG:LOCAL:끌려온날
        ;月日を比較する
        IF LOCAL:2 == DAY:5 && LOCAL:3 == DAY:6
            PRINTFORML %CALLNAME:LOCAL%이(가) 납치된지{LOCAL:1 - DAY:4}년 되었습니다
            CFLAG:LOCAL:기념일플래그 |= 2
        ENDIF
    ENDIF
    ;誕生日
    IF CFLAG:LOCAL:생일 > 0
        CALL CALENDAR_CALC,CFLAG:LOCAL:생일
        ;月日を比較する
        IF LOCAL:2 == DAY:5 && LOCAL:3 == DAY:6
            PRINTFORML %CALLNAME:LOCAL%의 생일
            CFLAG:LOCAL:기념일플래그 |= 4
        ENDIF
    ENDIF
    ;契約記念日
    IF CFLAG:LOCAL:계약기념일 > 0
        CALL CALENDAR_CALC,CFLAG:LOCAL:계약기념일
        ;月日を比較する
        IF LOCAL:2 == DAY:5 && LOCAL:3 == DAY:6
            PRINTFORML %CALLNAME:LOCAL%의{LOCAL:1 - DAY:4}년째 계약기념일
            CFLAG:LOCAL:기념일플래그 |= 8
        ENDIF
    ENDIF
    ;結婚記念日
    IF CFLAG:LOCAL:결혼기념일 > 0
        CALL CALENDAR_CALC,CFLAG:LOCAL:결혼기념일
        ;月日を比較する
        IF LOCAL:2 == DAY:5 && LOCAL:3 == DAY:6
            PRINTFORML %CALLNAME:LOCAL%의{LOCAL:1 - DAY:4}년째 결혼기념일
            CFLAG:LOCAL:기념일플래그 |= 16
        ENDIF
    ENDIF
    ;初妊娠記念日?
    IF CFLAG:LOCAL:첫임신일 > 0
        CALL CALENDAR_CALC,CFLAG:LOCAL:첫임신일
        ;月日を比較する
        IF LOCAL:2 == DAY:5 && LOCAL:3 == DAY:6
            PRINTFORML %CALLNAME:LOCAL%가(이) 여기서 처음으로 임신한지 {LOCAL:1 - DAY:4}년 지났습니다.
            CFLAG:LOCAL:기념일플래그 |= 32
        ENDIF
    ENDIF
NEXT

;今日の日にカレンダーを戻しておく.
CALL CALENDAR_CALC,DAY

;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;변수 교체에 수반한 세이브 데이터 자동 수정 처리
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;=============================================================================
;수정 처리 본체
;=============================================================================
;--------------------------------------------------
;데이터의 로드시에 호출
;--------------------------------------------------
@EVENTLOAD
;현행 ver. 이전의 데이터를 읽었을 경우에는 수정 처리로
SIF LASTLOAD_VERSION < GAMEBASE_VERSION
    CALL SAVE_SUPPORT_OLDVERSION

;--------------------------------------------------
;수정 처리 범용 부분
;--------------------------------------------------
@SAVE_SUPPORT_OLDVERSION
RESULT = 0
;오래된 패치(2년 이상?)는 삭제하는 것을 제안 
;(그 쯤이면 어지간한 유저들은 패치가 되있을 테니까...)
;GAMEBASE.CSV의 버젼(バージョン)을 바꿔야 한다는 것을 명심

;2018년 5월 5일 패치
SIF LASTLOAD_VERSION < 3162 
    CALL SAVE_SUPPORT_VERSION_3162

;2018년 6월 28일 패치
SIF LASTLOAD_VERSION < 3163 
    CALL SAVE_SUPPORT_VERSION_3163

;2018년 7월 10일 패치
SIF LASTLOAD_VERSION < 3164 
    CALL SAVE_SUPPORT_VERSION_3164

;2019년 2월 10일 패치
SIF LASTLOAD_VERSION < 3190
    CALL SAVE_SUPPORT_VERSION_3190

SIF MONEY:100 == 0
    MONEY:100 = 1000000

;セーブバージョンフラグは廃止

IF RESULT == 1
    PRINTFORML 세이브 데이터를 v{LASTLOAD_VERSION}에서 v{GAMEBASE_VERSION}으로 업데이트 했습니다
    PRINTFORMW 메뉴가 표시되면 「[200] - 세이브」를 선택해주십시오.
ENDIF

;=============================================================================
;각 버전마다 수정 처리
;=============================================================================
;--------------------------------------------------
;2018년 5월 5일 패치
;--------------------------------------------------
@SAVE_SUPPORT_VERSION_3162
;딸, 밖으로 나간다 플래그 재정리
;성격 2,4,5를 클리어하면 8502가 ON, 성격 1,3을 클리어하면 8503이 ON되는 것을 성격별로 ON되게 바꿈.
;일단은 성격별로 다른 대사가 나오는 것이 목표였던 것 같음.

PRINTL 딸의 밖으로 나간다 이밴트 플래그를 재설정합니다
FLAG:8502 = 0
FLAG:8503 = 0

;삭제된 아이템 제거

PRINTW 삭제된 아이템을 제거합니다

;현자의피
ITEM:66 = 0
;흥분제
ITEM:69 = 0
;감각증폭약
ITEM:70 = 0

PRINTL

RETURN 1

;--------------------------------------------------
;2018년 6월 28일 패치
;--------------------------------------------------
@SAVE_SUPPORT_VERSION_3163
;모든 캐릭터의 신체 사이즈 재갱신 필요
REPEAT CHARANUM
    CFLAG:COUNT:8 = 0
    CFLAG:COUNT:4997 = LIMIT(CFLAG:COUNT:4997 + 1, 1, 5)
REND

RETURN 1

;--------------------------------------------------
;2018년 7월 10일 패치
;--------------------------------------------------
@SAVE_SUPPORT_VERSION_3164
;모든 캐릭터의 신체 사이즈 재갱신 필요2
REPEAT CHARANUM
    CFLAG:COUNT:8 = 0
REND


LOADGLOBAL
IF GLOBAL:999 < 1
    PRINTW 버그 수정을 위해 전역 데이터에서 커맨드 필터링 정보를 초기화합니다.
    ;GLOBAL 202, 201, 202를 제외하고 초기화
    VARSET GLOBAL, 0, 21, 200
    VARSET GLOBAL, 0, 203, 621
    GLOBAL:999 = 1 ;전역 변수라 모든 불러오기에서 1회만 실행하면 됨. 임시로 GLOBAL:999를 이용
    SAVEGLOBAL
ENDIF
RETURN 1

;--------------------------------------------------
;2019년 2월 10일 패치
;--------------------------------------------------
@SAVE_SUPPORT_VERSION_3190

PRINTW YASAI 모드를 삭제합니다

REPEAT CHARANUM
    ;야채의 연금술사 삭제
    TALENT:COUNT:831 = 0
REND

;채소밭 플래그 초기화
FLAG:573 = 0;
